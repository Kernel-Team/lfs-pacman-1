pkgname=acpica-unix
pkgver=20170831
pkgrel=1
pkgdesc="Group of software components that together create an implementation of the ACPI specification"
arch=('x86_64')
url="https://acpica.org"
license=('custom')
#depends=()
source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/acpica-unix-${pkgver}/generate/unix"

	sed -i "/ifneq.*_APPLE/s#APPLE#LINUX#g" Makefile.config
	sed -i "/Werror/d" Makefile.config
}

build() {
	cd "${srcdir}/acpica-unix-${pkgver}/generate/unix"

	make CC="clang" HOST="_LINUX"
}

package() {
	cd "${srcdir}/acpica-unix-${pkgver}/generate/unix"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/bin/acpidump"
}

sha512sums=('abc0c98d02698a8a182d2f5ccdaaef3fabf4d833b1ac77a06158238d29b3d4103ea3bcf06e04c34d74aa21f862df65420eca126587ffb85bc0d9632852e4b9c6')

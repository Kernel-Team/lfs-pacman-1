pkgbase=intel-vaapi-driver
pkgname=('intel-vaapi-driver' 'lib32-intel-vaapi-driver')
pkgver=2.4.0
pkgrel=1
arch=('x86_64')
url="https://01.org/linuxmedia/vaapi"
license=('MIT')
#depends=()
source=("https://github.com/intel/intel-vaapi-driver/releases/download/${pkgver}/intel-vaapi-driver-${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/intel-vaapi-driver-${pkgver}"

	cp -a "${srcdir}"/intel-vaapi-driver{,32}-${pkgver}
}

build() {
	cd "${srcdir}/intel-vaapi-driver-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/intel-vaapi-driver32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/intel-vaapi-driver-${pkgver}"
	make -C "${srcdir}/intel-vaapi-driver32-${pkgver}"
}

package_intel-vaapi-driver() {
	pkgdesc="VA-API implementation for Intel G45 and HD Graphics family"

	cd "${srcdir}/intel-vaapi-driver-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-intel-vaapi-driver() {
	pkgdesc="VA-API implementation for Intel G45 and HD Graphics family (32 bit)"

	cd "${srcdir}/intel-vaapi-driver32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('9f8faf8d78ca485c03ba172473d0c3a4fd9127ce22391a209369cdb4895352e12e682b163b8d9b563bc9bc1e4ebfa463821c503a362e9e3e3a6dcbe7082e536c')

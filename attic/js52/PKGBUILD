pkgname=js52
pkgver=52.6.0
pkgrel=1
pkgdesc="JavaScript interpreter and libraries - Version 52"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey/Releases/52"
license=('MPL')
#depends=()
source=("https://download.gnome.org/teams/releng/tarballs-needing-help/mozjs/mozjs-${pkgver}gnome1.tar.xz")
noextract=("mozjs-${pkgver}gnome1.tar.xz")

prepare() {
	install -dm755 "${srcdir}/mozjs-${pkgver}"

	bsdtar -xf "${srcdir}/mozjs-${pkgver}gnome1.tar.xz" -C "${srcdir}/mozjs-${pkgver}"
}

build() {
	cd "${srcdir}/mozjs-${pkgver}/js/src"

	unset CFLAGS CXXFLAGS

	export CC="gcc"
	export CXX="g++"
	export PYTHON="/usr/bin/python"
	export SHELL="/bin/bash"

	./configure --prefix=/usr \
		--enable-readline \
		--with-intl-api \
		--with-system-icu \
		--with-system-nspr \
		--with-system-zlib

	make
}

package() {
	cd "${srcdir}/mozjs-${pkgver}/js/src"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/lib/libjs_static.ajs"

	find "${pkgdir}/usr/include" -type f -exec chmod 644 {} \;
	find "${pkgdir}/usr/lib/pkgconfig" -type f -exec chmod 644 {} \;
}

sha512sums=('e4f2a90fe25541a5120158ca58f837b20f350ab51da3f17a2c0c552166499155fc0519dd99f37e15caf00b4e373af0a06b9c6ddc5812830248423fe87380d6e4')

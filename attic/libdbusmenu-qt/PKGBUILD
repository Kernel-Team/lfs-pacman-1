pkgname=libdbusmenu-qt
pkgver=0.9.3+16.04.20160218
pkgrel=1
pkgdesc="A library that provides a Qt implementation of the DBusMenu spec"
arch=('x86_64')
url="https://launchpad.net/libdbusmenu-qt"
license=('GPL')
#depends=()
source=("http://archive.ubuntu.com/ubuntu/pool/main/libd/libdbusmenu-qt/libdbusmenu-qt_${pkgver}.orig.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DWITH_DOC=OFF \
		"${srcdir}/libdbusmenu-qt-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('dfaba59cedd320e6a3b5a9c0a5e13fb9e0952224e39cdd6ee3b32b03d95ac850a533e2a73835a7c0ead93b2283b9c235472bacb84e3d761ebe9e9398345314a9')

pkgname=libgconf
pkgver=3.2.6
pkgrel=1
pkgdesc="A configuration database system (shared library)"
arch=('x86_64')
url="https://git.gnome.org/browse/GConf/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/GConf/${pkgver:0:3}/GConf-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/GConf-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-orbit

	make -C gconf gconfmarshal.c gconfmarshal.h
	make -C gconf libgconf-2.la
}

package() {
	cd "${srcdir}/GConf-${pkgver}"

	make -C gconf lib_LTLIBRARIES="libgconf-2.la" install-libLTLIBRARIES DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/*.{a,so}
}

sha512sums=('35f5f659f9d03f7531a7102adacbda0eb310d8a55a831c768c91a82e07dae21247726e00e0e411f63b1de9ade0f042ded572a3ea4a4b2ad3135231f344540b58')

pkgbase=libjpeg-turbo6
pkgname=('libjpeg-turbo6' 'lib32-libjpeg-turbo6')
pkgver=1.5.2
pkgrel=1
arch=('x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/libjpeg-turbo/libjpeg-turbo-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/libjpeg-turbo{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libjpeg-turbo-${pkgver}"

	./configure --prefix=/usr --without-simd

	cd "${srcdir}/libjpeg-turbo32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--without-simd

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libjpeg-turbo-${pkgver}"
	make -C "${srcdir}/libjpeg-turbo32-${pkgver}"
}

package_libjpeg-turbo6() {
	pkgdesc="JPEG image codec with accelerated baseline compression and decompression (soversion 6)"

	cd "${srcdir}/libjpeg-turbo-${pkgver}"

	make lib_LTLIBRARIES="libjpeg.la" install-libLTLIBRARIES DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/*.{a,so}
}

package_lib32-libjpeg-turbo6() {
	pkgdesc="JPEG image codec with accelerated baseline compression and decompression (soversion 6) (32 bit)"

	cd "${srcdir}/libjpeg-turbo32-${pkgver}"

	make lib_LTLIBRARIES="libjpeg.la" install-libLTLIBRARIES DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib32/*.{a,so}
}

sha512sums=('c7fe5cc77e38fad33af3f10e6db961c8edf033a86c09541121f49bfa20547179760924e6d3e397f0add7030459ff3babadd3457ab2da4a40a2147dc1574aa444')

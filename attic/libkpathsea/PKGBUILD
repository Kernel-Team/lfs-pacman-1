pkgname=libkpathsea
pkgver=20190410
pkgrel=1
pkgdesc="Path search library for TeX"
arch=('x86_64')
url="http://tug.org/texlive/"
license=('LGPL2.1')
#depends=()
source=("ftp://tug.org/texlive/historic/${pkgver:0:4}/texlive-${pkgver}-source.tar.xz")

prepare() {
	cd "${srcdir}/texlive-${pkgver}-source/texk/kpathsea"

	sed -i "s@TEXMFROOT = \$SELFAUTOPARENT@TEXMFROOT = /usr/share@g" texmf.cnf
	sed -i "s@TEXMFSYSVAR = \$TEXMFROOT/texmf-var@TEXMFSYSVAR = /var/lib/texmf@g" texmf.cnf
	sed -i "s@TEXMFSYSCONFIG = \$TEXMFROOT/texmf-config@TEXMFSYSCONFIG = /etc/texmf@g" texmf.cnf
	sed -i "s@TEXMFHOME = ~/texmf@TEXMFHOME = ~/.texmf@g" texmf.cnf
}

build() {
	cd "${srcdir}/texlive-${pkgver}-source/texk/kpathsea"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var

	make
}

package() {
	cd "${srcdir}/texlive-${pkgver}-source/texk/kpathsea"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('7759d8e668e53c0c73e48907096da9d8fb6384dd9c3342ddfb05e17d077054c5351d35256cf4e63a6da502de2f09f521979bede7209cdd5bd8ae80eba0efed2e')

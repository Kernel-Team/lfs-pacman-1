pkgbase=libomxil-bellagio
pkgname=('libomxil-bellagio' 'lib32-libomxil-bellagio')
pkgver=0.9.3
pkgrel=1
arch=('x86_64')
url="http://omxil.sourceforge.net"
license=('LGPL')
#depends=()
source=("https://downloads.sourceforge.net/omxil/libomxil-bellagio-${pkgver}.tar.gz"
	"libomxil-bellagio-fixes.patch")

prepare() {
	cd "${srcdir}/libomxil-bellagio-${pkgver}"

	patch -Np1 -i "${srcdir}/libomxil-bellagio-fixes.patch"

	autoreconf -fi

	cp -a "${srcdir}"/libomxil-bellagio{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libomxil-bellagio-${pkgver}"

	export CC="gcc"
	export CXX="g++"

	./configure --prefix=/usr

	cd "${srcdir}/libomxil-bellagio32-${pkgver}"

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	cd "${srcdir}"

	make -C "${srcdir}/libomxil-bellagio-${pkgver}"
	make -C "${srcdir}/libomxil-bellagio32-${pkgver}"
}

package_libomxil-bellagio() {
	pkgdesc="An opensource implementation of the OpenMAX Integration Layer API"

	cd "${srcdir}/libomxil-bellagio-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libomxil-bellagio() {
	pkgdesc="An opensource implementation of the OpenMAX Integration Layer API (32 bit)"

	cd "${srcdir}/libomxil-bellagio32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/bin/omxregister-bellagio "${pkgdir}/usr/bin/omxregister-bellagio32"
}

sha512sums=('72b4c8c639f8cf767ba6323fdfc42b90ad4b62f0ece3ba786ae38afc7c5d64cb0ba464b7993a588c1c7fa2daafe2ac773c9c4367a154c32632e87ba0d6341d16'
            '6a763c164ed21ad9a8c0da01e02e823ed40f2539b68ef96d8052739dd433daa472c7d05f86bb06b95269a819403aa2e27670d37475ecf108f808bf9fa3ede413')

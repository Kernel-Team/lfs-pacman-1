pkgbase=python-libsemanage
pkgname=('python2-libsemanage' 'python3-libsemanage')
pkgver=2.7
pkgrel=1
arch=('x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
#depends=()
source=("https://raw.githubusercontent.com/wiki/SELinuxProject/selinux/files/releases/20170804/libsemanage-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/libsemanage-${pkgver}"

	make PYTHON=python2 pywrap
	make PYTHON=python3 pywrap
}

package_python2-libsemanage() {
	pkgdesc="SELinux binary policy manipulation library (Python 2 bindings)"

	cd "${srcdir}/libsemanage-${pkgver}"

	make PYTHON=python2 install-pywrap DESTDIR="${pkgdir}"
}

package_python3-libsemanage() {
	pkgdesc="SELinux binary policy manipulation library (Python 3 bindings)"

	cd "${srcdir}/libsemanage-${pkgver}"

	make PYTHON=python3 install-pywrap DESTDIR="${pkgdir}"
}

sha512sums=('6b30ea87f1ab3944935188539bbf869c8e287e05c174ea61cd19722a89cf8156518f336476e23b4117a5a05aec9a99a4db679b58a6952bf0c31ad809e9ab91d9')

pkgname=mozjs
pkgver=38.2.1
pkgrel=1
pkgdesc="JavaScript interpreter and libraries - Version 38"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey/Releases/38"
license=('MPL')
#depends=()
source=("https://people.mozilla.org/~sstangl/mozjs-${pkgver}.rc0.tar.bz2"
	"mozjs-fixes.patch")

prepare() {
	cd "${srcdir}/mozjs-38.0.0"

	patch -Np1 -i "${srcdir}/mozjs-fixes.patch"
}

build() {
	cd "${srcdir}/mozjs-38.0.0/js/src"

	export PYTHON="/usr/bin/python"
	export SHELL="/bin/bash"

	touch configure

	./configure --prefix=/usr \
		--enable-ctypes \
		--enable-readline \
		--enable-shared-js \
		--enable-system-ffi \
		--enable-threadsafe \
		--with-system-icu \
		--with-system-nspr \
		--with-system-zlib

	make
}

package() {
	cd "${srcdir}/mozjs-38.0.0/js/src"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/lib/libjs_static.ajs"

	find "${pkgdir}/usr/include" -type f -exec chmod 644 {} \;
	find "${pkgdir}/usr/lib/pkgconfig" -type f -exec chmod 644 {} \;
}

sha512sums=('60262c7619cc418569aec81775a671105acbc5bf372273828868f2fbbbc4ea33f78380f7455710816bfcdae828fb4115a08234d599cb30d0db859fcba17f019d'
            '6d84a79199cd520d1b9e2d614e6a748375510cf1cf996a4f2b83185d71174b1a155f086905f5c4d186b6b468b2367b0dd5a5aa2bb02aaa3bfb084a2b4810e0e5')

pkgname=openssl098
_basever=0.9.8zh
pkgver=${_basever/[a-z]*/.${_basever//[0-9.]/}}
pkgrel=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (0.9.8 branch)"
arch=('x86_64')
url="https://www.openssl.org"
license=('custom:BSD')
#depends=()
options=(emptydirs)
source=("https://www.openssl.org/source/old/0.9.x/openssl-${_basever}.tar.gz")

build() {
	cd "${srcdir}/openssl-${_basever}"

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib \
		shared \
		zlib \
		enable-md2 \
		linux-x86_64 \
		"-Wa,--noexecstack ${CFLAGS}"

	make -j1
}

package() {
	cd "${srcdir}/openssl-${_basever}"

	for _lib in crypto ssl
	do
		install -v -Dm755 lib${_lib}.so.0.9.8 "${pkgdir}/usr/lib/lib${_lib}.so.0.9.8"
	done
}

sha512sums=('b97fa2468211f86c0719c68ad1781eff84f772c479ed5193d6da14bac086b4ca706e7d851209d9df3f0962943b5e5333ab0def00110fb2e517caa73c0c6674c6')

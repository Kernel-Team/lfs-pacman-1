pkgbase=python-rdflib
pkgname=('python2-rdflib' 'python3-rdflib')
pkgver=4.2.2
pkgrel=1
pkgdesc="Python library for working with RDF"
arch=('x86_64')
url="https://pypi.python.org/pypi/rdflib/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/r/rdflib/rdflib-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/rdflib-${pkgver}" "${srcdir}/rdflib-${pkgver}-py2"
}

build() {
	cd "${srcdir}/rdflib-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/rdflib-${pkgver}-py2"

	python2 setup.py build
}

package_python3-rdflib() {
	cd "${srcdir}/rdflib-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*
	do
		mv ${file} ${file}3
	done
}

package_python2-rdflib() {
	cd "${srcdir}/rdflib-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('efc24d8ce1080e59950cecc9c779ae0d63673c8690f74db1772f6c2dbf24d6f4de5214d5870a074bffe549b42d8d5f5d131df29bbfd174b763ae1f843aec7d5c')

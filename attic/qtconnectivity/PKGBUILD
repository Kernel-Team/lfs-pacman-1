pkgname=qtconnectivity
pkgver=5.9.2
pkgrel=1
pkgdesc="Provides access to Bluetooth hardware"
arch=('x86_64')
url="https://www.qt.io/"
license=('GPL3' 'LGPL3' 'FDL' 'custom')
#depends=()
source=("https://download.qt.io/official_releases/qt/${pkgver:0:3}/${pkgver}/submodules/qtconnectivity-opensource-src-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/qtconnectivity-opensource-src-${pkgver}"

	sed -i "/unistd.h/i #include <errno.h>" src/bluetooth/bluez/bluetoothmanagement.cpp
}

build() {
	cd "${srcdir}/qtconnectivity-opensource-src-${pkgver}"

	qmake
	make
}

package() {
	cd "${srcdir}/qtconnectivity-opensource-src-${pkgver}"

	make install INSTALL_ROOT="${pkgdir}"

	sed -i "/^QMAKE_PRL_BUILD_DIR/d;s#${PWD}#/usr#g" "${pkgdir}"/usr/lib/*.prl
}

sha512sums=('c7a32ca45464670bb461139e48721c80859d2ef2eb5e30b61062578c07dfe1cf9cceb0bfe01304f4661a6b80cc7c2769399e0745a165596440a896bd1e1d0893')

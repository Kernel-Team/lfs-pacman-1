pkgname=speech-dispatcher
pkgver=0.8.7
pkgrel=1
pkgdesc="High-level device independent layer for speech synthesis interface"
arch=('x86_64')
url="https://devel.freebsoft.org/speechd"
license=('GPL2' 'FDL')
#depends=()
options=(emptydirs)
source=("https://devel.freebsoft.org/pub/projects/speechd/speech-dispatcher-${pkgver}.tar.gz"
	"speech-dispatcherd.service")

prepare() {
	cd "${srcdir}/speech-dispatcher-${pkgver}"

	sed -i "s/cicero //g" configure.ac
	sed -i "s/sd_cicero//g" src/modules/Makefile.am

	autoreconf -fi
}

build() {
	cd "${srcdir}/speech-dispatcher-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--without-flite

	make
}

package() {
	cd "${srcdir}/speech-dispatcher-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/var/log/speech-dispatcher"

	install -v -Dm644 "${srcdir}/speech-dispatcherd.service" "${pkgdir}/usr/lib/systemd/system/speech-dispatcherd.service"
}

sha512sums=('0e7af02e8521aa8d397ea4d0329f9a8d56970beed645456dfedc738ececd2ccd8ecfdf66e92ae852878a473ba991175e1d4dc2b8b0148c958163682b49678375'
            '73eb563003b03f4836b17af66335664050d1dc3bd8434be368cd09ff8933aa2adc4bd17c4ce5c1bbbd406a0b0880191426a679fa05eac0a30f4005eaeeb47340')

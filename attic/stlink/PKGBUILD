pkgname=stlink
pkgver=1.4.0
pkgrel=1
pkgdesc="Firmware programmer for STM32 STLINK v1/v2 protocol"
arch=('x86_64')
url="https://github.com/texane/stlink"
license=('BSD')
#depends=()
source=("https://github.com/texane/stlink/archive/${pkgver}/stlink-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DSTLINK_UDEV_RULES_DIR=/usr/lib/udev/rules.d \
		-DSTLINK_MODPROBED_DIR=/usr/lib/modprobe.d \
		"${srcdir}/stlink-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/lib/libstlink.a"
}

sha512sums=('cb7617068a6dcaf7e8d0ccfb8b918f0373f6120398dab40a8b8d6567705871226684fd19aad92e4bab623310a5da13ea9fd70153b619f58d3534f0a4ff3a9886')

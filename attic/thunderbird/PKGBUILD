pkgname=thunderbird
pkgver=52.3.0
pkgrel=1
pkgdesc="Standalone mail and news reader from mozilla.org"
arch=('x86_64')
url="https://www.mozilla.org/thunderbird/"
license=('MPL' 'GPL' 'LGPL')
#depends=()
options=(emptydirs !strip)
source=("https://ftp.mozilla.org/pub/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}.source.tar.xz"
	"thunderbird.desktop"
	"vendor.js"
	"mozconfig"
	"thunderbird-rust.patch")

prepare() {
	cd "${srcdir}/thunderbird-${pkgver}"

	install -m644 "${srcdir}/mozconfig" mozconfig

	sed -i "s:-\$(MOZ_APP_VERSION)::g" config/baseconfig.mk mozilla/config/baseconfig.mk

#	patch -d mozilla -Np1 -i "${srcdir}/thunderbird-rust.patch"
}

build() {
	cd "${srcdir}/thunderbird-${pkgver}"

	unset CFLAGS CXXFLAGS
	export SHELL="/bin/bash"

	make -f client.mk build
}

package() {
	cd "${srcdir}/thunderbird-${pkgver}"

	make -j1 -f client.mk install INSTALL_SDK= DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/vendor.js" "${pkgdir}/usr/lib/thunderbird/defaults/preferences/vendor.js"

	rm -rf "${pkgdir}/usr/lib/thunderbird/dictionaries"
	ln -sfv ../../share/hunspell "${pkgdir}/usr/lib/thunderbird/dictionaries"

	for i in 16 22 24 32 48 256
	do
		install -v -Dm644 other-licenses/branding/thunderbird/mailicon${i}.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/thunderbird.png"
	done

	install -v -Dm644 "${srcdir}/thunderbird.desktop" "${pkgdir}/usr/share/applications/thunderbird.desktop"

	sed -i "s#${srcdir}##" "${pkgdir}"/usr/lib/thunderbird/distribution/extensions/*/defaults/preferences/lightning.js
}

sha512sums=('10e6495d207328aae325a797be1de5e535b5d967df929b0af9d7554d0473b5a7931c8f9cb1793e843b1913f441f790169dd4ac1ad9e0e53e66bc8e1c1dd60e66'
            '760edafe6079f342dffa755cacd050d0c8f72bef38b972951c58e3d3ab97f665bf202ebb5787692a250c74a3f7f25335af84f38d7191e2e0352b23d93468451a'
            'aeb444784732267f1b1e87e6084a776f82a1912c4c2637d2cf1de1c135dd9d41d2ef66d2bd3f9cbd3a79fad32d17ea6e2968ba644d5f887cb66ba6c09a2098f5'
            'aa9a0c4f3c236f238106747a060c9a740eeb0cc0213cf81c530c0a9d21c18f98b5f4d6602a6add4c3c4520885fcd13402cba26f12d6113d92176ce09007e1a37'
            '9a02d89603ad2928e38d7350d5d71ec904815958c65cc13323f5d4cd9392b823264fae812ea658d83728839372b412fd185653ef65b7ab8dd0595158b3bdc2e2')

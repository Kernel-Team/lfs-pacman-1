pkgbase=alsa-lib
pkgname=('alsa-lib' 'lib32-alsa-lib')
pkgver=1.2.4
pkgrel=1
arch=('x86_64')
url="https://alsa-project.org/wiki/Main_Page"
license=('LGPL')
#depends=()
source=("https://www.alsa-project.org/files/pub/lib/alsa-lib-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/alsa-lib{,32}-${pkgver}
}

build() {
	cd "${srcdir}/alsa-lib-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/alsa-lib32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--disable-python

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/alsa-lib-${pkgver}"
	make -C "${srcdir}/alsa-lib32-${pkgver}"
}

package_alsa-lib() {
	pkgdesc="An alternative implementation of Linux sound support"

	cd "${srcdir}/alsa-lib-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-alsa-lib() {
	pkgdesc="An alternative implementation of Linux sound support (32 bit)"

	cd "${srcdir}/alsa-lib32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('12086952dc8f16f1cb6946517858e17b1c3276aeda9ff5703a84bb38aa78eb4c4e9cb4485c5b3f21f174fdbd976b3bcbbc481e85cb2460652858490df51ae844')

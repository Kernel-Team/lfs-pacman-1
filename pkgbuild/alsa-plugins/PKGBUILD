pkgbase=alsa-plugins
pkgname=('alsa-plugins' 'lib32-alsa-plugins')
pkgver=1.2.1
pkgrel=1
arch=('x86_64')
url="https://alsa-project.org/wiki/Main_Page"
license=('GPL')
#depends=()
source=("ftp://ftp.alsa-project.org/pub/plugins/alsa-plugins-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/alsa-plugins{,32}-${pkgver}
}

build() {
	cd "${srcdir}/alsa-plugins-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc

	cd "${srcdir}/alsa-plugins32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib32 \
		--disable-avcodec

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/alsa-plugins-${pkgver}"
	make -C "${srcdir}/alsa-plugins32-${pkgver}"
}

package_alsa-plugins() {
	pkgdesc="Extra alsa plugins"

	cd "${srcdir}/alsa-plugins-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-alsa-plugins() {
	pkgdesc="Extra alsa plugins (32 bit)"

	cd "${srcdir}/alsa-plugins32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('a49ef9290b66855112e301a5fd5a0932f13a50254e164f3340f2b342a36469532ee783beabfe5e95870dbf4cd81ca1e8d0415d94c8c7ee963133fb3c343721de')

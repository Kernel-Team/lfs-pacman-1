pkgbase=alsa-plugins
pkgname=('alsa-plugins' 'lib32-alsa-plugins')
pkgver=1.2.2
pkgrel=1
arch=('x86_64')
url="https://alsa-project.org/wiki/Main_Page"
license=('GPL')
#depends=()
source=("https://www.alsa-project.org/files/pub/plugins/alsa-plugins-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/alsa-plugins{,32}-${pkgver}
}

build() {
	cd "${srcdir}/alsa-plugins-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc

	cd "${srcdir}/alsa-plugins32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib32 \
		--disable-avcodec

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/alsa-plugins-${pkgver}"
	make -C "${srcdir}/alsa-plugins32-${pkgver}"
}

package_alsa-plugins() {
	pkgdesc="Extra alsa plugins"

	cd "${srcdir}/alsa-plugins-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-alsa-plugins() {
	pkgdesc="Extra alsa plugins (32 bit)"

	cd "${srcdir}/alsa-plugins32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('25a7dfaa64be024447d889b5cde668f1308d197f54880548a82d50beb4ac0dbff33b415da1e26dc5229408c934247a9bd38acfb0f82ca388deac0d77ab3cdadb')

pkgname=apr-util
pkgver=1.6.1
pkgrel=1
pkgdesc="The Apache Portable Runtime"
arch=('x86_64')
url="http://apr-util.apache.org/"
license=('APACHE')
#depends=()
source=("https://archive.apache.org/dist/apr/apr-util-${pkgver}.tar.bz2")

build() {
	cd "${srcdir}/apr-util-${pkgver}"

	./configure --prefix=/usr \
		--with-berkeley-db \
		--with-crypto \
		--with-dbm=db \
		--with-ldap \
		--with-openssl=/usr \
		--with-sqlite3 \
		--without-gdbm \
		--without-sqlite2 \
		--with-apr=/usr/bin/apr-1-config

	make
}

package() {
	cd "${srcdir}/apr-util-${pkgver}"

	make install DESTDIR="${pkgdir}"

	sed -i "s#${srcdir}#/usr/src#g" "${pkgdir}/usr/bin/apu-1-config"
}

sha512sums=('40eff8a37c0634f7fdddd6ca5e596b38de15fd10767a34c30bbe49c632816e8f3e1e230678034f578dd5816a94f246fb5dfdf48d644829af13bf28de3225205d')

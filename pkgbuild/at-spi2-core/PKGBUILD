pkgname=at-spi2-core
pkgver=2.38.0
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('x86_64')
url="https://www.gnome.org"
license=('GPL2')
#depends=()
source=("https://download.gnome.org/sources/at-spi2-core/${pkgver:0:4}/at-spi2-core-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/at-spi2-core-${pkgver}"

	sed -i 's#from "@filename@"##' atspi/atspi-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Ddocs=true \
		"${srcdir}/at-spi2-core-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('3a1eb27cff6e0dd03119b4f8361a3b6037b26c511e80e2d003d1d5c41fede6d49eb5e0ac1ee45cfb4f3ca8e53292a7e2da67df80be28e77014775e41777a96c1')

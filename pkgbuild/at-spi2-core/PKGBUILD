pkgname=at-spi2-core
pkgver=2.28.0
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('x86_64')
url="https://www.gnome.org"
license=('GPL2')
#depends=()
source=("https://download.gnome.org/sources/at-spi2-core/${pkgver:0:4}/at-spi2-core-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/at-spi2-core-${pkgver}"

	sed -i 's#from "@filename@"##' atspi/atspi-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/at-spi2-core-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('ce5251f234d48f657a5fd5fbd9a85799365e9814235ecff62fa5088611c0c8c0489e17fb27a805453a2864163cb83f8d8d5ed4cdb7e37c4ee9ebb897146e2d1d')

pkgname=atk
pkgver=2.26.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=('x86_64')
url="https://developer.gnome.org/atk/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/atk/${pkgver:0:4}/atk-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/atk-${pkgver}"

	sed -i 's#from "@filename@"##' atk/atk-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/atk-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('7d3425df3d8fe0010d2511d442709d851799d2b8ccec1d63147cb319a0fecd714a2c280ae22884f3e8b7cc688581dcffdd0e2cc513164b32e526ee69266c1372')

pkgname=atk
pkgver=2.28.1
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=('x86_64')
url="https://developer.gnome.org/atk/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/atk/${pkgver:0:4}/atk-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/atk-${pkgver}"

	sed -i 's#from "@filename@"##' atk/atk-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/atk-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('aa61e7718b3f4d9c33ec2b4747e35f894a51550ef39ecc4a7af5705811c029645bd3c728071f446b075a0b7aef0c35c1b73406a210cd30171838f24a8dfc0abb')

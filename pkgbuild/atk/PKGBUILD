pkgname=atk
pkgver=2.26.1
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=('x86_64')
url="https://developer.gnome.org/atk/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/atk/${pkgver:0:4}/atk-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/atk-${pkgver}"

	sed -i 's#from "@filename@"##' atk/atk-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/atk-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('9a802f1856f0f608c3b4ef6de27dc8174e103dbb32431a4dcffcac48c685cc48efb087ab73f661d7327341e8074b73bb7877c596f93228283ca5907910d64a6b')

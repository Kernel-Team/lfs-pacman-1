pkgname=atk
pkgver=2.25.90
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=('x86_64')
url="https://developer.gnome.org/atk/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/atk/${pkgver:0:4}/atk-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/atk-${pkgver}"

	sed -i 's#from "@filename@"##' atk/atk-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/atk-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('e21c45741c38552feae5406d9f0db4fa0d53a7683fc185e01e29662016f91b4311304c3efb18a59bd6771e9d0e6b0edde7f629acacc8ea7aec842e45f38741be')

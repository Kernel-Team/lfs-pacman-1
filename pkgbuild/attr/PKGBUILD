pkgbase=attr
pkgname=('attr' 'lib32-attr')
pkgver=2.4.47
pkgrel=1
arch=('x86_64')
url="http://savannah.nongnu.org/projects/attr"
license=('LGPL')
#depends=()
source=("http://download.savannah.gnu.org/releases/attr/attr-${pkgver}.src.tar.gz")

prepare() {
	cp -a "${srcdir}"/attr{,32}-${pkgver}
}

build() {
	cd "${srcdir}/attr-${pkgver}"

	sed -i "/SUBDIRS/s:man2::" man/Makefile

	export INSTALL_USER=root INSTALL_GROUP=root

	./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib

	cd "${srcdir}/attr32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/attr-${pkgver}"
	make -C "${srcdir}/attr32-${pkgver}"
}

package_attr() {
	pkgdesc="Extended attribute support library for ACL support"

	cd "${srcdir}/attr-${pkgver}"

	make install install-lib install-dev DIST_ROOT="${pkgdir}"

	chmod -v 755 "${pkgdir}/usr/lib/libattr.so"

	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libattr.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libattr.so") "${pkgdir}/usr/lib/libattr.so"
}

package_lib32-attr() {
	pkgdesc="Extended attribute support library for ACL support (32 bit)"

	cd "${srcdir}/attr32-${pkgver}"

	make install install-lib install-dev DIST_ROOT="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	chmod -v 755 "${pkgdir}/usr/lib32/libattr.so"
}

sha512sums=('2a333f63655758298650cf8f89c175efdc0112dcc4013e8d023e2a9a69f177e4bcb1f1d10b6666d6f2027dca4dec0833d54749952ab153d28367e1a72e6e2831')

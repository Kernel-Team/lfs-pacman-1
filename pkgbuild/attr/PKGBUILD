pkgbase=attr
pkgname=('attr' 'lib32-attr')
pkgver=2.4.48
pkgrel=1
arch=('x86_64')
url="http://savannah.nongnu.org/projects/attr"
license=('LGPL')
#depends=()
source=("http://download.savannah.gnu.org/releases/attr/attr-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/attr{,32}-${pkgver}
}

build() {
	cd "${srcdir}/attr-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc

	cd "${srcdir}/attr32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/attr-${pkgver}"
	make -C "${srcdir}/attr32-${pkgver}"
}

package_attr() {
	pkgdesc="Extended attribute support library for ACL support"

	cd "${srcdir}/attr-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-attr() {
	pkgdesc="Extended attribute support library for ACL support (32 bit)"

	cd "${srcdir}/attr32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('75f870a0e6e19b8975f3fdceee786fbaff3eadaa9ab9af01996ffa8e50fe5b2bba6e4c22c44a6722d11b55feb9e89895d0151d6811c1d2b475ef4ed145f0c923')

pkgname=audit
pkgver=2.7.6
pkgrel=1
pkgdesc="Userspace components of the audit framework"
arch=('x86_64')
url="https://people.redhat.com/sgrubb/audit"
license=(GPL)
#depends=()
options=(emptydirs)
source=("https://people.redhat.com/sgrubb/audit/audit-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/audit-${pkgver}"

	sed -i "s#/sbin#/usr&#g" init.d/auditd.service
	sed -i "s#/sbin/audispd#/usr&#g" init.d/auditd.conf
	sed -i "s#/sbin/audisp-remote#/usr&#g" audisp/plugins/remote/au-remote.conf

	sed -i "s#/sbin/auditctl#/usr&#g"  init.d/augenrules
	sed -i "s#/usr/sbin/restorecon#/sbin/restorecon#g" init.d/augenrules
}

build() {
	cd "${srcdir}/audit-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-systemd \
		--disable-zos-remote \
		--without-golang \
		--without-python{,3}

	make
}

package() {
	cd "${srcdir}/audit-${pkgver}"

	make install initdir=/lib/systemd/system DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/libexec"

	install -v -dm700 "${pkgdir}/var/log/audit"
	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libaudit.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libaudit.so") "${pkgdir}/usr/lib/libaudit.so"
}

sha512sums=('b219bf64b4cc9f1178751bd4f16a91d75d05b76165b57c4a1493e6a55c66481edfa2a661f479d7ecd46f6d719319776e9eabffb5dc7129dd953a3af3389aa8b8')

pkgname=audit
pkgver=2.7.6
pkgrel=1
pkgdesc="Userspace components of the audit framework"
arch=('x86_64')
url="https://people.redhat.com/sgrubb/audit"
license=(GPL)
#depends=()
source=("https://people.redhat.com/sgrubb/audit/audit-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/audit-${pkgver}"

	sed -i "s#/sbin#/usr&#g" init.d/auditd.service

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-systemd \
		--disable-zos-remote \
		--without-golang \
		--without-python{,3}

	make
}

package() {
	cd "${srcdir}/audit-${pkgver}"

	make install initdir=/lib/systemd/system DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/libexec"

	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libaudit.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libaudit.so") "${pkgdir}/usr/lib/libaudit.so"
}

md5sums=('55a81bbed973b58a90590c949e71dc3e')

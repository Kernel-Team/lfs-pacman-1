pkgname=autoconf2.13
pkgver=2.13
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code (Legacy 2.1x version)"
arch=('x86_64')
url="https://www.gnu.org/software/autoconf/"
license=('GPL2')
#depends=()
source=("https://ftp.gnu.org/gnu/autoconf/autoconf-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/autoconf-${pkgver}"

	sed -i "/acdatadir/s:autoconf:&2.13:g" Makefile.in

	./configure --prefix=/usr --program-suffix=2.13

	make
}

package() {
	cd "${srcdir}/autoconf-${pkgver}"

	make install prefix="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/info"
}

sha512sums=('602584f4c77b7a554aaa068eda5409b68eb0b3229e9c224bffb91c83c4314d25de15bd560a323626ff78f6df339c79e1ef8938c54b78ecadf4dc75c5241290ad')

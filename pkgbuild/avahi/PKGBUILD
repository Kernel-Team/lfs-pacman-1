pkgname=avahi
pkgver=0.7
pkgrel=1
pkgdesc="Service Discovery for Linux using mDNS/DNS-SD -- compatible with Bonjour"
arch=('x86_64')
url="https://github.com/lathiat/avahi"
license=('LGPL')
#depends=()
options=(emptydirs)
source=("https://github.com/lathiat/avahi/releases/download/v${pkgver}/avahi-${pkgver}.tar.gz"
	"avahi.sysusers")

build() {
	cd "${srcdir}/avahi-${pkgver}"

	sed -i "/if \".*HAVE_DBUS/s:if :&test :g" configure

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-mono \
		--disable-monodoc \
		--disable-python \
		--disable-qt3 \
		--disable-qt4 \
		--disable-static \
		--enable-compat-libdns_sd \
		--enable-compat-howl \
		--with-distro=none \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/avahi-${pkgver}"

	make install DESTDIR="${pkgdir}"

	make -C avahi-common uninstall DESTDIR="${pkgdir}"
	rm -rf "${pkgdir}/usr/include/avahi-common"

	for component in core client glib gobject compat-howl compat-libdns_sd
	do
		make -C avahi-${component} uninstall DESTDIR="${pkgdir}"
		make pkgconfig_DATA="avahi-${component}.pc" uninstall-pkgconfigDATA DESTDIR="${pkgdir}"
		rm -rf "${pkgdir}/usr/include/avahi-${component}"
	done

	rm -rf "${pkgdir}/usr/lib/girepository-1.0" "${pkgdir}/usr/share/gir-1.0"
	rm -rf "${pkgdir}"/etc/avahi/services/* "${pkgdir}/var"

	install -v -Dm644 "${srcdir}/avahi.sysusers" "${pkgdir}/usr/lib/sysusers.d/avahi.conf"
}

sha512sums=('bae5a1e9204aca90b90e7fd223d19e809e3514d03ba5fa2da1e55bf1d72d3d3b98567f357900c36393613dc17dc98e15ff3ebf0f226f2f6b9766e592452a6ce7'
            'ac999de61e2faa91faf015c68d9c7e8646b244899cb34b0518cf0e31a2de06dee655fd9f584bffa1850f8276242ed86571fafb1c7140810e0f70bbc957bbb2c9')

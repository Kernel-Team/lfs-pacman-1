pkgname=bash
_basever=4.4.12
#_patchlevel=012
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('x86_64')
url="https://www.gnu.org/software/bash/"
license=('GPL')
#depends=()
source=("https://ftp.gnu.org/gnu/bash/bash-${_basever}.tar.gz"
	"bash-config.patch"
	"bash.bashrc"
	"bash_logout"
	"bashrc"
	"profile")

if [ ! -z ${_patchlevel} ] && [ ${_patchlevel} -gt 0 ]
then
	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-${_basever}-patches/bash${_basever//.}-$(printf "%03d" ${_p}))
	done
fi

prepare() {
	cd "${srcdir}/bash-${_basever}"

	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		patch -p0 -i ../bash${_basever//.}-$(printf "%03d" ${_p})
	done

	patch -Np1 -i "${srcdir}/bash-config.patch"
}

build() {
	cd "${srcdir}/bash-${_basever}"

	./configure --prefix=/usr \
		--without-bash-malloc \
		--with-installed-readline

	make
}

package() {
	cd "${srcdir}/bash-${_basever}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/bin/bashbug" "${pkgdir}/usr/share/man/man1/bashbug.1"

	ln -sfv bash "${pkgdir}/usr/bin/sh"
	ln -sfv bash.1 "${pkgdir}/usr/share/man/man1/sh.1"

	install -v -dm755 "${pkgdir}/etc/skel"

	install -v -m644 "${srcdir}/bash.bashrc" "${pkgdir}/etc/bash.bashrc"

	install -v -m644 "${srcdir}/bash_logout" "${pkgdir}/etc/skel/.bash_logout"
	install -v -m644 "${srcdir}/bashrc" "${pkgdir}/etc/skel/.bashrc"
	install -v -m644 "${srcdir}/profile" "${pkgdir}/etc/skel/.profile"

	sed -i "s#BUILD_DIR = .*#BUILD_DIR = /usr#g" "${pkgdir}/usr/lib/bash/Makefile.inc"
}

sha512sums=('dee9d83319b0b8c0201c71d6b5f60c1eebfcec76d856a167a84a374d2296de7dcf4c4960515f888251bbf8feb0f8e1602f0061005346a0f48efb4297a2a87834'
            'e244b388f1ecb9bc86ed6a53a4aca0aadfea4a19f9b40e4848b8d04e41b9c4c808ad5496d01c93a8b63f4d5345ee1093c7064f1b5107d426b2b27aecc403e0d3'
            '22a0383de775db663bf06445c0613b4ccb36af3149a3b3b1c8a2ed8727dfe35d84a2aaefc5c68807f084d00f238f7d3df88554e909657e321c16151a47c9e366'
            '849d2370570cac4446ebd94594d0de98f69781156a0a4f6d0f480fe6d5ab61b1e9459ade247de7ca76d9d9f668dec2abd81a2c1ac75ccadbe639d45706883b67'
            'c1cba90698d09bb7b20e0aedc6aa6cdcbd66b239e41c3a8c33456d56b91980736ed2acab40db98a215f9cc9b7ba66709a637760b9696f40b215bbbd5c11d8561'
            '1e0aa37f785d0beb3172e47a6a38887e6fdbbfbeb4f0a8033e44b5f02e9dde627195df64c503cbd644cb85c2681daeaf820b2a0d3dbafebf08e8683a39e1df8d')

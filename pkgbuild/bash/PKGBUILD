pkgname=bash
_basever=4.4
_patchlevel=012
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('x86_64')
url="https://www.gnu.org/software/bash/"
license=('GPL')
groups=('core')
#depends=()
source=("https://ftp.gnu.org/gnu/bash/bash-${_basever}.tar.gz"
	"bash-config.patch"
	"bash.bashrc"
	"bash_logout"
	"bashrc"
	"profile")

if [ ${_patchlevel} -gt 0 ]
then
	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-${_basever}-patches/bash${_basever//.}-$(printf "%03d" ${_p}))
	done
fi

prepare() {
	cd "${srcdir}/bash-${_basever}"

	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		patch -p0 -i ../bash${_basever//.}-$(printf "%03d" ${_p})
	done

	patch -Np1 -i "${srcdir}/bash-config.patch"
}

build() {
	cd "${srcdir}/bash-${_basever}"

	./configure --prefix=/usr \
		--bindir=/bin \
		--without-bash-malloc \
		--with-installed-readline

	make
}

package() {
	cd "${srcdir}/bash-${_basever}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/bin/bashbug" "${pkgdir}/usr/share/man/man1/bashbug.1"

	ln -sfv bash "${pkgdir}/bin/sh"
	ln -sfv bash.1 "${pkgdir}/usr/share/man/man1/sh.1"

	install -v -dm755 "${pkgdir}/etc/skel"

	install -v -m644 "${srcdir}/bash.bashrc" "${pkgdir}/etc/bash.bashrc"

	install -v -m644 "${srcdir}/bash_logout" "${pkgdir}/etc/skel/.bash_logout"
	install -v -m644 "${srcdir}/bashrc" "${pkgdir}/etc/skel/.bashrc"
	install -v -m644 "${srcdir}/profile" "${pkgdir}/etc/skel/.profile"
}

md5sums=('148888a7c95ac23705559b6f477dfe25'
         '864a0a8e35abc881d658ff28f49c36a2'
         '54e0b7001446904d921cb125c70f653e'
         '6013ff27ef4f8e5bbd7378d0545a1822'
         '913a26e6789646447b8f8d3f1e9472ce'
         '0d2879cbe563c60a90ee16ea8a4e6129'
         '817d01a6c0af6f79308a8b7b649e53d8'
         '765e14cff12c7284009772e8e24f2fe0'
         '49e7da93bf07f510a2eb6bb43ac3e5a2'
         '4557d674ab5831a5fa98052ab19edaf4'
         'cce96dd77cdd1d293beec10848f6cbb5'
         'd3379f8d8abce5c6ee338f931ad008d5'
         'ec38c76ca439ca7f9c178e9baede84fc'
         'e0ba18c1e3b94f905da9b5bf9d38b58b'
         'e952d4f44e612048930c559d90eb99bb'
         '57b5b35955d68f9a09dbef6b86d2c782'
         'cc896e1fa696b93ded568e557e2392d5'
         'fa47fbfa56fb7e9e5367f19a9df5fc9e')

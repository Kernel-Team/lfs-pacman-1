pkgname=beignet
pkgver=1.3.2
pkgrel=1
pkgdesc="An open source OpenCL implementation for Intel IvyBridge+ iGPUs"
arch=('x86_64')
url="https://01.org/beignet"
license=('LGPL')
#depends=()
source=("https://01.org/sites/default/files/beignet-${pkgver}-source.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLIB_INSTALL_DIR=/usr/lib \
		-DBUILD_EXAMPLES=OFF \
		"${srcdir}/Beignet-${pkgver}-Source"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	find "${pkgdir}/usr/include/CL" ! -name "cl_intel.h" -type f -delete
}

sha512sums=('ed09a895c2f7e9b6e18687a899f1303dfda95e505c3c2d54bf7598cc4eba6c2fb5926589b19dd294384a4ff3f832c43235b1f03f8b7fd295cd558e78bda1400a')

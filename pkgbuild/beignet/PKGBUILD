pkgname=beignet
pkgver=1.3.1
pkgrel=1
pkgdesc="An open source OpenCL implementation for Intel IvyBridge+ iGPUs"
arch=('x86_64')
url="https://01.org/beignet"
license=('LGPL')
#depends=()
source=("https://01.org/sites/default/files/beignet-${pkgver}-source.tar.gz"
	"beignet-llvm40.patch")

prepare() {
	cd "${srcdir}/Beignet-${pkgver}-Source"

	patch -Np1 -i "${srcdir}/beignet-llvm40.patch"

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLIB_INSTALL_DIR=/usr/lib \
		-DBUILD_EXAMPLES=OFF \
		"${srcdir}/Beignet-${pkgver}-Source"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	find "${pkgdir}/usr/include/CL" ! -name "cl_intel.h" -type f -delete
}

sha512sums=('727c8ce68dc39672aa37370ebf2748955925a7cfd58c970c9a641706b45cc31d4a7d3eeebc366ed32ef69add5dd36189814454e5b31f12a6f09ea1a51c810338'
            '2b49ef2464ced849dad6b55da0ca5fa0105961ade3f1403442735b28d34fe1e9bb1b6a7b11294d519ee86b2ee2d1ca4af55fad4c1d235daa79e64ab8cd4b1a4b')

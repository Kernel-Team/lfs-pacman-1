pkgname=bind
_pkgver=9.12.1
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="The ISC DNS Server"
arch=('x86_64')
url="https://www.isc.org/software/bind/"
license=('custom:ISC')
#depends=()
options=(emptydirs)
install=bind.install
source=("ftp://ftp.isc.org/isc/bind/${_pkgver}/bind-${_pkgver}.tar.gz"
	"db.127"
	"db.local"
	"db.root"
	"named.conf"
	"named.service"
	"named.sysusers"
	"named.tmpfiles")

build() {
	cd "${srcdir}/bind-${_pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc/bind \
		--localstatedir=/var \
		--enable-seccomp \
		--without-libjson \
		--with-openssl=/usr \
		--with-gssapi=/usr \
		--with-libtool

	make -j1
}

package() {
	cd "${srcdir}/bind-${_pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/var/run"

	install -v -dm775 "${pkgdir}/var/cache/bind" "${pkgdir}/var/lib/bind"

	install -v -Dm664 "${srcdir}/named.conf" "${pkgdir}/etc/bind/named.conf"

	install -v -Dm644 "${srcdir}/db.127" "${pkgdir}/etc/bind/db.127"
	install -v -Dm644 "${srcdir}/db.local" "${pkgdir}/etc/bind/db.local"
	install -v -Dm644 "${srcdir}/db.root" "${pkgdir}/etc/bind/db.root"

	chown -v root:20 "${pkgdir}/etc/bind" "${pkgdir}/etc/bind/named.conf"
	chown -v root:20 "${pkgdir}/var/lib/bind" "${pkgdir}/var/cache/bind"

	chmod 2755 "${pkgdir}/etc/bind"

	install -v -Dm644 "${srcdir}/named.service" "${pkgdir}/usr/lib/systemd/system/named.service"
	install -v -Dm644 "${srcdir}/named.sysusers" "${pkgdir}/usr/lib/sysusers.d/named.conf"
	install -v -Dm644 "${srcdir}/named.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/named.conf"
}

sha512sums=('701e34a0a3313568c8e5cf76668fefc2a1fceb2ade0de201b8f678401569f1c622c3d1657a71772fa3298efc8b3cdefe974d98432c0087e8b45af3d50a4262fb'
            '97f3a47efc8a27511ad579cda326f443eec630649c23a7da4ef9b3ed060e036ba358cf50bec4e6656346c01047e1ecd494f741147e26d9d8620fff4980fb6ee5'
            '5daebe2c3d0dd48f4c47670a2be063dbe70e7a4c2b6bd4146f67845a701637fc2f612386cbab31654a904dcd9eb0105227e20698c40841de47aa777152ae38b2'
            'ed2aa57cd5a07f3d69645c6e6d3132cbd2a6932457874a7d8430d575245c71da966da770e52b267b03c18acf439c4b22116f6893a66956b9f9bf4c78886e19e6'
            '2d3599daf34dff2110dc7151e1b6b787bee571b394ddacc43f3e080b5f563840beb26f5f1ad39108f0f033ae5abf766f1e4a46c3488d39c8aa3bafdf32af747d'
            'c335ca1cee08dceac6384943f5203b84a406a3a5cdc39f4a09af8161c99925c46be94e91896bb7c39353bdb94a9e44c548cbc05afecfa07690f65e3a81a2ebda'
            '1275979d3747046c71761fcb47c2f38814daac7fa5472522ca8fd5c53e5954c68cd1c28c46534e53e45600a4fbcf8e28f156ecd1750d12eaa97e8ba3f27b80e4'
            'c344fd4949eb80114d0c157517ece3f4b9505b94e3de00489d4b2b23beb74612f38cf3443f2f3b98fe01d98ad1842f45ff1d1826d67937e72e4629bbdf683a59')

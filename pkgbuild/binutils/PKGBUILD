pkgname=binutils
pkgver=2.31.1
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
#depends=()
options=(staticlibs)
source=("https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.xz")

prepare() {
	rm -rf "${srcdir}/src" "${srcdir}/build"

	ln -sfv "binutils-${pkgver}" "${srcdir}/src"

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CC="gcc"
	export CXX="g++"

	../src/configure \
		--prefix=/usr \
		--with-lib-path="/usr/lib:/usr/local/lib" \
		--with-pkgversion="Krejzi ${pkgver}" \
		--enable-deterministic-archives \
		--enable-gold \
		--enable-ld=default \
		--enable-plugins \
		--enable-relro \
		--enable-shared \
		--enable-targets=x86_64-pep \
		--enable-threads \
		--disable-werror \
		--with-pic \
		--with-system-zlib

	unset CC CXX

	make configure-host
	make tooldir=/usr
}

package() {
	cd "${srcdir}/build"

	make tooldir=/usr install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/lib{bfd,opcodes}.so
	rm -rf "${pkgdir}/usr/lib64"
	rm -rf "${pkgdir}"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}.1
}

sha512sums=('0fca326feb1d5f5fe505a827b20237fe3ec9c13eaf7ec7e35847fd71184f605ba1cefe1314b1b8f8a29c0aa9d88162849ee1c1a3e70c2f7407d88339b17edb30')

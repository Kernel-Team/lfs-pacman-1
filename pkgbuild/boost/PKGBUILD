pkgname=boost
pkgver=1.64.0
_boostver=${pkgver//./_}
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="http://www.boost.org/"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/boost/boost_${_boostver}.tar.bz2")

build() {
	cd "${srcdir}/boost_${_boostver}"

	./bootstrap.sh --with-toolset=clang --with-icu --with-python=/usr/bin/python2

	./b2 stage threading=multi variant=release link=shared runtime-link=shared toolset=clang \
		cflags="${CFLAGS}" cxxflags="${CXXFLAGS}" ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/boost_${_boostver}"

	./b2 install threading=multi variant=release link=shared runtime-link=shared toolset=clang \
		cflags="${CFLAGS}" cxxflags="${CXXFLAGS}" --prefix="${pkgdir}/usr" ${MAKEFLAGS}
}

sha512sums=('68477f148f61be617552ef48559c5c2cb90d42264cabd6d5e87215d0d5024b48fca27c4afcfc1f77e490c6220d44fb1abdf0a53703867a2e4132c2857f69fedf')

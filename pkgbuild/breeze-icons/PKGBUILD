pkgname=breeze-icons
pkgver=5.37.0
pkgrel=1
pkgdesc="Breeze icon themes"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
#depends=()
options=(docs)
source=("https://download.kde.org/stable/frameworks/${pkgver:0:4}/breeze-icons-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DKDE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DBUILD_TESTING=OFF \
		"${srcdir}/breeze-icons-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('f026bb2ae5626faf082d6f1220023551ca8b1e2665a29707cf26176d33449fd9be0954ee1b01248bc1618211541ea62e91b5423450dbc87eced847a7d8839861')

pkgbase=cairo
pkgname=('cairo' 'lib32-cairo')
pkgver=1.15.8
pkgrel=1
arch=('x86_64')
url="https://cairographics.org/"
license=('LGPL' 'MPL')
#depends=()
source=("https://www.cairographics.org/snapshots/cairo-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/cairo-${pkgver}"

	sed -i "/am__append/s#boilerplate test perf#boilerplate perf#g" Makefile.in

	cp -a "${srcdir}"/cairo{,32}-${pkgver}
}

build() {
	cd "${srcdir}/cairo-${pkgver}"

	./configure --prefix=/usr --enable-tee

	cd "${srcdir}/cairo32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--enable-tee

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/cairo-${pkgver}"
	make -C "${srcdir}/cairo32-${pkgver}"
}

package_cairo() {
	pkgdesc="Cairo vector graphics library"

	cd "${srcdir}/cairo-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-cairo() {
	pkgdesc="Cairo vector graphics library (32 bit)"

	cd "${srcdir}/cairo32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('5af1eebf432201dae0efaa5b6766b151d8273ea00dae48e104d56477005b4d423d64b5d11c512736a4cb076632fb2a572ec35becd922825a68d933bb5ff96ca1')

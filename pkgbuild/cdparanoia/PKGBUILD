pkgname=cdparanoia
pkgver=10.2
pkgrel=1
pkgdesc="Compact Disc Digital Audio extraction tool"
arch=('x86_64')
url="https://www.xiph.org/paranoia/"
license=('GPL')
#depends=()
source=("http://downloads.xiph.org/releases/cdparanoia/cdparanoia-III-${pkgver}.src.tgz"
	"cdparanoia-fixes.patch")

prepare() {
	cd "${srcdir}/cdparanoia-III-${pkgver}"

	patch -Np1 -i "${srcdir}/cdparanoia-fixes.patch"

	autoreconf -fi
}

build() {
	cd "${srcdir}/cdparanoia-III-${pkgver}"

	CFLAGS+=" -fPIC"
	CXXFLAGS+=" -fPIC"

	./configure --prefix=/usr \
		--libdir='${prefix}/lib' \
		--mandir='${prefix}/share/man'

	make
}

package() {
	cd "${srcdir}/cdparanoia-III-${pkgver}"

	make install prefix="${pkgdir}/usr"
}

sha512sums=('4ab0a0f5ef44d56c1af72d1fc1035566a1a89c4eeddb9e8baea675fe51c06138d913342afc8bed167d9fa55672fa25a2763ce21f7e24c1232e4739aff20733a7'
            '753a4ab3aa4f65f890638e8fbda88ebe72a008ab062bd7626d4d3df2e4081fcce381ae979a9b56daca986f5eab6b68baa6a5ede3995e550cbda290c6888657ad')

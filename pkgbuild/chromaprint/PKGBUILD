pkgname=chromaprint
pkgver=1.4.3
pkgrel=1
pkgdesc="Library that implements a custom algorithm for extracting fingerprints from any audio source"
arch=('x86_64')
url="https://acoustid.org/chromaprint"
license=('LGPL')
#depends=()
source=("https://github.com/acoustid/chromaprint/releases/download/v${pkgver}/chromaprint-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DAUDIO_PROCESSOR_LIB=no \
		-DFFT_LIB=fftw3f \
		"${srcdir}/chromaprint-v${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('101360bd643d2410fafcdd4aef8fe4c716ec728f0e4b9b3a62ba125fe010b0315e55a1839a2ca8c29d9abfbf8171560b23d68b732e75632ce8f4a4698f77ea72')

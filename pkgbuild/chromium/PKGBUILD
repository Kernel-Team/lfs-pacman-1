pkgname=chromium
pkgver=62.0.3202.89
_launcher_ver=5
pkgrel=1
pkgdesc="A web browser built for speed, simplicity, and security"
arch=('x86_64')
url="https://www.chromium.org/Home"
license=('BSD')
#depends=()
source=("https://commondatastorage.googleapis.com/chromium-browser-official/chromium-${pkgver}.tar.xz"
	"chromium-launcher-${_launcher_ver}.tar.gz::https://github.com/foutrelis/chromium-launcher/archive/v${_launcher_ver}.tar.gz"
	"chromium.desktop"
	"breakpad-use-ucontext_t.patch"
	"crc32c-string-view-check.patch"
	"chromium-gn-bootstrap-r17.patch"
	"chromium-vaapi-r14.patch")

_system_libs=(
  flac
  harfbuzz-ng
  libdrm
  libjpeg
  libpng
  libwebp
  libxml
  libxslt
  opus
  yasm
  zlib
)

# icu
# ffmpeg
# freetype
# libvpx

_config=(
	'is_debug=false'
	'enable_nacl=false'
	'exclude_unwind_tables=true'
	'enable_hangout_services_extension=true'
	'enable_widevine=false'
	'use_cups=true'
	'use_gconf=false'
	'use_gnome_keyring=false'
	'use_gtk3=true'
	'use_kerberos=true'
	'use_pulseaudio=true'
	'fieldtrial_testing_like_official_build=true'
	'is_clang=true'
	'clang_use_chrome_plugins=false'
	'use_gold=false'
	'use_sysroot=false'
	'linux_use_bundled_binutils=false'
	'use_custom_libcxx=false'
	'proprietary_codecs=true'
	'ffmpeg_branding="Chrome"'
	'treat_warnings_as_errors=false'
	'fatal_linker_warnings=false'
	'use_vaapi=true'
)

if [ "$(hostname)" == "krejzi" ]
then
	. /media/ntfs/chromium-api.sh

	_config+=(
		"google_api_key=\"${_google_api_key}\""
		"google_default_client_id=\"${_google_default_client_id}\""
		"google_default_client_secret=\"${_google_default_client_secret}\""
	)
fi

prepare() {
	cd "${srcdir}/chromium-${pkgver}"

	for _lib in ${_system_libs[@]} libjpeg_turbo
	do
		find -type f -path "*third_party/${_lib}/*"        \
			\! -path "*third_party/${_lib}/chromium/*" \
			\! -path "*third_party/${_lib}/google/*"   \
			\! -path "*base/third_party/icu/*"         \
			\! -regex '.*\.\(gn\|gni\|isolate\|py\)'   \
			-delete
	done

	python build/linux/unbundle/replace_gn_files.py --system-libraries ${_system_libs[@]}

	install -v -dm755 third_party/node/linux/node-linux-x64/bin
	ln -sfv /usr/bin/node third_party/node/linux/node-linux-x64/bin/node

	install -v -dm755 third_party/llvm-build/Release+Asserts/bin
	ln -sfv /usr/bin/clang third_party/llvm-build/Release+Asserts/bin/clang
	ln -sfv /usr/bin/clang++ third_party/llvm-build/Release+Asserts/bin/clang++
	ln -sfv /usr/bin/llvm-ar third_party/llvm-build/Release+Asserts/bin/llvm-ar

	sed -i "/-Wno-unused-lambda-capture/d" build/config/compiler/BUILD.gn
	sed -i "/-Wno-enum-compare-switch/d" build/config/compiler/BUILD.gn

	patch -Np1 -i "${srcdir}/breakpad-use-ucontext_t.patch"
	patch -Np1 -i "${srcdir}/chromium-gn-bootstrap-r17.patch"

	patch -Np1 -d third_party/crc32c/src -i "${srcdir}/crc32c-string-view-check.patch"

	# VA-API patch
	patch -Np1 -i "${srcdir}/chromium-vaapi-r14.patch"

	# Fix paths.
	sed -e 's|i386-linux-gnu/||g' \
		-e 's|x86_64-linux-gnu/||g' \
		-e 's|/usr/lib/va/drivers|/usr/lib/dri|g' \
		-e 's|/usr/lib64/va/drivers|/usr/lib/dri|g' \
		-i content/common/sandbox_linux/bpf_gpu_policy_linux.cc

}

build() {
	cd "${srcdir}/chromium-launcher-${_launcher_ver}"

	make

	cd "${srcdir}/chromium-${pkgver}"

	export TMPDIR="${srcdir}/temp"
	install -v -dm755 "${TMPDIR}"

	python tools/gn/bootstrap/bootstrap.py --gn-gen-args "${_config[*]}"
	out/Release/gn gen out/Release --args="${_config[*]}"

	ninja -C out/Release chrome chrome_sandbox chromedriver
}

package() {
	cd "${srcdir}/chromium-launcher-${_launcher_ver}"

	make install PREFIX=/usr DESTDIR="${pkgdir}"

	cd "${srcdir}/chromium-${pkgver}"

	install -v -Dm755 out/Release/chrome "${pkgdir}/usr/lib/chromium/chromium"
	install -v -Dm4755 out/Release/chrome_sandbox "${pkgdir}/usr/lib/chromium/chrome-sandbox"

	install -v -Dm755 out/Release/chromedriver "${pkgdir}/usr/lib/chromium/chromedriver"
	ln -sfv ../lib/chromium/chromedriver "${pkgdir}/usr/bin/chromedriver"

	install -v -Dm644 out/Release/icudtl.dat "${pkgdir}/usr/lib/chromium/icudtl.dat"

	cp -a out/Release/*.pak out/Release/*.bin out/Release/locales "${pkgdir}/usr/lib/chromium"

	install -v -Dm644 "${srcdir}/chromium.desktop" "${pkgdir}/usr/share/applications/chromium.desktop"

	for size in 16 32
	do
		install -v -Dm644 "chrome/app/theme/default_100_percent/chromium/product_logo_${size}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
	done

	for size in 22 24 48 64 128 256
	do
		install -v -Dm644 "chrome/app/theme/chromium/product_logo_${size}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
	done

	install -v -Dm644 out/Release/chrome.1 "${pkgdir}/usr/share/man/man1/chromium.1"
}

sha512sums=('11ed71d2c9ebf82c9b704111d85d01c5600fc1caeb66e0dfaca172b20bb357817d717b3bcc13f704072b77550fa7fa74f990725b1d7d85d4d2b82cc1baec9578'
            '52fa910e2fff7197d670be41e925c71579f10bcdc879248b38876581dc7e4f384262a0e1d3714ed7b6382d680c56e53076b0be6fea679724360144395bf3973a'
            '41175d53e7dba3405ff154d52f2069c7685ecb7ce2587b982bc24e30951485686586848eb94d5033a98ddde6dc1fd13cc5614a95e8758133d1a579a5f6b9221d'
            '776ed0ee0020e3e0559d219447f2cad66da1ca176abc2e204bfcaad5141e115336290fe7d210e7e1d02b163a8039b8678f4b33edcd1874ecfddaf5c8172e9e1e'
            '61dab6f7d2e71a06c8c073fef061c93ff2e8d72aa4036db5c9f475f4a0e8e0167bce552b56c778a12b4aeadc43d404b2c67052c7ae9ca4f3009010276c6874cb'
            '28812a8bd6d591831a3bbd749f1d7c6dd434eb0237970d4e8ea8cfd48fa8efb4d98906eca3d175218fe62c50bcb0b6472b2d7dfdde73d5641cd3fde1d9dc5f3b'
            '673b341965ad0403adbdc376dc3025e1e7aba6d47201514691e01f1e36a52337ba9f1f5c188d9b6e8584cb9556d8609c75b592c75fc79727aa441c09958676ac')

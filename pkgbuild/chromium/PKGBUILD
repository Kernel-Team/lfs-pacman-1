pkgname=chromium
pkgver=64.0.3282.113
_launcher_ver=5
pkgrel=1
pkgdesc="A web browser built for speed, simplicity, and security"
arch=('x86_64')
url="https://www.chromium.org/Home"
license=('BSD')
#depends=()
source=("https://commondatastorage.googleapis.com/chromium-browser-official/chromium-${pkgver}.tar.xz"
	"https://github.com/foutrelis/chromium-launcher/archive/v${_launcher_ver}/chromium-launcher-${_launcher_ver}.tar.gz"
	"chromium-${pkgver}.txt::https://chromium.googlesource.com/chromium/src.git/+/${pkgver}?format=TEXT"
	"chromium.desktop"
	"chromium-exclude_unwind_tables.patch"
	"chromium-clang-r2.patch"
	"chromium-memcpy-r0.patch"
	"chromium-vaapi-r16.patch"
	"chromium-use-fromUTF8-for-UnicodeString-construction.patch"
	"chromium-omnibox-unescape-fragment.patch"
	"chromium-skia-harmony.patch")

_system_libs=(
  flac
  icu
  libdrm
  libjpeg
  libwebp
  libxslt
  opus
  yasm
  zlib
)

# harfbuzz-ng added down below
# ffmpeg
# freetype added down below
# libpng
# libvpx
# libxml

_config=(
	'custom_toolchain="//build/toolchain/linux/unbundle:default"'
	'host_toolchain="//build/toolchain/linux/unbundle:default"'
	'is_debug=false'
	'enable_nacl=false'
	'exclude_unwind_tables=true'
	'enable_hangout_services_extension=true'
	'enable_widevine=true'
	'use_cups=true'
	'use_gconf=false'
	'use_gnome_keyring=false'
	'use_gtk3=true'
	'use_kerberos=true'
	'use_pulseaudio=true'
	'fieldtrial_testing_like_official_build=true'
	'is_clang=true'
	'clang_use_chrome_plugins=false'
	'use_gold=false'
	'use_lld=false'
	'use_sysroot=false'
	'linux_use_bundled_binutils=false'
	'use_custom_libcxx=false'
	'proprietary_codecs=true'
	'ffmpeg_branding="Chrome"'
	'treat_warnings_as_errors=false'
	'fatal_linker_warnings=false'
	'use_vaapi=true'
	'use_system_freetype=true'
	'use_system_harfbuzz=true'
)

if [ "$(hostname)" == "krejzi" ]
then
	. /media/ntfs/chromium-api.sh

	_config+=(
		"google_api_key=\"${_google_api_key}\""
		"google_default_client_id=\"${_google_default_client_id}\""
		"google_default_client_secret=\"${_google_default_client_secret}\""
	)
fi

prepare() {
	cd "${srcdir}/chromium-${pkgver}"

	# https://crbug.com/710701
	local _chrome_build_hash=$(base64 -d "${srcdir}/chromium-${pkgver}.txt" | grep -Po '^parent \K[0-9a-f]{40}$')
	if [ -z $_chrome_build_hash ]
	then
		error "Unable to fetch Chrome build hash"
		return 1
	fi
	echo "LASTCHANGE=$_chrome_build_hash-" > build/util/LASTCHANGE

	# https://chromium-review.googlesource.com/c/chromium/src/+/712575
	patch -Np1 -i "${srcdir}/chromium-exclude_unwind_tables.patch"

	# https://crbug.com/772655
	patch -Np1 -i "${srcdir}/chromium-use-fromUTF8-for-UnicodeString-construction.patch"

	# https://crbug.com/789163
	patch -Np1 -i "${srcdir}/chromium-omnibox-unescape-fragment.patch"

	# https://crbug.com/skia/6663#c10
	patch -Np4 -i "${srcdir}/chromium-skia-harmony.patch"

	# Fixes from Gentoo
	patch -Np1 -i "${srcdir}/chromium-clang-r2.patch"
	patch -Np1 -i "${srcdir}/chromium-memcpy-r0.patch"

	sed -i "/-Wno-enum-compare-switch/d" build/config/compiler/BUILD.gn
	sed -i "/-Wno-null-pointer-arithmetic/d" build/config/compiler/BUILD.gn
	sed -i "/-Wno-tautological-unsigned-zero-compare/d" build/config/compiler/BUILD.gn
	sed -i "/-Wno-tautological-constant-compare/d" build/config/compiler/BUILD.gn

	# Just annoying
	sed -i "/-Wtautological-unsigned-enum-zero-compare/d" build/config/compiler/BUILD.gn
	sed -i "/-Wtautological-type-limit-compare/d" build/config/compiler/BUILD.gn

	# WineVine CDM
	sed -i '/WIDEVINE_CDM_AVAILABLE/a#define WIDEVINE_CDM_VERSION_STRING "Pinkie Pie"' \
		third_party/widevine/cdm/stub/widevine_cdm_version.h

	# VA-API patch
	patch -Np1 -i "${srcdir}/chromium-vaapi-r16.patch"

	install -v -dm755 third_party/node/linux/node-linux-x64/bin
	ln -sfv /usr/bin/node third_party/node/linux/node-linux-x64/bin/node

	for _lib in ${_system_libs[@]} libjpeg_turbo freetype harfbuzz-ng
	do
		find -type f -path "*third_party/${_lib}/*"        \
			\! -path "*third_party/${_lib}/chromium/*" \
			\! -path "*third_party/${_lib}/google/*"   \
			\! -path './base/third_party/icu/*' \
			\! -path './third_party/freetype/src/src/psnames/pstables.h' \
			\! -path './third_party/yasm/run_yasm.py' \
			\! -regex '.*\.\(gn\|gni\|isolate\)' \
			-delete
	done

	python build/linux/unbundle/replace_gn_files.py --system-libraries ${_system_libs[@]}
}

build() {
	cd "${srcdir}/chromium-launcher-${_launcher_ver}"

	make

	cd "${srcdir}/chromium-${pkgver}"

	export TMPDIR="${srcdir}/temp"
	install -v -dm755 "${TMPDIR}"

	export AR=ar
	export NM=nm

	python tools/gn/bootstrap/bootstrap.py --gn-gen-args "${_config[*]}"
	out/Release/gn gen out/Release --args="${_config[*]}"

	ninja -C out/Release chrome chrome_sandbox chromedriver widevinecdmadapter
}

package() {
	cd "${srcdir}/chromium-launcher-${_launcher_ver}"

	make install PREFIX=/usr DESTDIR="${pkgdir}"

	cd "${srcdir}/chromium-${pkgver}"

	install -v -Dm755 out/Release/chrome "${pkgdir}/usr/lib/chromium/chromium"
	install -v -Dm4755 out/Release/chrome_sandbox "${pkgdir}/usr/lib/chromium/chrome-sandbox"

	install -v -Dm755 out/Release/chromedriver "${pkgdir}/usr/lib/chromium/chromedriver"
	install -v -Dm755 out/Release/libwidevinecdmadapter.so "${pkgdir}/usr/lib/chromium/libwidevinecdmadapter.so"

	ln -sfv ../lib/chromium/chromedriver "${pkgdir}/usr/bin/chromedriver"

	if [ -e "out/Release/icudtl.dat" ]
	then
		install -v -Dm644 out/Release/icudtl.dat "${pkgdir}/usr/lib/chromium/icudtl.dat"
	fi

	cp -a out/Release/*.pak out/Release/*.bin "${pkgdir}/usr/lib/chromium"

	install -v -dm755 "${pkgdir}/usr/lib/chromium/locales"
	cp -a out/Release/locales/*.pak "${pkgdir}/usr/lib/chromium/locales"

	install -v -Dm644 chrome/installer/linux/common/desktop.template \
		"${pkgdir}/usr/share/applications/chromium.desktop"
	install -v -Dm644 chrome/app/resources/manpage.1.in \
		"${pkgdir}/usr/share/man/man1/chromium.1"

	sed -i  -e "s#@@MENUNAME@@#Chromium#g" \
		-e "s#@@PACKAGE@@#chromium#g" \
		-e "s#@@USR_BIN_SYMLINK_NAME@@#chromium#g" \
		"${pkgdir}/usr/share/applications/chromium.desktop" \
		"${pkgdir}/usr/share/man/man1/chromium.1"

	for size in 16 32
	do
		install -v -Dm644 "chrome/app/theme/default_100_percent/chromium/product_logo_${size}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
	done

	for size in 22 24 48 64 128 256
	do
		install -v -Dm644 "chrome/app/theme/chromium/product_logo_${size}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
	done
}

sha512sums=('b9e8a82ac69b0a87ed50e2b1840f118fe73937ce824aa6acc9770f2000873dfe33792da95663e3db5fc912a5df6d662b1cbdcfaf35233ec645083e9ef86050ed'
            '52fa910e2fff7197d670be41e925c71579f10bcdc879248b38876581dc7e4f384262a0e1d3714ed7b6382d680c56e53076b0be6fea679724360144395bf3973a'
            '14722da587f9bd4651c07870fea8ec29a7559ad8474091d560c21ce91bd5168562b3812c925f577f67563f1a8347dc04eea9704dfa776e58b3c2b98f94ede762'
            '41175d53e7dba3405ff154d52f2069c7685ecb7ce2587b982bc24e30951485686586848eb94d5033a98ddde6dc1fd13cc5614a95e8758133d1a579a5f6b9221d'
            '5275a9ad964152dced1f542eabb6116cdeeadc2391abb3788e64994a475af75350c8443f1737cf07e4b95da0c5df0b58e27552952aa5c4095b4f90a2873ede7d'
            'f5edc9b573269a7a6944e17019fa858e7162c548400eceabdabe695d7e10d3179b7593415ec3eaef226defdd1628fd8c9cc66c0b46ebe6ac1afe5035a319c379'
            '1aeeb70929acee529dea66860b42f106afe18c0f6219eb03c9c710faf8d20a997135550e289839599ec325d8f032243fd70c07d397bd89302a192c41e8c4660a'
            '688a3f250a6f1574a50ce58bd05b6a499f20b1b61b1b11b208b12a8fbcd6be083f86221f284cf100fd18b26876e72ecfd37aa5ac49d94f4a7920185948731b85'
            '2bbf63c922c24fedd429d81ba8b433d06870a8984c45dff22e6be5d161385a5650015906fab28fda31438ab2e3c6635723be52b860e5d57132f67570a8020f1f'
            '66cca4807f3e6c435aa09feedc7de1db969403a54bb0bcb20989172c5e5dc1e79842d6ef6e853ac1b7fd4c8dec9696d56190376c536ba0770597a74a9acde882'
            '5a0c6e9a8a66709afd7bfc50b4f1ee9533a30da67da06667835907804ff03c4d1dc9b93e513ab244a7ae532752eb2864c63c68ad74bacfcd89a1694d95363fdd')

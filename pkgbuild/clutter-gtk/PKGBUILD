pkgname=clutter-gtk
pkgver=1.8.4
pkgrel=1
pkgdesc="Clutter integration with GTK+"
arch=('x86_64')
url="https://git.gnome.org/browse/clutter-gtk/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/clutter-gtk/${pkgver:0:3}/clutter-gtk-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/clutter-gtk-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('810f879a56cd71f1e9c4678ef704a997ee652d052cfea56b3ed9478f7a813a35dd2fdcded028ce17411ae02da52fa31e0fb8d1f4130c7b9f6d25eeb774b51ada')

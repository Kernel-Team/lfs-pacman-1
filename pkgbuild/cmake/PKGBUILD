pkgname=cmake
pkgver=3.10.0
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
url="http://www.cmake.org/"
license=('custom')
#depends=()
source=("https://www.cmake.org/files/v${pkgver:0:4}/cmake-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/cmake-${pkgver}"

	# Workaround for temp. SSL being built with SSLv3 methods
	export LDFLAGS="-L/usr/lib"

 	./bootstrap --prefix=/usr \
		--sphinx-man \
		--system-libs \
		--no-system-jsoncpp \
		--no-system-librhash \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)

	make
}

package() {
	cd "${srcdir}/cmake-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('0bf05f5aacb17007d5beb0909263e31717102b685701eef45c20408895cc9dfd54c96ebc8eee148845c8ffb1d3db687e68a58d4e08395425e14ca0413ee84765')

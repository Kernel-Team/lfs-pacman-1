pkgname=cmake
pkgver=3.11.0
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
url="http://www.cmake.org/"
license=('custom')
#depends=()
source=("https://www.cmake.org/files/v${pkgver:0:4}/cmake-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/cmake-${pkgver}"

	# Workaround for temp. SSL being built with SSLv3 methods
	export LDFLAGS="-L/usr/lib"

 	./bootstrap --prefix=/usr \
		--sphinx-man \
		--system-libs \
		--no-system-jsoncpp \
		--no-system-librhash \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)

	make
}

package() {
	cd "${srcdir}/cmake-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('03b058483d236d4f4427c93cc41af77068c243fc4b6f24aeaf2daf97af215bc664bc1b733195463af4cfc94ec70076710f425661859d752ddf3b9610adca9834')

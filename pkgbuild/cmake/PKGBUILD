pkgname=cmake
pkgver=3.19.2
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
url="http://www.cmake.org/"
license=('custom')
#depends=()
source=("https://www.cmake.org/files/v${pkgver:0:4}/cmake-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/cmake-${pkgver}"

	# Workaround for temp. SSL being built with SSLv3 methods
	export LDFLAGS="-L/usr/lib"

 	./bootstrap --prefix=/usr \
		--sphinx-man \
		--system-libs \
		--no-system-jsoncpp \
		--no-system-librhash \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)

	make
}

package() {
	cd "${srcdir}/cmake-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('d811122af89d46174b8fdb2a26e8553200d9dc7eb6385ece4e4b3e7584880f73a799813d5a81e64e956fd2574b63264fa8944d862c74a076e9d61f5e6521e91a')

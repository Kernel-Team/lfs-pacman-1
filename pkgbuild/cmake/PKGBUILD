pkgname=cmake
pkgver=3.9.1
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
url="http://www.cmake.org/"
license=('custom')
#depends=()
source=("https://www.cmake.org/files/v${pkgver:0:3}/cmake-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/cmake-${pkgver}"

	# Workaround for temp. SSL being built with SSLv3 methods
	export LDFLAGS="-L/usr/lib"

 	./bootstrap --prefix=/usr \
		--sphinx-man \
		--system-libs \
		--no-system-jsoncpp \
		--no-system-librhash \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)

	make
}

package() {
	cd "${srcdir}/cmake-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('3bdc0f08cac8decd37df8043cf8fb436d08d19444353164bad02feda441bbd5efa636836352227255829ae5360b4ef4c952b3a7635dd61f4e86b01dff44e1ac6')

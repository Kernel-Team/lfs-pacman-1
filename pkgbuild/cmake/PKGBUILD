pkgname=cmake
pkgver=3.11.2
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
url="http://www.cmake.org/"
license=('custom')
#depends=()
source=("https://www.cmake.org/files/v${pkgver:0:4}/cmake-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/cmake-${pkgver}"

	# Workaround for temp. SSL being built with SSLv3 methods
	export LDFLAGS="-L/usr/lib"

 	./bootstrap --prefix=/usr \
		--sphinx-man \
		--system-libs \
		--no-system-jsoncpp \
		--no-system-librhash \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)

	make
}

package() {
	cd "${srcdir}/cmake-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('c8ffa0885778ea942eb2533dab72da8719f67a212a33883913af047717c87368cb58e1d6c578b542d96d7dcc85d126abe81686214366c32174fd5553b399b69e')

pkgname=colord
pkgver=1.4.1
pkgrel=1
pkgdesc="System daemon for managing color devices"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/colord/"
license=('GPL2')
#depends=()
options=(emptydirs)
source=("https://www.freedesktop.org/software/colord/releases/colord-${pkgver}.tar.xz"
	"colord.sysusers")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable-bash-completion=false \
		-Denable-argyllcms-sensor=false \
		-Denable-vala=true \
		-Denable-tests=false \
		-Dwith-daemon-user="colord" \
		"${srcdir}/colord-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	install -v -Dm644 "${srcdir}/colord.sysusers" "${pkgdir}/usr/lib/sysusers.d/colord.conf"

	chown -R 71:71 "${pkgdir}/var/lib/colord"
}

sha512sums=('b861ceaeabfe8d8c31ae0197e10b160869a17617436affb45d53b37738365aaccf77c89d32b0b8bf174e63a6ba99a2d16507a4ffa1c257c21aa6a9e8967df9b6'
            '46d421c626bf135b5cb2c491f3090b15bde6b4a06e5400ccea1e6d455c3fd406364555e34bfdeafac40072d9e64c1a9d9feb07d3dbac8594e83182f5b38eecf8')

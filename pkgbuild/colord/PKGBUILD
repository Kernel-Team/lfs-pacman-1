pkgname=colord
pkgver=1.3.5
pkgrel=1
pkgdesc="System daemon for managing color devices"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/colord/"
license=('GPL2')
#depends=()
options=(emptydirs)
source=("https://www.freedesktop.org/software/colord/releases/colord-${pkgver}.tar.xz"
	"colord.sysusers")

build() {
	cd "${srcdir}/colord-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-daemon-user=colord \
		--disable-argyllcms-sensor \
		--disable-bash-completion \
		--disable-sane \
		--enable-vala \
		--with-udevrulesdir=/lib/udev/rules.d \
		--with-systemdsystemunitdir=/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/colord-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/colord.sysusers" "${pkgdir}/usr/lib/sysusers.d/colord.conf"

	chown -R 71:71 "${pkgdir}/var/lib/colord"
}

sha512sums=('a0084ad2c08ae88c8023b8bade68f6cf25e13c77a6a37ef127b2914e86b06e6a7a783f9ab3585243d975dbbe1120d5daae13bdb586aefe519bf675458683025f'
            '46d421c626bf135b5cb2c491f3090b15bde6b4a06e5400ccea1e6d455c3fd406364555e34bfdeafac40072d9e64c1a9d9feb07d3dbac8594e83182f5b38eecf8')

pkgname=colord
pkgver=1.4.4
pkgrel=1
pkgdesc="System daemon for managing color devices"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/colord/"
license=('GPL2')
#depends=()
options=(emptydirs)
source=("https://www.freedesktop.org/software/colord/releases/colord-${pkgver}.tar.xz"
	"colord.sysusers")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dbash_completion=false \
		-Dargyllcms_sensor=false \
		-Dvapi=true \
		-Dtests=false \
		-Ddaemon_user="colord" \
		"${srcdir}/colord-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	install -v -Dm644 "${srcdir}/colord.sysusers" "${pkgdir}/usr/lib/sysusers.d/colord.conf"

	chown -R 71:71 "${pkgdir}/var/lib/colord"
}

sha512sums=('0187a363c41ae5102dc3b454fcd84df83d71ac0dc32d88b33651edc1df15cbf1f47218caee3037c876e892613a2301226886a73b39759cfb709d0fef8e168628'
            '46d421c626bf135b5cb2c491f3090b15bde6b4a06e5400ccea1e6d455c3fd406364555e34bfdeafac40072d9e64c1a9d9feb07d3dbac8594e83182f5b38eecf8')

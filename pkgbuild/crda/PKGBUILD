pkgname=crda
pkgver=3.18
pkgrel=1
pkgdesc="Central Regulatory Domain Agent for wireless networks"
arch=('x86_64')
url="https://wireless.wiki.kernel.org/en/developers/Regulatory/CRDA"
license=('custom')
#depends=()
source=("https://www.kernel.org/pub/software/network/crda/crda-${pkgver}.tar.xz"
	"crda-fixes.patch"
	"set-wireless-regdom")

prepare() {
	cd "${srcdir}/crda-${pkgver}"

	patch -Np1 -i "${srcdir}/crda-fixes.patch"

	sed -i "s:-O2 -fpic:${CFLAGS} -fpic:g" Makefile
}

build() {
	cd "${srcdir}/crda-${pkgver}"

	unset CFLAGS

	make PREFIX=/usr RUNTIME_PUBKEY_ONLY=1
}

package() {
	cd "${srcdir}/crda-${pkgver}"

	make install PREFIX=/usr DESTDIR="${pkgdir}"

	echo 'ACTION=="add" SUBSYSTEM=="module", DEVPATH=="/module/cfg80211", RUN+="/usr/lib/crda/set-wireless-regdom"' >> "${pkgdir}/usr/lib/udev/rules.d/85-regulatory.rules"

	install -Dm755 "${srcdir}/set-wireless-regdom" "${pkgdir}/usr/lib/crda/set-wireless-regdom"
}

sha512sums=('57ae6309159f396448f052c127f401c2f63d47f4193e87dca231c4b7bbbd7e69b5e5666f356fc76dfc8a6ae58ffa55c3794428d6eb34d9937df77c4276036588'
            '830476fda4a8b85f14bb9b135f21e880bcac12177a74fdcd71e5ed1ad148e7dd18d121a12a9657344cae03f338b1ecf0ad441e736dce3caac4f1bd1371922cff'
            'ebe61a0ed67bf94276007facab93e95993ddf609ae35aba7e16334e38cae6ce5cb729342cacfb7517ee4e79cec20ba8ccf273379061aaa2fda6551411d2b5b57')

pkgname=crda
pkgver=3.18
pkgrel=1
pkgdesc="Central Regulatory Domain Agent for wireless networks"
arch=('x86_64')
url="https://wireless.wiki.kernel.org/en/developers/Regulatory/CRDA"
license=('custom')
#depends=()
source=("https://www.kernel.org/pub/software/network/crda/crda-${pkgver}.tar.xz"
	"crda-fixes.patch"
	"set-wireless-regdom")

prepare() {
	cd "${srcdir}/crda-${pkgver}"

	sed -i "s:-O2 -fpic:${CFLAGS} -fpic:g" Makefile
	sed -i "s:-Werror ::g" Makefile

	sed -i "s:regdb.h reglib.h reglib.c:reglib.c:g" Makefile

	patch -Np1 -i "${srcdir}/crda-fixes.patch"
}

build() {
	cd "${srcdir}/crda-${pkgver}"

	unset CFLAGS

	make LIBDIR=/lib/crda REG_BIN=/lib/crda/regulatory.bin RUNTIME_PUBKEY_DIR=/lib/crda/pubkeys RUNTIME_PUBKEY_ONLY=1 V=1
}

package() {
	cd "${srcdir}/crda-${pkgver}"

	make install LIBDIR=/lib/crda DESTDIR="${pkgdir}"

	echo 'ACTION=="add" SUBSYSTEM=="module", DEVPATH=="/module/cfg80211", RUN+="/lib/crda/set-wireless-regdom"' >> "${pkgdir}/lib/udev/rules.d/85-regulatory.rules"

	install -Dm755 "${srcdir}/set-wireless-regdom" "${pkgdir}/lib/crda/set-wireless-regdom"
}

sha512sums=('57ae6309159f396448f052c127f401c2f63d47f4193e87dca231c4b7bbbd7e69b5e5666f356fc76dfc8a6ae58ffa55c3794428d6eb34d9937df77c4276036588'
            '551d7b1877fdf0f9d5766fc8d889db88bae24b6fdb23f9c7389200a5bc36371e9931cbb59267a2410e291bdb55b3fac8e29bea573289eddb133aa79690a0f882'
            'ebe61a0ed67bf94276007facab93e95993ddf609ae35aba7e16334e38cae6ce5cb729342cacfb7517ee4e79cec20ba8ccf273379061aaa2fda6551411d2b5b57')

pkgname=cups-filters
pkgver=1.17.7
pkgrel=1
pkgdesc="OpenPrinting CUPS Filters"
arch=('x86_64')
url="https://wiki.linuxfoundation.org/openprinting/cups-filters"
license=('custom')
#depends=()
source=("https://www.openprinting.org/download/cups-filters/cups-filters-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/cups-filters-${pkgver}"

	sed -i "s:cups.service:org.cups.cupsd.service:g" utils/cups-browsed.service
}

build() {
	cd "${srcdir}/cups-filters-${pkgver}"

	export CXX="${CXX} -std=c++11"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--without-rcdir \
		--enable-auto-setup-driverless

	make
}

package() {
	cd "${srcdir}/cups-filters-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 utils/cups-browsed.service "${pkgdir}/usr/lib/systemd/system/cups-browsed.service"

	chgrp -R lp "${pkgdir}/etc/cups"
}

sha512sums=('bac46c7ed0b7409c4b920f6c981f74e73da77ca2590222ea14e80f99aa04bb19c0521b5452b38700054ea704381f3bf4b099a71bf17cdcf7faf93d2f9c8c97c7')

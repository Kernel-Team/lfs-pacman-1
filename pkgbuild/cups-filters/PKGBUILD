pkgname=cups-filters
pkgver=1.16.1
pkgrel=1
pkgdesc="OpenPrinting CUPS Filters"
arch=('x86_64')
url="https://wiki.linuxfoundation.org/openprinting/cups-filters"
license=('custom')
#depends=()
source=("https://www.openprinting.org/download/cups-filters/cups-filters-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/cups-filters-${pkgver}"

	sed -i "s:cups.service:org.cups.cupsd.service:g" utils/cups-browsed.service
}

build() {
	cd "${srcdir}/cups-filters-${pkgver}"

	export CXX="${CXX} -std=c++11"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--without-rcdir \
		--enable-auto-setup-driverless

	make
}

package() {
	cd "${srcdir}/cups-filters-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 utils/cups-browsed.service "${pkgdir}/usr/lib/systemd/system/cups-browsed.service"

	chgrp -R lp "${pkgdir}/etc/cups"
}

sha512sums=('3d3f81047a8fee965998158f5b2be570104c74f11ea4e8a489ee30d1d97246d0829a1ebc4a9b060718c6cc0eaeb94dc518a08a2c14a8cdcdfa70228c09875a15')

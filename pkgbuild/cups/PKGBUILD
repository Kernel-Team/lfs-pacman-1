pkgname=cups
pkgver=2.2.10
pkgrel=1
pkgdesc="The CUPS Printing System"
arch=('x86_64')
url="https://www.cups.org/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://github.com/apple/cups/releases/download/v${pkgver}/cups-${pkgver}-source.tar.gz"
	"cups-systemd.patch"
	"cups.pam"
	"cups.sysusers"
	"client.conf")

prepare() {
	cd "${srcdir}/cups-${pkgver}"

	patch -Np1 -i "${srcdir}/cups-systemd.patch"

	sed -i "/MAN.*.EXT/s:.gz::g" configure config-scripts/cups-manpages.m4
	sed -i "s:555:755:g;s:444:644:g" Makedefs.in
}

build() {
	cd "${srcdir}/cups-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--with-rcdir=/tmp/cupsinit \
		--with-docdir=/usr/share/cups/doc \
		--with-system-groups=lpadmin \
		--with-icondir=/usr/share/icons \
		--with-menudir=/usr/share/applications \
		--with-optim="${CFLAGS}" \
		--with-systemd=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/cups-${pkgver}"

	make install BUILDROOT="${pkgdir}"

	rm -rf "${pkgdir}"/etc/cups/*.default "${pkgdir}"/usr/share/cups/banners/*
	rm -rf "${pkgdir}/usr/share/cups/data/testprint" "${pkgdir}/tmp" "${pkgdir}/var/run"

	perl -p -i -e 's:^(.*\s+bannertops\s*)$:#\1:' "${pkgdir}/usr/share/cups/mime/mime.convs"

	touch "${pkgdir}/etc/cups/classes.conf" "${pkgdir}/etc/cups/printers.conf" "${pkgdir}/etc/cups/subscriptions.conf"

	chmod 600 "${pkgdir}/etc/cups/classes.conf" "${pkgdir}/etc/cups/printers.conf"
	chmod 640 "${pkgdir}/etc/cups/subscriptions.conf"

	install -v -Dm644 "${srcdir}/cups.pam" "${pkgdir}/etc/pam.d/cups"
	install -v -Dm644 "${srcdir}/cups.sysusers" "${pkgdir}/usr/lib/sysusers.d/cups.conf"

	install -v -Dm644 "${srcdir}/client.conf" "${pkgdir}/etc/cups/client.conf"

	chgrp -R lp "${pkgdir}/etc/cups"
}

sha512sums=('1393987a263ebf20089dd3008ae4ed770a27a1f289032604eb9e18f2e863bd0e4215a70118f5a6d3940875625278b6798fbc9070e791ec559179c6cf7dc7b05f'
            '177cec946a938f20e91d4773f319b2c20c0f0440edb5b3f5e19d4a422e2ffed0eaa7def92433a2b0e83ea62757a608ff71bccb4dd90ac822ff0fbad75f10535d'
            'd94417c5313c61cef28129afaf5ae92c3e537688e63d82da1da9c32c1793030e94bdcee4f5a3ae4be13765f0c41f7a81683419659b79a1d31c4dc50ace29b8d0'
            'a34738a03ead755302bb9e2c94874d1b35623021ff791b454c36ea0ad4212d65638931c5b56eecc9d5128f20e8a188e3b242b4c2152779e5e9a1c9647c448178'
            'a4630b6964897014c0331a49060e3ed8289fb9b718f06b79350bc36b39f522aa76e89a8a792176b9f713b3ce24098d716f5ff7b4b05a4a9f6a340093e8628d44')

pkgname=cyrus-sasl
pkgver=2.1.27
pkgrel=1
pkgdesc="Cyrus Simple Authentication Service Layer (SASL)"
arch=('x86_64')
url="https://www.cyrusimap.org/"
license=('custom')
#depends=()
install=cyrus-sasl.install
source=("https://www.cyrusimap.org/releases/cyrus-sasl-${pkgver}.tar.gz"
	"cyrus-sasl-fixes.patch"
	"saslauthd.default"
	"saslauthd.service"
	"saslauthd.tmpfiles")

prepare() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	patch -Np1 -i "${srcdir}/cyrus-sasl-fixes.patch"

	autoreconf -fi
}

build() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-dbpath=/var/lib/sasl/sasldb2 \
		--with-saslauthd=/var/run/saslauthd \
		--with-sqlite3=/usr \
		--enable-login \
		--enable-ntlm \
		--enable-auth-sasldb

	make
}

package() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/libsasl2.so*
	rm -rf "${pkgdir}"/usr/lib/sasl2/libsasldb.so*

	rm -rf "${pkgdir}/usr/include"
	rm -rf "${pkgdir}/usr/lib/pkgconfig"

	install -v -Dm644 "${srcdir}/saslauthd.default" "${pkgdir}/etc/default/saslauthd"
	install -v -Dm644 "${srcdir}/saslauthd.service" "${pkgdir}/usr/lib/systemd/system/saslauthd.service"
	install -v -Dm644 "${srcdir}/saslauthd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/saslauthd.conf"
}

sha512sums=('d11549a99b3b06af79fc62d5478dba3305d7e7cc0824f4b91f0d2638daafbe940623eab235f85af9be38dcf5d42fc131db531c177040a85187aee5096b8df63b'
            '2ba7f5736701079b6213b1d407fc351b59fbc59e3364944ffa2504834eb7718606ac087476b8a07a026135cf2a4c871ac79eec7d3141a962ef033dcb5d2f60b6'
            '7b7686e1eb471abf6d2f7ad94d532bd83a3ba66a427b2f42dabfe91d4682609f9c648fb56e9d2f5a947fe8fc94c3a37b6f4fd915b2c7feeaf2a019ec4bc71502'
            'c85eccb630d0ec437ec2c146d8f6bd56d4bebc3c974704878c447fb886c1cf472dd6ea8410fe49626b32925eb5ae7cfeacac49a7ae39f79ad84ca7d55989556d'
            '2b57f712ef10d94c3bd64dab9d2c044a9a0e233c5ad9f0058bda6080c2cacd40f06b195ebd95b9aabf07a15c16771fd97c7be8684fe79e5436807ff36163dfe7')

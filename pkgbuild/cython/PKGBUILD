pkgbase=cython
pkgname=('cython2' 'cython3')
pkgver=0.27.3
pkgrel=1
pkgdesc="C-Extensions for Python"
arch=('x86_64')
url="http://cython.org/"
license=('APACHE')
#depends=()
source=("https://files.pythonhosted.org/packages/source/C/Cython/Cython-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Cython-${pkgver}" "${srcdir}/Cython-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Cython-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Cython-${pkgver}-py2"

	python2 setup.py build
}

package_cython3() {
	cd "${srcdir}/Cython-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*
	do
		mv ${file} ${file}3
	done
}

package_cython2() {
	cd "${srcdir}/Cython-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('3c91d3be063cea4a03420a4ef2d9485453d5aa8f1a605d6157b2456b71ab53601c27a5df9cb5337ac14345b09fc86063d1cc613abe9bcbb0e8257f93181a79cf')

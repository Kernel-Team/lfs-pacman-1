pkgname=db
pkgver=18.1.40
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('x86_64')
url="https://www.oracle.com/database/technologies/related/berkeleydb.html"
license=('custom')
#depends=()
source=("https://fossies.org/linux/misc/db-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/db-${pkgver}/build_unix"

	sed -i "s#gsg_db_server##g" ../dist/Makefile.in
	sed -i "s#bdb-sql##g" ../dist/Makefile.in

	../dist/configure --prefix=/usr \
		--enable-compat185 \
		--enable-cxx \
		--enable-dbm \
		--enable-stl \
		--enable-sql \
		--enable-tcl \
		--with-tcl=/usr/lib

	make
}

package() {
	cd "${srcdir}/db-${pkgver}/build_unix"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/docs"
}

sha512sums=('53787164fb8a198a0178c7f58d891c2b0943d1c52b11fe9de525938469327e85664f0bc63e33d740c171bc370954710a6b3e8b9be2a08237fb9757a795c5b19e')

pkgname=db
pkgver=6.2.32
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
#depends=()
source=("http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz"
	"db-clang.patch")


prepare() {
	cd "${srcdir}/db-${pkgver}"

	patch -Np1 -i "${srcdir}/db-clang.patch"
}

build() {
	cd "${srcdir}/db-${pkgver}/build_unix"

	../dist/configure --prefix=/usr \
		--enable-compat185 \
		--enable-cxx \
		--enable-dbm \
		--enable-stl \
		--enable-sql \
		--enable-tcl \
		--with-tcl=/usr/lib

	make
}

package() {
	cd "${srcdir}/db-${pkgver}/build_unix"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/docs"
}

sha512sums=('83bdbf18a7b9782409cf4563f8d5f11322d5e564a39946c890604a60440a6ea6361e0236bbc30bd4e8e1de1fa9196b8e815fd126baa035f55c5826c2c6aa3401'
            '2ead6d4942f35fa04e3654114fd06a153c9a2192056309a54bb81c9eea4c0ef9602e31687e341944000be760b5fc20cccd4208e90d0184cd1fcdff6a46883dac')

pkgbase=dbus
pkgname=('dbus' 'lib32-dbus')
pkgver=1.10.20
pkgrel=1
arch=('x86_64')
url="https://wiki.freedesktop.org/www/Software/dbus/"
license=('GPL' 'custom')
#depends=()
options=(emptydirs)
source=("https://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/dbus{,32}-${pkgver}
}

build() {
	cd "${srcdir}/dbus-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-systemd \
		--enable-user-session \
		--with-console-auth-dir=/run/console/ \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	cd "${srcdir}/dbus32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib32 \
		--enable-systemd \
		--enable-user-session \
		--with-console-auth-dir=/run/console/ \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/dbus-${pkgver}"
	make -C "${srcdir}/dbus32-${pkgver}"
}

package_dbus() {
	pkgdesc="Freedesktop.org message bus system"

	cd "${srcdir}/dbus-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/var/run"

	ln -sfv /etc/machine-id "${pkgdir}/var/lib/dbus"
}

package_lib32-dbus() {
	pkgdesc="Freedesktop.org message bus system shared library (32 bit)"

	cd "${srcdir}/dbus32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('0e43866b56d6ae4a5d885044793573908ce95b4dd84822292efa87f5eb0dec3d09adf58af0574ca41259b16c3d70476ef956b6c8aa3bf9ca7461e6196be71daf')

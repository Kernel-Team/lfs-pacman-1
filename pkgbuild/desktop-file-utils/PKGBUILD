pkgname=desktop-file-utils
pkgver=0.26
pkgrel=1
pkgdesc="Command line utilities for working with desktop entries"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/desktop-file-utils"
license=('GPL')
#depends=()
source=("https://www.freedesktop.org/software/desktop-file-utils/releases/desktop-file-utils-${pkgver}.tar.xz"
	"update-desktop-database.hook")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		"${srcdir}/desktop-file-utils-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	install -v -Dm644 "${srcdir}/update-desktop-database.hook" "${pkgdir}/usr/share/libalpm/hooks/update-desktop-database.hook"
}

sha512sums=('5b4e435f0b635d8f898ac7f5759f74a08ffbe2a56d41fee0e84ff57e73b98d08b57b4416f20b99f696dad8bcb1c20792b39acf836b0814220c0b386ea5f6b831'
            'a12917f81c13aacd0dd28c4c7db29e85db1fba8b9ef7d80c3b96de4926742f180fcafde353552e35fdfea1dba9fb6ac020305447ed86e0bf51a62efc53812b01')

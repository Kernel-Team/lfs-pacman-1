pkgbase=dhcp
pkgname=('dhclient' 'dhcpd')
pkgver=4.4.2
pkgrel=1
arch=('x86_64')
url="https://www.isc.org/software/dhcp"
license=('custom:isc-dhcp')
#depends=()
options=(emptydirs !makeflags)
source=("https://downloads.isc.org/isc/dhcp/${pkgver}/dhcp-${pkgver}.tar.gz"
	"gcc-fixes.patch"
	"dhclient.conf"
	"dhcpd.conf"
	"dhcpd6.conf"
	"dhcpd.default"
	"dhcpd6.default"
	"dhcpd.service"
	"dhcpd6.service")

build() {
	cd "${srcdir}/dhcp-${pkgver}"

	patch -Np1 -i "${srcdir}/gcc-fixes.patch"

	CFLAGS+=" -D_PATH_DHCLIENT_SCRIPT='\"/usr/sbin/dhclient-script\"'"
	CFLAGS+=" -D_PATH_DHCPD_CONF='\"/etc/dhcp/dhcpd.conf\"'"
	CFLAGS+=" -D_PATH_DHCLIENT_CONF='\"/etc/dhcp/dhclient.conf\"'"

	./configure --prefix=/usr \
		--sysconfdir=/etc/dhcp \
		--localstatedir=/var \
		--with-srv-lease-file=/var/lib/dhcpd/dhcpd.leases \
		--with-srv6-lease-file=/var/lib/dhcpd/dhcpd6.leases \
		--with-cli-lease-file=/var/lib/dhclient/dhclient.leases \
		--with-cli6-lease-file=/var/lib/dhclient/dhclient6.leases

	make
}

package_dhclient() {
	pkgdesc="A standalone DHCP client from the dhcp package"

	cd "${srcdir}/dhcp-${pkgver}"

	make -C client install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/dhcp"

	install -v -Dm755 client/scripts/linux "${pkgdir}/usr/sbin/dhclient-script"
	install -v -Dm644 "${srcdir}/dhclient.conf" "${pkgdir}/etc/dhcp/dhclient.conf"
	install -v -dm755 "${pkgdir}/var/lib/dhclient"
}

package_dhcpd() {
	pkgdesc="A DHCP server and relay agent"

	cd "${srcdir}/dhcp-${pkgver}"

	make install DESTDIR="${pkgdir}"
	make -C client uninstall DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/dhcp"

	install -v -Dm644 "${srcdir}/dhcpd.default" "${pkgdir}/etc/default/dhcpd"
	install -v -Dm644 "${srcdir}/dhcpd6.default" "${pkgdir}/etc/default/dhcpd6"

	install -v -Dm644 "${srcdir}/dhcpd.conf" "${pkgdir}/etc/dhcp/dhcpd.conf"
	install -v -Dm644 "${srcdir}/dhcpd6.conf" "${pkgdir}/etc/dhcp/dhcpd6.conf"

	install -v -Dm644 "${srcdir}/dhcpd.service" "${pkgdir}/usr/lib/systemd/system/dhcpd.service"
	install -v -Dm644 "${srcdir}/dhcpd6.service" "${pkgdir}/usr/lib/systemd/system/dhcpd6.service"

	install -v -dm755 "${pkgdir}/var/lib/dhcpd"
}

sha512sums=('c3dee2cf6e4b43d519d4bc89e9b8b12a6e3747d8c4edc0f83d4a88355a483b91a5f7d2353a3c0a2f37f88704fd2f64478ac5161ca72b10c42cebcb92907afa40'
            '1a0e0c57bc08e9746e372733fff3a726d99064cde800fc921cbaab0eb19d32bf8cd813d197fd0d64b6d8f4e18c4557d0ef2e81334077d363b995012301bbe176'
            '8e951a0b1c8e25b07b880b48ccbe5ffe127b2929eed1e2ccbc6c4b3dcd321a6f577612d6305a738d4e96d86dd8ad49450c01d656b361e2705589d1db1ff6df2b'
            '2c7e318fa8b55a6d5ecbd55f96704a82ba296bb2f0828eb3006c0eedb301d6265574169f444f83a899209f7b284ef3997422af9ed05f12bda62b822dd44c083d'
            'd018aadf1180a1e4c47266c162c5d635e3fdf0bb0d6f305c1b0fc2873fab25637d22170538672d6039d30a344f577fe6c6f20e370203cd000e683e082ce7e719'
            'b409b771fa960a1680b434b3b686dd2e187c5a5734d244a84fd5626ce79551819f460f92796acc4cf78b2cbc02b95cb9b56b3c502e3b0463b2d4bd2700121cb2'
            '217a28e1b86da283248a90bbfc5ad53f12d79c6ee2c4518e0e7120b8cd3771f5b343507402e31ea4ac4c714378254f7e2a16240d67dc3862e9b663aba5db798c'
            'b0c0fa90a374785bd957d17eb6a7463e4adaf7fddd423bf33f98ee7b5566dca31ab597f319411b240ea0d45a6419a6117ba380d1338c403a3b1aac5d9e5630a6'
            'f68a484cb4faf23781abf9c57d85fbb4a5b9d3a0fabd1245f5e4ee708a2fa3f29a44d843da1f02348eba0315679ed60f5111a8512567c213304a8f0e0b25f792')

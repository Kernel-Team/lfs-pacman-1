pkgname=djvulibre
pkgver=3.5.28
pkgrel=1
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
arch=('x86_64')
url="http://djvu.sourceforge.net/"
license=('GPL2')
#depends=()
source=("https://downloads.sourceforge.net/djvu/djvulibre-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/djvulibre-${pkgver}"

	autoreconf -fi

	./configure --prefix=/usr --disable-desktopfiles

	make
}

package() {
	cd "${srcdir}/djvulibre-${pkgver}"

	make install DESTDIR="${pkgdir}"

	for sz in 22 32 48 64
	do
		install -v -Dm644 desktopfiles/prebuilt-hi${sz}-djvu.png \
			"${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png"
	done
}

sha512sums=('26a09f50de4b5b373859b857d2867cb7b712913217ce79f6de2aae5dc59769d662ae464e491527bc9a8ed6d1126f16149f422853f6f9546c11c1f5a15f0543f7')

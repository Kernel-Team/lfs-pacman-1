pkgname=docbook-utils
pkgver=0.6.14
pkgrel=1
pkgdesc="Shell scripts to manage DocBook documents"
arch=('x86_64')
url="https://www.sourceware.org/docbook-tools/"
license=('GPL2')
#depends=()
source=("ftp://sources.redhat.com/pub/docbook-tools/new-trials/SOURCES/docbook-utils-${pkgver}.tar.gz"
	"docbook-utils.patch")

prepare() {
	cd "${srcdir}/docbook-utils-${pkgver}"

	patch -Np1 -i "${srcdir}/docbook-utils.patch"
}

build() {
	cd "${srcdir}/docbook-utils-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--mandir=/usr/share/man

	make
}

package() {
	cd "${srcdir}/docbook-utils-${pkgver}"

	make install DESTDIR="${pkgdir}"

	for type in dvi html man pdf ps rtf tex texi txt
	do
		ln -sfv docbook2${type} "${pkgdir}/usr/bin/db2${type}"
	done
}

sha512sums=('8052316a643b5eaa6c0373a9ed7f2406e7c3cb3da9d3c955a271c89813a8f3196fcd9e6c8385c7b6b7ecb916515e3ec7897aece4473989f0e4dc52a2bc8bbfdf'
            '9c33cfed8375d97b3ad40586ed570ed0dbc0f807d87b7052e794844d6b79215d61d6163522743af33b88748faefee9e7faa62d7bece84ffebc113930fc21c179')

pkgname=docbook-xml
pkgver=4.5
pkgrel=1
pkgdesc="A widely used XML scheme for writing documentation and help"
arch=('x86_64')
url="http://www.oasis-open.org/docbook/"
license=('MIT')
#depends=()
install=docbook-xml.install
source=("http://www.docbook.org/xml/4.5/docbook-xml-4.5.zip"
	"http://www.docbook.org/xml/4.4/docbook-xml-4.4.zip"
	"http://www.docbook.org/xml/4.3/docbook-xml-4.3.zip"
	"http://www.docbook.org/xml/4.2/docbook-xml-4.2.zip"
	"http://www.docbook.org/xml/4.1.2/docbkx412.zip")
noextract=("docbook-xml-4.5.zip" "docbook-xml-4.4.zip" "docbook-xml-4.3.zip" "docbook-xml-4.2.zip" "docbkx412.zip")

package() {
	install -v -dm755 "${srcdir}/install"

	cd "${srcdir}/install"

	install -v -dm755 "${pkgdir}/etc/xml"
	xmlcatalog --noout --create "${pkgdir}/etc/xml/docbook-xml"

	for BOOKVER in 4.1.2 4.2 4.3 4.4 4.5
	do
		if [ ${BOOKVER} == "4.1.2" ]
		then
			export ZIP="docbkx412.zip"
		else
			export ZIP="docbook-xml-${BOOKVER}.zip"
		fi

 		bsdtar xf "${srcdir}/${ZIP}"

		install -v -dm755 ${pkgdir}/usr/share/xml/docbook/xml-dtd-${BOOKVER}
		cp -rf docbook.cat *.dtd ent *.mod ${pkgdir}/usr/share/xml/docbook/xml-dtd-${BOOKVER}

		rm -rf *

		xmlcatalog --noout --add "public" \
			"-//OASIS//DTD DocBook XML V${BOOKVER}//EN" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}/docbookx.dtd" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "public" \
			"-//OASIS//DTD DocBook XML CALS Table Model V${BOOKVER}//EN" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}/calstblx.dtd" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "public" \
			"-//OASIS//DTD XML Exchange Table Model 19990315//EN" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}/soextblx.dtd" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "public" \
			"-//OASIS//ELEMENTS DocBook XML Information Pool V${BOOKVER}//EN" \
			"file:///usr/share/xml/docbook/xml-dtd-${BOOKVER}/dbpoolx.mod" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "public" \
			"-//OASIS//ELEMENTS DocBook XML Document Hierarchy V${BOOKVER}//EN" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}/dbhierx.mod" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "public" \
			"-//OASIS//ENTITIES DocBook XML Additional General Entities V${BOOKVER}//EN" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}/dbgenent.mod" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "public" \
			"-//OASIS//ENTITIES DocBook XML Notations V${BOOKVER}//EN" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}/dbnotnx.mod" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "public" \
			"-//OASIS//ENTITIES DocBook XML Character Entities V${BOOKVER}//EN" \
			"file:///usr/share/xml/docbook/xml-dtd-${BOOKVER}/dbcentx.mod" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "rewriteSystem" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}" \
			"file:///usr/share/xml/docbook/xml-dtd-${BOOKVER}" \
			"${pkgdir}/etc/xml/docbook-xml"

		xmlcatalog --noout --add "rewriteURI" \
			"http://www.oasis-open.org/docbook/xml/${BOOKVER}" \
			"file:///usr/share/xml/docbook/xml-dtd-${BOOKVER}" \
			"${pkgdir}/etc/xml/docbook-xml"

		if [ ${BOOKVER} == "4.3" ] || [ ${BOOKVER} == "4.4" ] || [ ${BOOKVER} == "4.5" ]
		then
			xmlcatalog --noout --add "public" \
				"-//OASIS//ELEMENTS DocBook XML HTML Tables V${BOOKVER}//EN" \
				"http://www.oasis-open.org/docbook/xml/${BOOKVER}/htmltblx.mod" \
				"${pkgdir}/etc/xml/docbook-xml"
		fi
	done
}

sha512sums=('1ee282fe86c9282610ee72c0e1d1acfc03f1afb9dc67166f438f2703109046479edb6329313ecb2949db27993077e077d111501c10b8769ebb20719eb6213d27'
            '7df5af4df24e4618b09814e4e20c147c722962531f03a40c28cd60f1db16b4c330420adf96adb7d66ed6eda84046ee91b467fd6f6fbfac2201537e2080735d76'
            'f5090fb74884bae3d4fac8a3c5060bffff5d6a74272de183c181a7642e4b91f4ed32ad028537d198010782c3d98575ce679672f76a9749ed124432195886a7cb'
            '0c836346130d1e8f4e26e00959f6b4fd2c3c11269ba5cbf11cdc904724e189606f431c99cd5ab188877daa0eb44c58d0bc30556df3b51df480396818d61c4e0a'
            'f700591a671694ca0ac51f5b5b7e825df5c0b3604b20baa6afd3aaafa7ce99470ca1c261781b105b42bfa5485c23217cf3db821b3fcf2ebdae9df07bb8ae4063')

pkgname=docbook-xsl
pkgver=1.79.1
pkgrel=1
pkgdesc="XML stylesheets for Docbook-xml transformations"
arch=('x86_64')
url="http://docbook.sourceforge.net/"
license=('custom')
#depends=()
install=docbook-xsl.install
source=("https://downloads.sourceforge.net/docbook/docbook-xsl-${pkgver}.tar.bz2")

package() {
	cd "${srcdir}/docbook-xsl-${pkgver}"

	install -v -dm755 "${pkgdir}"/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/{common,params}
	install -m644 common/*.{xml,xsl,dtd,ent} "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/common"
	install -m644 params/*.xml "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/params"

	for fn in fo highlighting html roundtrip
	do
		install -v -dm755 "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
		install -m644 ${fn}/*.{xml,xsl} "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
	done

	for fn in eclipse htmlhelp javahelp lib manpages profiling template website xhtml{,-1_1}
	do
		install -v -dm755 "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
		install -m644 ${fn}/*.xsl "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
	done

	install -m644 VERSION "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}"

	ln -sfv VERSION "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/VERSION.xsl"
	ln -sfv xsl-stylesheets-${pkgver} "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-current"
}

sha512sums=('83325cbaf1545da6b9b8b77f5f0e6fdece26e3c455164b300a1aa3d19e3bd29ae71fd563553a714a5394968d1a65684c6c7987c77524469358d18b8c227025c7')

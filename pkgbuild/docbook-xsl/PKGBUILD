pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=1
pkgdesc="XML stylesheets for Docbook-xml transformations"
arch=('x86_64')
url="http://www.docbook.org/"
license=('custom')
#depends=()
install=docbook-xsl.install
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F${pkgver}/docbook-xsl-nons-${pkgver}.tar.bz2"
	"docbook-xsl-fixes.patch")

prepare() {
	cd "${srcdir}/docbook-xsl-nons-${pkgver}"

	patch -Np1 -i "${srcdir}/docbook-xsl-fixes.patch"
}

package() {
	cd "${srcdir}/docbook-xsl-nons-${pkgver}"

	install -v -dm755 "${pkgdir}"/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/{common,params}
	install -m644 common/*.{xml,xsl,dtd,ent} "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/common"
	install -m644 params/*.xml "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/params"

	for fn in fo highlighting html roundtrip
	do
		install -v -dm755 "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
		install -m644 ${fn}/*.{xml,xsl} "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
	done

	for fn in eclipse htmlhelp javahelp lib manpages profiling template website xhtml{,-1_1}
	do
		install -v -dm755 "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
		install -m644 ${fn}/*.xsl "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}"
	done

	install -m644 VERSION "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}"

	ln -sfv VERSION "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/VERSION.xsl"
	ln -sfv xsl-stylesheets-${pkgver} "${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-current"
}

sha512sums=('d19aad5ae91c7f9a9690836bd472ec6e01a29eaeac6b1838b5c9ea54e2ef4cc90f515d7b6640fe94e909a5a51eee80cd32e2ccbee457de6f0c6652ea43325d7d'
            'f6b79c008e5df47364df7affe12c958a3b41ba102700db5331e1e5fc129ca3077d825e5959aeae39f9a61a511276dfe77865db45a390e57ad46182858d62281c')

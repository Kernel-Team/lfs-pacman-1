pkgname=docbook
pkgver=4.5
pkgrel=1
pkgdesc="Document type definitions for verification of SGML data files against the DocBook rule set"
arch=('x86_64')
url="http://www.docbook.org/sgml/"
license=('MIT')
#depends=()
install=docbook.install
source=("http://www.docbook.org/sgml/4.5/docbook-4.5.zip"
	"http://www.docbook.org/sgml/4.4/docbook-4.4.zip"
	"http://www.docbook.org/sgml/4.3/docbook-4.3.zip"
	"http://www.docbook.org/sgml/4.2/docbook-4.2.zip"
	"http://www.docbook.org/sgml/4.1/docbk41.zip"
	"http://www.docbook.org/sgml/3.1/docbk31.zip"
	"http://www.docbook.org/sgml/3.0/docbk30.zip")
noextract=("docbook-4.5.zip" "docbook-4.4.zip" "docbook-4.3.zip" "docbook-4.2.zip" "docbk41.zip" "docbk31.zip" "docbk30.zip")

package() {
	install -v -dm755 "${srcdir}/install"

	cd "${srcdir}/install"

	for BOOKVER in 3 4
	do
		for MINOR in 0 1
		do
			if [ "${BOOKVER}${MINOR}" != "40" ]
			then
				bsdtar xf "${srcdir}/docbk${BOOKVER}${MINOR}.zip"
				install -dm755 "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-${BOOKVER}.${MINOR}"
				install docbook.cat "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-${BOOKVER}.${MINOR}/catalog"
				ln -sf catalog "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-${BOOKVER}.${MINOR}/docbook.cat"
				cp -rf *.dtd *.mod *.dcl "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-${BOOKVER}.${MINOR}"
				rm -rf *
			fi
		done
	done

	for BOOKVER in 2 3 4 5
	do
		bsdtar xf "${srcdir}/docbook-4.${BOOKVER}.zip"
		install -dm755 "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-4.${BOOKVER}"
		install docbook.cat "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-4.${BOOKVER}/catalog"
		ln -sf catalog "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-4.${BOOKVER}/docbook.cat"
		cp -rf *.dtd *.mod *.dcl "${pkgdir}/usr/share/sgml/docbook/sgml-dtd-4.${BOOKVER}"
		rm -rf *
	done
}

sha512sums=('cde483678562a18f67fa5fbe631fbdfcbef907b6b6fb63749125fdc883af2448d4a95aefb8fba8bcdd0417fb078063984fe8eb3c7050978f4f4b03d527c329df'
            '561d6f68e293fb98d13010dc287c6ea59d9e6a033af3518ab4d735a374a7a603150ecf555a1126577dd00baa692a6c5d09afc4d3fed70f2fe8b5b86d6bb5ecd8'
            '18ead2e683a9e9bc39e1487ee06fb72542557fa0c76f0e899b566cb52207e3bbfcb415ed59163c6d59e8d58f92289e115416d56bc6ed8f182910f7a18565b227'
            'd51e07310a1426a5fecb809ef1bc32aa7d72c673528b3ffda0b6d3c1e22baab89f94dd5160386d657d567eb225c9bd2cff1096cd07c7184f6e0b690692a5661d'
            'd1c76a381b37c599c127286be67939bd8348721339ed9dde6140c69a2d89568e290e7554e6763ab903e050edc505520497eb612865e2a07224e4907b51fb87d5'
            '397c9df011007b71ddc9c052d28db7ab38ac2c05e7f575aaad41a6b4995b910cd147f9a822d162271bc073cbfca0788d7f323be381c900c3a32c352e658d37fe'
            '4e186c7b38bf70a3225bd9fca4edf99d3e7f316308b561aad9a0548f8e0ba941b54f20f6e653819de8a26db5eb54fc7442c3e2d740c2ba60311b3038a4bb9198')

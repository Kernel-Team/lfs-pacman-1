pkgname=doxygen
pkgver=1.8.17
pkgrel=1
pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
arch=('x86_64')
url="http://www.doxygen.nl/"
license=('GPL')
#depends=()
source=("http://doxygen.nl/files/doxygen-${pkgver}.src.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-Dbuild_search=ON \
		-Dbuild_wizard=ON \
		-Duse_libclang=OFF \
		"${srcdir}/doxygen-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	cd "${srcdir}/doxygen-${pkgver}"

	install -v -dm755 "${pkgdir}/usr/share/man/man1"
	install -v -m644 doc/*.1 "${pkgdir}/usr/share/man/man1"
}

sha512sums=('2fd087d127e301ea48355ea52c9af4f2091df06551cf64da80df81f0758194b296efb1e8d3962867a6a6d2da5a3fc323842f7766a445748005b30097ded30a75')

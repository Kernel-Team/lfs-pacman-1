pkgname=doxygen
pkgver=1.9.0
pkgrel=1
pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
arch=('x86_64')
url="https://www.doxygen.nl/"
license=('GPL')
#depends=()
source=("https://doxygen.nl/files/doxygen-${pkgver}.src.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-Dbuild_search=ON \
		-Dbuild_wizard=ON \
		-Duse_libclang=OFF \
		"${srcdir}/doxygen-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	cd "${srcdir}/doxygen-${pkgver}"

	install -v -dm755 "${pkgdir}/usr/share/man/man1"
	install -v -m644 doc/*.1 "${pkgdir}/usr/share/man/man1"
}

sha512sums=('7a9fb70e5fceb0003d098b28be01576cf365288c092feaf4d260bfe7e76d9f3e9480e29b76578fb5b5f208f8457f2dadd772e649748c328d079c0f3147e5400e')

pkgname=doxygen
pkgver=1.8.14
pkgrel=1
pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
arch=('x86_64')
url="http://www.stack.nl/~dimitri/doxygen/"
license=('GPL')
#depends=()
source=("http://ftp.stack.nl/pub/users/dimitri/doxygen-${pkgver}.src.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-Dbuild_search=ON \
		-Dbuild_wizard=ON \
		-Duse_libclang=OFF \
		"${srcdir}/doxygen-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	cd "${srcdir}/doxygen-${pkgver}"

	install -v -dm755 "${pkgdir}/usr/share/man/man1"
	install -v -m644 doc/*.1 "${pkgdir}/usr/share/man/man1"
}

sha512sums=('5fac7e457a51b1f8e39fb65de5e0c07633e573f74e876580e7bcad5693540d6970fde1b2cb799a4df6ab9dfca951ece15b76b3010134d7658db3500183bea85c')

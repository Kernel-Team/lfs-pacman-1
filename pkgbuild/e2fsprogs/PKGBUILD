pkgname=e2fsprogs
pkgver=1.43.7
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64')
url="http://e2fsprogs.sourceforge.net"
license=('GPL' 'LGPL' 'MIT')
#depends=()
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/e2fsprogs/v${pkgver}/e2fsprogs-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/e2fsprogs-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-elf-shlibs \
		--disable-libblkid \
		--disable-libuuid \
		--disable-uuidd \
		--disable-fsck

	make
}

package() {
	cd "${srcdir}/e2fsprogs-${pkgver}"

	make -j1 install install-libs DESTDIR="${pkgdir}" LDCONFIG=true

	sed -i 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "${pkgdir}/usr/bin/mk_cmds"
	sed -i 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "${pkgdir}/usr/bin/compile_et"
}

sha512sums=('2ef270364d3cea620db3c3b9932849d0ff5b49d4a9a9b24f0d1ac36888199bd67432edc5f939d9f697ee0342b71a063e1ad4ce8119528a7adab7a777c1de57ba')

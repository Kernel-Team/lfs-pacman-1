pkgname=e2fsprogs
pkgver=1.44.3
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64')
url="http://e2fsprogs.sourceforge.net"
license=('GPL' 'LGPL' 'MIT')
#depends=()
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/e2fsprogs/v${pkgver}/e2fsprogs-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/e2fsprogs-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-elf-shlibs \
		--enable-relative-symlinks \
		--disable-fsck \
		--disable-uuidd

	make
}

package() {
	cd "${srcdir}/e2fsprogs-${pkgver}"

	make -j1 install install-libs DESTDIR="${pkgdir}" LDCONFIG=true

	sed -i 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "${pkgdir}/usr/bin/mk_cmds"
	sed -i 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "${pkgdir}/usr/bin/compile_et"
}

sha512sums=('38fdf918762a8ef8b49f04fe229bbb226a22d084bbfd21bd2d5fe5579f5b40eaa1b769cd3112d540f6460ed6d5b2a49ddf72128d1458c8b733193998a079e0be')

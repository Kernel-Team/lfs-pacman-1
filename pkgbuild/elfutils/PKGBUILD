pkgbase=elfutils
pkgname=('elfutils' 'lib32-elfutils')
pkgver=0.169
pkgrel=1
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
#depends=()
options=(staticlibs)
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/elfutils{,32}-${pkgver}
}

build() {
	cd "${srcdir}/elfutils-${pkgver}"

	export CC="gcc"
	export CXX="g++"

	./configure --prefix=/usr --program-prefix="eu-"

	cd "${srcdir}/elfutils32-${pkgver}"

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	cd "${srcdir}"

	make -C "${srcdir}/elfutils-${pkgver}"
	make -C "${srcdir}/elfutils32-${pkgver}"
}

package_elfutils() {
	pkgdesc="Utilities to handle ELF object files and DWARF debugging information"

	cd "${srcdir}/elfutils-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-elfutils() {
	pkgdesc="Utilities to handle ELF object files and DWARF debugging information (32 bit)"

	cd "${srcdir}/elfutils32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('0a81a20bb2aff533d035d6b76f1403437b2e11bce390db57e34b8c26e4b9b3150346d83dddcbfbbdc58063f046ca3223508dba35c6ce88e375d201e7a777a8b9')

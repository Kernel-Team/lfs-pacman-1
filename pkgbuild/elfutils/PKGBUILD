pkgbase=elfutils
pkgname=('elfutils' 'lib32-elfutils')
pkgver=0.182
pkgrel=1
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
#depends=()
options=(staticlibs)
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/elfutils{,32}-${pkgver}
}

build() {
	cd "${srcdir}/elfutils-${pkgver}"

	export CC="gcc"
	export CXX="g++"

	CFLAGS+=" -Wno-packed-not-aligned"

	./configure --prefix=/usr --program-prefix="eu-" --disable-libdebuginfod --disable-debuginfod

	cd "${srcdir}/elfutils32-${pkgver}"

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --disable-debuginfod --disable-libdebuginfod --libdir=/usr/lib32

	cd "${srcdir}"

	make -C "${srcdir}/elfutils-${pkgver}"
	make -C "${srcdir}/elfutils32-${pkgver}"
}

package_elfutils() {
	pkgdesc="Utilities to handle ELF object files and DWARF debugging information"

	cd "${srcdir}/elfutils-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-elfutils() {
	pkgdesc="Utilities to handle ELF object files and DWARF debugging information (32 bit)"

	cd "${srcdir}/elfutils32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('8ab0735bbe11b4383169341bf674ace360038b6ae5239f1d5a991c46260cd4bce545e078735b7de3b8fab132bb5da41f60689ff1b1d7ebccfada117a954a2c81')

pkgname=evolution-data-server
pkgver=3.24.5
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Evolution"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/evolution-data-server/${pkgver:0:4}/evolution-data-server-${pkgver}.tar.xz")

prepare() {
        install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CXX="${CXX} -std=c++11"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DENABLE_INTROSPECTION=ON \
		-DENABLE_VALA_BINDINGS=ON \
		-DENABLE_UOA=OFF \
		-DENABLE_GOOGLE_AUTH=ON \
		-DENABLE_GTK_DOC=ON \
		-DENABLE_SCHEMAS_COMPILE=OFF \
		-DENABLE_EXAMPLES=OFF \
		"${srcdir}/evolution-data-server-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('ebe77bb8b1025b07378a32df5a822add11a2ef1e3daf57d8eae14ea05ba479cfe5e8ebf1d8dda585a6f3232d270f212dcfa9cd580fa1488b06a96204775a1c21')

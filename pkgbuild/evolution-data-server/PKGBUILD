pkgname=evolution-data-server
pkgver=3.28.5
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Evolution"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/evolution-data-server/${pkgver:0:4}/evolution-data-server-${pkgver}.tar.xz")

prepare() {
        install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	CXXFLAGS+=" -DU_USING_ICU_NAMESPACE=1"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DENABLE_INTROSPECTION=ON \
		-DENABLE_VALA_BINDINGS=ON \
		-DENABLE_UOA=OFF \
		-DENABLE_GTK_DOC=ON \
		-DENABLE_SCHEMAS_COMPILE=OFF \
		-DENABLE_EXAMPLES=OFF \
		"${srcdir}/evolution-data-server-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('f95593c7c47b80a1333a2c2a1026316ec9cdad401da46e0d9bb9ef155e97130216f136a6efec2476441052296050c49913843eea1f397b40b53a96511e853c3b')

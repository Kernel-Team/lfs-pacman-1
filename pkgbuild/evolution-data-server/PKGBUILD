pkgname=evolution-data-server
pkgver=3.26.5
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Evolution"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/evolution-data-server/${pkgver:0:4}/evolution-data-server-${pkgver}.tar.xz")

prepare() {
        install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CXX="${CXX} -std=c++11"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DENABLE_INTROSPECTION=ON \
		-DENABLE_VALA_BINDINGS=ON \
		-DENABLE_UOA=OFF \
		-DENABLE_GOOGLE_AUTH=ON \
		-DENABLE_GTK_DOC=ON \
		-DENABLE_SCHEMAS_COMPILE=OFF \
		-DENABLE_EXAMPLES=OFF \
		"${srcdir}/evolution-data-server-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('bce84b4ab7320e3780d9d7b74f2163a75bb5f60ff74c0dcaafd592ae4b2a335eaf011003fa070d14b3c414f1ac54cf29f4f6f38ea8058bf276542c7084208023')

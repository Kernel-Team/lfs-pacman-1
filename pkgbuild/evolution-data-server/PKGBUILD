pkgname=evolution-data-server
pkgver=3.26.0
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Evolution"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/evolution-data-server/${pkgver:0:4}/evolution-data-server-${pkgver}.tar.xz")

prepare() {
        install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CXX="${CXX} -std=c++11"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DENABLE_INTROSPECTION=ON \
		-DENABLE_VALA_BINDINGS=ON \
		-DENABLE_UOA=OFF \
		-DENABLE_GOOGLE_AUTH=ON \
		-DENABLE_GTK_DOC=ON \
		-DENABLE_SCHEMAS_COMPILE=OFF \
		-DENABLE_EXAMPLES=OFF \
		"${srcdir}/evolution-data-server-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('9cebde70e41d770c2641f78c49e8ebe6a00225ac3c298949f9e025979d4254f5fd45fffbd3cdc86a281756eeb090ba433e7f758813b8799ee1f5e118cec3bd17')

pkgname=evolution-data-server
pkgver=3.26.4
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Evolution"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/evolution-data-server/${pkgver:0:4}/evolution-data-server-${pkgver}.tar.xz")

prepare() {
        install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CXX="${CXX} -std=c++11"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DENABLE_INTROSPECTION=ON \
		-DENABLE_VALA_BINDINGS=ON \
		-DENABLE_UOA=OFF \
		-DENABLE_GOOGLE_AUTH=ON \
		-DENABLE_GTK_DOC=ON \
		-DENABLE_SCHEMAS_COMPILE=OFF \
		-DENABLE_EXAMPLES=OFF \
		"${srcdir}/evolution-data-server-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('e890dd8bbde6cd1d9d9421bd4746241705a3933dcadd95310ff7a59929829f80b0aad346b062f30619f0a8aeb29ce91ba37cfb4b389556f3def6627d693abc59')

pkgname=exiv2
pkgver=0.27.3
pkgrel=1
pkgdesc="Exif, Iptc and XMP metadata manipulation library and tools"
arch=('x86_64')
url="https://exiv2.org"
license=('GPL2')
#depends=()
source=("https://exiv2.org/builds/exiv2-${pkgver}-Source.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DEXIV2_ENABLE_BUILD_PO=ON \
		-DEXIV2_ENABLE_BUILD_SAMPLES=OFF \
		-DEXIV2_ENABLE_VIDEO=ON \
		"${srcdir}/exiv2-${pkgver}-Source"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('3f5758ee862b811eeb89cc75fc2bbd8bf10329efa2ce1e68555cdc7729faa6cfd1603e0cc859fbdbe6d8fd5e53bd9b9e6d869d8a20ed17497bf87ce78c005de9')

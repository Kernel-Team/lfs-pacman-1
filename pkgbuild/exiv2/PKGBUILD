pkgname=exiv2
pkgver=0.27.0
pkgrel=1
pkgdesc="Exif, Iptc and XMP metadata manipulation library and tools"
arch=('x86_64')
url="http://www.exiv2.org"
license=('GPL2')
#depends=()
source=("http://exiv2.org/builds/exiv2-${pkgver}-Source.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DEXIV2_ENABLE_BUILD_PO=ON \
		-DEXIV2_ENABLE_BUILD_SAMPLES=OFF \
		-DEXIV2_ENABLE_VIDEO=ON \
		"${srcdir}/exiv2-${pkgver}-Source"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('3037d8e84b414e3915b807c5bea39f903668c0774f9dbc1367e23c595d5abaf318c746009e994234bcaf0088ad32a820ea42c9c77c9a53c65a7b5d7781a2c6d4')

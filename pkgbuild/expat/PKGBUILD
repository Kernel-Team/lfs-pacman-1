pkgbase=expat
pkgname=('expat' 'lib32-expat')
pkgver=2.2.4
pkgrel=1
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/expat/expat-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/expat{,32}-${pkgver}
}

build() {
	cd "${srcdir}/expat-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/expat32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/expat-${pkgver}"
	make -C "${srcdir}/expat32-${pkgver}"
}

package_expat() {
	pkgdesc="An XML parser library"

	cd "${srcdir}/expat-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-expat() {
	pkgdesc="An XML parser library (32 bit)"

	cd "${srcdir}/expat32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('788249e2c6edf8521c4a99830fd5e51a55c062b834516f6775759cd71accf6375f12d3bd38f8b069777d081a4380b9549049921386ca0cb4b9b9daa4861d6592')

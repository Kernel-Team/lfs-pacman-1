pkgbase=expat
pkgname=('expat' 'lib32-expat')
pkgver=2.2.10
pkgrel=1
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
#depends=()
source=("https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/expat{,32}-${pkgver}
}

build() {
	cd "${srcdir}/expat-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/expat32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/expat-${pkgver}"
	make -C "${srcdir}/expat32-${pkgver}"
}

package_expat() {
	pkgdesc="An XML parser library"

	cd "${srcdir}/expat-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-expat() {
	pkgdesc="An XML parser library (32 bit)"

	cd "${srcdir}/expat32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('a8e0c8a9cf7e6fbacdc6e709f3c99c533ab550fba52557d24259bb8b360f9697624c7500c0e9886fa57ee2b529aadd0d1835d66fe8112e15c20df75cd3eb090f')

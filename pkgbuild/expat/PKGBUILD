pkgbase=expat
pkgname=('expat' 'lib32-expat')
pkgver=2.2.6
pkgrel=1
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
#depends=()
source=("https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/expat{,32}-${pkgver}
}

build() {
	cd "${srcdir}/expat-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/expat32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/expat-${pkgver}"
	make -C "${srcdir}/expat32-${pkgver}"
}

package_expat() {
	pkgdesc="An XML parser library"

	cd "${srcdir}/expat-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-expat() {
	pkgdesc="An XML parser library (32 bit)"

	cd "${srcdir}/expat32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('dbfb635a5fe7b190722664263a0dd437b512fdf519bc53bd4905567f4bfb4b1e89a021562da63df8cacd48b706d1dea60ccde47f279e57400ad3c846b6e9c4e6')

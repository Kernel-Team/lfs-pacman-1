pkgname=fakeroot
pkgver=1.25.3
pkgrel=1
pkgdesc="Tool for simulating superuser privileges"
arch=('x86_64')
url="http://packages.debian.org/fakeroot"
license=('GPL')
#depends=()
source=("http://cdn-fastly.deb.debian.org/debian/pool/main/f/fakeroot/fakeroot_${pkgver}.orig.tar.gz")

build() {
	cd "${srcdir}/fakeroot-${pkgver}"

	sed -i "/SUBDIRS/s#doc ##g" Makefile.am
	./bootstrap
	./configure --prefix=/usr --libdir=/usr/lib/libfakeroot

	make
}

package() {
	cd "${srcdir}/fakeroot-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/ld.so.conf.d"

	echo "/usr/lib/libfakeroot" > "${pkgdir}/etc/ld.so.conf.d/fakeroot.conf"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
	rm -rf "${pkgdir}"/usr/share/man/nl
}

sha512sums=('b54366ef82b49af414d7214d069a4bc7dd188dfa4f8e3121f6bd65fd67268b02346ff72e857087243ab0efe9fa82faaa9f9c3309cac4b5af0ea00908c2e5eb6e')

pkgname=fakeroot
pkgver=1.24
pkgrel=1
pkgdesc="Tool for simulating superuser privileges"
arch=('x86_64')
url="http://packages.debian.org/fakeroot"
license=('GPL')
#depends=()
source=("http://cdn-fastly.deb.debian.org/debian/pool/main/f/fakeroot/fakeroot_${pkgver}.orig.tar.gz"
	"fakeroot-fixes.patch")

prepare() {
	cd "${srcdir}/fakeroot-${pkgver}"

	patch -Np1 -i "${srcdir}/fakeroot-fixes.patch"
}

build() {
	cd "${srcdir}/fakeroot-${pkgver}"

	./configure --prefix=/usr --libdir=/usr/lib/libfakeroot

	make
}

package() {
	cd "${srcdir}/fakeroot-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/ld.so.conf.d"

	echo "/usr/lib/libfakeroot" > "${pkgdir}/etc/ld.so.conf.d/fakeroot.conf"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
	rm -rf "${pkgdir}"/usr/share/man/nl
}

sha512sums=('b15db7bbfb03264f294fb3b42813e17758b43ef7f121e55a24af9ec30ff3f4ec8eb0392db88133e56f9728b3828f6c1432d1e5d2e881a69d083ad444b9177487'
            '255366c8597f015fe718fdae1a913d165d2139f9983ec9ff1933dd44049722aeac267ef87af2f274760ce5da64a18d32fc243af7a915739b0ba46b46145c4ce3')

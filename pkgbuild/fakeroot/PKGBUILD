pkgname=fakeroot
pkgver=1.22
pkgrel=1
pkgdesc="Tool for simulating superuser privileges"
arch=('x86_64')
url="http://packages.debian.org/fakeroot"
license=('GPL')
#depends=()
source=("http://cdn-fastly.deb.debian.org/debian/pool/main/f/fakeroot/fakeroot_${pkgver}.orig.tar.bz2"
	"fakeroot-fixes.patch")

prepare() {
	cd "${srcdir}/fakeroot-${pkgver}"

	patch -Np1 -i "${srcdir}/fakeroot-fixes.patch"
}

build() {
	cd "${srcdir}/fakeroot-${pkgver}"

	./configure --prefix=/usr --libdir=/usr/lib/libfakeroot

	make
}

package() {
	cd "${srcdir}/fakeroot-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/ld.so.conf.d"

	echo "/usr/lib/libfakeroot" > "${pkgdir}/etc/ld.so.conf.d/fakeroot.conf"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
	rm -rf "${pkgdir}"/usr/share/man/nl
}

sha512sums=('193478d9ff88ca2ae69fe47b73b6c61aeb2ba3f2a9e7ed5d455022705857c6f34cdcbe2d3f614c4c4c9509368dc78b9a7b7aa56fb3b50bb3309448140abd796a'
            'bdc0bea42aea45d1ed1088c7d066c7e4a65aba53216790803c1a56fdf5496b438bdefddfcb2905868ae103872bfe184bb7a6fc59a4877127a2024a231677ebac')

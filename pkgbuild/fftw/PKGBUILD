pkgname=fftw
pkgver=3.3.9
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT)"
arch=('x86_64')
url="http://www.fftw.org/"
license=('GPL2')
#depends=()
source=("http://www.fftw.org/fftw-${pkgver}.tar.gz")

build() {
	configure_flags=(--prefix=/usr --enable-openmp
		--enable-shared --enable-threads)

	install -v -dm755 "${srcdir}/build1" "${srcdir}/build2" \
		"${srcdir}/build3"

	cd "${srcdir}/build1"

	"${srcdir}/fftw-${pkgver}/configure" "${configure_flags[@]}" \
		--enable-sse2 --enable-avx

	cd "${srcdir}/build2"

	"${srcdir}/fftw-${pkgver}/configure" "${configure_flags[@]}" \
		--enable-long-double

	cd "${srcdir}/build3"

	"${srcdir}/fftw-${pkgver}/configure" "${configure_flags[@]}" \
		--enable-float --enable-sse --enable-avx

	make -C "${srcdir}/build1"
	make -C "${srcdir}/build2"
	make -C "${srcdir}/build3"
}

package() {
	make -C "${srcdir}/build1" install DESTDIR="${pkgdir}"
	make -C "${srcdir}/build2" install DESTDIR="${pkgdir}"
	make -C "${srcdir}/build3" install DESTDIR="${pkgdir}"
}

sha512sums=('52ebc2a33063a41fd478f6ea2acbf3b511867f736591d273dd57f9dfca5d3e0b0c73157921b3a36f1a7cfd741a8a6bde0fd80de578040ae730ea168b5ba466cf')

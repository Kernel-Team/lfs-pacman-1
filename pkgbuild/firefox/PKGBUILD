pkgname=firefox
pkgver=54.0
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=('x86_64')
url="https://www.mozilla.org/firefox/"
license=('MPL' 'GPL' 'LGPL')
#depends=()
options=(emptydirs !strip)
source=("https://ftp.mozilla.org/pub/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.xz"
	"firefox.desktop"
	"vendor.js"
	"mozconfig")

prepare() {
	cd "${srcdir}/firefox-${pkgver}"

	install -m644 "${srcdir}/mozconfig" mozconfig

	sed -i "s:-\$(MOZ_APP_VERSION)::g" config/baseconfig.mk
}

build() {
	cd "${srcdir}/firefox-${pkgver}"

	unset CFLAGS CXXFLAGS
	export SHELL="/bin/bash"

	make -f client.mk build
}

package() {
	cd "${srcdir}/firefox-${pkgver}"

	make -j1 -f client.mk install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/vendor.js" "${pkgdir}/usr/lib/firefox/browser/defaults/preferences/vendor.js"

	install -v -dm755 "${pkgdir}/usr/lib/mozilla/plugins"
	ln -sfv ../mozilla/plugins "${pkgdir}/usr/lib/firefox/plugins"

	rm -rf "${pkgdir}/usr/lib/firefox/dictionaries"
	ln -sfv ../../share/hunspell "${pkgdir}/usr/lib/firefox/dictionaries"

	for i in 16 22 24 32 48 256
	do
		install -v -Dm644 browser/branding/official/default${i}.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
	done

	install -v -Dm644 browser/branding/official/content/icon64.png "${pkgdir}/usr/share/icons/hicolor/64x64/apps/firefox.png"
	install -v -Dm644 browser/branding/official/mozicon128.png "${pkgdir}/usr/share/icons/hicolor/128x128/apps/firefox.png"
	install -v -Dm644 browser/branding/official/content/about-logo.png "${pkgdir}/usr/share/icons/hicolor/192x192/apps/firefox.png"
	install -v -Dm644 browser/branding/official/content/about-logo@2x.png "${pkgdir}/usr/share/icons/hicolor/384x384/apps/firefox.png"

	install -v -Dm644 "${srcdir}/firefox.desktop" "${pkgdir}/usr/share/applications/firefox.desktop"
}

sha512sums=('0ff6960804e7f6d3e15faeb14b237fee45acae31b4652a6cc5cafa1a1b1eab3537616c3e8ea6d8f3109c87dcc8f86f0df3da2627903b80061c8a62fb11598ed9'
            '0a534d59ac99d3a1e7d4d8b96cb002c3320f6eacf38ee0ba854a9e018a67970cc3bc9cb14a560fd3c1de810d5df10e641be71165ab9de5c8f1d56a2a92124081'
            'e46c15e864eeba68852b9b63917ece651d2f57835ba5978fdae8fff21e38c5a512723744b73578117332720b33e2c338f4bd78fbc2c6967229dfeceb1e0a7877'
            '9236c08c414d11493a6b986d018b4a6855af7824cc2d21525f33fb63c7bcfd2883e7e083ac92aa83d0ebe4930cbffba14be645ecb2c75e4b960eb62ad9b8b7ae')

pkgname=firefox
pkgver=53.0.2
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=('x86_64')
url="https://www.mozilla.org/firefox/"
license=('MPL' 'GPL' 'LGPL')
#depends=()
options=(emptydirs !strip)
source=("https://ftp.mozilla.org/pub/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.xz"
	"firefox.desktop"
	"vendor.js"
	"mozconfig"
	"firefox-rust.patch")

prepare() {
	cd "${srcdir}/firefox-${pkgver}"

	install -m644 "${srcdir}/mozconfig" mozconfig

	sed -i "s:-\$(MOZ_APP_VERSION)::g" config/baseconfig.mk

	patch -Np1 -i "${srcdir}/firefox-rust.patch"
}

build() {
	cd "${srcdir}/firefox-${pkgver}"

	unset CFLAGS CXXFLAGS
	export SHELL="/bin/bash"

	make -f client.mk build
}

package() {
	cd "${srcdir}/firefox-${pkgver}"

	make -j1 -f client.mk install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/vendor.js" "${pkgdir}/usr/lib/firefox/browser/defaults/preferences/vendor.js"

	install -v -dm755 "${pkgdir}/usr/lib/mozilla/plugins"
	ln -sfv ../mozilla/plugins "${pkgdir}/usr/lib/firefox/plugins"

	rm -rf "${pkgdir}/usr/lib/firefox/dictionaries"
	ln -sfv ../../share/hunspell "${pkgdir}/usr/lib/firefox/dictionaries"

	for i in 16 22 24 32 48 256
	do
		install -v -Dm644 browser/branding/official/default${i}.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
	done

	install -v -Dm644 browser/branding/official/content/icon64.png "${pkgdir}/usr/share/icons/hicolor/64x64/apps/firefox.png"
	install -v -Dm644 browser/branding/official/mozicon128.png "${pkgdir}/usr/share/icons/hicolor/128x128/apps/firefox.png"
	install -v -Dm644 browser/branding/official/content/about-logo.png "${pkgdir}/usr/share/icons/hicolor/192x192/apps/firefox.png"
	install -v -Dm644 browser/branding/official/content/about-logo@2x.png "${pkgdir}/usr/share/icons/hicolor/384x384/apps/firefox.png"

	install -v -Dm644 "${srcdir}/firefox.desktop" "${pkgdir}/usr/share/applications/firefox.desktop"
}

md5sums=('bf4643f9a95f13adf8336532490ea3d2'
         '14e0f6237a79b85e60256f4808163160'
         'aa9f776d2187cba09de65cbb02b39ca0'
         '5d8d4f5e713ce38b985f5ff47b463aa9'
         '4d338d1b4b9308284979e3d19401b98d')

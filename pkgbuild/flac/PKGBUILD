pkgbase=flac
pkgname=('flac' 'lib32-flac')
pkgver=1.3.2
pkgrel=1
arch=('x86_64')
url="https://www.xiph.org/flac/"
license=('BSD' 'GPL')
#depends=()
source=("http://downloads.xiph.org/releases/flac/flac-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/flac{,32}-${pkgver}
}

build() {
	cd "${srcdir}/flac-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/flac32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		LDFLAGS="-L/usr/lib32"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/flac-${pkgver}"
	make -C "${srcdir}/flac32-${pkgver}"
}

package_flac() {
	pkgdesc="Free Lossless Audio Codec"

	cd "${srcdir}/flac-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-flac() {
	pkgdesc="Free Lossless Audio Codec (32 bit)"

	cd "${srcdir}/flac32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('63910e8ebbe508316d446ffc9eb6d02efbd5f47d29d2ea7864da9371843c8e671854db6e89ba043fe08aef1845b8ece70db80f1cce853f591ca30d56ef7c3a15')

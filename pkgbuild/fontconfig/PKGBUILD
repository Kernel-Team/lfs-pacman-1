# libalpmhook
pkgname=fontconfig
pkgver=2.12.1
pkgrel=1
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('custom')
groups=('core')
#depends=()
source=("https://www.freedesktop.org/software/fontconfig/release/fontconfig-${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/fontconfig-${pkgver}"

	sed -e '/FC_CHAR_WIDTH/s/CHAR_WIDTH/CHARWIDTH/' \
    		-e '/FC_CHARWIDTH/a #define FC_CHAR_WIDTH FC_CHARWIDTH' \
    		-i fontconfig/fontconfig.h

	sed -i 's/CHAR_WIDTH/CHARWIDTH/' -i src/fcobjs.h
}

build() {
	cd "${srcdir}/fontconfig-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-docs

	make
}

package() {
	cd "${srcdir}/fontconfig-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}"/usr/share/man/man{1,3,5}

	install -v -m644 fc-*/*.1 "${pkgdir}/usr/share/man/man1"
	install -v -m644 doc/*.3 "${pkgdir}/usr/share/man/man3"
	install -v -m644 doc/fonts-conf.5 "${pkgdir}/usr/share/man/man5"
}

md5sums=('b5af5a423ee3b5cfc34846838963c058')

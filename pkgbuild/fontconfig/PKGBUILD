pkgbase=fontconfig
pkgname=('fontconfig' 'lib32-fontconfig')
pkgver=2.13.93
pkgrel=1
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('custom')
#depends=()
options=(emptydirs)
source=("https://www.freedesktop.org/software/fontconfig/release/fontconfig-${pkgver}.tar.xz"
	"fontconfig.hook")

prepare() {
	cd "${srcdir}/fontconfig-${pkgver}"

	rm -f src/fcobjshash.h

	cp -a "${srcdir}"/fontconfig{,32}-${pkgver}
}

build() {
	cd "${srcdir}/fontconfig-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-docs

	cd "${srcdir}/fontconfig32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib32 \
		--disable-docs

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/fontconfig-${pkgver}"
	make -C "${srcdir}/fontconfig32-${pkgver}"
}

package_fontconfig() {
	pkgdesc="A library for configuring and customizing font access"

	cd "${srcdir}/fontconfig-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}"/usr/share/man/man{1,3,5}

	install -v -m644 fc-*/*.1 "${pkgdir}/usr/share/man/man1"
	install -v -m644 doc/*.3 "${pkgdir}/usr/share/man/man3"
	install -v -m644 doc/fonts-conf.5 "${pkgdir}/usr/share/man/man5"

	install -v -Dm644 "${srcdir}/fontconfig.hook" "${pkgdir}/usr/share/libalpm/hooks/fontconfig.hook"
}

package_lib32-fontconfig() {
	pkgdesc="A library for configuring and customizing font access (32 bit)"

	cd "${srcdir}/fontconfig32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('1ba119ea3faba662e108df6fce22f242eb2b7c5ec087159ca0cb76944991b19563f744c181263343941c50547bc0c73c6437d5380df09b5029facaab80465b58'
            '5ec6d8c52273a0edd90e588a3c5079f7bcce63ec8f7032b8f716d698c95d7ac604d9dbee706f7ce1148a8821dda7b44ea787a4f8f5b6c858b583884ee6e024f4')

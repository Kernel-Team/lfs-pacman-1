pkgbase=freeglut
pkgname=('freeglut' 'lib32-freeglut')
pkgver=3.2.1
pkgrel=1
arch=('x86_64')
url="http://freeglut.sourceforge.net/"
license=('LGPL' 'GPL' 'custom')
#depends=()
source=("https://downloads.sourceforge.net/freeglut/freeglut-${pkgver}.tar.gz"
	"freeglut-fixes.patch")

prepare() {
	cd "${srcdir}/freeglut-${pkgver}"
	patch -Np1 -i "${srcdir}/freeglut-fixes.patch"

	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	cd "${srcdir}/build64"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DFREEGLUT_BUILD_STATIC_LIBS=OFF \
		"${srcdir}/freeglut-${pkgver}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib32 \
		-DFREEGLUT_BUILD_STATIC_LIBS=OFF \
		"${srcdir}/freeglut-${pkgver}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/build64"
	make -C "${srcdir}/build32"
}

package_freeglut() {
	pkgdesc="Provides functionality for small OpenGL programs"

	cd "${srcdir}/build64"

	make install DESTDIR="${pkgdir}"
}

package_lib32-freeglut() {
	pkgdesc="Provides functionality for small OpenGL programs (32 bit)"

	cd "${srcdir}/build32"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('aced4bbcd36269ce6f4ee1982e0f9e3fffbf18c94f785d3215ac9f4809b992e166c7ada496ed6174e13d77c0f7ef3ca4c57d8a282e96cbbe6ff086339ade3b08'
            '0845bc4c91c6233cab8c56d15d1efa8fcdac7ae12270a089f8dd20520fc5c94078b7abceadef304f8f42cd575a61ad5b1f444f21e0bbdd3ff310bcc40192336b')

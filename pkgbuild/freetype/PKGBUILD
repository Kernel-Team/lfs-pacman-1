pkgbase=freetype
pkgname=('freetype' 'lib32-freetype')
pkgver=2.10.4
pkgrel=1
arch=('x86_64')
url="http://www.freetype.org/"
license=('GPL')
#depends=()
source=("https://downloads.sourceforge.net/freetype/freetype-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/freetype-${pkgver}"

	sed -ri "s:.*(AUX_MODULES.*valid):\1:" modules.cfg

	sed -ri "s:.*(#.*SUBPIXEL_RENDERING) .*:\1:g" \
		include/freetype/config/ftoption.h

	cp -a "${srcdir}"/freetype{,32}-${pkgver}
}

build() {
	cd "${srcdir}/freetype-${pkgver}"

	./configure --prefix=/usr --enable-freetype-config

	cd "${srcdir}/freetype32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/freetype-${pkgver}"
	make -C "${srcdir}/freetype32-${pkgver}"
}

package_freetype() {
	pkgdesc="TrueType font rendering library"

	cd "${srcdir}/freetype-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-freetype() {
	pkgdesc="TrueType font rendering library (32 bit)"

	cd "${srcdir}/freetype32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('827cda734aa6b537a8bcb247549b72bc1e082a5b32ab8d3cccb7cc26d5f6ee087c19ce34544fa388a1eb4ecaf97600dbabc3e10e950f2ba692617fee7081518f')

pkgbase=freetype
pkgname=('freetype' 'lib32-freetype')
pkgver=2.8.1
pkgrel=1
arch=('x86_64')
url="http://www.freetype.org/"
license=('GPL')
#depends=()
source=("https://downloads.sourceforge.net/freetype/freetype-${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/freetype-${pkgver}"

	sed -ri "s:.*(AUX_MODULES.*valid):\1:" modules.cfg

	sed -ri "s:.*(#.*SUBPIXEL_RENDERING) .*:\1:g" \
		include/freetype/config/ftoption.h

	cp -a "${srcdir}"/freetype{,32}-${pkgver}
}

build() {
	cd "${srcdir}/freetype-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/freetype32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/freetype-${pkgver}"
	make -C "${srcdir}/freetype32-${pkgver}"
}

package_freetype() {
	pkgdesc="TrueType font rendering library"

	cd "${srcdir}/freetype-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-freetype() {
	pkgdesc="TrueType font rendering library (32 bit)"

	cd "${srcdir}/freetype32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/bin/freetype-config "${pkgdir}/usr/bin/freetype-config32"
}

sha512sums=('ca59e47f0fceeeb9b8032be2671072604d0c79094675df24187829c05e99757d0a48a0f8062d4d688e056f783aa8f6090d732ad116562e94784fccf1339eb823')

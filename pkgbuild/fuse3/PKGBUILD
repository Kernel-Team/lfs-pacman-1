pkgname=fuse3
pkgver=3.9.0
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program"
arch=('x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
#depends=()
options=(!zipman)
source=("https://github.com/libfuse/libfuse/releases/download/fuse-${pkgver}/fuse-${pkgver}.tar.xz"
	"fuse.conf")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/fuse-${pkgver}"

	sed -i "33,36d;41,51d" util/install_helper.sh
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		"${srcdir}/fuse-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	install -v -Dm644 "${srcdir}/fuse.conf" "${pkgdir}/etc/fuse.conf"
}

sha512sums=('0c96df5db4d0ceb7885ddb6f6c05b523ee7e179f8f411ad0614caecb9fa0f5fd682ab9bf4dfdaa3aff9d90c18b8947a122ee376328535e49fa6091d784aa0cb7'
            '2d2be62dad148a71d072cff5ae01c9788e7a9a01baad79ae4d696b0192567ac46cdb92a836a2a33cafca3c07b322c970c1f91db35af426bbd105d43d5968b1a0')

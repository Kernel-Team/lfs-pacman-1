pkgname=fuse3
pkgver=3.0.2
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program"
arch=('x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
#depends=()
source=("https://github.com/libfuse/libfuse/releases/download/fuse-${pkgver}/fuse-${pkgver}.tar.gz"
	"fuse.conf")

build() {
	cd "${srcdir}/fuse-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/fuse-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/fuse.conf" "${pkgdir}/etc/fuse.conf"

	rm -rf "${pkgdir}/dev" "${pkgdir}/etc/init.d"
	mv "${pkgdir}"/usr/share/man/man8/mount.fuse{,3}.8
}

sha512sums=('1eced537670dd2337e7df316c9ef7afd70e81af57a39340ac405ed76ec6b2f0da8d7427dab10ed09a6d6eac55984a7dbbe49b93836b733a53c4fd8f3f30aba9f'
            '2d2be62dad148a71d072cff5ae01c9788e7a9a01baad79ae4d696b0192567ac46cdb92a836a2a33cafca3c07b322c970c1f91db35af426bbd105d43d5968b1a0')

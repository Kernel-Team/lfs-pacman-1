pkgname=fuse3
pkgver=3.0.2
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program"
arch=('x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
#depends=()
source=("https://github.com/libfuse/libfuse/releases/download/fuse-${pkgver}/fuse-${pkgver}.tar.gz"
	"fuse.conf")

build() {
	cd "${srcdir}/fuse-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		MOUNT_FUSE_PATH=/sbin \
		UDEV_RULES_PATH=/lib/udev/rules.d

	make
}

package() {
	cd "${srcdir}/fuse-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/fuse.conf" "${pkgdir}/etc/fuse.conf"

	rm -rf "${pkgdir}/dev" "${pkgdir}/etc/init.d"
	mv "${pkgdir}"/usr/share/man/man8/mount.fuse{,3}.8

	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libfuse3.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libfuse3.so") "${pkgdir}/usr/lib/libfuse3.so"
}

md5sums=('51e832a27095ca7c5d835dc0ecb01565'
         '298587592c8444196833f317def414f2')

pkgbase=gc
pkgname=('gc' 'lib32-gc')
pkgver=7.6.6
pkgrel=1
arch=('x86_64')
url="http://www.hboehm.info/gc/"
license=('GPL')
#depends=()
source=("https://github.com/ivmai/bdwgc/releases/download/v${pkgver}/gc-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/gc{,32}-${pkgver}
}

build() {
	cd "${srcdir}/gc-${pkgver}"

	./configure --prefix=/usr --enable-cplusplus

	cd "${srcdir}/gc32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--enable-cplusplus

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/gc-${pkgver}"
	make -C "${srcdir}/gc32-${pkgver}"
}

package_gc() {
	pkgdesc="A garbage collector for C and C++"

	cd "${srcdir}/gc-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/share"

	install -v -Dm644 doc/gc.man "${pkgdir}/usr/share/man/man3/gc_malloc.3"
	ln -sfv gc_malloc.3.gz "${pkgdir}/usr/share/man/man3/gc.3.gz"
}

package_lib32-gc() {
	pkgdesc="A garbage collector for C and C++ (32 bit)"

	cd "${srcdir}/gc32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('85be341132fa460fcad7ca323cf1c594f37e19430269d639ca796b9e3502b0d496822cdccb2b7ca77859f3d19a43165b963142b60fce6b1271ed9a3cccb6620b')

pkgname=('gcc' 'gcc-libs' 'lib32-gcc-libs' 'gcc-gfortran' 'gcc-objc')
pkgbase=gcc
pkgver=6.3.0
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('core')
#depends=()
options=(staticlibs debug !emptydirs)
source=("https://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2"
	"gcc-pure64.patch")

prepare() {
	cd "gcc-${pkgver}"

	patch -Np1 -i "${srcdir}/gcc-pure64.patch"

	sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
	sed -i 's/\(install.*:\) install-.*recursive/\1/' libffi/Makefile.in
	sed -i 's/\(install-data-am:\).*/\1/' libffi/include/Makefile.in

	rm -rf "${srcdir}/src" "${srcdir}/build"

	ln -sfv "gcc-${pkgver}" "${srcdir}/src"

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CC="gcc"
	export CXX="g++"

	export SED=sed

	../src/configure --prefix=/usr \
		--libdir=/usr/lib \
		--disable-bootstrap \
		--disable-cloog-version-check \
		--disable-libssp \
		--disable-libstdcxx-pch \
		--disable-libunwind-exceptions \
		--enable-__cxa_atexit \
		--enable-checking=release \
		--enable-clocale=gnu \
		--enable-gnu-unique-object \
		--enable-install-libiberty \
		--enable-languages=c,c++,fortran,lto,objc,obj-c++ \
		--enable-linker-build-id \
		--enable-lto \
		--enable-multilib \
		--enable-objc-gc \
		--enable-plugin \
		--enable-shared \
		--enable-threads=posix \
		--with-linker-hash-style=gnu \
		--with-pkgversion="Krejzi ${pkgver}" \
		--with-system-zlib

	unset CC CXX SED

	make
}

package_gcc() {
	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)
	_gccver=$(cat ../src/gcc/BASE-VER)

	for d in gcc intl libbacktrace libcc1 libcpp libdecnumber \
		libiberty lto-plugin ${_gccarch}/libgcc
	do
 		make -C ${d} install DESTDIR="${pkgdir}"
	done

	install -v -m644 libiberty/pic/libiberty.a "${pkgdir}/usr/lib"

	install -v -dm755 "${pkgdir}/usr/lib/bfd-plugins"
 	ln -sfv ../../libexec/gcc/${_gccarch}/${_gccver}/liblto_plugin.so "${pkgdir}/usr/lib/bfd-plugins/"

	rm -rf "${pkgdir}"/usr/bin/*gfortran*
	rm -rf "${pkgdir}"/usr/lib{,32}/libgcc_s.so*
	rm -rf "${pkgdir}"/usr/libexec/gcc/x86_64-pc-linux-gnu/6.3.0/{cc1obj,cc1objplus,f951}
	rm -rf "${pkgdir}"/usr/share/info/gfortran.info
	rm -rf "${pkgdir}"/usr/share/man/man1/gfortran.1

	install -v -dm755 "${pkgdir}/lib"
	ln -sfv ../usr/bin/cpp "${pkgdir}/lib/cpp"

	pushd "${pkgdir}/usr/bin"
		ln -f gcc cc
	popd
}

package_gcc-libs() {
	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)

	make -C ${_gccarch}/libgcc install DESTDIR="${pkgdir}"
	rm -rf "${pkgdir}/usr/lib/gcc"

	for d in libatomic libbacktrace libcilkrts libgomp libitm libmpx libsanitizer libstdc++-v3 libvtv
	do
		make -C ${_gccarch}/${d} install DESTDIR="${pkgdir}"
	done

	rm -rf "${pkgdir}/usr/lib32"

	install -v -dm755 "${pkgdir}/lib"
	mv -v "${pkgdir}/usr/lib/libgcc_s.so.1" "${pkgdir}/lib"

	ln -sfv ../../lib/libgcc_s.so.1 "${pkgdir}/usr/lib/libgcc_s.so"

	rm -rf "${pkgdir}"/usr/lib64
	rm -rf "${pkgdir}"/usr/lib/*.py
	rm -rf "${pkgdir}"/usr/share/gcc-*

	chmod -v 755 "${pkgdir}/lib/libgcc_s.so.1"
}

package_lib32-gcc-libs() {
	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)

	for d in boehm-gc libatomic libbacktrace libcilkrts libgcc libgomp libitm libmpx \
		libobjc libquadmath libsanitizer libstdc++-v3 libvtv libgfortran
	do
		make -C ${_gccarch}/32/${d} install DESTDIR="${pkgdir}"
	done

	rm -rf "${pkgdir}"/usr/{include,lib,share}
	rm -rf "${pkgdir}"/usr/lib32/*.py

	chmod -v 755 "${pkgdir}/usr/lib32/libgcc_s.so.1"
}

package_gcc-gfortran() {
	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)
	_gccver=$(cat ../src/gcc/BASE-VER)

	install -v -dm755 "${pkgdir}/usr/bin"
	install -v -dm755 "${pkgdir}/usr/libexec/gcc/${_gccarch}/${_gccver}"
	install -v -dm755 "${pkgdir}/usr/share/info"
	install -v -dm755 "${pkgdir}/usr/share/man/man1"

	install -v -m755 gcc/gfortran "${pkgdir}/usr/bin"
	install -v -m755 gcc/f951 "${pkgdir}/usr/libexec/gcc/${_gccarch}/${_gccver}"
	install -v -m644 gcc/doc/gfortran.info "${pkgdir}/usr/share/info"
	install -v -m644 gcc/doc/gfortran.1 "${pkgdir}/usr/share/man/man1"

	pushd "${pkgdir}/usr/bin"
		ln -f gfortran ${_gccarch}-gfortran
	popd

	for d in libquadmath libgfortran
	do
		make -C ${_gccarch}/${d} install DESTDIR="${pkgdir}"
	done

	rm -rf "${pkgdir}/usr/lib32"
}

package_gcc-objc() {
	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)
	_gccver=$(cat ../src/gcc/BASE-VER)

	install -v -dm755 "${pkgdir}/usr/libexec/gcc/${_gccarch}/${_gccver}"

	install -v -m755 gcc/{cc1obj,cc1objplus} "${pkgdir}/usr/libexec/gcc/${_gccarch}/${_gccver}"

	for d in libobjc boehm-gc
	do
		make -C ${_gccarch}/${d} install DESTDIR="${pkgdir}"
	done

	rm -rf "${pkgdir}/usr/lib32"
}

md5sums=('677a7623c7ef6ab99881bc4e048debb6'
         '8e765eb7c873eda6dd9552add6d8d624')

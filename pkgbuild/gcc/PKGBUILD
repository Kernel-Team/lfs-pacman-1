pkgname=('gcc' 'gcc-libs' 'lib32-gcc-libs' 'gcc-ada')
pkgbase=gcc
pkgver=7.2.0
_pkgver=${pkgver}
_gnatver=2017
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
#depends=()
options=(staticlibs debug)
source=("https://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.xz"
#source=("ftp://ftp.gwdg.de/pub/misc/gcc/snapshots/${_pkgver}/gcc-${_pkgver}.tar.xz"
	"gnat-gpl-${_gnatver}-x86_64-linux-bin.tar.gz::http://mirrors.cdn.adacore.com/art/591c6d80c7a447af2deed1d7"
	"gcc-pure64.patch")

# GNAT available from: http://libre.adacore.com/download/configurations

gnat_available=0
test -e "/usr/bin/gnatmake" && gnat_available=1

prepare() {
	cd "${srcdir}/gcc-${_pkgver}"

	patch -Np1 -i "${srcdir}/gcc-pure64.patch"

	sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
	sed -i 's/\(install.*:\) install-.*recursive/\1/' libffi/Makefile.in
	sed -i 's/\(install-data-am:\).*/\1/' libffi/include/Makefile.in

	rm -rf "${srcdir}/src" "${srcdir}/build"

	ln -sfv "gcc-${_pkgver}" "${srcdir}/src"

	install -v -dm755 "${srcdir}/build"

	if [ "${gnat_available}" == "0" ]
	then
		cd "${srcdir}/gnat-gpl-${_gnatver}-x86_64-linux-bin"
		make ins-all prefix="${srcdir}/build/gnat-bin"

		find "${srcdir}/build/gnat-bin" -name ld -exec mv -v {} {}.old \;
		find "${srcdir}/build/gnat-bin" -name as -exec mv -v {} {}.old \;
	fi
}

build() {
	cd "${srcdir}/build"

	export CC="gcc"
	export CXX="g++"

	export SED=sed

	enable_bootstrap="--disable-bootstrap"

	if [ "${gnat_available}" == "0" ]
	then
		export CFLAGS=${CFLAGS/-march=skylake/-march=native}
		export CXXFLAGS=${CXXFLAGS/-march=skylake/-march=native}
		export PATH="${srcdir}/build/gnat-bin/bin:$PATH"

		enable_bootstrap="--enable-bootstrap"
	fi

	../src/configure --prefix=/usr \
		--libdir=/usr/lib \
		${enable_bootstrap} \
		--disable-cloog-version-check \
		--disable-libssp \
		--disable-libstdcxx-pch \
		--disable-libunwind-exceptions \
		--enable-__cxa_atexit \
		--enable-checking=release \
		--enable-clocale=gnu \
		--enable-gnu-unique-object \
		--enable-install-libiberty \
		--enable-languages=c,c++,ada,fortran,lto,objc,obj-c++ \
		--enable-linker-build-id \
		--enable-lto \
		--enable-multilib \
		--enable-objc-gc \
		--enable-plugin \
		--enable-shared \
		--enable-threads=posix \
		--with-linker-hash-style=gnu \
		--with-pkgversion="Krejzi ${pkgver/+/ }" \
		--with-system-zlib

	unset CC CXX SED

	make
	make -j1 -k install DESTDIR="${srcdir}/fakeinstall"
}

package_gcc() {
	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)
	_gccver=$(cat ../src/gcc/BASE-VER)

	cp -r "${srcdir}"/fakeinstall/* "${pkgdir}"

	if [ -e "${pkgdir}/usr/lib64" ]
	then
		mv "${pkgdir}"/usr/lib64/* "${pkgdir}/usr/lib"
		rm -rf "${pkgdir}/usr/lib64"
	fi

	install -v -m644 libiberty/pic/libiberty.a "${pkgdir}/usr/lib"

	mv "${pkgdir}"/usr/lib/libcc1.so* "${pkgdir}"
	rm -rf "${pkgdir}"/usr/lib/*.so* "${pkgdir}"/usr/lib32/*.so*
	mv "${pkgdir}"/libcc1.so* "${pkgdir}/usr/lib"

	install -v -dm755 "${pkgdir}/usr/lib/bfd-plugins"
 	ln -sfv ../../libexec/gcc/${_gccarch}/${_gccver}/liblto_plugin.so "${pkgdir}/usr/lib/bfd-plugins/"

	ln -sfv ../bin/cpp "${pkgdir}/usr/lib/cpp"

	pushd "${pkgdir}/usr/bin"
		ln -f gcc cc
	popd

	# gcc-ada package
	rm -rf "${pkgdir}"/usr/bin/gnat*
	rm -rf "${pkgdir}"/usr/lib/gcc/${_gccarch}/${_gccver}/{adalib,adainclude}
	rm -rf "${pkgdir}"/usr/lib/gcc/${_gccarch}/${_gccver}/32/{adalib,adainclude}
	rm -rf "${pkgdir}"/usr/libexec/gcc/${_gccarch}/${_gccver}/gnat1
	rm -rf "${pkgdir}"/usr/share/info/gnat*.info

	# gcc-libs package
	rm -rf "${pkgdir}"/usr/share/info/lib{gomp,itm,quadmath}.info
	rm -rf "${pkgdir}"/usr/share/locale/*/LC_MESSAGES/libstdc++.mo
}

package_gcc-libs() {
	pkgdesc+=" - Shared libraries"

	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)

	install -v -dm755 "${pkgdir}/lib" "${pkgdir}/usr/lib"

	cp -r "${srcdir}"/fakeinstall/usr/lib/*.so* "${pkgdir}/usr/lib"
	rm -rf "${pkgdir}"/usr/lib/libcc1.so*
	rm -rf "${pkgdir}"/usr/lib/*.py

	for l in libgomp libitm libquadmath
	do
		make -C ${_gccarch}/${l} install-info DESTDIR="${pkgdir}"
	done

	make -C ${_gccarch}/libstdc++-v3/po install DESTDIR="${pkgdir}"

	chmod -v 755 "${pkgdir}/usr/lib/libgcc_s.so.1"
}

package_lib32-gcc-libs() {
	pkgdesc+=" - Shared libraries (32 bit)"

	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)

	install -v -dm755 "${pkgdir}/usr/lib32"

	cp -r "${srcdir}"/fakeinstall/usr/lib32/*.so* "${pkgdir}/usr/lib32"
	rm -rf "${pkgdir}"/usr/lib32/*.py

	chmod -v 755 "${pkgdir}/usr/lib32/libgcc_s.so.1"
}

package_gcc-ada() {
	pkgdesc+=" - ADA Compiler"

	cd "${srcdir}/build"

	_gccarch=$(gcc/xgcc -dumpmachine)
	_gccver=$(cat ../src/gcc/BASE-VER)

	make -C gcc ada.install-common DESTDIR="${pkgdir}"
	make -C gcc ada.install-info DESTDIR="${pkgdir}"

	install -v -Dm755 gcc/gnat1 "${pkgdir}/usr/libexec/gcc/${_gccarch}/${_gccver}/gnat1"

	make -C ${_gccarch}/32/libada INSTALL="install" INSTALL_DATA="install -m644" \
		install-gnatlib DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/lib32"

	mv "${pkgdir}"/usr/lib/gcc/${_gccarch}/${_gccver}/adalib/*.so "${pkgdir}/usr/lib"
	mv "${pkgdir}"/usr/lib/gcc/${_gccarch}/${_gccver}/32/adalib/*.so "${pkgdir}/usr/lib32"
}

sha512sums=('f853cd6530b4055d8d8289da74687cb4c6d5f363598d386332d31852b581bac76c3adb7d61889edec3b779f63d8646f0122840f12965ce4a4389ba535dbbb6e1'
            '01a8f3be9b7b7e83cc5bd4a45137b1d35c6448bc22a88bcaf5d312cd63e11081b6b2fe9f09ad2a27d8f0b6471fc5c1a99573bf3bcee1589329054074eaeef04f'
            'bebd4c4ed025afb4759b833c2c399221f0d23502abacf44b1173a699be5c76f01ba91813dcbb4003affdfa47844554bad217281be4a763723268f18e99eea316')

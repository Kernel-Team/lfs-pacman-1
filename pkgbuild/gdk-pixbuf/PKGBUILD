pkgname=gdk-pixbuf
pkgver=2.40.0
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GdkPixbuf"
license=('LGPL2.1')
#depends=()
source=("https://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-${pkgver}.tar.xz"
	"gdk-pixbuf-query-loaders.hook")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/gdk-pixbuf-${pkgver}"

	sed -i 's#from "@filename@"##' gdk-pixbuf/gdk-pixbuf-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Djasper=true \
		-Ddocs=true \
		-Dinstalled_tests=false \
		"${srcdir}/gdk-pixbuf-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	install -v -Dm644 "${srcdir}/gdk-pixbuf-query-loaders.hook" "${pkgdir}/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook"
}

sha512sums=('6512befd379494dbfd89a16fc4c92641842eb7b82fc820ec83a5b057526209947db646570db1124c073b0ef69c117bdf0f8d3fea807e302a4950db39187c35aa'
            'a3d558fb92a0802307427f5c936ca4b86dd966acb96978f247f64cac54b434657cc9b952cece266b992d22655bb658129025718b29beb380a4a0429c34fdd417')

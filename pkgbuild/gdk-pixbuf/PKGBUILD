pkgname=gdk-pixbuf
pkgver=2.36.12
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GdkPixbuf"
license=('LGPL2.1')
#depends=()
source=("https://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-${pkgver}.tar.xz"
	"gdk-pixbuf-query-loaders.hook")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/gdk-pixbuf-${pkgver}"

	sed -i 's#from "@filename@"##' gdk-pixbuf/gdk-pixbuf-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_jasper=true \
		-Dwith_docs=true \
		"${srcdir}/gdk-pixbuf-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	install -v -Dm644 "${srcdir}/gdk-pixbuf-query-loaders.hook" "${pkgdir}/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook"
}

sha512sums=('5d3bd620cf464bf92079b15c78f8400db48c427053c6eeedc4ea9652a4c290a09a26310100cc7eb487daf4b565df9f7e3a6edf14685f81ddbff6a0652fb6b41b'
            'a3d558fb92a0802307427f5c936ca4b86dd966acb96978f247f64cac54b434657cc9b952cece266b992d22655bb658129025718b29beb380a4a0429c34fdd417')

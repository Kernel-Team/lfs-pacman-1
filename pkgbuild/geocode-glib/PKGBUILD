pkgname=geocode-glib
pkgver=3.26.1
pkgrel=1
pkgdesc="Helper library for geocoding services"
arch=('x86_64')
url="https://git.gnome.org/browse/geocode-glib/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/geocode-glib/${pkgver:0:4}/geocode-glib-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/geocode-glib-${pkgver}"

	sed -i 's#from "@filename@"##' geocode-glib/geocode-enum-types.h.in
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable-gtk-doc=true \
		-Denable-installed-tests=false \
		"${srcdir}/geocode-glib-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('0eb8e8a668c3950d06e77a379d375e2297cf7d95435aa8212715ddc36c3a57a53192e75dd0c82a2f2e7e558ea0e903a477c9dfa54f8cd2805d048fe46265d621')

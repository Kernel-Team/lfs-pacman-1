pkgname=ghostscript
pkgver=9.53.3
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
url="https://ghostscript.com/"
license=('AGPL' 'custom')
#depends=()
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${pkgver//[-.]/}/ghostscript-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/ghostscript-${pkgver}"

	rm -rf cups/libs ijs jpeg lcms2mt libpng openjpeg tiff zlib
}

build() {
	cd "${srcdir}/ghostscript-${pkgver}"

	./configure --prefix=/usr \
		--enable-dynamic \
		--disable-compile-inits \
		--disable-gtk \
		--with-drivers=ALL \
		--with-system-libtiff \
		--without-libidn \
		--with-fontpath=/usr/share/fonts/ghostscript:/usr/share/fonts

	make so all
}

package() {
	cd "${srcdir}/ghostscript-${pkgver}"

	make install soinstall DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/bin/*.sh "${pkgdir}/usr/bin/gsx"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('eb832c27eecd30f15e346408c592d7096fd23ef0a6fa59bd50ca327578915434530a4868e69249c2594def0910c527302e99d54f0877f726a8ca8bea6f0f17b7')

pkgname=ghostscript
pkgver=9.27
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
url="https://ghostscript.com/"
license=('AGPL' 'custom')
#depends=()
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${pkgver/./}/ghostscript-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/ghostscript-${pkgver}"

	rm -rf cups/libs ijs jpeg lcms2mt libpng openjpeg tiff zlib
}

build() {
	cd "${srcdir}/ghostscript-${pkgver}"

	./configure --prefix=/usr \
		--enable-dynamic \
		--disable-compile-inits \
		--disable-gtk \
		--with-drivers=ALL \
		--with-system-libtiff \
		--without-libidn \
		--with-fontpath=/usr/share/fonts/ghostscript:/usr/share/fonts

	make so all
}

package() {
	cd "${srcdir}/ghostscript-${pkgver}"

	make install soinstall DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/bin/*.sh "${pkgdir}/usr/bin/gsx"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('5e67ad45a80f01c6ef0eabb1c76dfa8fb6e7f0fde8d82fd5daaf12f370c288a672f8fa69c74d9e30255582267e9a906e4e8b13655f8d993fefdfc8dbdb5d5401')

pkgname=ghostscript
pkgver=9.26
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
url="https://ghostscript.com/"
license=('AGPL' 'custom')
#depends=()
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${pkgver/./}/ghostscript-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/ghostscript-${pkgver}"

	rm -rf cups/libs ijs jpeg lcms2mt libpng openjpeg tiff zlib
}

build() {
	cd "${srcdir}/ghostscript-${pkgver}"

	./configure --prefix=/usr \
		--enable-dynamic \
		--disable-compile-inits \
		--disable-gtk \
		--with-drivers=ALL \
		--with-system-libtiff \
		--without-libidn \
		--with-fontpath=/usr/share/fonts/ghostscript:/usr/share/fonts

	make so all
}

package() {
	cd "${srcdir}/ghostscript-${pkgver}"

	make install soinstall DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/bin/*.sh "${pkgdir}/usr/bin/gsx"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('3ddb83029edf32282357bf606f4045a9ac73df6543cd423cfad09158ec12ada083a0dbb5aac3b73ae24cbc6c1e9d7574257a5c1fae63ba8776fbb00150ef2a3e')

pkgname=git
pkgver=2.17.0
pkgrel=1
pkgdesc="The fast distributed version control system"
arch=('x86_64')
url="http://git-scm.com/"
license=('GPL2')
#depends=()
install=git.install
source=("https://www.kernel.org/pub/software/scm/git/git-${pkgver}.tar.xz"
	"https://www.kernel.org/pub/software/scm/git/git-manpages-${pkgver}.tar.xz"
	"git-daemon@.service"
	"git-daemon.socket"
	"git.sysusers")

noextract=("git-manpages-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/git-${pkgver}"

	sed -i "/CC/s#cc#${CC}#g;s#-g -O2#${CFLAGS}#g" Makefile

	make prefix=/usr CC="${CC}" CFLAGS="${CFLAGS}" USE_LIBPCRE2=YesPlease \
		NO_CROSS_DIRECTORY_HARDLINKS=1 INSTALLDIRS=vendor V=1

	make -C contrib/credential/gnome-keyring CC="${CC}" CFLAGS="${CFLAGS}" V=1
	make -C contrib/credential/libsecret CC="${CC}" CFLAGS="${CFLAGS}" V=1
}

package() {
	cd "${srcdir}/git-${pkgver}"

	make prefix=/usr CC="${CC}" CFLAGS="${CFLAGS}" USE_LIBPCRE2=YesPlease \
		NO_CROSS_DIRECTORY_HARDLINKS=1 INSTALLDIRS=vendor install \
		DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/share/man"

	bsdtar --no-same-owner --no-same-permissions -xf "${srcdir}/git-manpages-${pkgver}.tar.xz" -C "${pkgdir}/usr/share/man"

	find "${pkgdir}/usr/share/man" -type d -exec chmod 755 {} \;

	install -v -Dm755 contrib/credential/gnome-keyring/git-credential-gnome-keyring \
		"${pkgdir}/usr/libexec/git-core/git-credential-gnome-keyring"

	install -v -Dm755 contrib/credential/libsecret/git-credential-libsecret \
		"${pkgdir}/usr/libexec/git-core/git-credential-libsecret"

	install -v -Dm644 "${srcdir}/git-daemon@.service" "${pkgdir}/usr/lib/systemd/system/git-daemon@.service"
	install -v -Dm644 "${srcdir}/git-daemon.socket" "${pkgdir}/usr/lib/systemd/system/git-daemon.socket"

	install -v -Dm644 "${srcdir}/git.sysusers" "${pkgdir}/usr/lib/sysusers.d/git.conf"

	rm -rf "${pkgdir}/usr/lib/perl"
}

sha512sums=('dab1c1d5d384b36720abc049a66ba60631e17958b214cfbec467be7adc02e82190e5282554da71797892c16bfe52d65b6244a281f504385083125bcb98ec7ee9'
            'b2f7734416f5c336cea272baa87efd10b175a357f5f722f20bb5d287b1886bdb213c4fc2f0c6a3b83fa34423e83139bab5eac70dc9669a72c75c2392dbd517f1'
            '75d189f4f7165631004789efe4de2df2fc8a76ceb2fa90b29cd20561b660f1c4f888f0a801d0adeb2bf2adeddd101af231253c96c0cf00ef83a2500fe93a7ce1'
            'bd4aff421e547044a2a91b8a77c86ce14f05321008aa2e28aab35154b297803ca716ccba3e0fca3805033d4adb455adb41086ceeca98200b8006582c13f2c7d3'
            'fda8e919c1e998685364815e06bc549a2357d6519c8653e59f017648fe3a5b573823bef049fd48fa113728c010e613776a6ac78b4259d958649f6facc87d4ceb')

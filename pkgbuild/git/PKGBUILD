pkgname=git
pkgver=2.16.1
pkgrel=1
pkgdesc="The fast distributed version control system"
arch=('x86_64')
url="http://git-scm.com/"
license=('GPL2')
#depends=()
install=git.install
source=("https://www.kernel.org/pub/software/scm/git/git-${pkgver}.tar.xz"
	"https://www.kernel.org/pub/software/scm/git/git-manpages-${pkgver}.tar.xz"
	"git-daemon@.service"
	"git-daemon.socket"
	"git.sysusers")

noextract=("git-manpages-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/git-${pkgver}"

	sed -i "/CC/s#cc#${CC}#g;s#-g -O2#${CFLAGS}#g" Makefile

	make prefix=/usr CC="${CC}" CFLAGS="${CFLAGS}" USE_LIBPCRE2=YesPlease \
		NO_CROSS_DIRECTORY_HARDLINKS=1 INSTALLDIRS=vendor V=1

	make -C contrib/credential/gnome-keyring CC="${CC}" CFLAGS="${CFLAGS}" V=1
	make -C contrib/credential/libsecret CC="${CC}" CFLAGS="${CFLAGS}" V=1
}

package() {
	cd "${srcdir}/git-${pkgver}"

	make prefix=/usr CC="${CC}" CFLAGS="${CFLAGS}" USE_LIBPCRE2=YesPlease \
		NO_CROSS_DIRECTORY_HARDLINKS=1 INSTALLDIRS=vendor install \
		DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/share/man"

	bsdtar xf "${srcdir}/git-manpages-${pkgver}.tar.xz" -C "${pkgdir}/usr/share/man"

	find "${pkgdir}/usr/share/man" -type d -exec chmod 755 {} \;

	install -v -Dm755 contrib/credential/gnome-keyring/git-credential-gnome-keyring \
		"${pkgdir}/usr/libexec/git-core/git-credential-gnome-keyring"

	install -v -Dm755 contrib/credential/libsecret/git-credential-libsecret \
		"${pkgdir}/usr/libexec/git-core/git-credential-libsecret"

	install -v -Dm644 "${srcdir}/git-daemon@.service" "${pkgdir}/usr/lib/systemd/system/git-daemon@.service"
	install -v -Dm644 "${srcdir}/git-daemon.socket" "${pkgdir}/usr/lib/systemd/system/git-daemon.socket"

	install -v -Dm644 "${srcdir}/git.sysusers" "${pkgdir}/usr/lib/sysusers.d/git.conf"

	rm -rf "${pkgdir}/usr/lib/perl"
}

sha512sums=('09e30ada7bcf41f78f15784908062c10162d58d2fd7a68ad27e42fd49dc37eb0e82c19ad21db31034a189a8cdd009013a29f943e1ae5d5ab5688c913d2a4d42a'
            '20c7646b5f1e3c633ce6aae5ee5fcbe4ca1e59fc9253ec9a9e45c7cd8a8ac7a5e71e36258eb747e3bd6d51b3b7971137315dcdbdff493e0f8774ecd3e309735a'
            '75d189f4f7165631004789efe4de2df2fc8a76ceb2fa90b29cd20561b660f1c4f888f0a801d0adeb2bf2adeddd101af231253c96c0cf00ef83a2500fe93a7ce1'
            'bd4aff421e547044a2a91b8a77c86ce14f05321008aa2e28aab35154b297803ca716ccba3e0fca3805033d4adb455adb41086ceeca98200b8006582c13f2c7d3'
            'fda8e919c1e998685364815e06bc549a2357d6519c8653e59f017648fe3a5b573823bef049fd48fa113728c010e613776a6ac78b4259d958649f6facc87d4ceb')

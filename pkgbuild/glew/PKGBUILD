pkgname=glew
pkgver=2.0.0
pkgrel=1
pkgdesc="The OpenGL Extension Wrangler Library"
arch=('x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
#depends=()
source=("https://downloads.sourceforge.net/glew/glew-${pkgver}.tgz")

prepare() {
	cd "${srcdir}/glew-${pkgver}"

	sed -i "s#lib64#lib#" config/Makefile.linux-clang
	sed -i "/^.PHONY: .*\.pc$/d" Makefile
}

build() {
	cd "${srcdir}/glew-${pkgver}"

	make POPT="${CFLAGS}" SYSTEM="linux-clang"
}

package() {
	cd "${srcdir}/glew-${pkgver}"

	make POPT="${CFLAGS}" SYSTEM="linux-clang" install.all GLEW_DEST="${pkgdir}/usr"

	chmod -v 755 "${pkgdir}/usr/lib/libGLEW.so"
}

sha512sums=('e9bcd5f19a4495ce6511dfd76e64b4e4d958603c513ee9063eb9fe24fc6e0413f168620661230f1baef558f2f907cef7fe7ab2bdf957a6f7bda5fe96e9319c6a')

pkgname=glib
pkgver=2.50.3
pkgrel=1
pkgdesc="Low level core library"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/glib/${pkgver:0:4}/glib-${pkgver}.tar.xz"
	"glib-compile-schemas-quiet.patch"
	"gio-querymodules.hook"
	"glib-compile-schemas.hook")

prepare() {
	cd "${srcdir}/glib-${pkgver}"

	patch -Np1 -i "${srcdir}/glib-compile-schemas-quiet.patch"
}

build() {
	cd "${srcdir}/glib-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-pcre=system \
		--with-python=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/glib-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/share/gdb"
	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libglib-2.0.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libglib-2.0.so") "${pkgdir}/usr/lib/libglib-2.0.so"

	install -v -Dm644 "${srcdir}/gio-querymodules.hook" "${pkgdir}/usr/share/libalpm/hooks/gio-querymodules.hook"
	install -v -Dm644 "${srcdir}/glib-compile-schemas.hook" "${pkgdir}/usr/share/libalpm/hooks/glib-compile-schemas.hook"
}

md5sums=('381ab22934f296750d036aa55a397ded'
         '9a2c5949a8d352c5887c099ed3a9003d'
         'a52ea506b5cf162a4f72d09cc33fae50'
         '9e25d7de10bc4dc105f171e9ae46ef69')

pkgbase=glib
pkgname=('glib' 'lib32-glib')
pkgver=2.54.0
pkgrel=1
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
#depends=()
options=(emptydirs)
source=("https://download.gnome.org/sources/glib/${pkgver:0:4}/glib-${pkgver}.tar.xz"
	"glib-compile-schemas-quiet.patch"
	"gio-querymodules.hook"
	"gio-querymodules32.hook"
	"glib-compile-schemas.hook")

prepare() {
	cd "${srcdir}/glib-${pkgver}"

	patch -Np1 -i "${srcdir}/glib-compile-schemas-quiet.patch"

	cp -a "${srcdir}"/glib{,32}-${pkgver}
}

build() {
	cd "${srcdir}/glib-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-pcre=system \
		--enable-man \
		--with-python=/usr/bin/python3

	cd "${srcdir}/glib32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib32 \
		--with-pcre=system \
		--with-python=/usr/bin/python3

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/glib-${pkgver}"
	make -C "${srcdir}/glib32-${pkgver}"
}

package_glib() {
	pkgdesc="Low level core library"

	cd "${srcdir}/glib-${pkgver}"

	make install DESTDIR="${pkgdir}" PKG_CONFIG_PATH="${PWD}"

	rm -rf "${pkgdir}/usr/share/gdb"

	install -v -Dm644 "${srcdir}/gio-querymodules.hook" "${pkgdir}/usr/share/libalpm/hooks/gio-querymodules.hook"
	install -v -Dm644 "${srcdir}/glib-compile-schemas.hook" "${pkgdir}/usr/share/libalpm/hooks/glib-compile-schemas.hook"
}

package_lib32-glib() {
	pkgdesc="Low level core library (32 bit)"

	cd "${srcdir}/glib32-${pkgver}"

	make install DESTDIR="${PWD}/dest" PKG_CONFIG_PATH="${PWD}"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/bin/gio-querymodules "${pkgdir}/usr/bin/gio-querymodules32"

	install -v -Dm644 "${srcdir}/gio-querymodules32.hook" "${pkgdir}/usr/share/libalpm/hooks/gio-querymodules32.hook"
}

sha512sums=('c2da947beb23ff93df0313613743e5941b0b734d0e9405ccc1e1d88d8f7cfb16e6467d8a540cdff8e497fb854c84743f257e2537a7b765d6593457f98531596f'
            'e9a7dc8f9d78630c32a99181b556b5a4ef70c08ab059a23cf148d7084d81a82d48994ecd91f6f7bed9cbe34a1bd84fff3d9218984474b54e3da5cdec9923c705'
            '5afd6f275c8fff16df3e685818f2e7989b39ffb3b8f5fc261a5a6d54a9b28ef53af62f3bf5067cf87cb74691572f85730cbc508691956ae048a0f3ecc1a0a39c'
            '1ea3011bbf658c71fce167399775cca4ec7fd6267cbea9dc39555c10636035dd56391208a56c35e84f127ad23fd7126e025ea8e7de9ba5cd6912ac984a8b60af'
            'c04fe25afc217c295b5ce4034733cec046126482d00fb8d0299e4815ac57129dd3f1c9ac824b9386d208a4f113e9dae682ea5b72f75387ed6b6b96a9cbbee8ca')

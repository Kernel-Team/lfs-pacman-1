pkgbase=glib
pkgname=('glib' 'lib32-glib')
pkgver=2.53.6
pkgrel=1
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
#depends=()
options=(emptydirs)
#source=("https://download.gnome.org/sources/glib/${pkgver:0:4}/glib-${pkgver}.tar.xz"
source=("git+https://github.com/GNOME/glib.git#commit=9ff6f24"
	"glib-compile-schemas-quiet.patch"
	"gio-querymodules.hook"
	"gio-querymodules32.hook"
	"glib-compile-schemas.hook"
	"0001-docs-Fix-building-with-meson.patch"
	"0001-meson-Compile-guuid.c-into-libglib-2.0.patch")

prepare() {
	cd "${srcdir}"

	rm -rf glib-${pkgver} glib32-${pkgver}
	cp -a glib glib-${pkgver}

	cd "${srcdir}/glib-${pkgver}"

	chmod +x glib-gettextize.in gio/gdbus-2.0/codegen/gdbus-codegen.in

	patch -Np1 -i "${srcdir}/glib-compile-schemas-quiet.patch"
	patch -Np1 -i "${srcdir}/0001-docs-Fix-building-with-meson.patch"
	patch -Np1 -i "${srcdir}/0001-meson-Compile-guuid.c-into-libglib-2.0.patch"

	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	cd "${srcdir}/build64"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dwith-docs=yes \
		-Dwith-man=yes \
		-Dwith-pcre=system \
		"${srcdir}/glib-${pkgver}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--libdir /usr/lib32 \
		--buildtype=plain \
		-Dwith-docs=no \
		-Dwith-man=no \
		-Dwith-pcre=system \
		"${srcdir}/glib-${pkgver}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	ninja -C build64 ${MAKEFLAGS}
	ninja -C build32 ${MAKEFLAGS}
}

package_glib() {
	pkgdesc="Low level core library"

	cd "${srcdir}/build64"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/share/gdb"

	install -v -Dm644 "${srcdir}/gio-querymodules.hook" "${pkgdir}/usr/share/libalpm/hooks/gio-querymodules.hook"
	install -v -Dm644 "${srcdir}/glib-compile-schemas.hook" "${pkgdir}/usr/share/libalpm/hooks/glib-compile-schemas.hook"
}

package_lib32-glib() {
	pkgdesc="Low level core library (32 bit)"

	cd "${srcdir}/build32"

	DESTDIR="${PWD}/dest" ninja install

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/bin/gio-querymodules "${pkgdir}/usr/bin/gio-querymodules32"

	install -v -Dm644 "${srcdir}/gio-querymodules32.hook" "${pkgdir}/usr/share/libalpm/hooks/gio-querymodules32.hook"
}

sha512sums=('SKIP'
            'e9a7dc8f9d78630c32a99181b556b5a4ef70c08ab059a23cf148d7084d81a82d48994ecd91f6f7bed9cbe34a1bd84fff3d9218984474b54e3da5cdec9923c705'
            '5afd6f275c8fff16df3e685818f2e7989b39ffb3b8f5fc261a5a6d54a9b28ef53af62f3bf5067cf87cb74691572f85730cbc508691956ae048a0f3ecc1a0a39c'
            '1ea3011bbf658c71fce167399775cca4ec7fd6267cbea9dc39555c10636035dd56391208a56c35e84f127ad23fd7126e025ea8e7de9ba5cd6912ac984a8b60af'
            'c04fe25afc217c295b5ce4034733cec046126482d00fb8d0299e4815ac57129dd3f1c9ac824b9386d208a4f113e9dae682ea5b72f75387ed6b6b96a9cbbee8ca'
            '572149580d987c905c0b2c587cd369d77cd4de81ae4f951159f661c558dc2bff6feb5bcf90859db8a32e43ee2997b2076f5d57a47500cc3449b844eda33d63b0'
            'f072e0750fc208e5710e417aec725645b9106e2dd680cf22f2a82aeed7b49a774859d4d3685c29c579b999d456a1aa293f6e0b092cb33fe3c76f7509b724ea81')

pkgbase=glib
pkgname=('glib' 'lib32-glib')
pkgver=2.52.3
pkgrel=1
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
#depends=()
options=(emptydirs)
source=("https://download.gnome.org/sources/glib/${pkgver:0:4}/glib-${pkgver}.tar.xz"
	"glib-compile-schemas-quiet.patch"
	"gio-querymodules.hook"
	"gio-querymodules32.hook"
	"glib-compile-schemas.hook")

prepare() {
	cd "${srcdir}/glib-${pkgver}"

	patch -Np1 -i "${srcdir}/glib-compile-schemas-quiet.patch"

	cp -a "${srcdir}"/glib{,32}-${pkgver}
}

build() {
	cd "${srcdir}/glib-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-pcre=system \
		--with-python=/usr/bin/python3

	cd "${srcdir}/glib32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib32 \
		--with-pcre=system \
		--with-python=/usr/bin/python3

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/glib-${pkgver}"
	make -C "${srcdir}/glib32-${pkgver}"
}

package_glib() {
	pkgdesc="Low level core library"

	cd "${srcdir}/glib-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/share/gdb"
	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libglib-2.0.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libglib-2.0.so") "${pkgdir}/usr/lib/libglib-2.0.so"

	install -v -Dm644 "${srcdir}/gio-querymodules.hook" "${pkgdir}/usr/share/libalpm/hooks/gio-querymodules.hook"
	install -v -Dm644 "${srcdir}/glib-compile-schemas.hook" "${pkgdir}/usr/share/libalpm/hooks/glib-compile-schemas.hook"
}

package_lib32-glib() {
	pkgdesc="Low level core library (32 bit)"

	cd "${srcdir}/glib32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/bin/gio-querymodules "${pkgdir}/usr/bin/gio-querymodules32"

	install -v -Dm644 "${srcdir}/gio-querymodules32.hook" "${pkgdir}/usr/share/libalpm/hooks/gio-querymodules32.hook"
}

sha512sums=('a068f2519cfb82de8d4b7f004e7c1f15e841cad4046430a83b02b359d011e0c4077cdff447a1687ed7c68f1a11b4cf66b9ed9fc23ab5f0c7c6be84eb0ddc3017'
            'eb248c6be91b918369d74a21ecfc74a8ad8f77ff46d1ca62b192ad03a9fcadd472b10ccaea0197cbab7ed23d6fd5ff166399de125c151f8a95dd56855854905d'
            '5afd6f275c8fff16df3e685818f2e7989b39ffb3b8f5fc261a5a6d54a9b28ef53af62f3bf5067cf87cb74691572f85730cbc508691956ae048a0f3ecc1a0a39c'
            '1ea3011bbf658c71fce167399775cca4ec7fd6267cbea9dc39555c10636035dd56391208a56c35e84f127ad23fd7126e025ea8e7de9ba5cd6912ac984a8b60af'
            'c04fe25afc217c295b5ce4034733cec046126482d00fb8d0299e4815ac57129dd3f1c9ac824b9386d208a4f113e9dae682ea5b72f75387ed6b6b96a9cbbee8ca')

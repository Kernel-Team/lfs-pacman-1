pkgname=('glibc' 'lib32-glibc')
pkgbase=glibc
pkgver=2.25
pkgrel=1
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL' 'LGPL')
#depends=()
options=(staticlibs debug emptydirs)
source=("https://ftp.gnu.org/gnu/glibc/glibc-${pkgver}.tar.xz"
	"glibc.conf"
	"locale-gen"
	"locale.gen"
	"glibc-paths.patch"
	"glibc-pure64.patch")

prepare() {
	cd "glibc-${pkgver}"

	patch -Np1 -i "${srcdir}/glibc-paths.patch"
	patch -Np1 -i "${srcdir}/glibc-pure64.patch"

	rm -rf "${srcdir}/src" "${srcdir}/build64" "${srcdir}/build32"

	ln -sfv "glibc-${pkgver}" "${srcdir}/src"

	install -v -dm755 "${srcdir}/build64"
	install -v -dm755 "${srcdir}/build32"
}

build() {
	local configure_flags=(
		--disable-profile --enable-add-ons --enable-kernel=4.0
		--enable-bind-now --enable-multi-arch --enable-obsolete-rpc
		--enable-stackguard-randomization --enable-lock-elision
		--with-headers=/usr/include --with-pkgversion="Krejzi ${pkgver}"
		--disable-werror
	)

	cd "${srcdir}/build64"

	echo slibdir=/lib > configparms
	echo rtlddir=/lib >> configparms

	export CC="gcc"
	export CXX="g++"

	../src/configure --prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		"${configure_flags[@]}"

	cd "${srcdir}/build32"

	echo slibdir=/lib32 > configparms
	echo rtlddir=/lib32 >> configparms

	export CC="gcc -m32"
	export CXX="g++ -m32"

	../src/configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--libexecdir=/usr/lib32 \
		"${configure_flags[@]}" \
		i686-pc-linux-gnu

	unset CC CXX

	make -C "${srcdir}/build64"
	make -C "${srcdir}/build32"

	install -v -dm755 "${srcdir}/build32/install/etc"
	touch "${srcdir}/build32/install/etc/ld.so.conf"

	make -C "${srcdir}/build32" install install_root="${srcdir}/build32/install"
}

package_glibc() {
	pkgdesc="GNU C Library"
	backup=(etc/locale.gen)

	cd "${srcdir}/build64"

	install -v -dm755 "${pkgdir}/etc"
	touch "${pkgdir}/etc/ld.so.conf"

	make install install_root="${pkgdir}"

	rm -rf "${pkgdir}/etc/ld.so.conf" "${pkgdir}/etc/ld.so.cache" "${pkgdir}/lib64"

	install -v -dm755 "${pkgdir}/etc/ld.so.conf.d" "${pkgdir}/lib/systemd/system" \
		"${pkgdir}/usr/lib/locale" "${pkgdir}/usr/lib/tmpfiles.d" \
		"${pkgdir}/var/cache/nscd"

	install -v -dm700 "${pkgdir}/var/cache/ldconfig"

	install -v -m755 "${srcdir}/locale-gen" "${pkgdir}/usr/sbin/locale-gen"
	install -v -m644 "${srcdir}/glibc.conf" "${pkgdir}/etc/ld.so.conf.d/glibc.conf"
	install -v -m644 "${srcdir}/locale.gen" "${pkgdir}/etc/locale.gen"

	cd "${srcdir}/src"

	install -v -m644 nscd/nscd.conf posix/gai.conf "${pkgdir}/etc"
	install -v -m644 nscd/nscd.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d"
	install -v -m644 nscd/nscd.service "${pkgdir}/lib/systemd/system"

	sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' localedata/SUPPORTED \
		>> "${pkgdir}/etc/locale.gen"
}

package_lib32-glibc() {
	pkgdesc="GNU C Library (32 bit)"

	cd "${srcdir}/build32/install"

	install -v -dm755 "${pkgdir}/lib" "${pkgdir}/usr/include/gnu" "${pkgdir}/usr/lib"

	cp -a lib32 "${pkgdir}"
	cp -a usr/lib32 "${pkgdir}/usr"

	cp -a usr/include/gnu/lib-names-32.h "${pkgdir}/usr/include/gnu"
	cp -a usr/include/gnu/stubs-32.h "${pkgdir}/usr/include/gnu"

	ln -sfv ../lib32/ld-linux.so.2 "${pkgdir}/lib/ld-linux.so.2"
	ln -sfv ../lib/locale "${pkgdir}/usr/lib32/locale"
}

md5sums=('1496c3bf41adf9db0ebd0af01f202eed'
         'b0ce6f7738594ac12ab7600f116b70d7'
         '3c06a4a99a20e3fc40d613f0cf24c1ec'
         '07ac979b6ab5eeb778d55f041529d623'
         '63933a4f92a1820766173fa1b6779852'
         'c59a03431338c0ccfb3b0d30feb60c5d')

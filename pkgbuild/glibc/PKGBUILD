pkgname=('glibc' 'lib32-glibc')
pkgbase=glibc
pkgver=2.29
pkgrel=1
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL' 'LGPL')
#depends=()
options=(staticlibs emptydirs)
source=("https://ftp.gnu.org/gnu/glibc/glibc-${pkgver}.tar.xz"
	"glibc.conf"
	"locale-gen"
	"locale.gen"
	"glibc-paths.patch"
	"glibc-pure64.patch")

prepare() {
	cd "${srcdir}/glibc-${pkgver}"

	patch -Np1 -i "${srcdir}/glibc-paths.patch"
	patch -Np1 -i "${srcdir}/glibc-pure64.patch"

	rm -rf "${srcdir}/src" "${srcdir}/build64" "${srcdir}/build32"

	ln -sfv "glibc-${pkgver}" "${srcdir}/src"

	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	local configure_flags=(
		--disable-profile
		--disable-werror
		--enable-add-ons
		--enable-kernel=4.0
		--enable-bind-now
		--enable-multi-arch
		--enable-lock-elision
		--enable-stackguard-randomization
		--enable-stack-protector=strong
		--enable-static-pie
		--with-headers=/usr/include
		--with-pkgversion="Krejzi ${pkgver}"
		--without-selinux
	)

	cd "${srcdir}/build64"

	echo slibdir=/usr/lib > configparms
	echo rtlddir=/usr/lib >> configparms
	echo rootsbindir=/usr/sbin >> configparms

	export CC="gcc"
	export CXX="g++"

	../src/configure --prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--enable-cet \
		"${configure_flags[@]}"

	cd "${srcdir}/build32"

	echo slibdir=/usr/lib32 > configparms
	echo rtlddir=/usr/lib32 >> configparms
	echo rootsbindir=/usr/sbin >> configparms

	export CC="gcc -m32"
	export CXX="g++ -m32"

	../src/configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--libexecdir=/usr/lib32 \
		--host=i686-pc-linux-gnu \
		"${configure_flags[@]}"

	unset CC CXX

	make -C "${srcdir}/build64"
	make -C "${srcdir}/build32"

	install -v -dm755 "${srcdir}/build32/install/etc"
	touch "${srcdir}/build32/install/etc/ld.so.conf"

	make -C "${srcdir}/build32" install install_root="${srcdir}/build32/install"
}

package_glibc() {
	pkgdesc="GNU C Library"
	backup=(etc/locale.gen)

	cd "${srcdir}/build64"

	install -v -dm755 "${pkgdir}/etc"
	touch "${pkgdir}/etc/ld.so.conf"

	make install install_root="${pkgdir}"

	rm -rf "${pkgdir}/etc/ld.so.conf" "${pkgdir}/etc/ld.so.cache" "${pkgdir}/lib64"
	rm -rf "${pkgdir}"/usr/lib/libnsl.{a,so}
	rm -rf "${pkgdir}/usr/bin/tzselect"
	rm -rf "${pkgdir}/usr/sbin/zic"

	install -v -dm755 "${pkgdir}/etc/ld.so.conf.d" "${pkgdir}/usr/lib/systemd/system" \
		"${pkgdir}/usr/lib/locale" "${pkgdir}/usr/lib/tmpfiles.d" \
		"${pkgdir}/var/cache/nscd"

	install -v -dm700 "${pkgdir}/var/cache/ldconfig"

	install -v -m755 "${srcdir}/locale-gen" "${pkgdir}/usr/sbin/locale-gen"
	install -v -m644 "${srcdir}/glibc.conf" "${pkgdir}/etc/ld.so.conf.d/glibc.conf"
	install -v -m644 "${srcdir}/locale.gen" "${pkgdir}/etc/locale.gen"

	cd "${srcdir}/src"

	install -v -m644 nscd/nscd.conf posix/gai.conf "${pkgdir}/etc"
	install -v -m644 nscd/nscd.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d"
	install -v -m644 nscd/nscd.service "${pkgdir}/usr/lib/systemd/system"

	sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' localedata/SUPPORTED \
		>> "${pkgdir}/etc/locale.gen"
}

package_lib32-glibc() {
	pkgdesc="GNU C Library (32 bit)"

	cd "${srcdir}/build32/install"

	install -v -dm755 "${pkgdir}/usr/include/gnu" "${pkgdir}/usr/lib"

	cp -a usr/lib32 "${pkgdir}/usr"

	cp -a usr/include/gnu/lib-names-32.h "${pkgdir}/usr/include/gnu"
	cp -a usr/include/gnu/stubs-32.h "${pkgdir}/usr/include/gnu"

	ln -sfv ../lib32/ld-linux.so.2 "${pkgdir}/usr/lib/ld-linux.so.2"
	ln -sfv ../lib/locale "${pkgdir}/usr/lib32/locale"

	rm -rf "${pkgdir}"/usr/lib32/libnsl.{a,so}
}

sha512sums=('146bc0769fe853d9edbf93cea7f74c5b3d84d69cb7614c62588e7acdecd1ec789a9d8949f3e8b99f8f36f2ccac740a003bed94f32b07817baf780b06cfeb6ed0'
            '062341482c1b44d3a97a00cb0915afc2d45b0acf5c7a79c98e37927a902a5ba040db973d0ee9f24e645a245c54704139e43df67e5adb8fa93c91411deeba01ff'
            'ba1e4d42b1ccc07c0a3a11b8c788b96526a40a1d70eb5807749067b2591d27e3d00b88752f1689e918f1129d110797ece2861ba7a508fa55ae089e7cc5c8bf8b'
            '3d9facc6da5159dcd877473c8a4d77006ef5615e2619ef743255112bfa136baf65239367222e87e7ada823d5c7d747aa5204f8365a806141e5884477898ccd14'
            '4f8a34bfb260889baf1ab448d2cfc4aa607015ca14546326b5e54a27bb608f3d73366d3f2856b0fd9717ca0f0dcaabe3c7e6b63ba90857dd74288aa6546d901e'
            '6e5135be9d3761c32720e51a0addd5760e58988024ab3cb059e95cd4792c1d746d4ae3e9393ea9b383d7c871d52e04cc436dcfb115d9b37d63f5db7adc42a421')

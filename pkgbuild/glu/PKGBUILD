pkgbase=glu
pkgname=('glu' 'lib32-glu')
pkgver=9.0.1
pkgrel=1
arch=('x86_64')
url="https://cgit.freedesktop.org/mesa/glu/"
license=('LGPL')
#depends=()
source=("https://archive.mesa3d.org/glu/glu-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/glu{,32}-${pkgver}
}

build() {
	cd "${srcdir}/glu-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/glu32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/glu-${pkgver}"
	make -C "${srcdir}/glu32-${pkgver}"
}

package_glu() {
	pkgdesc="Mesa OpenGL Utility library"

	cd "${srcdir}/glu-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-glu() {
	pkgdesc="Mesa OpenGL Utility library (32 bit)"

	cd "${srcdir}/glu32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('8a6dae5b4bd63efb96d15f23ccda4ad9c2ffaa964897e5fa63d1e58360d8d4e6732c5efd2109dba04155d5fc457ab1718a65cf9b544ce0d452679ba988d04018')

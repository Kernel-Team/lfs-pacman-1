pkgname=gnome-desktop
pkgver=3.32.1
pkgrel=1
pkgdesc="Library with common API for various GNOME modules"
arch=('x86_64')
url="https://git.gnome.org/browse/gnome-desktop/"
license=('GPL' 'LGPL')
#depends=()
source=("https://download.gnome.org/sources/gnome-desktop/${pkgver:0:4}/gnome-desktop-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dgnome_distributor=Krejzi \
		-Dgtk_doc=true \
		"${srcdir}/gnome-desktop-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('d1489d036784db9405d2ebc330d55c7413e0c5ad35e4c7965fdf9345456aef1e5651a2768a5f9e83b889838badfce889b18e2e8fc9289bdfe46627e4e8025f17')

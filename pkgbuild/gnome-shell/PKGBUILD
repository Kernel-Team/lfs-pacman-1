pkgname=gnome-shell
pkgver=3.26.1
pkgrel=1
pkgdesc="The next generation GNOME Shell"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GnomeShell"
license=('GPL2')
#depends=()
options=(emptydirs)
source=("https://download.gnome.org/sources/gnome-shell/${pkgver:0:4}/gnome-shell-${pkgver}.tar.xz"
	"https://github.com/elkrejzi/gnome-shell-autotools/archive/${pkgver:0:4}/gnome-shell-autotools-${pkgver:0:4}.tar.gz")

prepare() {
	cd "${srcdir}/gnome-shell-${pkgver}"

	cp -a "${srcdir}"/gnome-shell-autotools-${pkgver:0:4}/* .

	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd "${srcdir}/gnome-shell-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-schemas-compile

	make
}

package() {
	cd "${srcdir}/gnome-shell-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/share/gnome-shell/modes"
	install -v -dm755 "${pkgdir}/usr/lib/gnome-shell/mozilla"

	mv -v "${pkgdir}/usr/lib/mozilla/plugins/libgnome-shell-browser-plugin.so" "${pkgdir}/usr/lib/gnome-shell/mozilla"
	ln -sfv ../../gnome-shell/mozilla/libgnome-shell-browser-plugin.so "${pkgdir}/usr/lib/mozilla/plugins/libgnome-shell-browser-plugin.so"
}

sha512sums=('74fbbdd15939baa6b762664fd7da2fe033292aa3b6d4480863dd61ed2485842f7cfaf46ce30738a16942d258ae36402b559f9e962a1d699fcfd0b731f2be479e'
            'a7093be514c4f3a7d2fdd63680afb9686c2fdccb24b26fdeff312809ff37cdef33085f4219d59d0297671c92975442f75f167d10b2ae0766a88fba236ffee23b')

pkgname=gnu-efi-libs
pkgver=3.0.6
pkgrel=1
pkgdesc="Library for building UEFI Applications using GNU toolchain"
arch=('x86_64')
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
#depends=()
options=(!strip !makeflags !buildflags)
source=("https://downloads.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")

build() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	make lib gnuefi inc apps CC="gcc"
}

package() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -v -dm755 "${pkgdir}/usr/share/gnu-efi/apps/x86_64"
	install -v -m644 "${srcdir}"/gnu-efi-${pkgver}/x86_64/apps/*.efi \
		"${pkgdir}/usr/share/gnu-efi/apps/x86_64/"
}

sha512sums=('f1b17766d8ae000c0177ea2126d87883f254ab01ac020e4bfaa9ad2d3d60a66d07b6237d9842e5989c4da3fd23152b5c592ef863fdc9806901f43c43582a2258')

pkgname=gnu-efi-libs
pkgver=3.0.5
pkgrel=1
pkgdesc="Library for building UEFI Applications using GNU toolchain"
arch=('x86_64')
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
#depends=()
options=(!strip !makeflags !buildflags)
source=("https://downloads.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")

build() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	sed -i "s#-Werror##g" Make.defaults

	make lib gnuefi inc apps CC="gcc"
}

package() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -v -dm755 "${pkgdir}/usr/share/gnu-efi/apps/x86_64"
	install -v -m644 "${srcdir}"/gnu-efi-${pkgver}/x86_64/apps/*.efi \
		"${pkgdir}/usr/share/gnu-efi/apps/x86_64/"
}

sha512sums=('848ea9b0a9b900a237d04c2fe95fbaaa08d786c8abe4cbfdca9b666a5cd955ad1097668561aa90899b654a606ff822509fb23b6aeafce69bc4407e5b4547fdcf')

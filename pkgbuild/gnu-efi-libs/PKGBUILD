pkgname=gnu-efi-libs
pkgver=3.0.9
pkgrel=1
pkgdesc="Library for building UEFI Applications using GNU toolchain"
arch=('x86_64')
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
#depends=()
options=(!strip !makeflags !buildflags)
source=("https://downloads.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")

build() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	make lib gnuefi inc apps CC="gcc"
}

package() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -v -dm755 "${pkgdir}/usr/share/gnu-efi/apps/x86_64"
	install -v -m644 "${srcdir}"/gnu-efi-${pkgver}/x86_64/apps/*.efi \
		"${pkgdir}/usr/share/gnu-efi/apps/x86_64/"
}

sha512sums=('1a775476fcbe354e57c9db258b1c6ad4346b84d1794178ab5d5195b3ffba250066ca4c42a553d5b2866fa1b4e03019d61df0197f269188c73297042990adf316')

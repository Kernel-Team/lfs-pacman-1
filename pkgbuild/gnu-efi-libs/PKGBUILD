pkgname=gnu-efi-libs
pkgver=3.0.12
pkgrel=1
pkgdesc="Library for building UEFI Applications using GNU toolchain"
arch=('x86_64')
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
#depends=()
options=(!strip !makeflags !buildflags)
source=("https://downloads.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")

build() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	make lib gnuefi inc apps CC="gcc"
}

package() {
	cd "${srcdir}/gnu-efi-${pkgver}"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -v -dm755 "${pkgdir}/usr/share/gnu-efi/apps/x86_64"
	install -v -m644 "${srcdir}"/gnu-efi-${pkgver}/x86_64/apps/*.efi \
		"${pkgdir}/usr/share/gnu-efi/apps/x86_64/"
}

sha512sums=('cbec6f6e37271c22c71e3ce44d3579e48e7f3b797ec3946f56eb843ed3e472722763c566fc3b749f0c692ab6cb26e8909bb258cd164771a5b1eeff5c7cd51368')

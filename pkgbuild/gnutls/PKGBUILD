pkgname=gnutls
pkgver=3.6.6
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('x86_64')
url="http://www.gnutls.org/"
license=('GPL3' 'LGPL2.1')
#depends=()
source=("https://www.gnupg.org/ftp/gcrypt/gnutls/v${pkgver:0:3}/gnutls-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/gnutls-${pkgver}"

	./configure --prefix=/usr \
		--enable-local-libopts \
		--disable-guile

	make
}

package() {
	cd "${srcdir}/gnutls-${pkgver}"

	make install DESTDIR="${pkgdir}"

	make -C doc/reference install-data-local DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/share/info/*.png "${pkgdir}"/usr/share/info/*guile*
}

sha512sums=('4ff34f38d7dc543bc5750d8fdfe9be84af60c66e8d41da45f6cffc11d6c6c726784fd2d471b3416604ca1f3f9efb22ff7a290d5c92c96deda38df6ae3e794cc1')

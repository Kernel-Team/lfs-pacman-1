pkgname=gobject-introspection
pkgver=1.66.1
pkgrel=1
pkgdesc="Introspection system for GObject-based libraries"
arch=('x86_64')
url="https://live.gnome.org/GObjectIntrospection"
license=('LGPL' 'GPL')
#depends=()
source=("https://download.gnome.org/sources/gobject-introspection/${pkgver:0:4}/gobject-introspection-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Ddoctool=enabled \
		-Dgtk_doc=true \
		"${srcdir}/gobject-introspection-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('ea1e20cd94ff8af3572f417f35e96648ffc3e94a91d4e4c81adf99bb0f408ac21ecf40990f9dbd5f2e0f4e83360286ca5db88dbc45bd59289596a324acf7df3d')

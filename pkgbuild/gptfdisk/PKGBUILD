pkgname=gptfdisk
pkgver=1.0.3
pkgrel=1
pkgdesc="A text-mode partitioning tool that works on GUID Partition Table (GPT) disks"
arch=('x86_64')
url="http://www.rodsbooks.com/gdisk/"
license=('GPL2')
#depends=()
source=("https://downloads.sourceforge.net/gptfdisk/gptfdisk-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/gptfdisk-${pkgver}"

	sed -i "/CC=/d;/CXX=/d" Makefile
}

build() {
	cd "${srcdir}/gptfdisk-${pkgver}"

	make
}

package() {
	cd "${srcdir}/gptfdisk-${pkgver}"

	install -v -dm755 "${pkgdir}/usr/sbin" "${pkgdir}/usr/share/man/man8"
	install -v -t "${pkgdir}/usr/sbin" cgdisk fixparts gdisk sgdisk
	install -v -t "${pkgdir}/usr/share/man/man8" *.8
}

sha512sums=('80d437bfa2365abfffbd4812ea928fcebd52c5421de321cf190d395549e32c5ebdbb2d060cc4b95f77fe13cdd719f4d2bb3d0ed5d97792b100325f92d7c852d7')

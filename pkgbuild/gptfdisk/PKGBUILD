pkgname=gptfdisk
pkgver=1.0.4
pkgrel=1
pkgdesc="A text-mode partitioning tool that works on GUID Partition Table (GPT) disks"
arch=('x86_64')
url="http://www.rodsbooks.com/gdisk/"
license=('GPL2')
#depends=()
source=("https://downloads.sourceforge.net/gptfdisk/gptfdisk-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/gptfdisk-${pkgver}"

	sed -i "/CC=/d;/CXX=/d" Makefile
}

build() {
	cd "${srcdir}/gptfdisk-${pkgver}"

	make
}

package() {
	cd "${srcdir}/gptfdisk-${pkgver}"

	install -v -dm755 "${pkgdir}/usr/sbin" "${pkgdir}/usr/share/man/man8"
	install -v -t "${pkgdir}/usr/sbin" cgdisk fixparts gdisk sgdisk
	install -v -t "${pkgdir}/usr/share/man/man8" *.8
}

sha512sums=('2df6db6974c06a6677a155d32566b8b46dad0df7781131e9018072db9acce5e41f6c4e7024b0e5f236dd43350a63572be34fb4b6fdb580cd9b946619a7a683bf')

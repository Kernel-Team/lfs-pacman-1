pkgbase=graphite2
pkgname=('graphite2' 'lib32-graphite2')
pkgver=1.3.13
pkgrel=1
arch=('x86_64')
url="https://github.com/silnrsi/graphite"
license=('LGPL' 'GPL' 'custom')
#depends=()
source=("https://github.com/silnrsi/graphite/releases/download/${pkgver}/graphite2-${pkgver}.tgz")

prepare() {
	install -v -dm755 "${srcdir}"/build{32,64}

	sed -i "/target_link/s# gcc)#)#g" "${srcdir}/graphite2-${pkgver}/src/CMakeLists.txt"
}

build() {
	cd "${srcdir}/build64"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DGRAPHITE2_COMPARE_RENDERER=OFF \
		"${srcdir}/graphite2-${pkgver}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DGRAPHITE2_COMPARE_RENDERER=OFF \
		-DLIB_SUFFIX=32 \
		"${srcdir}/graphite2-${pkgver}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/build64"
	make -C "${srcdir}/build32"
}

package_graphite2() {
	pkgdesc="Reimplementation of the SIL Graphite text processing engine"

	cd "${srcdir}/build64"

	make install DESTDIR="${pkgdir}"
}

package_lib32-graphite2() {
	pkgdesc="Reimplementation of the SIL Graphite text processing engine (32 bit)"

	cd "${srcdir}/build32"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('44ac2f353c1a795a55c78b4ff72df35a3a5a94700356c0eefe105d66cd499ac93f9cfd5ab7b3ea842b575156cb8df19e0b57ff8b9dbaee8a576319ff5889c131')

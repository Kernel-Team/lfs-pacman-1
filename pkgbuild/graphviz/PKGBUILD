pkgname=graphviz
pkgver=2.40.1
pkgrel=1
pkgdesc="Graph visualization software"
arch=('x86_64')
url="http://www.graphviz.org/"
license=('CPL')
#depends=()
install=graphviz.install
source=("https://graphviz.gitlab.io/pub/graphviz/stable/SOURCES/graphviz.tar.gz"
	"graphviz-qt5.patch")

prepare() {
	cd "${srcdir}/graphviz-${pkgver}"

	patch -Np1 -i "${srcdir}/graphviz-qt5.patch"

	autoreconf -fi

	sed -i "s:ruby-1.9:ruby-2.4:g" configure
}

build() {
	cd "${srcdir}/graphviz-${pkgver}"

	export PYTHON=python2
	export LIBPOSTFIX=/

	export CXX="${CXX} -std=c++11"

	CFLAGS+=" -fPIC -w"
	CXXFLAGS+=" -fPIC -w"

	./configure --prefix=/usr \
		--disable-java \
		--disable-php

	make
}

package() {
	cd "${srcdir}/graphviz-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('a3f358a7050523a39b91a259563a95925b37853ffec799e571211af5b686d3af42457c937882954482785745d90416b1abd945caf05f8abb52b3876e07aa70f5'
            'fffdfdc61665f0372354bd38200a52ff7b27a3aa295331c990117bc8db9d888f0fc099785eedd4255255a79fc4d9c5cdc9f0af16a170adf2b76899c06dfbde2e')

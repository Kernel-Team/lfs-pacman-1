pkgname=grilo
pkgver=0.3.7
pkgrel=1
pkgdesc="Framework that provides access to various sources of multimedia content"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/Grilo"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/grilo/${pkgver:0:3}/grilo-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable-gtk-doc=true \
		"${srcdir}/grilo-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('82c41d63edc23c056e189a73b0de87db342bba575d42efeda78ed4bf78fb8994111a2e9457e6f2453045b9b7b2d171b9e89d71c3a107cfa06af24644d9cb71df')

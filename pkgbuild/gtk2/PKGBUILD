pkgname=gtk2
pkgver=2.24.31
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (legacy)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/gtk+/${pkgver:0:4}/gtk+-${pkgver}.tar.xz"
	"gtk-query-immodules-2.0.hook"
	"gtkrc")

prepare() {
	cd "${srcdir}/gtk+-${pkgver}"

	sed -i 's#l \(gtk-.*\).sgml#& -o \1#' docs/{faq,tutorial}/Makefile.in
}

build() {
	cd "${srcdir}/gtk+-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-man

	make
}

package() {
	cd "${srcdir}/gtk+-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/bin/gtk-update-icon-cache" "${pkgdir}/usr/share/man/man1/gtk-update-icon-cache.1"

	install -v -Dm644 "${srcdir}/gtkrc" "${pkgdir}/usr/share/gtk-2.0/gtkrc"
	install -v -Dm644 "${srcdir}/gtk-query-immodules-2.0.hook" "${pkgdir}/usr/share/libalpm/hooks/gtk-query-immodules-2.0.hook"
}

sha512sums=('75b846661308d94b5236e7e1c367deabf57753792ca87e92f9769822dd5ec90e00cd8d5574adcc12162e3702e1ae6dc152278a1d218f9bd9e4d1c8dc1dd520c1'
            'be7a807046f27e5796f6069dfd2c88f9b7508e3b9e209d97c1575e0d809b279aacacd0e1247f1f7643fa47eede2d987bf433cd17cea0524526ad31982ae8d90d'
            'b124433dd4b20d1d62f073df87e253ca23b3b51625cce55f29a220a4369eda5108c0de07fdc686f570232322c3ff04f7758383f2be5aeace40f843907aa3696d')

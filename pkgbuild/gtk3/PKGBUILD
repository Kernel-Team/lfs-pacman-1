pkgname=gtk3
pkgver=3.22.16
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/gtk+/${pkgver:0:4}/gtk+-${pkgver}.tar.xz"
	"gtk-query-immodules-3.0.hook"
	"gtk-update-icon-cache.hook"
	"gtk-update-icon-cache.script"
	"settings.ini")

build() {
	cd "${srcdir}/gtk+-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-schemas-compile \
		--enable-broadway-backend \
		--enable-wayland-backend \
		--enable-x11-backend \
		--enable-man

	make
}

package() {
	cd "${srcdir}/gtk+-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/settings.ini" "${pkgdir}/usr/share/gtk-3.0/settings.ini"

	install -v -Dm644 "${srcdir}/gtk-query-immodules-3.0.hook" "${pkgdir}/usr/share/libalpm/hooks/gtk-query-immodules-3.0.hook"
	install -v -Dm644 "${srcdir}/gtk-update-icon-cache.hook" "${pkgdir}/usr/share/libalpm/hooks/gtk-update-icon-cache.hook"
	install -v -Dm755 "${srcdir}/gtk-update-icon-cache.script" "${pkgdir}/usr/share/libalpm/scripts/gtk-update-icon-cache"
}

sha512sums=('a981ba3c420db649645787916d603a22f1da9d839aba39f3786bab47f9a247867d94d342ce4c3aa47de4876072ed9cf3615304a73de7e613063cef08b8c0d612'
            'f0ffd95544863f2e10fda81488b4727aa9a8a35a7d39fb96872db6664d03442db2b58af788b5990825c7b3a83681f7220ca481409cca5421dfb39b9a3bbac9ac'
            'abfd73de4faa6f53784182800395aa3c39bb98e15a0eb300fb4142073ff7ce565a0836a2363393b2f132060b5293dbc0c30c380a023f38d5bd39c62cb58389c2'
            '5cd50d93bb6bc203438a2a0764bd717409658e124058b18a1da26a21f10ef7564a16f32fc0633a68b45b2e303fa63a5efefeadd6b0bf1d7f474556df8cdb6c58'
            'ad2c0b0388f4169592b9574f0b3db673a969b7c4721548c4ac7c438eddbcdc378fcaac04e2b6c858a1562cc23ddf4804e5f7be08068340b7c9365e2b11ddcfb8')

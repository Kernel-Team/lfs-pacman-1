pkgbase=harfbuzz
pkgname=('harfbuzz' 'lib32-harfbuzz')
pkgver=1.5.1
pkgrel=1
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
#depends=()
source=("https://www.freedesktop.org/software/harfbuzz/release/harfbuzz-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/harfbuzz{,32}-${pkgver}
}

build() {
	cd "${srcdir}/harfbuzz-${pkgver}"

	./configure --prefix=/usr \
		--with-gobject \
		--with-graphite2

	cd "${srcdir}/harfbuzz32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--with-gobject \
		--with-graphite2 \
		--disable-introspection

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/harfbuzz-${pkgver}"
	make -C "${srcdir}/harfbuzz32-${pkgver}"
}

package_harfbuzz() {
	pkgdesc="OpenType text shaping engine"

	cd "${srcdir}/harfbuzz-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-harfbuzz() {
	pkgdesc="OpenType text shaping engine (32 bit)"

	cd "${srcdir}/harfbuzz32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('e5235c9fe4524059128f97e2773056533803b40db70c18b43c8dc6e27a970efc40d9ce6e8754ae7075c245cf8f4386b33e922a6edb3b6a722a596cb7759b8b36')

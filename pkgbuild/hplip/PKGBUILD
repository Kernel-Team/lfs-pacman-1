pkgname=hplip
pkgver=3.19.3
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet"
arch=('x86_64')
url="http://hplipopensource.com/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://downloads.sourceforge.net/hplip/hplip-${pkgver}.tar.gz"
	"hplip-fixes.patch")

prepare() {
	cd "${srcdir}/hplip-${pkgver}"

	patch -Np1 -i "${srcdir}/hplip-fixes.patch"
}

build() {
	cd "${srcdir}/hplip-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-hpcups-install \
		--enable-cups-drv-install \
		--disable-qt4 \
		PYTHON=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/hplip-${pkgver}"

	make -j1 install rulesdir=/usr/lib/udev/rules.d DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/sane.d/dll.d"

	mv "${pkgdir}/etc/sane.d/dll.conf" "${pkgdir}/etc/sane.d/dll.d/hpaio"

	rm -rf "${pkgdir}/etc/xdg" "${pkgdir}/usr/share/hal"
}

sha512sums=('6191974d6c9a35e58a9b0d2ff1077b3a9479e44ccb76a551f3e29ca56d420bfe0778fa6b7a4e51b18f2e55cae9ad1f5216f716e22b651122ec0fe9e730fcd29a'
            '96beaf08aa8a866693efd1d3a863ccbc9db468629ab6b86a11b9e6cfad5933b031c198ff4a86a836ea1cd66f2c375454580219100a196665aa63faa43d71aa79')

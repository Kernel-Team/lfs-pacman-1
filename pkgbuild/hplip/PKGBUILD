pkgname=hplip
pkgver=3.18.4
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet"
arch=('x86_64')
url="http://hplipopensource.com/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://downloads.sourceforge.net/hplip/hplip-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/hplip-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-new-hpcups \
		--disable-qt4 \
		PYTHON=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/hplip-${pkgver}"

	make -j1 install rulesdir=/usr/lib/udev/rules.d DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/sane.d/dll.d"

	mv "${pkgdir}/etc/sane.d/dll.conf" "${pkgdir}/etc/sane.d/dll.d/hpaio"

	rm -rf "${pkgdir}/etc/xdg" "${pkgdir}/usr/share/hal"
}

sha512sums=('dfe0e21606498112d57fd76763bcc9457a130896bd86508736422c1d82a8be5b5e4b4277b07e2efd2c777cedb9f573ac9fe8198afcf425386c7d8ac0cd62d086')

pkgname=hplip
pkgver=3.20.11
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet"
arch=('x86_64')
url="http://hplipopensource.com/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://downloads.sourceforge.net/hplip/hplip-${pkgver}.tar.gz"
	"hplip-fixes.patch")

prepare() {
	cd "${srcdir}/hplip-${pkgver}"

	patch -Np1 -i "${srcdir}/hplip-fixes.patch"
}

build() {
	cd "${srcdir}/hplip-${pkgver}"

	export CFLAGS+=" $(python3-config --includes)"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-hpcups-install \
		--enable-cups-drv-install \
		--disable-qt4 \
		PYTHON=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/hplip-${pkgver}"

	make -j1 install rulesdir=/usr/lib/udev/rules.d DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/sane.d/dll.d"

	mv "${pkgdir}/etc/sane.d/dll.conf" "${pkgdir}/etc/sane.d/dll.d/hpaio"

	rm -rf "${pkgdir}/etc/xdg" "${pkgdir}/usr/share/hal"
}

sha512sums=('f583fb4140a29c5e21956c3e51fe3620baa65d33b40447af419d7fc61473a1148a1873216a84e89be785b155ee8e9aebd5e3eb19d2ded82b8415b1a35de1b55d'
            '96beaf08aa8a866693efd1d3a863ccbc9db468629ab6b86a11b9e6cfad5933b031c198ff4a86a836ea1cd66f2c375454580219100a196665aa63faa43d71aa79')

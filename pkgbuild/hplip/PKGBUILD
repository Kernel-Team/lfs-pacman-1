pkgname=hplip
pkgver=3.18.3
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet"
arch=('x86_64')
url="http://hplipopensource.com/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://downloads.sourceforge.net/hplip/hplip-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/hplip-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-new-hpcups \
		--disable-qt4 \
		PYTHON=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/hplip-${pkgver}"

	make -j1 install rulesdir=/usr/lib/udev/rules.d DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/sane.d/dll.d"

	mv "${pkgdir}/etc/sane.d/dll.conf" "${pkgdir}/etc/sane.d/dll.d/hpaio"

	rm -rf "${pkgdir}/etc/xdg" "${pkgdir}/usr/share/hal"
}

sha512sums=('cb51e357cc9def3c087231b01752941b469e5fc4508ec83c6f5875352694249ef72339f2f20c4a6831c49ace7c1f6b902cc11a961c0e1f43ce098b2bf800728f')

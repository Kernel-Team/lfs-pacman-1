pkgname=http-parser
pkgver=2.9.0
pkgrel=1
pkgdesc="Parser for HTTP Request/Response written in C"
arch=('x86_64')
url="https://github.com/nodejs/http-parser"
license=('MIT')
#depends=()
source=("https://github.com/nodejs/http-parser/archive/v${pkgver}/http-parser-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/http-parser-${pkgver}"

	make library
}

package() {
	cd "${srcdir}/http-parser-${pkgver}"

	make install PREFIX="${pkgdir}/usr"

	ln -sfv libhttp_parser.so.${pkgver} "${pkgdir}/usr/lib/libhttp_parser.so"

	chmod 644 "${pkgdir}/usr/include/http_parser.h"
}

sha512sums=('40acecbf71b9f0b4ae857c74c3ec0784d7f341a0cb83cf82b308387d0c5b56d38b282241aaf8ca93816970f2a9e67989f3d9d456459f3986c29fe51ab520155e')

pkgname=hunspell-en
pkgver=2019.10.06
pkgrel=1
pkgdesc="English hunspell dictionaries"
arch=('x86_64')
url="http://wordlist.aspell.net/dicts/"
license=('LGPL' 'custom:scowl')
#depends=()
source=("https://downloads.sourceforge.net/wordlist/hunspell-en_CA-${pkgver}.zip"
	"https://downloads.sourceforge.net/wordlist/hunspell-en_GB-large-${pkgver}.zip"
	"https://downloads.sourceforge.net/wordlist/hunspell-en_US-${pkgver}.zip")

package() {
	cd "${srcdir}"

	install -v -dm755 "${pkgdir}/usr/share/hunspell"

	install -v -m644 *.aff *.dic "${pkgdir}/usr/share/hunspell"

	en_GB_aliases="en_AG en_AU en_BS en_BW en_BZ en_DK en_GB en_GH en_HK en_IE \
		en_IN en_JM en_NA en_NG en_NZ en_SG en_TT en_ZA en_ZW"

	for lang in ${en_GB_aliases}
	do
		ln -sfv en_GB-large.aff "${pkgdir}/usr/share/hunspell/${lang}.aff"
		ln -sfv en_GB-large.dic "${pkgdir}/usr/share/hunspell/${lang}.dic"
	done

	en_US_aliases="en_PH"

	for lang in ${en_US_aliases}
	do
		ln -sfv en_US.aff "${pkgdir}/usr/share/hunspell/${lang}.aff"
		ln -sfv en_US.dic "${pkgdir}/usr/share/hunspell/${lang}.dic"
	done
}

sha512sums=('a2a7526686f950311fdecc24010f4a79081fb2f2e65b588f817e7bcf9e17c902f72139674fa8d7272f16cf43126cd8468f2e38c7edccc9083b8a2916c0026ce0'
            '51d9268f02b54804ef364d67e3f38d428abfacac1995186751b2e46477c570653e8fa58fcdc561040a6a03c9aa90631b8c46f3df6250ff04aa7c95a5ef6af886'
            '14c50a2fd7d390aa1fe20dd1f5e9536bcfd9a36cf829532b340cfc86f72e6415cd41a28e23be67be5a87482bad2af7d7b961524eff10449cad6923518ae0612a')

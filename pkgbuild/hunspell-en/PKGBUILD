pkgname=hunspell-en
pkgver=2017.01.22
pkgrel=1
pkgdesc="English hunspell dictionaries"
arch=('x86_64')
url="http://wordlist.aspell.net/dicts/"
license=('LGPL' 'custom:scowl')
#depends=()
source=("https://downloads.sourceforge.net/wordlist/hunspell-en_CA-${pkgver}.zip"
	"https://downloads.sourceforge.net/wordlist/hunspell-en_GB-large-${pkgver}.zip"
	"https://downloads.sourceforge.net/wordlist/hunspell-en_US-${pkgver}.zip")

package() {
	cd "${srcdir}"

	install -v -dm755 "${pkgdir}/usr/share/hunspell"

	install -v -m644 *.aff *.dic "${pkgdir}/usr/share/hunspell"

	en_GB_aliases="en_AG en_AU en_BS en_BW en_BZ en_DK en_GB en_GH en_HK en_IE \
		en_IN en_JM en_NA en_NG en_NZ en_SG en_TT en_ZA en_ZW"

	for lang in ${en_GB_aliases}
	do
		ln -sfv en_GB-large.aff "${pkgdir}/usr/share/hunspell/${lang}.aff"
		ln -sfv en_GB-large.dic "${pkgdir}/usr/share/hunspell/${lang}.dic"
	done

	en_US_aliases="en_PH"

	for lang in ${en_US_aliases}
	do
		ln -sfv en_US.aff "${pkgdir}/usr/share/hunspell/${lang}.aff"
		ln -sfv en_US.dic "${pkgdir}/usr/share/hunspell/${lang}.dic"
	done
}

sha512sums=('0d99ba9d427d0a7cc4752f0df686bbeac5295a028d03582ab741fd2e8d1691ffc735854a84ebd0f93e6e905d018eeaa540dce64d990a8b8be0135bd75358fdba'
            '2200652e57a50e8b84dd2703871b03756b844afe210c7f95013ea07dbc760926e312a7bb5ade43c692025fff56cd11dc20bfd1f83bd492345973427af16a3e04'
            '1ac2160136cf914e3838bbe29949bd027276fdc8aa9fd3f79a74e037e9231f4a3ee1cb29b635a331ba7c1bc87d05d9809bbd44b2e82925430e2935a47507e635')

pkgname=hunspell-en
pkgver=2017.08.24
pkgrel=1
pkgdesc="English hunspell dictionaries"
arch=('x86_64')
url="http://wordlist.aspell.net/dicts/"
license=('LGPL' 'custom:scowl')
#depends=()
source=("https://downloads.sourceforge.net/wordlist/hunspell-en_CA-${pkgver}.zip"
	"https://downloads.sourceforge.net/wordlist/hunspell-en_GB-large-${pkgver}.zip"
	"https://downloads.sourceforge.net/wordlist/hunspell-en_US-${pkgver}.zip")

package() {
	cd "${srcdir}"

	install -v -dm755 "${pkgdir}/usr/share/hunspell"

	install -v -m644 *.aff *.dic "${pkgdir}/usr/share/hunspell"

	en_GB_aliases="en_AG en_AU en_BS en_BW en_BZ en_DK en_GB en_GH en_HK en_IE \
		en_IN en_JM en_NA en_NG en_NZ en_SG en_TT en_ZA en_ZW"

	for lang in ${en_GB_aliases}
	do
		ln -sfv en_GB-large.aff "${pkgdir}/usr/share/hunspell/${lang}.aff"
		ln -sfv en_GB-large.dic "${pkgdir}/usr/share/hunspell/${lang}.dic"
	done

	en_US_aliases="en_PH"

	for lang in ${en_US_aliases}
	do
		ln -sfv en_US.aff "${pkgdir}/usr/share/hunspell/${lang}.aff"
		ln -sfv en_US.dic "${pkgdir}/usr/share/hunspell/${lang}.dic"
	done
}

sha512sums=('6be6c979fd26fb2e15f5938cc5146157a0cbaa2f11d212c7f98e5dea526e0cfb32a136555989759f55c371f980516e69841bf7b576ba2d9c03144162101c1a8e'
            '4668c611a72a169b954a428daca2475545537de1eb43d09e59a4315122e4160bdcddafd69b86c273d75a7267ac5ed42b802d1b346564ac33d5789444ec7dedba'
            '99a6037806438cb20dcd35e1f482235e86497f688ce3d0f650348f883228cec598157ca979423e77de40b62f91e4d658ec7894bf72c0d954347e19e096cafb07')

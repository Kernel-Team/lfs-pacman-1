pkgbase=icu
pkgname=('icu' 'lib32-icu')
pkgver=61.1
pkgrel=1
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:icu')
#depends=()
source=("http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz")

prepare() {
	cd "${srcdir}/icu/source"

	sed -i "s#xlocale.h#locale.h#g" i18n/digitlst.cpp

	cp -a "${srcdir}"/icu{,32}
}

build() {
	cd "${srcdir}/icu/source"

	./configure --prefix=/usr

	cd "${srcdir}/icu32/source"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/icu/source"
	make -C "${srcdir}/icu32/source"

}

package_icu() {
	pkgdesc="International Components for Unicode library"

	cd "${srcdir}/icu/source"

	make -j1 install DESTDIR="${pkgdir}"
}

package_lib32-icu() {
	pkgdesc="International Components for Unicode library (32 bit)"

	cd "${srcdir}/icu32/source"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/bin/icu-config "${pkgdir}/usr/bin/icu-config32"
}

sha512sums=('4c37691246db802e4bae0c8c5f6ac1dac64c5753b607e539c5c1c36e361fcd9dd81bd1d3b5416c2960153b83700ccdb356412847d0506ab7782ae626ac0ffb94')

pkgname=imagemagick
_pkgver=7.0.8-35
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
#depends=()
options=(libtool)
source=("https://www.imagemagick.org/download/ImageMagick-${_pkgver}.tar.xz")

build() {
	cd "${srcdir}/ImageMagick-${_pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-hdri \
		--with-gslib \
		--with-gvc \
		--with-modules \
		--with-perl \
		--with-rsvg \
		--with-dejavu-font-dir=/usr/share/fonts/dejavu \
		--with-gs-font-dir=/usr/share/ghostscript/fonts \
		--with-perl-options=INSTALLDIRS=vendor

	make
}

package() {
	cd "${srcdir}/ImageMagick-${_pkgver}"

	make -j1 install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/*.la "${pkgdir}/usr/lib/perl"
}

sha512sums=('6da0c64838488262809c8e70167b021624a436fc5569583075ee5878cb96ac5f511e05eede9009a49ee9856c7e96692e425dc41fe8d80a0599d300f45f2d3186')

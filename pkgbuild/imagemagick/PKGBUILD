pkgname=imagemagick
_pkgver=6.9.9-20
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
#depends=()
options=(libtool)
source=("http://www.imagemagick.org/download/ImageMagick-${_pkgver}.tar.xz")

build() {
	cd "${srcdir}/ImageMagick-${_pkgver}"

	sed -i "s#LIB_OMP#GOMP_LIBS#g" configure configure.ac

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-hdri \
		--with-gslib \
		--with-gvc \
		--with-modules \
		--with-perl \
		--with-rsvg \
		--with-dejavu-font-dir=/usr/share/fonts/dejavu \
		--with-gs-font-dir=/usr/share/ghostscript/fonts \
		--with-perl-options=INSTALLDIRS=vendor

	make
}

package() {
	cd "${srcdir}/ImageMagick-${_pkgver}"

	make -j1 install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/*.la "${pkgdir}/usr/lib/perl"
}

sha512sums=('36c241490ee41de5cd72e5bc297ac2353a0d4574337776445798b0c5823f491304c3f3a728d420d03f66b652eafcdb68ec2992347321ac3919c1d72c0afdb849')

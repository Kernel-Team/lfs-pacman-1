pkgname=intel-ucode
_dlid=27776
pkgver=20180425
pkgrel=1
pkgdesc="Microcode update files for Intel CPUs"
arch=('x86_64')
url="https://downloadcenter.intel.com/SearchResult.aspx?lang=eng&keyword=%22microcode%22"
license=('custom')
#depends=()
source=("https://downloadmirror.intel.com/${_dlid}/eng/microcode-${pkgver}.tgz")

build() {
	cd "${srcdir}"

	rm -rfv intel-ucode/list intel-ucode-with-caveats/list
	install -v -dm755 kernel/x86/microcode
	iucode_tool -w kernel/x86/microcode/GenuineIntel.bin intel-ucode/ intel-ucode-with-caveats/
	echo kernel/x86/microcode/GenuineIntel.bin | bsdcpio -o -H newc -R 0:0 > intel-ucode.img
}

package() {
	cd "${srcdir}"

	install -v -Dm644 intel-ucode.img "${pkgdir}/boot/intel-ucode.img"
}

sha512sums=('6cea53cc0f486891fb9ddffc1e03e8e0a6d1d91df6bfda81250b2c60714e7b4111caa9df5afa7f13d8144e591550ef7eb4fd1e153fc67fc904afb83ccc2e3bb0')

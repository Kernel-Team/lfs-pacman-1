pkgname=iproute2
pkgver=5.10.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/linux/kernel/git/shemminger/iproute2.git/"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/net/iproute2/iproute2-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/iproute2-${pkgver}"

	make CCOPTS="${CFLAGS}" CC="${CC:-gcc}" SBINDIR=/usr/bin
}

package() {
	cd "${srcdir}/iproute2-${pkgver}"

	make install SBINDIR=/usr/bin DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/sbin"

	mv "${pkgdir}/usr/bin/arpd" "${pkgdir}/usr/sbin"

	ln -sf ../bin/ip "${pkgdir}/usr/sbin/ip"
}

sha512sums=('a468eefa797898b6de452212aa432c0a931434defacae5fb4335754c1292c5d86a8c8dbf3017f90cec266d71a2a17ae469aac17e1a85a5cbddc70814313e4c13')

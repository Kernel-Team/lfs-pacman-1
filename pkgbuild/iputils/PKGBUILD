pkgname=iputils
pkgver=20161105
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
url="http://www.skbuff.net/iputils/"
license=('GPL')
#depends=()
install=iputils.install
source=("iputils-s${pkgver}.tar.gz::https://github.com/iputils/iputils/archive/s${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/iputils-s${pkgver}"

	sed -i "s#-O3#${CFLAGS}#g" Makefile
}

build() {
	cd "${srcdir}/iputils-s${pkgver}"

	unset CFLAGS

	make
	make -C doc man
}

package() {
	cd "${srcdir}/iputils-s${pkgver}"

	install -v -Dm755 ping "${pkgdir}/usr/bin/ping"
	install -v -Dm755 arping "${pkgdir}/usr/sbin/arping"
	install -v -Dm755 tracepath "${pkgdir}/usr/bin/tracepath"

	install -v -dm755 "${pkgdir}/usr/share/man/man8"
	install -v -m644 doc/arping.8 doc/tracepath.8 doc/ping.8 "${pkgdir}/usr/share/man/man8"

	# compat symlinks
	ln -sfv ping "${pkgdir}/usr/bin/ping6"
	ln -sfv ping.8 "${pkgdir}/usr/share/man/man8/ping6.8"

	ln -sfv tracepath "${pkgdir}/usr/bin/tracepath6"
	ln -sfv tracepath.8 "${pkgdir}/usr/share/man/man8/tracepath6.8"
}

sha512sums=('1593077b6eff1e57eb8a32897dd9624a745d0c87db1501a96436b168b109ef22d41ee69406a4a2154d8d72c823c1df723b4c52eeec8d82ba2762fb01dc0d4797')

pkgname=iputils
pkgver=20180515
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
url="http://www.skbuff.net/iputils/"
license=('GPL')
#depends=()
install=iputils.install
#source=("https://github.com/iputils/iputils/archive/s${pkgver}/iputils-s${pkgver}.tar.gz")
source=("git+https://github.com/iputils/iputils.git#commit=db372e3")

prepare() {
	cd "${srcdir}/iputils" #-s${pkgver}"

	sed -i "s#-O3#${CFLAGS}#g" Makefile
}

build() {
	cd "${srcdir}/iputils" #-s${pkgver}"

	unset CFLAGS

	make
	make -C doc man
}

package() {
	cd "${srcdir}/iputils" #-s${pkgver}"

	install -v -Dm755 ping "${pkgdir}/usr/bin/ping"
	install -v -Dm755 arping "${pkgdir}/usr/sbin/arping"
	install -v -Dm755 tracepath "${pkgdir}/usr/bin/tracepath"

	install -v -dm755 "${pkgdir}/usr/share/man/man8"
	install -v -m644 doc/arping.8 doc/tracepath.8 doc/ping.8 "${pkgdir}/usr/share/man/man8"

	# compat symlinks
	ln -sfv ping "${pkgdir}/usr/bin/ping6"
	ln -sfv ping.8 "${pkgdir}/usr/share/man/man8/ping6.8"

	ln -sfv tracepath "${pkgdir}/usr/bin/tracepath6"
	ln -sfv tracepath.8 "${pkgdir}/usr/share/man/man8/tracepath6.8"
}

sha512sums=('SKIP')

pkgname=iputils
pkgver=20190709
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
url="http://www.skbuff.net/iputils/"
license=('GPL')
#depends=()
install=iputils.install
source=("https://github.com/iputils/iputils/archive/s${pkgver}/iputils-s${pkgver}.tar.gz")
#source=("git+https://github.com/iputils/iputils.git#commit=db372e3")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-DBUILD_RDISC=false \
		-DBUILD_TFTPD=false \
		-DBUILD_NINFOD=false \
		"${srcdir}/iputils-s${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	# compat symlinks
	ln -sfv ping "${pkgdir}/usr/bin/ping6"
	ln -sfv ping.8 "${pkgdir}/usr/share/man/man8/ping6.8"

	ln -sfv tracepath "${pkgdir}/usr/bin/tracepath6"
	ln -sfv tracepath.8 "${pkgdir}/usr/share/man/man8/tracepath6.8"
}

sha512sums=('5db18ac49fa46ed810da4d508e78f4baf2d5c07e7f923eb49d005ad8745743d8861e5788b34a7e37fb7261cee7ddfb768b737eee9d200502ea53537142cac6b5')

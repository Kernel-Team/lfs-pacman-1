pkgname=iputils
pkgver=20200821
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
url="http://www.skbuff.net/iputils/"
license=('GPL')
#depends=()
install=iputils.install
source=("https://github.com/iputils/iputils/archive/s${pkgver}/iputils-s${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-DBUILD_RDISC=false \
		-DBUILD_TFTPD=false \
		-DBUILD_NINFOD=false \
		"${srcdir}/iputils-s${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	# compat symlinks
	ln -sfv ping "${pkgdir}/usr/bin/ping6"
	ln -sfv ping.8 "${pkgdir}/usr/share/man/man8/ping6.8"

	ln -sfv tracepath "${pkgdir}/usr/bin/tracepath6"
	ln -sfv tracepath.8 "${pkgdir}/usr/share/man/man8/tracepath6.8"
}

sha512sums=('4a57c3637cdd9aab2600682774e27370716cbdf1c7ac8ae61bf86c21c08701a5b697792df4aa95309b196eaa74f3cb6b2836a40f04da0e602156e982ac99d8c9')

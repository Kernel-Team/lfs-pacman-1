pkgname=itstool
pkgver=2.0.4
pkgrel=1
pkgdesc="XML to PO and back again"
arch=('x86_64')
url="http://itstool.org/"
license=('GPL3')
#depends=()
source=("http://files.itstool.org/itstool/itstool-${pkgver}.tar.bz2"
	"itstool-fixes.patch::https://github.com/itstool/itstool/commit/9b84c007a73e8275ca45762f1bfa3ab7c3a852e2.patch")

prepare() {
	cd "${srcdir}/itstool-${pkgver}"

	patch -Np1 -i "${srcdir}/itstool-fixes.patch"
}

build() {
	cd "${srcdir}/itstool-${pkgver}"

	./configure --prefix=/usr PYTHON=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/itstool-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('f5cfa9c75710f9cc43a61caeb92ca04ec77023bce6fb14d770dda82efde2e5d9050245618c249d309227a3d43555cb1739f5fe680b4a370786ed4631fa9ee4b7'
            '909543dc724ff4ec9ba1f39a327922ecd9a537a72f63bf001f98fe7a84511baac6dc2f8e5ffec7a75fcc4eb9f8077b217fc2f703c6b45904fe6e783cfb61b043')

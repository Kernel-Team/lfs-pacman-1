pkgbase=json-c
pkgname=('json-c' 'lib32-json-c')
pkgver=0.15
pkgrel=1
arch=('x86_64')
url="https://github.com/json-c/json-c/wiki"
license=('MIT')
#depends=()
source=("https://s3.amazonaws.com/json-c_releases/releases/json-c-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	cd "${srcdir}/build64"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		-DENABLE_THREADING=ON \
		-DENABLE_RDRAND=OFF \
		"${srcdir}/json-c-${pkgver}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
		-DENABLE_THREADING=ON \
		-DENABLE_RDRAND=OFF \
		"${srcdir}/json-c-${pkgver}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/build64"
	make -C "${srcdir}/build32"
}

package_json-c() {
pkgdesc="A JSON implementation in C"

	cd "${srcdir}/build64"

	make install DESTDIR="${pkgdir}"
}

package_lib32-json-c() {
pkgdesc="A JSON implementation in C (32 bit)"

	cd "${srcdir}/build32"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('dc01298bcc78f0f31a34f5fcfe45c0feebfd88518e97fb4f96f1a652f71ccdd303415a4c7bf5b573bdcbcca80428281f0dfccefc6545ea3a7f18dbb819332f34')

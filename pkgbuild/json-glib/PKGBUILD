pkgname=json-glib
pkgver=1.4.4
pkgrel=1
pkgdesc="JSON library built on GLib"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/JsonGlib"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/json-glib/${pkgver:0:3}/json-glib-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/json-glib-${pkgver}"

	sed -i 's#from "@filename@"##' json-glib/json-enum-types.h.in
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Ddocs=true \
		"${srcdir}/json-glib-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/libexec" "${pkgdir}/usr/share/installed-tests"
}

sha512sums=('cf56a99dce3938b5c0075810f105719836fac65392da33a49b26ebf33aee1fab89ca9fac58059a2008d688ecc75a3e524de60621a5b027d566963541f38b971f')

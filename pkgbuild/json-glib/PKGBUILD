pkgname=json-glib
pkgver=1.4.2
pkgrel=1
pkgdesc="JSON library built on GLib"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/JsonGlib"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/json-glib/${pkgver:0:3}/json-glib-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/json-glib-${pkgver}"

	sed -i 's#from "@filename@"##' json-glib/json-enum-types.h.in
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Ddocs=true \
		"${srcdir}/json-glib-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/libexec" "${pkgdir}/usr/share/installed-tests"
}

sha512sums=('e1495c4d0167db4cc31c7d9a6059de1f9126ab48fb45666a1bd10c69e10df9db195160cfba35cfac9c438a868559068e39402adf98a3e416d425c095c0946a51')

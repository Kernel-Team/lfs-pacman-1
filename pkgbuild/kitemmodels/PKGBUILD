pkgname=kitemmodels
pkgver=5.36.0
pkgrel=1
pkgdesc="Models for Qt Model/View system"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
#depends=()
options=(docs)
source=("https://download.kde.org/stable/frameworks/${pkgver:0:4}/kitemmodels-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DKDE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DBUILD_TESTING=OFF \
		"${srcdir}/kitemmodels-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('a1ded2f47c03ebd8395a7bc9fcae53d44c0654fd8b0229283e8ec78e222e2b2aace0b119055d76d54a7df5d491e6ca58011b274c7843808e185e15c5b916ee42')

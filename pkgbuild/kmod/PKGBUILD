pkgname=kmod
pkgver=23
pkgrel=1
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL2')
groups=('core')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/kernel/kmod/kmod-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/kmod-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--bindir=/bin \
		--with-rootlibdir=/lib \
		--with-xz \
		--with-zlib

	make
}

package() {
	cd "${srcdir}/kmod-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/sbin"

	for file in depmod insmod lsmod modinfo modprobe rmmod
	do
		ln -sfv ../bin/kmod "${pkgdir}/sbin/${file}"
	done

	ln -sfv kmod "${pkgdir}/bin/lsmod"

	sed -i "s:.so modules.dep.5:.so man5/modules.dep.5:" "${pkgdir}/usr/share/man/man5/modules.dep.bin.5"
}

md5sums=('3cf469f40ec2ed51f56ba45ea03793e7')

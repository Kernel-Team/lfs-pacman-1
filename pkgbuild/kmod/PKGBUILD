pkgname=kmod
pkgver=24
pkgrel=1
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/kernel/kmod/kmod-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/kmod-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--bindir=/bin \
		--with-rootlibdir=/lib \
		--with-xz \
		--with-zlib

	make
}

package() {
	cd "${srcdir}/kmod-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/sbin"

	for file in depmod insmod lsmod modinfo modprobe rmmod
	do
		ln -sfv ../bin/kmod "${pkgdir}/sbin/${file}"
	done

	ln -sfv kmod "${pkgdir}/bin/lsmod"
}

sha512sums=('58c8da100418b6325de1595bceee4115cbdd7151d03a028c0b1f6d5d3e6611c667e27f3a0245d29781125581849fd1e854fa8d87b63c4b8fa3f196d0a97e1779')

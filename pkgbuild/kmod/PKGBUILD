pkgname=kmod
pkgver=25
pkgrel=1
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/kernel/kmod/kmod-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/kmod-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-xz \
		--with-zlib

	make
}

package() {
	cd "${srcdir}/kmod-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/sbin"

	for file in depmod insmod lsmod modinfo modprobe rmmod
	do
		ln -sfv ../bin/kmod "${pkgdir}/usr/sbin/${file}"
	done

	ln -sfv kmod "${pkgdir}/usr/bin/lsmod"
}

sha512sums=('d579cd0cea24a06362a74927b7a3c777e9e01c990306e1032e4781cd441ffe435c70f2c2c4f6ae39eb1d857e622746411d5824d0c0d8bb79f91dc9fa51956252')

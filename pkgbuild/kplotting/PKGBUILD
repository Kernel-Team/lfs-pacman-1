pkgname=kplotting
pkgver=5.35.0
pkgrel=1
pkgdesc="Lightweight plotting framework"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
#depends=()
options=(docs)
source=("https://download.kde.org/stable/frameworks/${pkgver:0:4}/kplotting-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DKDE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DBUILD_TESTING=OFF \
		"${srcdir}/kplotting-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('56263048189264004c6d06f68165e8d92196c30056a4f0c1052bd4cbcb67e17c6f4965d8b8f1e0ac6b1ec4b91d2195284887464b1bb1f0d24162e80cbd7ab1ee')

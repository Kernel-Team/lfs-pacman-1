pkgname=krb5
pkgver=1.15.1
pkgrel=1
pkgdesc="The Kerberos network authentication system"
arch=('x86_64')
url='https://web.mit.edu/kerberos/'
license=('custom')
#depends=()
options=(emptydirs)
source=("https://web.mit.edu/kerberos/dist/krb5/${pkgver:0:4}/krb5-${pkgver}.tar.gz"
	"krb5-kadmind.service"
	"krb5-kdc.service"
	"krb5-kpropd.service")

prepare() {
	cd "${srcdir}/krb5-${pkgver}/src"

	sed -e "s@python2.5/Python.h@& python2.7/Python.h@g" \
		-e "s@-lpython2.5]@&,\n  AC_CHECK_LIB(python2.7,main,[PYTHON_LIB=-lpython2.7])@g" \
		-i configure.in

	sed -e "/KRB5ROOT=/s@/local@@" \
		-i util/ac_check_krb5.m4

	autoconf
}

build() {
	cd "${srcdir}/krb5-${pkgver}/src"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var/lib \
		--enable-dns-for-realm \
		--with-system-et \
		--with-system-ss \
		--with-ldap \
		--without-system-verto \

	make
}

package() {
	cd "${srcdir}/krb5-${pkgver}/src"

	make install DESTDIR="${pkgdir}"

	chmod 755 "${pkgdir}"/usr/lib/*.so*

	install -v -dm755 "${pkgdir}/usr/share/krb5/examples"

	install -v -Dm644 config-files/kdc.conf "${pkgdir}/var/lib/krb5kdc/kdc.conf"
	install -v -Dm644 config-files/krb5.conf "${pkgdir}/etc/krb5.conf"

	install -v -Dm644 util/ac_check_krb5.m4 "${pkgdir}/usr/share/aclocal/ac_check_krb5.m4"

	install -v -m644 config-files/kdc.conf config-files/krb5.conf "${pkgdir}/usr/share/krb5/examples"
	install -v -m644 plugins/kdb/ldap/libkdb_ldap/kerberos.ldif plugins/kdb/ldap/libkdb_ldap/kerberos.schema "${pkgdir}/usr/share/krb5/examples"

	rm -rf "${pkgdir}/usr/share/examples" "${pkgdir}/usr/share/gnats"

	install -v -Dm644 "${srcdir}/krb5-kadmind.service" "${pkgdir}/usr/lib/systemd/system/krb5-kadmind.service"
	install -v -Dm644 "${srcdir}/krb5-kdc.service" "${pkgdir}/usr/lib/systemd/system/krb5-kdc.service"
	install -v -Dm644 "${srcdir}/krb5-kpropd.service" "${pkgdir}/usr/lib/systemd/system/krb5-kpropd.service"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('068b4c012722d8c232049d2a617f7ee28ceeaba6be94a78439e69e37b66cfdc49085641e42cfb03b2fbb72d21517b537e437061ec4dd2bf864f31e55e05fe918'
            'ad39299de04db90b280d0573b870588bbf8cd87b661636ff111cd1013440ca534f75d288c0c424512f70291e4930bd58af0e9731b70d4138337a2bf5f223898e'
            '0b4d41f658769b0134764dafeab9008fa0a5916adc9bd9362dfda6f92681d8a8cc4c6b78058b2c2ec6e6e3d991e2de3d883dac1813530ce791cd8df2b73c9658'
            'a75fb86c71476db9c60bbbc4372bf469244d5b5c99b82fab4bfc4edeb1db2e15aada177cea9c711723b5b3e7fbb75ad38f46f4f3bb24e64304bc89602b9192fd')

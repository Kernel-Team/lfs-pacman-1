pkgbase=libasyncns
pkgname=('libasyncns' 'lib32-libasyncns')
pkgver=0.8
pkgrel=1
arch=('x86_64')
url="http://0pointer.de/lennart/projects/libasyncns/"
license=('LGPL')
#depends=()
source=("http://0pointer.de/lennart/projects/libasyncns/libasyncns-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/libasyncns{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libasyncns-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libasyncns32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libasyncns-${pkgver}"
	make -C "${srcdir}/libasyncns32-${pkgver}"
}

package_libasyncns() {
	pkgdesc="A C library for executing name service queries asynchronously"

	cd "${srcdir}/libasyncns-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libasyncns() {
	pkgdesc="A C library for executing name service queries asynchronously (32 bit)"

	cd "${srcdir}/libasyncns32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('1f553d6ce1ad255bc83b3d8e9384f515')

pkgbase=libatomic_ops
pkgname=('libatomic_ops' 'lib32-libatomic_ops')
pkgver=7.4.8
pkgrel=1
arch=('x86_64')
url="https://github.com/ivmai/libatomic_ops"
license=('GPL2' 'MIT')
#depends=()
source=("https://github.com/ivmai/libatomic_ops/releases/download/v${pkgver}/libatomic_ops-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/libatomic_ops{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libatomic_ops-${pkgver}"

	./configure --prefix=/usr --enable-shared

	cd "${srcdir}/libatomic_ops32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--enable-shared

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libatomic_ops-${pkgver}"
	make -C "${srcdir}/libatomic_ops32-${pkgver}"

}

package_libatomic_ops() {
	pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"

	cd "${srcdir}/libatomic_ops-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/share"
}

package_lib32-libatomic_ops() {
	pkgdesc="Provides semi-portable access to hardware provided atomic memory operations (32 bit)"

	cd "${srcdir}/libatomic_ops32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('d03bcb946f8143bf79c1f44ef0637d0462efc1eb7a0abf15231341f0f9193dd2d57a17f0670a95fda3731642f444a85d0944f86447cff8e69bc851b8d5acc863')

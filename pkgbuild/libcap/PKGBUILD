pkgbase=libcap
pkgname=('libcap' 'lib32-libcap')
pkgver=2.29
pkgrel=1
arch=('x86_64')
url="http://sites.google.com/site/fullycapable/"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/libcap-${pkgver}"

	sed -i "s|-D_FILE_OFFSET_BITS=64|& ${CFLAGS}|" Make.Rules
	sed -i "/C tests/d" Makefile
	sed -i "12,18d" libcap/libcap.pc.in

	cp -a "${srcdir}"/libcap{,32}-${pkgver}
}

build() {
	unset CFLAGS

	cd "${srcdir}/libcap-${pkgver}"

	sed -i "s| gcc| ${CC}|" Make.Rules

	make BUILD_GPERF=no

	cd "${srcdir}/libcap32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	sed -i "s| gcc| ${CC}|" Make.Rules

	make -C libcap BUILD_GPERF=no lib=lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH
}

package_libcap() {
	pkgdesc="POSIX 1003.1e capabilities"

	cd "${srcdir}/libcap-${pkgver}"

	make install BUILD_GPERF=no RAISE_SETFCAP=no prefix=/usr lib=lib DESTDIR="${pkgdir}"

	chmod 755 "${pkgdir}/usr/lib/libcap.so"

	install -v -dm755 "${pkgdir}/etc/security"
	install -v -m644 pam_cap/capability.conf "${pkgdir}/etc/security"
}

package_lib32-libcap() {
	pkgdesc="POSIX 1003.1e capabilities (32 bit)"

	cd "${srcdir}/libcap32-${pkgver}"

	make -C libcap prefix=/usr lib=lib32 install BUILD_GPERF=no DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	chmod 755 "${pkgdir}/usr/lib32/libcap.so"
}

sha512sums=('400a6883037bbc1f00847e2fcfb940d733784adec857969069972a3d5120dde6b554326ba6dd652670d854308433c66147944dda8b8b344ca249cef73ea866ef')

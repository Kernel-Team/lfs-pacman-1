pkgbase=libcap
pkgname=('libcap' 'lib32-libcap')
pkgver=2.26
pkgrel=1
arch=('x86_64')
url="http://sites.google.com/site/fullycapable/"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-${pkgver}.tar.xz")

CC=clang
prepare() {
	cd "${srcdir}/libcap-${pkgver}"

	sed -i "s|-D_FILE_OFFSET_BITS=64|& ${CFLAGS}|" Make.Rules

	cp -a "${srcdir}"/libcap{,32}-${pkgver}
}

build() {
	unset CFLAGS

	cd "${srcdir}/libcap-${pkgver}"

	sed -i "s| gcc| ${CC}|" Make.Rules

	make BUILD_GPERF=no

	cd "${srcdir}/libcap32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	sed -i "s| gcc| ${CC}|" Make.Rules

	make -C libcap BUILD_GPERF=no lib=lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH
}

package_libcap() {
	pkgdesc="POSIX 1003.1e capabilities"

	cd "${srcdir}/libcap-${pkgver}"

	make install BUILD_GPERF=no RAISE_SETFCAP=no prefix=/usr lib=lib DESTDIR="${pkgdir}"

	chmod 755 "${pkgdir}/usr/lib/libcap.so"

	install -v -dm755 "${pkgdir}/etc/security"
	install -v -m644 pam_cap/capability.conf "${pkgdir}/etc/security"
}

package_lib32-libcap() {
	pkgdesc="POSIX 1003.1e capabilities (32 bit)"

	cd "${srcdir}/libcap32-${pkgver}"

	make -C libcap prefix=/usr lib=lib32 install BUILD_GPERF=no DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	chmod 755 "${pkgdir}/usr/lib32/libcap.so"
}

sha512sums=('1c2d59f007226405a924950b2c2090393527e06f0692a84e6463e33915a070df61a9070b8f30a624d5630ddd39290eac117e5d440577d1edd48510195b9d12f0')

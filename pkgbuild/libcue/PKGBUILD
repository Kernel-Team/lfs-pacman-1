pkgname=libcue
pkgver=2.1.0
pkgrel=1
pkgdesc="Parses so-called cue sheets and handles the parsed data"
arch=('x86_64')
url="https://github.com/lipnitsk/libcue/"
license=('GPL2')
#depends=()
source=("libcue-${pkgver}.tar.gz::https://github.com/lipnitsk/libcue/archive/v${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		"${srcdir}/libcue-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('2e69e79b6e96eb999e432e10557aaaa986d685d6e69e215bf878760032621bb2a80fafecc9bb1d80bf2f5f57b6288731fab07e1967d8dad29fa864b31d01a9b1')

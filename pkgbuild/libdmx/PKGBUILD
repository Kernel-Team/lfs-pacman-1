pkgbase=libdmx
pkgname=('libdmx' 'lib32-libdmx')
pkgver=1.1.4
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libdmx-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libdmx{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libdmx-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libdmx32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libdmx-${pkgver}"
	make -C "${srcdir}/libdmx32-${pkgver}"
}

package_libdmx() {
	pkgdesc="X11 Distributed Multihead extension library"

	cd "${srcdir}/libdmx-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libdmx() {
	pkgdesc="X11 Distributed Multihead extension library (32 bit)"

	cd "${srcdir}/libdmx32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('77aba6cadfd17db0cc7e1bc26f14d908223a0fdb135d46f86c3be34e3bc7e2f843f08d4d0ef773357e83b17122bffe56a0442d12908378c05a22177b10ae10e9')

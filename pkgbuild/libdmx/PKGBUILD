pkgbase=libdmx
pkgname=('libdmx' 'lib32-libdmx')
pkgver=1.1.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libdmx-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libdmx{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libdmx-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libdmx32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libdmx-${pkgver}"
	make -C "${srcdir}/libdmx32-${pkgver}"
}

package_libdmx() {
	pkgdesc="X11 Distributed Multihead extension library"

	cd "${srcdir}/libdmx-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libdmx() {
	pkgdesc="X11 Distributed Multihead extension library (32 bit)"

	cd "${srcdir}/libdmx32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('551cbba06e87ddc6f9a1f4567de4b8c592a4b6f6b6c27970c67c644ffe5c7f396e3c64c6df564d83a73f1b3842ba0a29a2463580fc0a264c552d76fbcba5a021')

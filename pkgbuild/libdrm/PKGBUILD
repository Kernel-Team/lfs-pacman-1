pkgbase=libdrm
pkgname=('libdrm' 'lib32-libdrm')
pkgver=2.4.100
pkgrel=1
arch=('x86_64')
url="https://dri.freedesktop.org/wiki/"
license=('custom')
#depends=()
source=("https://dri.freedesktop.org/libdrm/libdrm-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libdrm{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libdrm-${pkgver}"

	./configure --prefix=/usr \
		--enable-udev \
		--disable-valgrind

	cd "${srcdir}/libdrm32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--enable-udev \
		--disable-valgrind

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libdrm-${pkgver}"
	make -C "${srcdir}/libdrm32-${pkgver}"
}

package_libdrm() {
	pkgdesc="Userspace interface to kernel DRM services"

	cd "${srcdir}/libdrm-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libdrm() {
	pkgdesc="Userspace interface to kernel DRM services (32 bit)"

	cd "${srcdir}/libdrm32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('4d3a5556e650872944af52f49de395e0ce8ac9ac58530e39a34413e94dc56c231ee71b8b8de9fb944263515a922b3ebbf7ddfebeaaa91543c2604f9bcf561247')

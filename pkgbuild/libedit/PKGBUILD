pkgbase=libedit
pkgname=('libedit' 'lib32-libedit')
_realver=20191211-3.1
pkgver=${_realver/-/+}
pkgrel=1
arch=('x86_64')
url="http://www.thrysoee.dk/editline/"
license=('BSD')
#depends=()
source=("http://thrysoee.dk/editline/libedit-${_realver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/libedit{,32}-${_realver}
}

build() {
	cd "${srcdir}/libedit-${_realver}"

	./configure --prefix=/usr --enable-widec

	cd "${srcdir}/libedit32-${_realver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--enable-widec

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libedit-${_realver}"
	make -C "${srcdir}/libedit32-${_realver}"
}

package_libedit() {
	pkgdesc="Command line editor library providing generic line editing, history, and tokenization functions"

	cd "${srcdir}/libedit-${_realver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/share/man/man3/history.3"
}

package_lib32-libedit() {
	pkgdesc="Command line editor library providing generic line editing, history, and tokenization functions (32 bit)"

	cd "${srcdir}/libedit32-${_realver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('8393477a87e65d330b037494110501b5d6d2c1cbe80d82b9bc7a6315a21fd1327fe91354346966d30541015e2c277be2187fa4bb737b14f706957c70f4083547')

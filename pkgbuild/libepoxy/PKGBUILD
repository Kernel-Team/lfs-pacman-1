pkgname=libepoxy
pkgver=1.5.5
pkgrel=1
pkgdesc="Library handling OpenGL function pointer management"
arch=('x86_64')
url="https://github.com/anholt/libepoxy"
license=('MIT')
#depends=()
source=("https://github.com/anholt/libepoxy/releases/download/${pkgver}/libepoxy-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dtests=false \
		"${srcdir}/libepoxy-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('d5e008764f21ab86c17dbc548f353ed71434100a19855830d6851690b1c8475dbe2053358698e37d4fcc86a3db21f8c497e15375412509e2493fcf18825e61b6')

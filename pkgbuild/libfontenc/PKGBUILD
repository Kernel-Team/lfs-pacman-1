pkgbase=libfontenc
pkgname=('libfontenc' 'lib32-libfontenc')
pkgver=1.1.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libfontenc-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libfontenc{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libfontenc-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libfontenc32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libfontenc-${pkgver}"
	make -C "${srcdir}/libfontenc32-${pkgver}"
}

package_libfontenc() {
	pkgdesc="X11 font encoding library"

	cd "${srcdir}/libfontenc-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libfontenc() {
	pkgdesc="X11 font encoding library (32 bit)"

	cd "${srcdir}/libfontenc32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('7346079a6980464f57f5da0e96025bbf4b13fbb7ed976b58d9c54b78525d9d0b4174b4783326a12048971cf5d05221bafe2f41c6b02931743913719cde21740c')

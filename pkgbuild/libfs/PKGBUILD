pkgbase=libfs
pkgname=('libfs' 'lib32-libfs')
pkgver=1.0.7
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libFS-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libFS{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libFS-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libFS32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libFS-${pkgver}"
	make -C "${srcdir}/libFS32-${pkgver}"
}

package_libfs() {
	pkgdesc="X11 Font Services Library"

	cd "${srcdir}/libFS-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libfs() {
	pkgdesc="X11 Font Services Library (32 bit)"

	cd "${srcdir}/libFS32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('3596e87710c14706ab345377799e9b2dbab79500fd75b4213f9615bf7db7df3265b4dbc65fdeca9d32343b848013780cfa1a43e76229665ce80246332bb8b96f')

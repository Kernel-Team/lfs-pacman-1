pkgbase=libfs
pkgname=('libfs' 'lib32-libfs')
pkgver=1.0.8
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libFS-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libFS{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libFS-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libFS32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libFS-${pkgver}"
	make -C "${srcdir}/libFS32-${pkgver}"
}

package_libfs() {
	pkgdesc="X11 Font Services Library"

	cd "${srcdir}/libFS-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libfs() {
	pkgdesc="X11 Font Services Library (32 bit)"

	cd "${srcdir}/libFS32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('466198ae5c0b83e3579713d86ddfd0b039f5c5b511cf6d5dc0e9664ee2d40aec9e8929445c8e97394d85faf76a7cf2f81bddb13d23de7ce19919e3b1e36ba56f')

pkgbase=libgcrypt
pkgname=('libgcrypt' 'lib32-libgcrypt')
pkgver=1.8.2
pkgrel=1
arch=('x86_64')
url="http://www.gnupg.org"
license=('LGPL')
#depends=()
source=("https://www.gnupg.org/ftp/gcrypt/libgcrypt/libgcrypt-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libgcrypt{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libgcrypt-${pkgver}"

	./configure --prefix=/usr --disable-padlock-support

	cd "${srcdir}/libgcrypt32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	sed -i 's:path="amd64":path="i586 i386":' mpi/config.links

	./configure --prefix=/usr --libdir=/usr/lib32 --disable-padlock-support

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libgcrypt-${pkgver}"
	make -C "${srcdir}/libgcrypt32-${pkgver}"
}

package_libgcrypt() {
	pkgdesc="General purpose cryptographic library based on the code from GnuPG"

	cd "${srcdir}/libgcrypt-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libgcrypt() {
	pkgdesc="General purpose cryptographic library based on the code from GnuPG (32 bit)"

	cd "${srcdir}/libgcrypt32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('1e8c414f95bf6b50e778102ca7c1b3b1f30d8320826d9fff747a0a098ef85499cdc3e6de736853b9cd4e5dadda35c7c0a291e13643dcac5eaef44f2ddc7a6c09')

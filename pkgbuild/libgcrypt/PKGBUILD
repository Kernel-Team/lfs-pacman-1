pkgbase=libgcrypt
pkgname=('libgcrypt' 'lib32-libgcrypt')
pkgver=1.8.7
pkgrel=1
arch=('x86_64')
url="http://www.gnupg.org"
license=('LGPL')
#depends=()
source=("https://www.gnupg.org/ftp/gcrypt/libgcrypt/libgcrypt-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libgcrypt{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libgcrypt-${pkgver}"

	./configure --prefix=/usr --disable-padlock-support

	cd "${srcdir}/libgcrypt32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	sed -i 's:path="amd64":path="i586 i386":' mpi/config.links

	./configure --prefix=/usr --libdir=/usr/lib32 --disable-padlock-support

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libgcrypt-${pkgver}"
	make -C "${srcdir}/libgcrypt32-${pkgver}"
}

package_libgcrypt() {
	pkgdesc="General purpose cryptographic library based on the code from GnuPG"

	cd "${srcdir}/libgcrypt-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libgcrypt() {
	pkgdesc="General purpose cryptographic library based on the code from GnuPG (32 bit)"

	cd "${srcdir}/libgcrypt32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('6309d17624d8029848990d225d5924886c951cef691266c8e010fbbb7f678972cee70cbb91d370ad0bcdc8c8761402a090c2c853c9427ec79293624a59da5060')

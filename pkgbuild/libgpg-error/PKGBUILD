pkgbase=libgpg-error
pkgname=('libgpg-error' 'lib32-libgpg-error')
pkgver=1.26
pkgrel=1
arch=('x86_64')
url="http://www.gnupg.org"
license=('LGPL')
#depends=()
source=("ftp://ftp.gnupg.org/gcrypt/libgpg-error/libgpg-error-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libgpg-error{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libgpg-error-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libgpg-error32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libgpg-error-${pkgver}"
	make -C "${srcdir}/libgpg-error32-${pkgver}"
}

package_libgpg-error() {
	pkgdesc="Support library for libgcrypt"

	cd "${srcdir}/libgpg-error-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libgpg-error.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libgpg-error.so") "${pkgdir}/usr/lib/libgpg-error.so"

	rm -rf "${pkgdir}/usr/share/common-lisp"
}

package_lib32-libgpg-error() {
	pkgdesc="Support library for libgcrypt (32 bit)"

	cd "${srcdir}/libgpg-error32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('97456709dbbcbb69354317ffe3e347cd')

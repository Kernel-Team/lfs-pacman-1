pkgbase=libgpg-error
pkgname=('libgpg-error' 'lib32-libgpg-error')
pkgver=1.41
pkgrel=1
arch=('x86_64')
url="http://www.gnupg.org"
license=('LGPL')
#depends=()
source=("https://gnupg.org/ftp/gcrypt/libgpg-error/libgpg-error-${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/libgpg-error-${pkgver}"

	sed -i 's/namespace/pkg_&/' src/Makefile.{am,in} src/mkstrtable.awk

	cp -a "${srcdir}"/libgpg-error{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libgpg-error-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libgpg-error32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libgpg-error-${pkgver}"
	make -C "${srcdir}/libgpg-error32-${pkgver}"
}

package_libgpg-error() {
	pkgdesc="Support library for libgcrypt"

	cd "${srcdir}/libgpg-error-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/share/common-lisp"
}

package_lib32-libgpg-error() {
	pkgdesc="Support library for libgcrypt (32 bit)"

	cd "${srcdir}/libgpg-error32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('578d1d8090b9f6e6cbe59d9aeebec7df81aae492f75ec343f8df530950d30866fa0a7903ca050bf7bf721413f7b2708abe34f7467e2b208cc1447ffc9bd5b9a5')

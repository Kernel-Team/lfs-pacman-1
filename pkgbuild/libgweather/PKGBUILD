pkgname=libgweather
pkgver=3.36.1
pkgrel=1
pkgdesc="Location and timezone database and weather-lookup library"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/LibGWeather"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/libgweather/${pkgver:0:4}/libgweather-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/libgweather-${pkgver}"

	sed -i 's#from "@filename@"##' libgweather/gweather-enum-types.h.tmpl
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dgtk_doc=true \
		"${srcdir}/libgweather-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('6c7090b0b6dfac61e73a398d34329cd706080a007694ceb3d47700b4f444982f8ca989dd6dfaefe85b79ffd5af9c423be4bf1bd8491ac0ba6bd15c150d97b26d')

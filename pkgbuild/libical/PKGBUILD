pkgname=libical
pkgver=3.0.8
pkgrel=1
pkgdesc="An open source reference implementation of the icalendar data type and serialization format"
arch=('x86_64')
url="https://github.com/libical/libical"
license=('LGPL' 'MPL')
#depends=()
source=("https://github.com/libical/libical/releases/download/v${pkgver}/libical-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/libical-${pkgver}"

	sed -i '/define LEAP_MONTH/a #define FALSE 0' src/libical/icalrecur.c

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DGOBJECT_INTROSPECTION=ON \
		-DICAL_GLIB_VAPI=ON \
		-DSHARED_ONLY=ON \
		"${srcdir}/libical-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('ce015e6d4c1c7cb4af7b45748ce8251c663f80f6a4357ddff6a97796642619abe882f4cadeca10cabeb1b25577869f436da15bca882e032eb3ff0475f6010d8b')

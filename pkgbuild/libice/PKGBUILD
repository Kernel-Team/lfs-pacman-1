pkgbase=libice
pkgname=('libice' 'lib32-libice')
pkgver=1.0.9
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libICE-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libICE{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libICE-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libICE32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libICE-${pkgver}"
	make -C "${srcdir}/libICE32-${pkgver}"
}

package_libice() {
	pkgdesc="X11 Inter-Client Exchange library"

	cd "${srcdir}/libICE-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libice() {
	pkgdesc="X11 Inter-Client Exchange library (32 bit)"

	cd "${srcdir}/libICE32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('daa8126ee5279c08f801274a2754132762dea2a40f4733c4b0bf8e8bdad61cba826939a2e067beb3524e256a98a2b83f23c8d4643f3e75a284ab02cc73da41b7')

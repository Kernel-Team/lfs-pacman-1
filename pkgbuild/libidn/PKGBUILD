pkgname=libidn
pkgver=1.33
pkgrel=1
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications"
arch=('x86_64')
url="https://www.gnu.org/software/libidn/"
license=('GPL3' 'LGPL')
#depends=()
source=("https://ftp.gnu.org/gnu/libidn/libidn-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/libidn-${pkgver}"

	./configure --prefix=/usr

	make
}

package() {
	cd "${srcdir}/libidn-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/share/emacs" "${pkgdir}"/usr/share/info/*.png
	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libidn.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libidn.so") "${pkgdir}/usr/lib/libidn.so"
}

sha512sums=('38dd459eaeda0c9e3cc2d24d967113515a499747550a2a9157f32357def90d71a3a3b52398e96a44a28cd5948dc353b0473c4ff0453a69720191c4cb49cac2c6')

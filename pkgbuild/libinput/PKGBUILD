pkgname=libinput
pkgver=1.10.5
pkgrel=1
pkgdesc="Input device management and event handling library"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libinput/"
license=('custom:X11')
#depends=()
source=("https://freedesktop.org/software/libinput/libinput-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Ddebug-gui=false \
		-Dtests=false \
		-Ddocumentation=false \
		"${srcdir}/libinput-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('61f19ea777b523f0e76869b8e7dbbff5e51132d3c856c6edb9915a695f7903256228e71be2567642866923a800f1283bdd5aff97266ac26c2e42dfce0bffa50b')

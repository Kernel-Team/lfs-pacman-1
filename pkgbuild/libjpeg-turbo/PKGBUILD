pkgname=libjpeg-turbo
pkgver=2.0.2
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=('x86_64')
url="https://libjpeg-turbo.org/"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/libjpeg-turbo/libjpeg-turbo-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_DEFAULT_LIBDIR=lib \
		-DWITH_JPEG8=ON \
		"${srcdir}/libjpeg-turbo-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('204b6d083e99488c975c75efb08699e4dc1c409556e4dee4f21e3ee67e9c6682eb342f2e5712816b0342c00399fbe6e43fbce30c3d22f30f7ef91db006b3be08')

pkgname=libnotify
pkgver=0.7.8
pkgrel=1
pkgdesc="Library for sending desktop notifications"
arch=('x86_64')
url="https://developer.gnome.org/notification-spec/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/libnotify/${pkgver:0:3}/libnotify-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dtests=false \
		-Dgtk_doc=true \
		"${srcdir}/libnotify-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('9973d7a1af5dd7cfbaa0d0d2c9e72a40df9d814967fde47e95763a69690cc4ed32ecf713d75f91f3dff99a693eff9dab963be124e0530ebf231d517a44e75e94')

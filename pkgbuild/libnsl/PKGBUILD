pkgbase=libnsl
pkgname=('libnsl' 'lib32-libnsl')
pkgver=1.3.0
pkgrel=1
arch=('x86_64')
url="https://github.com/thkukuk/libnsl"
license=('LGPL2.1')
#depends=()
source=("https://github.com/thkukuk/libnsl/releases/download/v${pkgver}/libnsl-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/libnsl-${pkgver}"

	cp -a "${srcdir}"/libnsl{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libnsl-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libnsl32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libnsl-${pkgver}"
	make -C "${srcdir}/libnsl32-${pkgver}"
}

package_libnsl() {
	pkgdesc="Public client interface for NIS(YP) and NIS+"

	cd "${srcdir}/libnsl-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -f "${pkgdir}/usr/lib/libnsl.so"
	echo "INPUT (libnsl.so.2 -ltirpc)" > "${pkgdir}/usr/lib/libnsl.so"
}

package_lib32-libnsl() {
	pkgdesc="Public client interface for NIS(YP) and NIS+ (32 bit)"

	cd "${srcdir}/libnsl32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	rm -f "${pkgdir}/usr/lib32/libnsl.so"
	echo "INPUT (libnsl.so.2 -ltirpc)" > "${pkgdir}/usr/lib32/libnsl.so"
}

sha512sums=('a5a6c3ccb2d1e724c8c1f65e55dcd09383eb1ae019c55f4c09441eadf23ffbc2196cfad259805b0ac40ddf3a10af0da453e4d739d67d46829c64d0995dab4e55')

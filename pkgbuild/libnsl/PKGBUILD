pkgbase=libnsl
pkgname=('libnsl' 'lib32-libnsl')
pkgver=1.2.0
pkgrel=1
arch=('x86_64')
url="https://github.com/thkukuk/libnsl"
license=('LGPL2.1')
#depends=()
source=("https://github.com/thkukuk/libnsl/archive/v${pkgver}/libnsl-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/libnsl-${pkgver}"

	export ACLOCAL_PATH=/usr/share/aclocal

	autoreconf -fi

	cp -a "${srcdir}"/libnsl{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libnsl-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libnsl32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libnsl-${pkgver}"
	make -C "${srcdir}/libnsl32-${pkgver}"
}

package_libnsl() {
	pkgdesc="Public client interface for NIS(YP) and NIS+"

	cd "${srcdir}/libnsl-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -f "${pkgdir}/usr/lib/libnsl.so"
	echo "INPUT (libnsl.so.2 -ltirpc)" > "${pkgdir}/usr/lib/libnsl.so"
}

package_lib32-libnsl() {
	pkgdesc="Public client interface for NIS(YP) and NIS+ (32 bit)"

	cd "${srcdir}/libnsl32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	rm -f "${pkgdir}/usr/lib32/libnsl.so"
	echo "INPUT (libnsl.so.2 -ltirpc)" > "${pkgdir}/usr/lib32/libnsl.so"
}

sha512sums=('30115aa679a04ad01f55cff9dd1890b4c41c730a9bac2adab5add9ae03a0b4687c5f4b6e3b8652ecbb074eefac8faee3f1f13ea60d42cf4432db8a575ca72cd8')

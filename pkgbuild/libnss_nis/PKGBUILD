pkgbase=libnss_nis
pkgname=('libnss_nis' 'lib32-libnss_nis')
pkgver=3.0
pkgrel=1
arch=('x86_64')
url="https://github.com/thkukuk/libnss_nis"
license=('LGPL2.1')
#depends=()
source=("https://github.com/thkukuk/libnss_nis/archive/v${pkgver}/libnss_nis-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/libnss_nis-${pkgver}"

	export ACLOCAL_PATH=/usr/share/aclocal

	autoreconf -fi

	cp -a "${srcdir}"/libnss_nis{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libnss_nis-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libnss_nis32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libnss_nis-${pkgver}"
	make -C "${srcdir}/libnss_nis32-${pkgver}"
}

package_libnss_nis() {
	pkgdesc="NSS NIS plugin for glibc"

	cd "${srcdir}/libnss_nis-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libnss_nis() {
	pkgdesc="NSS NIS plugin for glibc (32 bit)"

	cd "${srcdir}/libnss_nis32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('3dde989a278f97a8cb9d831d5c10c1e5dce5b75c2e2b755177a054df0fb86795311e1750d721c52066d59a026035df4fdd7dc5d90ef743315e5b962ffef9f737')

pkgbase=libogg
pkgname=('libogg' 'lib32-libogg')
pkgver=1.3.2
pkgrel=1
arch=('x86_64')
url="https://www.xiph.org/ogg/"
license=('BSD')
#depends=()
source=("http://downloads.xiph.org/releases/ogg/libogg-${pkgver}.tar.xz"
	"config_types.h")

prepare() {
	cp -a "${srcdir}"/libogg{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libogg-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libogg32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libogg-${pkgver}"
	make -C "${srcdir}/libogg32-${pkgver}"
}

package_libogg() {
	pkgdesc="Ogg bitstream and framing library"

	cd "${srcdir}/libogg-${pkgver}"

	make install DESTDIR="${pkgdir}"

	mv "${pkgdir}/usr/include/ogg/config_types.h" \
		"${pkgdir}/usr/include/ogg/config_types-64.h"

	install -v -Dm644 "${srcdir}/config_types.h" \
		"${pkgdir}/usr/include/ogg/config_types.h"
}

package_lib32-libogg() {
	pkgdesc="Ogg bitstream and framing library (32 bit)"

	cd "${srcdir}/libogg32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	install -v -Dm644 dest/usr/include/ogg/config_types.h \
		"${pkgdir}/usr/include/ogg/config_types-32.h"
}

sha512sums=('dc9552b15ceccd7e12bff39a6670120ae53af74ce97254d047c28f49ba6a320f55059ed509ec1d8758a972c72514f2866634d829f3cb528c2f595cb056e9bca6'
            'eaab24ad104942c8366cf133d0413240eb7d8a50d1e0affa9fc99493327f4b06d239273e380a0e5a5751148bab03c166bdfe23dc12899789c1ae453b4e0f0904')

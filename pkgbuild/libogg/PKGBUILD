pkgbase=libogg
pkgname=('libogg' 'lib32-libogg')
pkgver=1.3.3
pkgrel=1
arch=('x86_64')
url="https://www.xiph.org/ogg/"
license=('BSD')
#depends=()
source=("http://downloads.xiph.org/releases/ogg/libogg-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/libogg{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libogg-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libogg32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libogg-${pkgver}"
	make -C "${srcdir}/libogg32-${pkgver}"
}

package_libogg() {
	pkgdesc="Ogg bitstream and framing library"

	cd "${srcdir}/libogg-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libogg() {
	pkgdesc="Ogg bitstream and framing library (32 bit)"

	cd "${srcdir}/libogg32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('37d8e4329bf7a997a95d84a55c325a5a893ec84bb4d5b7ea638e5cb13201a0619e71ecd9710a8371def69f092c62dcc59ec0a607db246028ee8a62678386260b')

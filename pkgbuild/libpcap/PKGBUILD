pkgname=libpcap
pkgver=1.10.0
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=('x86_64')
url="http://www.tcpdump.org/"
license=('BSD')
#depends=()
source=("http://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz"
	"libpcap-fixes.patch")

prepare() {
	cd "${srcdir}/libpcap-${pkgver}"

	patch -Np1 -i "${srcdir}/libpcap-fixes.patch"
}

build() {
	cd "${srcdir}/libpcap-${pkgver}"

	./configure --prefix=/usr --disable-dbus

	make
}

package() {
	cd "${srcdir}/libpcap-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('007710386ff3435ef97fc99293076eae3c39b424e986141184c712b0285f8589357a1b25085f7eba28730de04312042c724d193934a399797e95a56f5301da7f'
            '67894c05d441935c50dbfdc022d1ee14c3b138e7d0a0db300fdebaef0ded22124999aae5c633ceef0e59c35c31a10b144c629c414adcabe1d89d5260dfefb0a2')

pkgbase=libpciaccess
pkgname=('libpciaccess' 'lib32-libpciaccess')
pkgver=0.16
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libpciaccess-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libpciaccess{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libpciaccess-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libpciaccess32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libpciaccess-${pkgver}"
	make -C "${srcdir}/libpciaccess32-${pkgver}"
}

package_libpciaccess() {
	pkgdesc="X11 PCI access library"

	cd "${srcdir}/libpciaccess-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libpciaccess() {
	pkgdesc="X11 PCI access library (32 bit)"

	cd "${srcdir}/libpciaccess32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('2f250048a270dfc0823d4bdd613aa272c58a80eaafd922850f56c4b6f7a45a263ed4cf521b52b49f04484ea44ebefb7407d079aa058318a5751ffb181e38fed1')

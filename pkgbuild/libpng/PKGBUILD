pkgbase=libpng
pkgname=('libpng' 'lib32-libpng')
pkgver=1.6.32
pkgrel=1
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/libpng/libpng-${pkgver}.tar.xz"
	"https://downloads.sourceforge.net/apng/libpng-${pkgver}-apng.patch.gz")

prepare() {
	cd "${srcdir}/libpng-${pkgver}"

	patch -Np0 -i "${srcdir}/libpng-${pkgver}-apng.patch"

	cp -a "${srcdir}"/libpng{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libpng-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libpng32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libpng-${pkgver}"
	make -C "${srcdir}/libpng32-${pkgver}"
}

package_libpng() {
	pkgdesc="A collection of routines used to create PNG format graphics files"

	cd "${srcdir}/libpng-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libpng() {
	pkgdesc="A collection of routines used to create PNG format graphics files (32 bit)"

	cd "${srcdir}/libpng32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"

	mv dest/usr/bin/libpng16-config "${pkgdir}/usr/bin/libpng16-config32"
	ln -sf libpng16-config32 "${pkgdir}/usr/bin/libpng-config32"
}

sha512sums=('24cf4d38dc08d1db7721fc580d8bf6cee9a105afcea32f1245b7bb3727c8a8645ad733a24534782ef5dfb0d4315a5fd6b0eee9e7f0704e0c635350f80c19a708'
            'a7f5e8f18ca762c19c69a1ce2d6be2e6425e2d8935379554b5017049d1f3b2eca461120d15b5a9123998cd4a0b24b62b0b98e40ce6f2973d22449dd367757cdb')

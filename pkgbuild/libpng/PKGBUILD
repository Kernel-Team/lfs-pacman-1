pkgbase=libpng
pkgname=('libpng' 'lib32-libpng')
pkgver=1.6.35
pkgrel=1
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/libpng/libpng-${pkgver}.tar.xz"
	"https://downloads.sourceforge.net/apng/libpng-${pkgver}-apng.patch.gz")

prepare() {
	cd "${srcdir}/libpng-${pkgver}"

	patch -Np0 -i "${srcdir}/libpng-${pkgver}-apng.patch"

	cp -a "${srcdir}"/libpng{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libpng-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libpng32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libpng-${pkgver}"
	make -C "${srcdir}/libpng32-${pkgver}"
}

package_libpng() {
	pkgdesc="A collection of routines used to create PNG format graphics files"

	cd "${srcdir}/libpng-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libpng() {
	pkgdesc="A collection of routines used to create PNG format graphics files (32 bit)"

	cd "${srcdir}/libpng32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"

	mv dest/usr/bin/libpng16-config "${pkgdir}/usr/bin/libpng16-config32"
	ln -sf libpng16-config32 "${pkgdir}/usr/bin/libpng-config32"
}

sha512sums=('b69152bc7869e98536effa8e45e2dac7bffee26aa9e31fc90b37506a702256919bcf4dd9cadc42fa4e45143bc023ffd7c637742367e58707e8c2226b3a09eee0'
            '40cda24d003089c31786b70fd581cdd960f338ae89535646a2188bc8e25e603a6ad2080406017bd45edb298442a88dd60ada1ec650b001596d216e8194a3dda1')

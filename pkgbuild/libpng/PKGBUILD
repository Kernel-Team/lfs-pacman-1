pkgbase=libpng
pkgname=('libpng' 'lib32-libpng')
pkgver=1.6.37
pkgrel=1
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/libpng/libpng-${pkgver}.tar.xz"
	"https://downloads.sourceforge.net/apng/libpng-${pkgver}-apng.patch.gz")

prepare() {
	cd "${srcdir}/libpng-${pkgver}"

	patch -Np0 -i "${srcdir}/libpng-${pkgver}-apng.patch"

	cp -a "${srcdir}"/libpng{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libpng-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libpng32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libpng-${pkgver}"
	make -C "${srcdir}/libpng32-${pkgver}"
}

package_libpng() {
	pkgdesc="A collection of routines used to create PNG format graphics files"

	cd "${srcdir}/libpng-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libpng() {
	pkgdesc="A collection of routines used to create PNG format graphics files (32 bit)"

	cd "${srcdir}/libpng32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"

	mv dest/usr/bin/libpng16-config "${pkgdir}/usr/bin/libpng16-config32"
	ln -sf libpng16-config32 "${pkgdir}/usr/bin/libpng-config32"
}

sha512sums=('59e8c1059013497ae616a14c3abbe239322d3873c6ded0912403fc62fb260561768230b6ab997e2cccc3b868c09f539fd13635616b9fa0dd6279a3f63ec7e074'
            'a9365257a33d67d7e7284494a37747bb1f3ab89f42a1730f2745c604538f59861828dc91d9bc1d08a5b79ab2f4eef1bb9e438dda1774b3548a1c56be77f8435f')

pkgbase=libpng12
pkgname=('libpng12' 'lib32-libpng12')
pkgver=1.2.58
pkgrel=1
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/libpng/libpng-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/libpng{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libpng-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libpng32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libpng-${pkgver}"
	make -C "${srcdir}/libpng32-${pkgver}"
}

package_libpng12() {
	pkgdesc="A collection of routines used to create PNG format graphics files (version 1.2)"

	cd "${srcdir}/libpng-${pkgver}"

	make lib_LTLIBRARIES="libpng.la libpng12.la" install-libLTLIBRARIES DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/*.{a,so}
}

package_lib32-libpng12() {
	pkgdesc="A collection of routines used to create PNG format graphics files (version 1.2) (32 bit)"

	cd "${srcdir}/libpng32-${pkgver}"

	make lib_LTLIBRARIES="libpng.la libpng12.la" install-libLTLIBRARIES DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib32/*.{a,so}
}

sha512sums=('5dc48cacc56fd34514b7ff9c1dbe88d9534b91bc70b3765beda1b8ea5bee6b9be7b7ce8321b1e135b3266e5780590f5f010c2cce86aceedce881485f2c7fc3c4')

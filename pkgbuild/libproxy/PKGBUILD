pkgname=libproxy
pkgver=0.4.15
pkgrel=1
pkgdesc="A library that provides automatic proxy configuration management"
arch=('x86_64')
url="http://libproxy.github.io/libproxy/"
license=('LGPL')
#depends=()
source=("libproxy-${pkgver}.tar.gz::https://github.com/libproxy/libproxy/archive/${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/libproxy-${pkgver}"

	sed -i "s:VERSIONS 3.6:VERSIONS 3.7 3.6:g" cmake/FindPython3Interp.cmake

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CXX="${CXX} -std=c++11"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DBUILD_TESTING=OFF \
		-DPERL_VENDORINSTALL=yes \
		-DWITH_VALA=ON \
		"${srcdir}/libproxy-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('8f68bd56e44aeb3f553f4657bef82a5d14302780508dafa32454d6f724b724c884ceed6042f8df53a081d26ea0b05598cf35eab44823257c47c5ef8afb36442b')

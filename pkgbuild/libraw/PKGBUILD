pkgname=libraw
pkgver=0.18.11
pkgrel=1
pkgdesc="A library for reading RAW files obtained from digital photo cameras (CRW/CR2, NEF, RAF, DNG, and others)"
arch=('x86_64')
url="http://www.libraw.org/"
license=('CDDL' 'LGPL')
#depends=()
source=("http://www.libraw.org/data/LibRaw-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/LibRaw-${pkgver}"

	sed -i "s:-lstdc++::g" configure
}

build() {
	cd "${srcdir}/LibRaw-${pkgver}"

	export CXX="${CXX} -std=c++03"

	./configure --prefix=/usr --disable-examples

	make
}

package() {
	cd "${srcdir}/LibRaw-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('4d114bdb589c96d95f646b1d016cfb9546ea7a53b9f23af1dc5d0e38cb4d1b2aa7270f2d4dbe7c6c323fb7f21403914a0bb75afa7b6204d0a99c72ecf0c812c9')

pkgname=libsasl
pkgver=2.1.27
pkgrel=1
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('x86_64')
url="https://www.cyrusimap.org/"
license=('custom')
#depends=()
source=("https://www.cyrusimap.org/releases/cyrus-sasl-${pkgver}.tar.gz"
	"cyrus-sasl-fixes.patch")

prepare() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	patch -Np1 -i "${srcdir}/cyrus-sasl-fixes.patch"

	autoreconf -fi
}

build() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-dbpath=/var/lib/sasl/sasldb2 \
		--with-saslauthd=/var/run/saslauthd \
		--with-sqlite3=/usr \
		--enable-login \
		--enable-ntlm \
		--enable-auth-sasldb \
		--disable-gssapi \
		--disable-ldapdb

	make
}

package() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	make install-pkgconfigDATA DESTDIR="${pkgdir}"
	make -C include install DESTDIR="${pkgdir}"
	make -C lib install DESTDIR="${pkgdir}"
	make -C plugins plugin_LTLIBRARIES="libsasldb.la" install-pluginLTLIBRARIES DESTDIR="${pkgdir}"

}

sha512sums=('d11549a99b3b06af79fc62d5478dba3305d7e7cc0824f4b91f0d2638daafbe940623eab235f85af9be38dcf5d42fc131db531c177040a85187aee5096b8df63b'
            '2ba7f5736701079b6213b1d407fc351b59fbc59e3364944ffa2504834eb7718606ac087476b8a07a026135cf2a4c871ac79eec7d3141a962ef033dcb5d2f60b6')

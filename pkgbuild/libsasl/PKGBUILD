pkgname=libsasl
pkgver=2.1.26
pkgrel=1
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('x86_64')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
#depends=()
source=("ftp://ftp.cyrusimap.org/cyrus-sasl/cyrus-sasl-${pkgver}.tar.gz"
	"cyrus-sasl-fixes.patch")

prepare() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	patch -Np1 -i "${srcdir}/cyrus-sasl-fixes.patch"

	autoreconf -fi

	pushd saslauthd
		autoreconf -fi
	popd
}

build() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-dbpath=/var/lib/sasl/sasldb2 \
		--with-saslauthd=/var/run/saslauthd \
		--with-sqlite3=/usr \
		--enable-login \
		--enable-ntlm \
		--enable-auth-sasldb \
		--disable-gssapi \
		--disable-ldapdb

	make
}

package() {
	cd "${srcdir}/cyrus-sasl-${pkgver}"

	make install-pkgconfigDATA DESTDIR="${pkgdir}"
	make -C include install DESTDIR="${pkgdir}"
	make -C lib install DESTDIR="${pkgdir}"
	make -C plugins sasl_LTLIBRARIES="libsasldb.la" install-saslLTLIBRARIES DESTDIR="${pkgdir}"

	sed -i "s:libdir =:exec_prefix=/usr\n&:" "${pkgdir}/usr/lib/pkgconfig/libsasl2.pc"
}

sha512sums=('78819cb9bb38bea4537d6770d309deeeef09ff44a67526177609d3e1257ff4334d2b5e5131d5a1e4dea7430d8db1918ea9d171f0dee38b5e8337f4b72ed068f0'
            'f40936566e88eec8cddfa5b024bd1fce3b951e162d39e410f11dfb563d888c3795d4e4be5215c75503440ac867c50bc57f3f2be2ef235213c20a7c52ce817676')

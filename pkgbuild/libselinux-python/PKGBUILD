pkgbase=python-libselinux
pkgname=('python2-libselinux' 'python3-libselinux')
pkgver=2.7
pkgrel=1
arch=('x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
#depends=()
source=("https://raw.githubusercontent.com/wiki/SELinuxProject/selinux/files/releases/20170804/libselinux-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/libselinux-${pkgver}"

	sed -i "s#-Werror ##g" src/Makefile
}

build() {
	cd "${srcdir}/libselinux-${pkgver}"

	export EXTRA_CFLAGS="${CFLAGS} -Wunused-command-line-argument -fPIC"
	unset CFLAGS

	make USE_PCRE2=y

	make PYTHON=python2 pywrap
	make PYTHON=python3 pywrap
}

package_python2-libselinux() {
	pkgdesc="SELinux library and simple utilities (Python 2 bindings)"

	cd "${srcdir}/libselinux-${pkgver}"

	make PYTHON=python2 LIBSEPOLA=/usr/lib/libsepol.a install-pywrap DESTDIR="${pkgdir}"
}

package_python3-libselinux() {
	pkgdesc="SELinux library and simple utilities (Python 3 bindings)"

	cd "${srcdir}/libselinux-${pkgver}"

	make PYTHON=python3  LIBSEPOLA=/usr/lib/libsepol.a install-pywrap DESTDIR="${pkgdir}"
}

sha512sums=('415d10306692d4323455b61fb61d7e56e53b9144276ff206d72760e1df9b04ab07c62a4d6f04bf4e5fa708f9f14b21a9801069b16ece0a0aade886950941ab8d')

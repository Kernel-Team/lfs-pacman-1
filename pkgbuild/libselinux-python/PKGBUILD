pkgbase=python-libselinux
pkgname=('python2-libselinux' 'python3-libselinux')
pkgver=2.6
pkgrel=1
arch=('x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
#depends=()
source=("https://raw.githubusercontent.com/wiki/SELinuxProject/selinux/files/releases/20161014/libselinux-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/libselinux-${pkgver}"

	CFLAGS+=" -fPIC"

	make PYTHON=python2 pywrap
	make PYTHON=python3 pywrap
}

package_python2-libselinux() {
	pkgdesc="SELinux library and simple utilities (Python 2 bindings)"

	cd "${srcdir}/libselinux-${pkgver}"

	make PYTHON=python2 install-pywrap DESTDIR="${pkgdir}"
}

package_python3-libselinux() {
	pkgdesc="SELinux library and simple utilities (Python 3 bindings)"

	cd "${srcdir}/libselinux-${pkgver}"

	make PYTHON=python3 install-pywrap DESTDIR="${pkgdir}"
}

sha512sums=('906e1bf98c669862ab4f4e883d511db8d739a5763dc857c9405ad3cc6c70766a482853d07134698a1a98257a8632cc756d0549a7640c2915d051714f502ff14b')

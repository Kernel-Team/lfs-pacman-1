pkgbase=libsm
pkgname=('libsm' 'lib32-libsm')
pkgver=1.2.2
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libSM-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libSM{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libSM-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libSM32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libSM-${pkgver}"
	make -C "${srcdir}/libSM32-${pkgver}"
}

package_libsm() {
	pkgdesc="X11 Session Management library"

	cd "${srcdir}/libSM-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libsm() {
	pkgdesc="X11 Session Management library (32 bit)"

	cd "${srcdir}/libSM32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('dfb91dcc7200fce8d0a62953ae4b81fcb00d525674bbcc6dfcaa23ecb35e1213ceb07cc51f590072dce2107909903160d9f3636a0434fe2a605b1e9a9bd0a6ca')

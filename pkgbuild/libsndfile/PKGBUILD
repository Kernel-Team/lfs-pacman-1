pkgbase=libsndfile
pkgname=('libsndfile' 'lib32-libsndfile')
pkgver=1.0.27
pkgrel=1
arch=('x86_64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
#depends=()
source=("http://www.mega-nerd.com/libsndfile/files/libsndfile-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/libsndfile{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libsndfile-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libsndfile32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libsndfile-${pkgver}"
	make -C "${srcdir}/libsndfile32-${pkgver}"
}

package_libsndfile() {
	pkgdesc="A C library for reading and writing files containing sampled sound"

	cd "${srcdir}/libsndfile-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libsndfile() {
	pkgdesc="A C library for reading and writing files containing sampled sound (32 bit)"

	cd "${srcdir}/libsndfile32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('fd1d97c6077f03b5d984d7956ffedb7a')

pkgname=libsoup
pkgver=2.66.1
pkgrel=1
pkgdesc="GNOME HTTP Library"
arch=('x86_64')
url="http://www.gnome.org"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/libsoup/${pkgver:0:4}/libsoup-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dntlm_auth=/usr/bin/ntlm_auth \
		-Ddoc=true \
		-Dntlm=true \
		-Dtls_check=false \
		-Dtests=false \
		"${srcdir}/libsoup-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('bb9c15cae965606d5f7c009fab9223445235982269d23ff20e89249b10d3423797427e7cff39c31a3f5db99b0c2d7a01eb911edb61309603cdeb8f2467fe6039')

pkgbase=libsystemd
pkgname=('libsystemd' 'lib32-libsystemd')
pkgver=247
pkgrel=1
arch=('x86_64')
url="https://www.github.com/systemd/systemd"
license=('GPL2' 'LGPL2.1')
#depends=()
source=("https://github.com/systemd/systemd/archive/v${pkgver}/systemd-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	local configure_shared=(
		--prefix /usr
		--sysconfdir /etc
		--localstatedir /var
		--buildtype=plain
		-Dfirstboot=false
		-Dhomed=false
		-Dseccomp=false
		-Dselinux=false
		-Dima=false
		-Dsmack=false
		-Dsysvinit-path=/etc/init.d
		-Dsysvrcnd-path=/etc
		-Dhalt-local=""
		-Drc-local=/etc/rc.local
		-Ddbuspolicydir=/etc/dbus-1/system.d
		-Ddbussessionservicedir=/usr/share/dbus-1/services
		-Ddbussystemservicedir=/usr/share/dbus-1/system-services
		-Dbashcompletiondir=no
		-Drpmmacrosdir=no
		-Dzshcompletiondir=no
		-Ddefault-hierarchy=hybrid
		-Ddefault-locale=C
		-Ddefault-kill-user-processes=false
		-Dwheel-group=false
		-Dnobody-group=nogroup
		-Dacl=false
		-Daudit=false
		-Defi=false
		-Dglib=false
		-Dgnutls=false
		-Dkmod=false
		-Dlibcryptsetup=false
		-Dlibcurl=false
		-Dlibidn=false
		-Dlibidn2=false
		-Dlibiptc=false
		-Dmicrohttpd=false
		-Dman=false
		-Dp11kit=false
		-Dpam=false
		-Dqrencode=false
		-Dxkbcommon=false
	)

	cd "${srcdir}/build64"

	meson "${configure_shared[@]}" "${srcdir}/systemd-${pkgver}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	meson "${configure_shared[@]}" \
		--libdir /usr/lib32 \
		"${srcdir}/systemd-${pkgver}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	ninja -C build64 ${MAKEFLAGS}
	ninja -C build32 ${MAKEFLAGS}
}

package_libsystemd() {
	pkgdesc="System and service manager libraries"

	cd "${srcdir}/build64"

	DESTDIR="${PWD}/dest" ninja install

	install -v -dm755 "${pkgdir}/usr/include"
	install -v -dm755 "${pkgdir}/usr/lib/pkgconfig"

	mv dest/usr/include/* "${pkgdir}/usr/include"
	mv dest/usr/lib/pkgconfig/* "${pkgdir}/usr/lib/pkgconfig"

	mv dest/usr/lib/lib{systemd,udev}.so* "${pkgdir}/usr/lib"
}

package_lib32-libsystemd() {
	pkgdesc="System and service manager libraries (32 bit)"

	cd "${srcdir}/build32"

	DESTDIR="${PWD}/dest" ninja install

	install -v -dm755 "${pkgdir}/usr/lib32/pkgconfig"

	mv dest/usr/lib32/pkgconfig/* "${pkgdir}/usr/lib32/pkgconfig"
	mv dest/usr/lib32/lib{systemd,udev}.so* "${pkgdir}/usr/lib32"
}

sha512sums=('dd11cf46e5d9cbf44beb2d383262e9b13eb80fbb3403d86d011b4c2f9e0a4778c7c9779c856960f5654177581c20d7336c00ce687d35fe35ce069c56924604c2')

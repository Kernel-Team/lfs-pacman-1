pkgbase=libsystemd
pkgname=('libsystemd' 'lib32-libsystemd')
pkgver=233
pkgrel=1
arch=('x86_64')
url="https://www.github.com/systemd/systemd"
license=('GPL2' 'LGPL2.1')
#depends=()
source=("http://www.linuxfromscratch.org/~krejzi/systemd/systemd-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/systemd-${pkgver}"

	sed -i "s#-Werror=format=2##g" configure

	cp -a "${srcdir}"/systemd{,32}-${pkgver}
}

build() {
	local configure_shared=(--prefix=/usr
		--sysconfdir=/etc
		--localstatedir=/var
		--disable-firstboot
		--disable-ima
		--disable-lto
		--disable-smack
		--disable-tests
		--disable-wheel-group
		--with-pamlibdir=/usr/lib/security
		--with-sysvinit-path=/etc/init.d
		--with-sysvrcnd-path=/etc
		--with-rc-local-script-path-stop=
		--with-rc-local-script-path-start=/etc/rc.local
		--with-dbuspolicydir=/etc/dbus-1/system.d
		--with-dbussessionservicedir=/usr/share/dbus-1/services
		--with-dbussystemservicedir=/usr/share/dbus-1/system-services)

	cd "${srcdir}/systemd-${pkgver}"

	./configure "${configure_shared[@]}"

	cd "${srcdir}/systemd32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure "${configure_shared[@]}" \
		--libdir=/usr/lib32 \
		--disable-acl \
		--disable-audit \
		--disable-pam

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/systemd-${pkgver}" built-sources
	make -C "${srcdir}/systemd-${pkgver}" libudev.la libsystemd.la

	make -C "${srcdir}/systemd32-${pkgver}" built-sources
	make -C "${srcdir}/systemd32-${pkgver}" libudev.la libsystemd.la
}

package_libsystemd() {
	pkgdesc="System and service manager libraries"

	cd "${srcdir}/systemd-${pkgver}"

	make rootlib_LTLIBRARIES="libsystemd.la libudev.la" install-rootlibLTLIBRARIES \
		install-includeHEADERS install-pkgincludeHEADERS install-pkgconfiglibDATA \
		DESTDIR="${pkgdir}"
}

package_lib32-libsystemd() {
	pkgdesc="System and service manager libraries (32 bit)"

	cd "${srcdir}/systemd32-${pkgver}"

	make rootlib_LTLIBRARIES="libsystemd.la libudev.la" install-rootlibLTLIBRARIES \
		install-includeHEADERS install-pkgincludeHEADERS install-pkgconfiglibDATA \
		DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('a749a208df75a75afd714a07d9f61dd2c641c62f432c7a707baebdf4db0e24b5ed3e213cd0a82af6687aa14943193e0b7e9e70a3a603015a2a483eaaeee8bc41')

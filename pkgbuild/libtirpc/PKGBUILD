pkgbase=libtirpc
pkgname=('libtirpc' 'lib32-libtirpc')
pkgver=1.1.4
pkgrel=1
arch=('x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
#depends=()
source=("https://downloads.sourceforge.net/libtirpc/libtirpc-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libtirpc{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libtirpc-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-gssapi

	cd "${srcdir}/libtirpc32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-gssapi  \
		--libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libtirpc-${pkgver}"
	make -C "${srcdir}/libtirpc32-${pkgver}"
}

package_libtirpc() {
	pkgdesc="Transport Independent RPC library"

	cd "${srcdir}/libtirpc-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libtirpc() {
	pkgdesc="Transport Independent RPC library (32 bit)"

	cd "${srcdir}/libtirpc32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('392f391f9fc1bd68d81dc44e4058831a64b32790b5c8c37338b0ab416fad2ae4d16389e632596734dba09780347918cc65c6f134e0c1afd09e81ec250785ed23')

pkgbase=libtirpc
pkgname=('libtirpc' 'lib32-libtirpc')
pkgver=1.3.1
pkgrel=1
arch=('x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
#depends=()
source=("https://downloads.sourceforge.net/libtirpc/libtirpc-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libtirpc{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libtirpc-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-gssapi

	cd "${srcdir}/libtirpc32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-gssapi  \
		--libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libtirpc-${pkgver}"
	make -C "${srcdir}/libtirpc32-${pkgver}"
}

package_libtirpc() {
	pkgdesc="Transport Independent RPC library"

	cd "${srcdir}/libtirpc-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libtirpc() {
	pkgdesc="Transport Independent RPC library (32 bit)"

	cd "${srcdir}/libtirpc32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('131f746800ac7280cc3900597018fc8dbc8da50c14e29dbaccf36a6d110eded117351108c6b069eaac90d77cfec17014b08e9afddcf153fda2d780ba64260cbc')

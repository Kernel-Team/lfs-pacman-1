pkgbase=libtool
pkgname=('libtool' 'lib32-libtool')
pkgver=2.4.6
pkgrel=1
arch=('x86_64')
url="https://www.gnu.org/software/libtool/"
license=('GPL')
#depends=()
source=("https://ftp.gnu.org/gnu/libtool/libtool-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/libtool{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libtool-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libtool32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libtool-${pkgver}"
	make -C "${srcdir}/libtool32-${pkgver}"
}

package_libtool() {
	pkgdesc="A generic library support script"

	cd "${srcdir}/libtool-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libtool() {
	pkgdesc="A generic library support script (32 bit)"

	cd "${srcdir}/libtool32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('a6eef35f3cbccf2c9e2667f44a476ebc80ab888725eb768e91a3a6c33b8c931afc46eb23efaee76c8696d3e4eed74ab1c71157bcb924f38ee912c8a90a6521a4')

pkgbase=libunwind
pkgname=('libunwind' 'lib32-libunwind')
pkgver=1.5.0
pkgrel=1
arch=('x86_64')
url="http://www.nongnu.org/libunwind/"
license=('GPL')
#depends=()
source=("http://download.savannah.gnu.org/releases/libunwind/libunwind-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/libunwind-${pkgver}"

	sed -i "/LIBCRTS/s#-lgcc_s##g" configure

	cp -a "${srcdir}"/libunwind{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libunwind-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libunwind32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--build=i686-pc-linux-gnu

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libunwind-${pkgver}"
	make -C "${srcdir}/libunwind32-${pkgver}"
}

package_libunwind() {
	pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program"

	cd "${srcdir}/libunwind-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libunwind() {
	pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program (32 bit)"

	cd "${srcdir}/libunwind32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/include"

	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/include/libunwind-x86.h "${pkgdir}/usr/include"
}

sha512sums=('1df20ca7a8cee2f2e61294fa9b677e88fec52e9d5a329f88d05c2671c69fa462f6c18808c97ca9ff664ef57292537a844f00b18d142b1938c9da701ca95a4bab')

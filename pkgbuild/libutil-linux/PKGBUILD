pkgbase=libutil-linux
pkgname=('libutil-linux' 'lib32-libutil-linux' 'mount')
pkgver=2.29.2
pkgrel=1
arch=('x86_64')
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/util-linux/v${pkgver:0:4}/util-linux-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/util-linux{,32}-${pkgver}
}

build() {
	cd "${srcdir}/util-linux-${pkgver}"

	./configure --enable-mesg \
		--disable-nologin \
		--disable-chfn-chsh \
		--disable-su \
		--disable-login \
		--disable-kill \
		--with-selinux \
		--without-python \
		--without-systemd

	cd "${srcdir}/util-linux32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --enable-mesg \
		--disable-nologin \
		--disable-chfn-chsh \
		--disable-su \
		--disable-login \
		--disable-kill \
		--with-selinux \
		--without-python \
		--without-systemd \
		--libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/util-linux-${pkgver}" libfdisk.la libmount.la libsmartcols.la
	make -C "${srcdir}/util-linux-${pkgver}" mount umount

	make -C "${srcdir}/util-linux32-${pkgver}" libfdisk.la libmount.la libsmartcols.la
}

package_libutil-linux() {
	pkgdesc="Miscellaneous system utilities for Linux (shared libraries)"

	cd "${srcdir}/util-linux-${pkgver}"

	make install-usrlib_execLTLIBRARIES install-data-am DESTDIR="${pkgdir}"

	make install-exec-hook-libuuid \
		install-exec-hook-libblkid \
		install-exec-hook-libmount \
		install-exec-hook-libsmartcols \
		install-exec-hook-libfdisk \
		DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/lib/systemd" "${pkgdir}/usr/share"
}

package_lib32-libutil-linux() {
	pkgdesc="Miscellaneous system utilities for Linux (shared libraries) (32 bit)"

	cd "${srcdir}/util-linux32-${pkgver}"

	make install-usrlib_execLTLIBRARIES install-data-am DESTDIR="${PWD}/dest"

	make install-exec-hook-libuuid \
		install-exec-hook-libblkid \
		install-exec-hook-libmount \
		install-exec-hook-libsmartcols \
		install-exec-hook-libfdisk \
		DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/lib32/systemd"
}

package_mount() {
	pkgdesc="Miscellaneous system utilities for Linux (mount utility)"

	cd "${srcdir}/util-linux-${pkgver}"

	install -v -dm755 "${pkgdir}/bin" "${pkgdir}"/usr/share/man/man{5,8}

	install -v -m755 -t "${pkgdir}/bin" .libs/mount .libs/umount
	install -v -m644 -t "${pkgdir}/usr/share/man/man8" sys-utils/mount.8 sys-utils/umount.8
	install -v -m644 sys-utils/fstab.5 "${pkgdir}/usr/share/man/man5"

	make install-exec-hook-mount DESTDIR="${pkgdir}"
}

md5sums=('63c40c2068fcbb7e1d5c1d281115d973')

pkgbase=libutil-linux
pkgname=('libutil-linux' 'lib32-libutil-linux' 'mount')
pkgver=2.32
pkgrel=1
arch=('x86_64')
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/util-linux/v${pkgver:0:4}/util-linux-${pkgver/.rc/-rc}.tar.xz")

prepare() {
	cp -a "${srcdir}"/util-linux{,32}-${pkgver/.rc/-rc}
}

build() {
	cd "${srcdir}/util-linux-${pkgver/.rc/-rc}"

	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--libdir=/usr/lib \
		--sbindir=/usr/sbin \
		--enable-mesg \
		--disable-nologin \
		--disable-chfn-chsh \
		--disable-su \
		--disable-login \
		--disable-kill \
		--without-python \
		--without-systemd \
		--enable-fs-paths-default=/usr/sbin

	cd "${srcdir}/util-linux32-${pkgver/.rc/-rc}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--libdir=/usr/lib32 \
		--sbindir=/usr/sbin \
		--enable-mesg \
		--disable-nologin \
		--disable-chfn-chsh \
		--disable-su \
		--disable-login \
		--disable-kill \
		--without-python \
		--without-systemd \
		--enable-fs-paths-default=/usr/sbin

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/util-linux-${pkgver/.rc/-rc}" libfdisk.la libmount.la libsmartcols.la
	make -C "${srcdir}/util-linux-${pkgver/.rc/-rc}" mount umount

	make -C "${srcdir}/util-linux32-${pkgver/.rc/-rc}" libfdisk.la libmount.la libsmartcols.la
}

package_libutil-linux() {
	pkgdesc="Miscellaneous system utilities for Linux (shared libraries)"

	cd "${srcdir}/util-linux-${pkgver/.rc/-rc}"

	make install-usrlib_execLTLIBRARIES install-data-am DESTDIR="${pkgdir}"

	make install-exec-hook-libuuid \
		install-exec-hook-libblkid \
		install-exec-hook-libmount \
		install-exec-hook-libsmartcols \
		install-exec-hook-libfdisk \
		DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/lib/systemd" "${pkgdir}/usr/share"
}

package_lib32-libutil-linux() {
	pkgdesc="Miscellaneous system utilities for Linux (shared libraries) (32 bit)"

	cd "${srcdir}/util-linux32-${pkgver/.rc/-rc}"

	make install-usrlib_execLTLIBRARIES install-data-am DESTDIR="${PWD}/dest"

	make install-exec-hook-libuuid \
		install-exec-hook-libblkid \
		install-exec-hook-libmount \
		install-exec-hook-libsmartcols \
		install-exec-hook-libfdisk \
		DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/lib32/systemd"
}

package_mount() {
	pkgdesc="Miscellaneous system utilities for Linux (mount utility)"

	cd "${srcdir}/util-linux-${pkgver/.rc/-rc}"

	install -v -dm755 "${pkgdir}/usr/bin" "${pkgdir}"/usr/share/man/man{5,8}

	install -v -m755 -t "${pkgdir}/usr/bin" .libs/mount .libs/umount
	install -v -m644 -t "${pkgdir}/usr/share/man/man8" sys-utils/mount.8 sys-utils/umount.8
	install -v -m644 sys-utils/fstab.5 "${pkgdir}/usr/share/man/man5"

	make install-exec-hook-mount DESTDIR="${pkgdir}"
}

sha512sums=('320c8d364f014aba16483dd26564d7cb47804337567c3e9c2dc495f3865f524e7480d3fec7ef5a05b6e99763b0278da70fffe6d2b88f3f1a832ada5932556482')

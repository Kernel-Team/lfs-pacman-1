pkgbase=libva
pkgname=('libva' 'lib32-libva')
pkgver=2.10.0
pkgrel=1
arch=('x86_64')
url="https://01.org/linuxmedia/vaapi"
license=('MIT')
#depends=()
source=("https://github.com/intel/libva/archive/${pkgver}/libva-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	cd "${srcdir}/build64"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		"${srcdir}/libva-${pkgver}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--libdir /usr/lib32 \
		--buildtype=plain \
		"${srcdir}/libva-${pkgver}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	ninja -C build64 ${MAKEFLAGS}
	ninja -C build32 ${MAKEFLAGS}
}

package_libva() {
	pkgdesc="Video Acceleration (VA) API for Linux"

	cd "${srcdir}/build64"

	DESTDIR="${pkgdir}" ninja install
}

package_lib32-libva() {
	pkgdesc="Video Acceleration (VA) API for Linux (32 bit)"

	cd "${srcdir}/build32"

	DESTDIR="${PWD}/dest" ninja install

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('b736cde67a8eac0d4c4ff6ea38905111e85ad8f2c9f4324a7f3324813286b3a04afdd386928be5d54fb787f003d13dfe82f44fe11049009c2ff8bf88e981579f')

pkgbase=libva
pkgname=('libva' 'lib32-libva')
pkgver=2.4.0
pkgrel=1
arch=('x86_64')
url="https://01.org/linuxmedia/vaapi"
license=('MIT')
#depends=()
source=("https://github.com/intel/libva/releases/download/${pkgver}/libva-${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/libva-${pkgver}"

	cp -a "${srcdir}"/libva{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libva-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libva32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libva-${pkgver}"
	make -C "${srcdir}/libva32-${pkgver}"
}

package_libva() {
	pkgdesc="Video Acceleration (VA) API for Linux"

	cd "${srcdir}/libva-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libva() {
	pkgdesc="Video Acceleration (VA) API for Linux (32 bit)"

	cd "${srcdir}/libva32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('67373e20932bca0d52cd8f48ed10c1f01752be73642ad4d2c85dcee813dccf205c8ca62fdc69fb3f4f83a685e2a8ef2c6646174e3453a8f5763cac16a58321c3')

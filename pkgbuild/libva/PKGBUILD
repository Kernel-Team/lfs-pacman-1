pkgbase=libva
pkgname=('libva' 'lib32-libva')
pkgver=2.6.0
pkgrel=1
arch=('x86_64')
url="https://01.org/linuxmedia/vaapi"
license=('MIT')
#depends=()
source=("https://github.com/intel/libva/archive/${pkgver}/libva-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	cd "${srcdir}/build64"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		"${srcdir}/libva-${pkgver}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--libdir /usr/lib32 \
		--buildtype=plain \
		"${srcdir}/libva-${pkgver}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	ninja -C build64 ${MAKEFLAGS}
	ninja -C build32 ${MAKEFLAGS}
}

package_libva() {
	pkgdesc="Video Acceleration (VA) API for Linux"

	cd "${srcdir}/build64"

	DESTDIR="${pkgdir}" ninja install
}

package_lib32-libva() {
	pkgdesc="Video Acceleration (VA) API for Linux (32 bit)"

	cd "${srcdir}/build32"

	DESTDIR="${PWD}/dest" ninja install

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('190fd9875d8ec91e94d42c41b770bb51ef9f430534f99d22fa394d6e4cf8dd308ffac61a37171855e4b499982c3cc59925d238b131dd2261a3beaae03e4937d0')

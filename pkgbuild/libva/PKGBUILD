pkgbase=libva
pkgname=('libva' 'lib32-libva')
pkgver=1.7.3
pkgrel=1
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/vaapi/"
license=('MIT')
#depends=()
source=("https://www.freedesktop.org/software/vaapi/releases/libva/libva-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libva{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libva-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libva32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libva-${pkgver}"
	make -C "${srcdir}/libva32-${pkgver}"
}

package_libva() {
	pkgdesc="Video Acceleration (VA) API for Linux"

	cd "${srcdir}/libva-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libva() {
	pkgdesc="Video Acceleration (VA) API for Linux (32 bit)"

	cd "${srcdir}/libva32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('dafb1d7d6449e850e9eb1a099895c683')

pkgbase=libvdpau
pkgname=('libvdpau' 'lib32-libvdpau')
pkgver=1.2
pkgrel=1
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/VDPAU/"
license=('custom')
#depends=()
source=("https://people.freedesktop.org/~aplattner/vdpau/libvdpau-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libvdpau{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libvdpau-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc

	cd "${srcdir}/libvdpau32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libvdpau-${pkgver}"
	make -C "${srcdir}/libvdpau32-${pkgver}"
}

package_libvdpau() {
pkgdesc="Nvidia VDPAU library"

	cd "${srcdir}/libvdpau-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libvdpau() {
	pkgdesc="Nvidia VDPAU library (32 bit)"

	cd "${srcdir}/libvdpau32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('86aa3f2a39cb73a597bf417da2eeef73ff01160ed9f54dff3725785ff5a289f47040496de44e2f5292d59657d4746e369b3fa307c4f83a32f7cc28e4cd8bce6f')

pkgbase=libxau
pkgname=('libxau' 'lib32-libxau')
pkgver=1.0.9
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXau-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXau{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXau-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXau32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXau-${pkgver}"
	make -C "${srcdir}/libXau32-${pkgver}"
}

package_libxau() {
	pkgdesc="X11 authorisation library"

	cd "${srcdir}/libXau-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxau() {
	pkgdesc="X11 authorisation library (32 bit)"

	cd "${srcdir}/libXau32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('3ca454ba466a807ea28b0f715066d73dc76ad312697b121d43e4d5766215052e9b7ffb8fe3ed3e496fa3f2a13f164ac692ff85cc428e26731b679f0f06a1d562')

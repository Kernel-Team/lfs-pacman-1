pkgbase=libxcb
pkgname=('libxcb' 'lib32-libxcb')
pkgver=1.14
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/xcb/libxcb-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/libxcb-${pkgver}"

	sed -e "s/pthread-stubs//" -i configure.ac
	autoreconf -fi

	cp -a "${srcdir}"/libxcb{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libxcb-${pkgver}"

	./configure --prefix=/usr --disable-devel-docs

	cd "${srcdir}/libxcb32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--disable-devel-docs \
		--libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libxcb-${pkgver}"
	make -C "${srcdir}/libxcb32-${pkgver}"
}

package_libxcb() {
	pkgdesc="X11 client-side library"

	cd "${srcdir}/libxcb-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxcb() {
	pkgdesc="X11 client-side library (32 bit)"

	cd "${srcdir}/libxcb32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('b90a23204b0d2c29d8b115577edb01df0465e02d6a8876550fecd62375d24a5d5f872ddd5946772ddba077cadce75b12c7a6d218469dc30b5b92bc82188e8bc6')

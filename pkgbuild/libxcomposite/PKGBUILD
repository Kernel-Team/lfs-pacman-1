pkgbase=libxcomposite
pkgname=('libxcomposite' 'lib32-libxcomposite')
pkgver=0.4.5
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXcomposite-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXcomposite{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXcomposite-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXcomposite32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXcomposite-${pkgver}"
	make -C "${srcdir}/libXcomposite32-${pkgver}"
}

package_libxcomposite() {
	pkgdesc="X11 Composite extension library"

	cd "${srcdir}/libXcomposite-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxcomposite() {
	pkgdesc="X11 Composite extension library (32 bit)"

	cd "${srcdir}/libXcomposite32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('502fd51fd9097bb3ca72174ac5b25b9d3b1ff240d32c4765199df03d89337d94b4ddea49e90b177b370862430089d966ce9c38988337156352cfeae911c2d3d5')

pkgbase=libxcursor
pkgname=('libxcursor' 'lib32-libxcursor')
pkgver=1.1.14
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXcursor-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXcursor{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXcursor-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXcursor32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXcursor-${pkgver}"
	make -C "${srcdir}/libXcursor32-${pkgver}"
}

package_libxcursor() {
	pkgdesc="X cursor management library"

	cd "${srcdir}/libXcursor-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxcursor() {
	pkgdesc="X cursor management library (32 bit)"

	cd "${srcdir}/libXcursor32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('687963b54da66be25371f64a88f91812b38de4ffc3b806070f3aeb1da379df16ede819201a352b3fb66efc6b40a36a22770add3ae464f2008aff3e23cb60a8e6')

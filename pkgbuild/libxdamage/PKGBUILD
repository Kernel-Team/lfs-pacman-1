pkgbase=libxdamage
pkgname=('libxdamage' 'lib32-libxdamage')
pkgver=1.1.5
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXdamage-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXdamage{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXdamage-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXdamage32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXdamage-${pkgver}"
	make -C "${srcdir}/libXdamage32-${pkgver}"
}

package_libxdamage() {
	pkgdesc="X11 damaged region extension library"

	cd "${srcdir}/libXdamage-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxdamage() {
	pkgdesc="X11 damaged region extension library (32 bit)"

	cd "${srcdir}/libXdamage32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('a3ca6cc33b1727f717a3e2aa5593f660508a81a47918a0aa949e9e8fba105e303fe5071983b48caac92feea0fe6e8e01620805e4d19b41f21f20d837b191c124')

pkgbase=libxdmcp
pkgname=('libxdmcp' 'lib32-libxdmcp')
pkgver=1.1.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXdmcp-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXdmcp{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXdmcp-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXdmcp32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXdmcp-${pkgver}"
	make -C "${srcdir}/libXdmcp32-${pkgver}"
}

package_libxdmcp() {
	pkgdesc="X11 Display Manager Control Protocol library"

	cd "${srcdir}/libXdmcp-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxdmcp() {
	pkgdesc="X11 Display Manager Control Protocol library (32 bit)"

	cd "${srcdir}/libXdmcp32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('cb1d4650f97d66e73acd2465ec7d757b9b797cce2f85e301860a44997a461837eea845ec9bd5b639ec5ca34c804f8bdd870697a5ce3f4e270b687c9ef74f25ec')

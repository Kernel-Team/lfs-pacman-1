pkgbase=libxext
pkgname=('libxext' 'lib32-libxext')
pkgver=1.3.4
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXext-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXext{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXext-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXext32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXext-${pkgver}"
	make -C "${srcdir}/libXext32-${pkgver}"
}

package_libxext() {
	pkgdesc="X11 miscellaneous extensions library"

	cd "${srcdir}/libXext-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxext() {
	pkgdesc="X11 miscellaneous extensions library (32 bit)"

	cd "${srcdir}/libXext32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('09146397d95f80c04701be1cc0a9c580ab5a085842ac31d17dfb6d4c2e42b4253b89cba695e54444e520be359883a76ffd02f42484c9e2ba2c33a5a40c29df4a')

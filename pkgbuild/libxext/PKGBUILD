pkgbase=libxext
pkgname=('libxext' 'lib32-libxext')
pkgver=1.3.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXext-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXext{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXext-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXext32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXext-${pkgver}"
	make -C "${srcdir}/libXext32-${pkgver}"
}

package_libxext() {
	pkgdesc="X11 miscellaneous extensions library"

	cd "${srcdir}/libXext-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxext() {
	pkgdesc="X11 miscellaneous extensions library (32 bit)"

	cd "${srcdir}/libXext32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('669d61545d10b999872addbf5628127840a221cbeaafb5ffa4328e0e0e1823583ece973bcafd5f13852ed58d4b0fe313a35f304ac80c6ed2b382cbbca977761a')

pkgbase=libxfixes
pkgname=('libxfixes' 'lib32-libxfixes')
pkgver=5.0.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXfixes-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXfixes{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXfixes-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXfixes32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXfixes-${pkgver}"
	make -C "${srcdir}/libXfixes32-${pkgver}"
}

package_libxfixes() {
	pkgdesc="X11 miscellaneous 'fixes' extension library"

	cd "${srcdir}/libXfixes-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxfixes() {
	pkgdesc="X11 miscellaneous 'fixes' extension library (32 bit)"

	cd "${srcdir}/libXfixes32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('72d5ee496e5c0520c95ef6bbb52deff1ee4c29792f11aa17aeb25c8ec5eb992ca502de040c77ad95835d1b1432f315b6cb7a3308b434847c28b9c2c6f9d1ac10')

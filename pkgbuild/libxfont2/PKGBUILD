pkgbase=libxfont2
pkgname=('libxfont2' 'lib32-libxfont2')
pkgver=2.0.4
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXfont2-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXfont2{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXfont2-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXfont232-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXfont2-${pkgver}"
	make -C "${srcdir}/libXfont232-${pkgver}"
}

package_libxfont2() {
	pkgdesc="X11 font rasterisation library"

	cd "${srcdir}/libXfont2-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxfont2() {
	pkgdesc="X11 font rasterisation library (32 bit)"

	cd "${srcdir}/libXfont232-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('7cf6c58e520e48e24fc4f05fec760fcbeaaac0cedeed57dded262c855e1515cc34cd033222945a0b016f6857b83009fc2a6946c7d632c2f7bb0060f8e8a8a106')

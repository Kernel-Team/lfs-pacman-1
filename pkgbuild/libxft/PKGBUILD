pkgbase=libxft
pkgname=('libxft' 'lib32-libxft')
pkgver=2.3.2
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXft-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXft{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXft-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXft32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXft-${pkgver}"
	make -C "${srcdir}/libXft32-${pkgver}"
}

package_libxft() {
	pkgdesc="FreeType-based font drawing library for X"

	cd "${srcdir}/libXft-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxft() {
	pkgdesc="FreeType-based font drawing library for X (32 bit)"

	cd "${srcdir}/libXft32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('331b3a2a3a1a78b5b44cfbd43f86fcfe')

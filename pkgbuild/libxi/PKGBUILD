pkgbase=libxi
pkgname=('libxi' 'lib32-libxi')
pkgver=1.7.10
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXi-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXi{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXi-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXi32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXi-${pkgver}"
	make -C "${srcdir}/libXi32-${pkgver}"
}

package_libxi() {
	pkgdesc="X11 Input extension library"

	cd "${srcdir}/libXi-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxi() {
	pkgdesc="X11 Input extension library (32 bit)"

	cd "${srcdir}/libXi32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('591f0860bf5904897587c4990d6c852f3729a212d1ef390362d41242440e078221877c31db2232d5cc81727fe97f4e194b077f7de917e251e60641bbd06ee218')

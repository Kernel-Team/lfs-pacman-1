pkgbase=libxinerama
pkgname=('libxinerama' 'lib32-libxinerama')
pkgver=1.1.4
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXinerama-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXinerama{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXinerama-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXinerama32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXinerama-${pkgver}"
	make -C "${srcdir}/libXinerama32-${pkgver}"
}

package_libxinerama() {
	pkgdesc="X11 Xinerama extension library"

	cd "${srcdir}/libXinerama-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxinerama() {
	pkgdesc="X11 Xinerama extension library (32 bit)"

	cd "${srcdir}/libXinerama32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('cff777ad942614fbf6bc6d8529f399e62debf3ecbf6cc0694a94e38c022bf929ffd5636fb59f55533c394d89b23af3ea51fa5128927f12a85787e16239330f14')

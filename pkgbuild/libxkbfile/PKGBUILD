pkgbase=libxkbfile
pkgname=('libxkbfile' 'lib32-libxkbfile')
pkgver=1.0.9
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libxkbfile-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libxkbfile{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libxkbfile-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libxkbfile32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libxkbfile-${pkgver}"
	make -C "${srcdir}/libxkbfile32-${pkgver}"
}

package_libxkbfile() {
	pkgdesc="X11 keyboard file manipulation library"

	cd "${srcdir}/libxkbfile-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxkbfile() {
	pkgdesc="X11 keyboard file manipulation library (32 bit)"

	cd "${srcdir}/libxkbfile32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('4a4cfeaf24dab1b991903455d6d7d404')

pkgbase=libxml2
pkgname=('libxml2' 'lib32-libxml2')
pkgver=2.9.10
pkgrel=1
arch=('x86_64')
url="http://www.xmlsoft.org/"
license=('MIT')
#depends=()
source=("http://xmlsoft.org/sources/libxml2-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/libxml2-${pkgver}"

	sed -i '/if Py/{s/Py/(Py/;s/)/))/}' python/{types.c,libxml.c}
	sed -i 's/ TRUE/ true/' encoding.c

	cp -a "${srcdir}"/libxml2{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libxml2-${pkgver}"

	./configure --prefix=/usr \
		--with-history \
		--with-icu \
		--with-python=/usr/bin/python3

	cd "${srcdir}/libxml232-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--with-history \
		--with-icu \
		--without-python

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libxml2-${pkgver}"
	make -C "${srcdir}/libxml232-${pkgver}"
}

package_libxml2() {
	pkgdesc="XML parsing library, version 2"

	cd "${srcdir}/libxml2-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxml2() {
	pkgdesc="XML parsing library, version 2 (32 bit)"

	cd "${srcdir}/libxml232-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('0adfd12bfde89cbd6296ba6e66b6bed4edb814a74b4265bda34d95c41d9d92c696ee7adb0c737aaf9cc6e10426a31a35079b2a23d26c074e299858da12c072ed')

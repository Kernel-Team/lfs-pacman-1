pkgbase=libxml2
pkgname=('libxml2' 'lib32-libxml2')
pkgver=2.9.8
pkgrel=1
arch=('x86_64')
url="http://www.xmlsoft.org/"
license=('MIT')
#depends=()
source=("http://xmlsoft.org/sources/libxml2-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/libxml2{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libxml2-${pkgver}"

	./configure --prefix=/usr \
		--with-history \
		--with-icu \
		--with-python=/usr/bin/python3

	cd "${srcdir}/libxml232-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--with-history \
		--with-icu \
		--without-python

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libxml2-${pkgver}"
	make -C "${srcdir}/libxml232-${pkgver}"
}

package_libxml2() {
	pkgdesc="XML parsing library, version 2"

	cd "${srcdir}/libxml2-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxml2() {
	pkgdesc="XML parsing library, version 2 (32 bit)"

	cd "${srcdir}/libxml232-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('28903282c7672206effa1362fd564cbe4cf5be44264b083a7d14e383f73bccd1b81bcafb5f4f2f56f5e7e05914c660e27668c9ce91b1b9f256ef5358d55ba917')

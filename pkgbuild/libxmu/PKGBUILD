pkgbase=libxmu
pkgname=('libxmu' 'lib32-libxmu')
pkgver=1.1.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXmu-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXmu{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXmu-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXmu32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXmu-${pkgver}"
	make -C "${srcdir}/libXmu32-${pkgver}"
}

package_libxmu() {
	pkgdesc="X11 miscellaneous micro-utility library"

	cd "${srcdir}/libXmu-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxmu() {
	pkgdesc="X11 miscellaneous micro-utility library (32 bit)"

	cd "${srcdir}/libXmu32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('8c6cc65b22aa031ad870dd92736681a068a0878a425a53dbed909943da1136c4a24034d467cfd3785c3a8d78f66850b69f1ebe1eb24aaf9bc176b1d171a5c762')

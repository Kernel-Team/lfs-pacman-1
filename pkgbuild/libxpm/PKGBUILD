pkgbase=libxpm
pkgname=('libxpm' 'lib32-libxpm')
pkgver=3.5.13
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXpm-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXpm{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXpm-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXpm32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXpm-${pkgver}"
	make -C "${srcdir}/libXpm32-${pkgver}"
}

package_libxpm() {
	pkgdesc="X11 pixmap library"

	cd "${srcdir}/libXpm-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxpm() {
	pkgdesc="X11 pixmap library (32 bit)"

	cd "${srcdir}/libXpm32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('3b8f6e926272f39b1d95135c2274c00f5aaee1f9fb2ef79f71628df5edeb7ba20158819ef6a778101cc4092493a3b5b613c53634fdccadcc0fc85f0605e5e9a5')

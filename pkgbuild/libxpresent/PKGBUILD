pkgbase=libxpresent
pkgname=('libxpresent' 'lib32-libxpresent')
pkgver=1.0.0
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXpresent-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXpresent{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXpresent-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXpresent32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXpresent-${pkgver}"
	make -C "${srcdir}/libXpresent32-${pkgver}"
}

package_libxpresent() {
	pkgdesc="Xlib-compatible API for the Present extension"

	cd "${srcdir}/libXpresent-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxpresent() {
	pkgdesc="Xlib-compatible API for the Present extension (32 bit)"

	cd "${srcdir}/libXpresent32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('edd87ac15bb666081898dd7382fcbff5')

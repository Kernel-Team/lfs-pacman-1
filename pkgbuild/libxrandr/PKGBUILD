pkgbase=libxrandr
pkgname=('libxrandr' 'lib32-libxrandr')
pkgver=1.5.1
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXrandr-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXrandr{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXrandr-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXrandr32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXrandr-${pkgver}"
	make -C "${srcdir}/libXrandr32-${pkgver}"
}

package_libxrandr() {
	pkgdesc="X11 RandR extension library"

	cd "${srcdir}/libXrandr-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxrandr() {
	pkgdesc="X11 RandR extension library (32 bit)"

	cd "${srcdir}/libXrandr32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('ddcee1c50da8810412fd747b05a073ee5522b2f3b0a74e6a9b48b489ce9712356d1bc3009f8898aeee85671cdb44e2dafa04c8caf23d97963b36b1af9174445a')

pkgbase=libxrandr
pkgname=('libxrandr' 'lib32-libxrandr')
pkgver=1.5.2
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXrandr-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXrandr{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXrandr-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXrandr32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXrandr-${pkgver}"
	make -C "${srcdir}/libXrandr32-${pkgver}"
}

package_libxrandr() {
	pkgdesc="X11 RandR extension library"

	cd "${srcdir}/libXrandr-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxrandr() {
	pkgdesc="X11 RandR extension library (32 bit)"

	cd "${srcdir}/libXrandr32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('fcd005f9839e7ef980607128a5d76d7b671cc2f5755949e03c569c500d7e987cb3f6932750ab8bf6e2c1086ec69dde09d5831f0c2098b9f9ad46be4f56db0d87')

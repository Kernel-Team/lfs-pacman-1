pkgbase=libxrender
pkgname=('libxrender' 'lib32-libxrender')
pkgver=0.9.10
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXrender-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXrender{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXrender-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXrender32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXrender-${pkgver}"
	make -C "${srcdir}/libXrender32-${pkgver}"
}

package_libxrender() {
	pkgdesc="X Rendering Extension client library"

	cd "${srcdir}/libXrender-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxrender() {
	pkgdesc="X Rendering Extension client library (32 bit)"

	cd "${srcdir}/libXrender32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('16ea0cf638b32d7df54b270457ef8c9d9a80da27fa845b105b560cb31027b4c7fe799cf23d6b6bac492be5961264e96d7845d316a9af4de9ff38bf40885ea6fe')

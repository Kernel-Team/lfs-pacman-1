pkgbase=libxres
pkgname=('libxres' 'lib32-libxres')
pkgver=1.2.0
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXres-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXres{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXres-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXres32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXres-${pkgver}"
	make -C "${srcdir}/libXres32-${pkgver}"
}

package_libxres() {
	pkgdesc="X11 Resource extension library"

	cd "${srcdir}/libXres-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxres() {
	pkgdesc="X11 Resource extension library (32 bit)"

	cd "${srcdir}/libXres32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('8eb5a36b1f030eb4e27e676cc4befcc073da66a7bce2b39089314e1facb629d13b087bb53ca2c95fe5291857da7ff9f80c6ab28fb3e245eeb9e0344937900172')

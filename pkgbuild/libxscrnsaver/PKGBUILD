pkgbase=libxscrnsaver
pkgname=('libxscrnsaver' 'lib32-libxscrnsaver')
pkgver=1.2.2
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXScrnSaver-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXScrnSaver{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXScrnSaver-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXScrnSaver32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXScrnSaver-${pkgver}"
	make -C "${srcdir}/libXScrnSaver32-${pkgver}"
}

package_libxscrnsaver() {
	pkgdesc="X11 Screen Saver extension library"

	cd "${srcdir}/libXScrnSaver-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxscrnsaver() {
	pkgdesc="X11 Screen Saver extension library (32 bit)"

	cd "${srcdir}/libXScrnSaver32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('973aed3ad296c7160b66fccd4fff7375afc4332239acbe0ae1644fed9c453de69040f8174882ba264c731448edba9b499d398d013d556aea72434339399ed29f')

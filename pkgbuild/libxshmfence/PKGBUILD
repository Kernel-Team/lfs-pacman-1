pkgbase=libxshmfence
pkgname=('libxshmfence' 'lib32-libxshmfence')
pkgver=1.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libxshmfence-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libxshmfence{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libxshmfence-${pkgver}"

	CFLAGS+=" -D_GNU_SOURCE"

	./configure --prefix=/usr

	cd "${srcdir}/libxshmfence32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libxshmfence-${pkgver}"
	make -C "${srcdir}/libxshmfence32-${pkgver}"
}

package_libxshmfence() {
	pkgdesc="a library that exposes a event API on top of Linux futexes"

	cd "${srcdir}/libxshmfence-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxshmfence() {
	pkgdesc="a library that exposes a event API on top of Linux futexes (32 bit)"

	cd "${srcdir}/libxshmfence32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('c446e055f8fac62b9aa266132289a4cfc030282147974c45ce96d1768a98d1afb997470e58e4a68513174c404cbf373bdde2f0cd4b34abdbce1d89dd0b6fe2b7')

pkgbase=libxt
pkgname=('libxt' 'lib32-libxt')
pkgver=1.2.0
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXt-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXt{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXt-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXt32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXt-${pkgver}"
	make -C "${srcdir}/libXt32-${pkgver}"
}

package_libxt() {
	pkgdesc="X11 toolkit intrinsics library"

	cd "${srcdir}/libXt-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxt() {
	pkgdesc="X11 toolkit intrinsics library (32 bit)"

	cd "${srcdir}/libXt32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('06248508b6fe5dfba8ceb4518475f656162351d78136eeb5d65086d680dabe9aca7bba3c94347f9c13ef03f82dab3ac19d0952ee610bc8c51c14cee7cf65f0b1')

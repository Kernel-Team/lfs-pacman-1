pkgbase=libxtst
pkgname=('libxtst' 'lib32-libxtst')
pkgver=1.2.3
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXtst-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXtst{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXtst-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXtst32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXtst-${pkgver}"
	make -C "${srcdir}/libXtst32-${pkgver}"
}

package_libxtst() {
	pkgdesc="X11 Testing -- Resource extension library"

	cd "${srcdir}/libXtst-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxtst() {
	pkgdesc="X11 Testing -- Resource extension library (32 bit)"

	cd "${srcdir}/libXtst32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('6f6741fd3596775eaa056465319f78c29c91b3893a851a4899df651a2023a4d762497b112a33d7d3e8865fe85d173d03e4b49daef76a66af1ae1eaab82a12765')

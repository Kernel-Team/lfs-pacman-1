pkgbase=libxv
pkgname=('libxv' 'lib32-libxv')
pkgver=1.0.11
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXv-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXv{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXv-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXv32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXv-${pkgver}"
	make -C "${srcdir}/libXv32-${pkgver}"
}

package_libxv() {
	pkgdesc="X11 Video extension library"

	cd "${srcdir}/libXv-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxv() {
	pkgdesc="X11 Video extension library (32 bit)"

	cd "${srcdir}/libXv32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('210b6ef30dda2256d54763136faa37b9')

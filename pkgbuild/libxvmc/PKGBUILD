pkgbase=libxvmc
pkgname=('libxvmc' 'lib32-libxvmc')
pkgver=1.0.11
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXvMC-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXvMC{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXvMC-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXvMC32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXvMC-${pkgver}"
	make -C "${srcdir}/libXvMC32-${pkgver}"
}

package_libxvmc() {
	pkgdesc="X11 Video Motion Compensation extension library"

	cd "${srcdir}/libXvMC-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxvmc() {
	pkgdesc="X11 Video Motion Compensation extension library (32 bit)"

	cd "${srcdir}/libXvMC32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('4fe49e956ac97a45d851292cbba7bf242706aaf3bd9eb7ea4d2fc50e40745115d857a4fe179d2b87bc7cb6f9ac93911e89a167f02f6d1ae25a59df9a517a2476')

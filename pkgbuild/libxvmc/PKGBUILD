pkgbase=libxvmc
pkgname=('libxvmc' 'lib32-libxvmc')
pkgver=1.0.12
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXvMC-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXvMC{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXvMC-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXvMC32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXvMC-${pkgver}"
	make -C "${srcdir}/libXvMC32-${pkgver}"
}

package_libxvmc() {
	pkgdesc="X11 Video Motion Compensation extension library"

	cd "${srcdir}/libXvMC-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxvmc() {
	pkgdesc="X11 Video Motion Compensation extension library (32 bit)"

	cd "${srcdir}/libXvMC32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('62cb9a72b20af3b081d2d555a253b0e2b8b26b31f4fad10bd3e53bbf96e1663b61bd8df2b78b16f18218cd37cce0c9e6809ae16ded226b37a8b199e41b223955')

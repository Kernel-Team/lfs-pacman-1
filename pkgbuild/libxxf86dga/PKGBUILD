pkgbase=libxxf86dga
pkgname=('libxxf86dga' 'lib32-libxxf86dga')
pkgver=1.1.5
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXxf86dga-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXxf86dga{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXxf86dga-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXxf86dga32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXxf86dga-${pkgver}"
	make -C "${srcdir}/libXxf86dga32-${pkgver}"
}

package_libxxf86dga() {
	pkgdesc="X11 Direct Graphics Access extension library"

	cd "${srcdir}/libXxf86dga-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxxf86dga() {
	pkgdesc="X11 Direct Graphics Access extension library (32 bit)"

	cd "${srcdir}/libXxf86dga32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('0e77c4003aeba737b10c17d55aeefa935fc57c4ba9e8f79a834f3f8c690d6a1d98f69ad707eded784346c1cf40c41829719d257283b5eae3207147fb4c79ed89')

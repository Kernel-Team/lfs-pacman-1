pkgbase=libxxf86vm
pkgname=('libxxf86vm' 'lib32-libxxf86vm')
pkgver=1.1.4
pkgrel=1
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
source=("https://www.x.org/releases/individual/lib/libXxf86vm-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/libXxf86vm{,32}-${pkgver}
}

build() {
	cd "${srcdir}/libXxf86vm-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/libXxf86vm32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/libXxf86vm-${pkgver}"
	make -C "${srcdir}/libXxf86vm32-${pkgver}"
}

package_libxxf86vm() {
	pkgdesc="X11 XFree86 video mode extension library"

	cd "${srcdir}/libXxf86vm-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-libxxf86vm() {
	pkgdesc="X11 XFree86 video mode extension library (32 bit)"

	cd "${srcdir}/libXxf86vm32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('c5f92d86e143db02ebb36bcd25618acaa2cb2831f5a23800a06dd431cd73b6702d95de8fe7407ce626336bf614c288d5256f4d87ea7781fad2ab6c517cbf09e0')

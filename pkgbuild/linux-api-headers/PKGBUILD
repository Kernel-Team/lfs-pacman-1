pkgname=linux-api-headers
_basever=5.0
pkgver=5.0.3
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL2')
source=("https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-${_basever}.tar.xz")

[ ${_basever} != ${pkgver} ] && source+=("https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/patch-${pkgver}.xz")

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make mrproper
	make headers_check
}

package() {
	cd "${srcdir}/linux-${_basever}"

	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/include/drm"

	find "${pkgdir}/usr/include" \( -name .install -o -name ..install.cmd \) -delete
}

sha512sums=('3fbab70c7b03b1a10e9fa14d1e2e1f550faba4f5792b7699ca006951da74ab86e7d7f19c6a67849ab99343186e7d6f2752cd910d76222213b93c1eab90abf1b0'
            'f14f1f456acacf9272cd07f1ed9da02ddeef98687b18cb9d62968689c055405b839c1b043e92ece6639afc035c67abe1187ee74b0271860b40824ad1bd130a48')

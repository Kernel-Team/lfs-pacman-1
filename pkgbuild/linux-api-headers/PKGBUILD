pkgname=linux-api-headers
_basever=4.17
pkgver=4.17.12
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL2')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-${_basever}.tar.xz")

[ ${_basever} != ${pkgver} ] && source+=("https://cdn.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz")

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make mrproper
	make headers_check
}

package() {
	cd "${srcdir}/linux-${_basever}"

	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/include/drm"

	find "${pkgdir}/usr/include" \( -name .install -o -name ..install.cmd \) -delete
}

sha512sums=('4d9de340a26155a89ea8773131c76220cc2057f2b5d031b467b60e8b14c1842518e2d60a863d8c695f0f7640f3f18d43826201984a238dade857b6cef79837db'
            '516270daaa65a0f40bf6202909fc1950a8c723b77d5a54089a72eb664c3708ece050c938230cdd3b1b3e281d73c1c5e909def5fe1e0c8ddecbf9fbd43713a3aa')

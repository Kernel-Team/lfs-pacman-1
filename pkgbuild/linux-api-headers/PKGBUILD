pkgname=linux-api-headers
_basever=5.4
pkgver=5.4.86
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL2')
source=("https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-${_basever}.tar.xz")

[ ${_basever} != ${pkgver} ] && source+=("https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/patch-${pkgver}.xz")

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make mrproper
	make headers

	rm -rf usr/include/drm
	find usr/include \( -name '.*' -o -name Makefile \) -delete
}

package() {
	cd "${srcdir}/linux-${_basever}"
	install -dm755 "${pkgdir}/usr/include"
	cp -r usr/include/* "${pkgdir}/usr/include"
}

sha512sums=('9f60f77e8ab972b9438ac648bed17551c8491d6585a5e85f694b2eaa4c623fbc61eb18419b2656b6795eac5deec0edaa04547fc6723fbda52256bd7f3486898f'
            '3973098c6cd985a81a441d77e149961b1ddd638c7d137e527c84ba846147ccded881c40acc078c2fe524fb4e828a2e2f2da765b169da64258f5a3e1be39e4e17')

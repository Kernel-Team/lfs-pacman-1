pkgname=linux-api-headers
_basever=4.16
pkgver=4.16.7
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL2')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-${_basever}.tar.xz")

[ ${_basever} != ${pkgver} ] && source+=("https://cdn.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz")

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make mrproper
	make headers_check
}

package() {
	cd "${srcdir}/linux-${_basever}"

	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/include/drm"

	find "${pkgdir}/usr/include" \( -name .install -o -name ..install.cmd \) -delete
}

sha512sums=('ab47849314b177d0eec9dbf261f33972b0d89fb92fb0650130ffa7abc2f36c0fab2d06317dc1683c51a472a9a631573a9b1e7258d6281a2ee189897827f14662'
            '576c2b520d444e11a9ca45ed3ed03822007ab6ff778a1759aa0f65c96946fe3e169e71d48d11e6d3b8627a99cdc20abfb0c84d7b6c9b0d2afa4d5fee9ed3aa41')

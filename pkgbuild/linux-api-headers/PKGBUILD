pkgname=linux-api-headers
_basever=4.10
pkgver=4.10
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL2')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-${_basever}.tar.xz")
#	"https://cdn.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz")

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make mrproper
	make headers_check
}

package() {
	cd "${srcdir}/linux-${_basever}"

	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/include/drm"

	find "${pkgdir}/usr/include" \( -name .install -o -name ..install.cmd \) -delete
}

md5sums=('b5e7f6b9b2fe1b6cc7bc56a3a0bfc090')

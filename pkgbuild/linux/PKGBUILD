pkgbase=linux
pkgname=('linux' 'linux-headers')
_basever=4.15
pkgver=4.15.15
pkgrel=1
arch=('x86_64')
url="https://www.kernel.org/"
license=('GPL2')
options=(!strip)
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-${_basever}.tar.xz"
	"linux-march-native.patch"
	"config")

[ ${_basever} != ${pkgver} ] && source+=("https://cdn.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz")

export KBUILD_BUILD_USER="krejzi"
export KBUILD_BUILD_HOST="email.com"

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true

	patch -Np1 -i "${srcdir}/linux-march-native.patch"

	install -v -m644 "${srcdir}/config" .config
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make
}

package_linux() {
	pkgdesc="The Linux kernel and modules"
	options=(!strip)

	cd "${srcdir}/linux-${_basever}"

	export _kernver=$(make kernelrelease)

	install -v -Dm644 .config "${pkgdir}/boot/config"
	install -v -Dm644 System.map "${pkgdir}/boot/System.map"
	install -v -Dm644 arch/x86/boot/bzImage "${pkgdir}/boot/vmlinuz"

	make modules_install INSTALL_MOD_PATH="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/lib/firmware"
	rm -rf "${pkgdir}"/usr/lib/modules/${_kernver}/{build,source}

	/usr/sbin/depmod -b "${pkgdir}/usr" $(make kernelrelease)
}

package_linux-headers() {
	pkgdesc="Header files and scripts for building modules for Linux kernel"

	cd "${srcdir}/linux-${_basever}"

	export _kernver=$(make kernelrelease)
	export _hdrdir=${pkgdir}/usr/src/linux-headers-${_kernver}

	install -v -dm755 "${pkgdir}/usr/lib/modules/${_kernver}"

	ln -sfv /usr/src/linux-headers-${_kernver} "${pkgdir}/usr/lib/modules/${_kernver}/build"
	ln -sfv /usr/src/linux-headers-${_kernver} "${pkgdir}/usr/lib/modules/${_kernver}/source"

	install -v -dm755 "${_hdrdir}/arch/x86/kernel"
	install -v -dm755 "${_hdrdir}"/scripts/{basic,genksyms,kconfig,mod}

	install -v -m644 .config "${_hdrdir}"
	install -v -m644 Makefile "${_hdrdir}"
	install -v -m644 Module.symvers "${_hdrdir}"
	install -v -m644 arch/x86/Makefile "${_hdrdir}/arch/x86"
	install -v -m644 arch/x86/kernel/asm-offsets.s "${_hdrdir}/arch/x86/kernel"

	cp -r arch/x86/include "${_hdrdir}/arch/x86"
	cp -r include "${_hdrdir}"

	ln -sfv ../generated/uapi/linux/version.h "${_hdrdir}/include/linux/version.h"

	find "${_hdrdir}/arch/x86" -name ".*.cmd" -delete

	for i in $(find . -name "Kconfig*")
	do
		install -dm755 "${_hdrdir}"/$(dirname ${i})
		install -m644 ${i} "${_hdrdir}/${i}"
	done

	cp -r scripts "${_hdrdir}"

	find "${_hdrdir}/scripts" -name "*.o" -delete
	find "${_hdrdir}/scripts" -name "*.h" -delete
	find "${_hdrdir}/scripts" -name "*.c*" -delete
}

sha512sums=('c00d92659df815a53dcac7dde145b742b1f20867d380c07cb09ddb3295d6ff10f8931b21ef0b09d7156923a3957b39d74d87c883300173b2e20690d2b4ec35ea'
            '472f338a97395a3d3bd4507ce108e4fe432fc0e4c93c458601a6e5f1764e449c495a6ada5733938f4500c66a92828605971452e50e90790f1e518d5879a5357a'
            '98e1bfee14fe63b5c8fb6edb755b21a44eec9551a83a01d6873305a5c892fc6af0bdf151652cf61231d7422cebf5804d5c7c7d5394ce1b73a41efb842939ceb7'
            '4f3da42c3540b621706b74477e1e1922f5fca295d38e57f323f53a005d6aa8d247f30e19440af052d832ee19f298d108d1f8fc8a0a7d1a7c05470b32d8af07b3')

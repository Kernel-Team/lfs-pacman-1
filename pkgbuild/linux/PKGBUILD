pkgbase=linux
pkgname=('linux' 'linux-headers')
_basever=4.13
pkgver=4.13.11
pkgrel=1
arch=('x86_64')
url="https://www.kernel.org/"
license=('GPL2')
options=(!strip)
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-${_basever}.tar.xz"
	"https://cdn.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz"
	"linux-march-native.patch"
	"config")

export KBUILD_BUILD_USER="krejzi"
export KBUILD_BUILD_HOST="email.com"

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true

	patch -Np1 -i "${srcdir}/linux-march-native.patch"

	# https://bugzilla.kernel.org/show_bug.cgi?id=196941
	sed -i "633 s:-EIO:-ENODATA:" drivers/net/wireless/intel/iwlwifi/mvm/tt.c

	install -v -m644 "${srcdir}/config" .config
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make
}

package_linux() {
	pkgdesc="The Linux kernel and modules"
	options=(!strip)

	cd "${srcdir}/linux-${_basever}"

	export _kernver=$(make kernelrelease)

	install -v -Dm644 .config "${pkgdir}/boot/config"
	install -v -Dm644 System.map "${pkgdir}/boot/System.map"
	install -v -Dm644 arch/x86/boot/bzImage "${pkgdir}/boot/vmlinuz"

	make modules_install INSTALL_MOD_PATH="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/lib/firmware"
	rm -rf "${pkgdir}"/usr/lib/modules/${_kernver}/{build,source}

	/usr/sbin/depmod -b "${pkgdir}/usr" $(make kernelrelease)
}

package_linux-headers() {
	pkgdesc="Header files and scripts for building modules for Linux kernel"

	cd "${srcdir}/linux-${_basever}"

	export _kernver=$(make kernelrelease)
	export _hdrdir=${pkgdir}/usr/src/linux-headers-${_kernver}

	install -v -dm755 "${pkgdir}/usr/lib/modules/${_kernver}"

	ln -sfv /usr/src/linux-headers-${_kernver} "${pkgdir}/usr/lib/modules/${_kernver}/build"
	ln -sfv /usr/src/linux-headers-${_kernver} "${pkgdir}/usr/lib/modules/${_kernver}/source"

	install -v -dm755 "${_hdrdir}/arch/x86/kernel"
	install -v -dm755 "${_hdrdir}"/scripts/{basic,genksyms,kconfig,mod}

	install -v -m644 .config "${_hdrdir}"
	install -v -m644 Makefile "${_hdrdir}"
	install -v -m644 Module.symvers "${_hdrdir}"
	install -v -m644 arch/x86/Makefile "${_hdrdir}/arch/x86"
	install -v -m644 arch/x86/kernel/asm-offsets.s "${_hdrdir}/arch/x86/kernel"

	cp -r arch/x86/include "${_hdrdir}/arch/x86"
	cp -r include "${_hdrdir}"

	ln -sfv ../generated/uapi/linux/version.h "${_hdrdir}/include/linux/version.h"

	find "${_hdrdir}/arch/x86" -name ".*.cmd" -delete

	for i in $(find . -name "Kconfig*")
	do
		install -dm755 "${_hdrdir}"/$(dirname ${i})
		install -m644 ${i} "${_hdrdir}/${i}"
	done

	cp -r scripts "${_hdrdir}"

	find "${_hdrdir}/scripts" -name "*.o" -delete
	find "${_hdrdir}/scripts" -name "*.h" -delete
	find "${_hdrdir}/scripts" -name "*.c*" -delete
}

sha512sums=('a557c2f0303ae618910b7106ff63d9978afddf470f03cb72aa748213e099a0ecd5f3119aea6cbd7b61df30ca6ef3ec57044d524b7babbaabddf8b08b8bafa7d2'
            'ad38845a4c05fcaace68563ffa005cf537d3564448b28750b2c872788cbc0c2495dbc9fdf98817d21aef41863614d8b707acdfb05d8f07845d921c909b5f1d22'
            '472f338a97395a3d3bd4507ce108e4fe432fc0e4c93c458601a6e5f1764e449c495a6ada5733938f4500c66a92828605971452e50e90790f1e518d5879a5357a'
            '1d57cff39af7a7e9cac2d16cedf6431459750d6ffccc704ea207ae4bfc7815d313b18854658c028b04efc22029ec8005645a54825b2a18501cfa45e0a5d454eb')

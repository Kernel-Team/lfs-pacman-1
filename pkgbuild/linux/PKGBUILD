
pkgbase=linux
pkgname=('linux' 'linux-headers')
_basever=5.4
pkgver=5.4.86
pkgrel=1
arch=('x86_64')
url="https://www.kernel.org/"
license=('GPL2')
options=(!strip)
source=("https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-${_basever}.tar.xz"
	"config")

[ ${_basever} != ${pkgver} ] && source+=("https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/patch-${pkgver}.xz")

export KBUILD_BUILD_USER="krejzi"
export KBUILD_BUILD_HOST="email.com"

prepare() {
	cd "${srcdir}/linux-${_basever}"
	[ ${_basever} != ${pkgver} ] && patch -Np1 -i "${srcdir}/patch-${pkgver}" || true

	# patch -Np1 -i "${srcdir}/linux-march-native.patch"

	install -v -m644 "${srcdir}/config" .config
}

build() {
	cd "${srcdir}/linux-${_basever}"

	make
}

package_linux() {
	pkgdesc="The Linux kernel and modules"
	options=(!strip)

	cd "${srcdir}/linux-${_basever}"

	export _kernver=$(make kernelrelease)

	install -v -Dm644 .config "${pkgdir}/boot/config"
	install -v -Dm644 System.map "${pkgdir}/boot/System.map"
	install -v -Dm644 arch/x86/boot/bzImage "${pkgdir}/boot/vmlinuz"

	make modules_install INSTALL_MOD_PATH="${pkgdir}/usr"

	rm -rf "${pkgdir}/usr/lib/firmware"
	rm -rf "${pkgdir}"/usr/lib/modules/${_kernver}/{build,source}

	/usr/sbin/depmod -b "${pkgdir}/usr" $(make kernelrelease)
}

package_linux-headers() {
	pkgdesc="Header files and scripts for building modules for Linux kernel"

	cd "${srcdir}/linux-${_basever}"

	export _kernver=$(make kernelrelease)
	export _hdrdir=${pkgdir}/usr/src/linux-headers-${_kernver}

	install -v -dm755 "${pkgdir}/usr/lib/modules/${_kernver}"

	ln -sfv /usr/src/linux-headers-${_kernver} "${pkgdir}/usr/lib/modules/${_kernver}/build"
	ln -sfv /usr/src/linux-headers-${_kernver} "${pkgdir}/usr/lib/modules/${_kernver}/source"

	install -v -dm755 "${_hdrdir}/arch/x86/kernel"
	install -v -dm755 "${_hdrdir}"/scripts/{basic,genksyms,kconfig,mod}

	install -v -m644 .config "${_hdrdir}"
	install -v -m644 Makefile "${_hdrdir}"
	install -v -m644 Module.symvers "${_hdrdir}"
	install -v -m644 arch/x86/Makefile "${_hdrdir}/arch/x86"
	install -v -m644 arch/x86/kernel/asm-offsets.s "${_hdrdir}/arch/x86/kernel"

	cp -r arch/x86/include "${_hdrdir}/arch/x86"
	cp -r include "${_hdrdir}"

	ln -sfv ../generated/uapi/linux/version.h "${_hdrdir}/include/linux/version.h"

	find "${_hdrdir}/arch/x86" -name ".*.cmd" -delete

	for i in $(find . -name "Kconfig*")
	do
		install -dm755 "${_hdrdir}"/$(dirname ${i})
		install -m644 ${i} "${_hdrdir}/${i}"
	done

	cp -r scripts "${_hdrdir}"

	find "${_hdrdir}/scripts" -name "*.o" -delete
	find "${_hdrdir}/scripts" -name "*.h" -delete
	find "${_hdrdir}/scripts" -name "*.c*" -delete
}

sha512sums=('9f60f77e8ab972b9438ac648bed17551c8491d6585a5e85f694b2eaa4c623fbc61eb18419b2656b6795eac5deec0edaa04547fc6723fbda52256bd7f3486898f'
            '5695abef8dcd17486da3b45aa7c10a378ec18aec0fbe701629a543da8572ef3c5ad8ef027bf9685e0f625b75bf6b7353897dd6d5c9dae32d45713e93c4f9a1b4'
            '3973098c6cd985a81a441d77e149961b1ddd638c7d137e527c84ba846147ccded881c40acc078c2fe524fb4e828a2e2f2da765b169da64258f5a3e1be39e4e17')

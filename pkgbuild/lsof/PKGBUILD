pkgname=lsof
pkgver=4.89
pkgrel=1
pkgdesc="Lists open files for running Unix processes"
arch=('x86_64')
url="http://people.freebsd.org/~abe/"
license=('custom')
#depends=()
source=("ftp://ftp.fu-berlin.de/pub/unix/tools/lsof/lsof_${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/lsof_${pkgver}"

	bsdtar xf lsof_${pkgver}_src.tar
}

build() {
	cd "${srcdir}/lsof_${pkgver}/lsof_${pkgver}_src"

	export LSOF_CC="${CC}"

	./Configure -n linux

	sed -i "/DEBUG/s:-O:${CFLAGS}:" Makefile
	unset CFLAGS

	make
}

package() {
	cd "${srcdir}/lsof_${pkgver}/lsof_${pkgver}_src"

	install -Dm755 lsof "${pkgdir}/usr/bin/lsof"
	install -Dm644 lsof.8 "${pkgdir}/usr/share/man/man8/lsof.8"
}

sha512sums=('cafb0209143c7863e5cebe7efc33a2520d36f9e26222350e4edfa18debbcfb5c81f42f71469e5913fb21f49a7562bbe761b72dfc8f297dadf75ac619cdcb318c')

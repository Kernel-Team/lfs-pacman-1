pkgname=lsof
pkgver=4.91
pkgrel=1
pkgdesc="Lists open files for running Unix processes"
arch=('x86_64')
url="https://people.freebsd.org/~abe/"
license=('custom')
#depends=()
source=("ftp://ftp.fu-berlin.de/pub/unix/tools/lsof/lsof_${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/lsof_${pkgver}"

	bsdtar xf lsof_${pkgver}_src.tar
}

build() {
	cd "${srcdir}/lsof_${pkgver}/lsof_${pkgver}_src"

	export LSOF_CC="${CC}"

	./Configure -n linux

	sed -i "/DEBUG/s:-O:${CFLAGS}:" Makefile
	unset CFLAGS

	make
}

package() {
	cd "${srcdir}/lsof_${pkgver}/lsof_${pkgver}_src"

	install -Dm755 lsof "${pkgdir}/usr/bin/lsof"
	install -Dm644 lsof.8 "${pkgdir}/usr/share/man/man8/lsof.8"
}

sha512sums=('49f811941dd6303f7cb0655fddb8b1177af5d1b18f2bd1edfab09d2c128aea73daecf09c7a5375979c66ba764c88a6e70c9086b55c3634e3be01ab1aa12e9f92')

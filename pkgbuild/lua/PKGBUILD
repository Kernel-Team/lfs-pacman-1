pkgname=lua
_luamajor=5.3
_luaminor=4
pkgver=${_luamajor}.${_luaminor}
pkgrel=1
pkgdesc="Powerful lightweight programming language designed for extending applications"
arch=('x86_64')
url="http://www.lua.org/"
license=('MIT')
#depends=()
options=(emptydirs)
source=("http://www.lua.org/ftp/lua-${pkgver}.tar.gz"
	"lua.pc"
	"lua-shared-lib.patch")

prepare() {
	cd "${srcdir}/lua-${pkgver}"

	patch -Np1 -i "${srcdir}/lua-shared-lib.patch"

	sed -i '/#define LUA_ROOT/s:/usr/local:/usr:' src/luaconf.h
	sed -i "s/gcc/${CC}/g" src/Makefile
}

build() {
	cd "${srcdir}/lua-${pkgver}"

	make MYCFLAGS="${CFLAGS} -DLUA_COMPAT_5_1" linux
}

package() {
	cd "${srcdir}/lua-${pkgver}"

	make INSTALL_TOP="${pkgdir}/usr" TO_LIB="liblua.so liblua.so.${_luamajor} liblua.so.${pkgver}" \
	     INSTALL_DATA="cp -d" INSTALL_MAN="${pkgdir}/usr/share/man/man1" install

	install -v -dm755 "${pkgdir}/usr/lib/pkgconfig"

	sed -e "s#%LUAMAJOR%#${_luamajor}#g" \
	    -e "s#%LUAVER%#${pkgver}#g" \
	    "${srcdir}/lua.pc" > "${pkgdir}/usr/lib/pkgconfig/lua.pc"
}

sha512sums=('739e31f82e6a60fa99910c2005e991b3a1e21339af52847f653cb190b30842054d189ca116ffcfdf9b36e07888c9ce5642b1dd2988cc7eff9f8789f9a2e34997'
            '067276b85c2b31d24412e95b20e9b3aa46bb2df01d4ebc09f83f4cc8bc880bac361244cc0d4a77b9f835f25db7b8ac63cffea019c45e4e5274226a6b028e453a'
            '3f26ae0a1eb78c2c697cacb6c7c21e5daacd9f365108bb3f397c814f1e174833b7fb8cb86362fc6da161698add9576083b616238fcb0c284ccb1884b49bd6247')

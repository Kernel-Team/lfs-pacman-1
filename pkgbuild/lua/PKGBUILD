pkgname=lua
_luamajor=5.3
_luaminor=4
pkgver=${_luamajor}.${_luaminor}
pkgrel=1
pkgdesc="Powerful lightweight programming language designed for extending applications"
arch=('x86_64')
url="http://www.lua.org/"
license=('MIT')
#depends=()
options=(emptydirs)
source=("http://www.lua.org/ftp/lua-${pkgver}.tar.gz"
	"lua.pc"
	"lua-shared-lib.patch")

prepare() {
	cd "${srcdir}/lua-${pkgver}"

	patch -Np1 -i "${srcdir}/lua-shared-lib.patch"

	sed -i '/#define LUA_ROOT/s:/usr/local:/usr:' src/luaconf.h
	sed -i "s/gcc/${CC}/g" src/Makefile
}

build() {
	cd "${srcdir}/lua-${pkgver}"

	make MYCFLAGS="${CFLAGS} -DLUA_COMPAT_5_1" linux
}

package() {
	cd "${srcdir}/lua-${pkgver}"

	make INSTALL_TOP="${pkgdir}/usr" TO_LIB="liblua.so liblua.so.${_luamajor} liblua.so.${pkgver}" \
	     INSTALL_DATA="cp -d" INSTALL_MAN="${pkgdir}/usr/share/man/man1" install

	install -v -dm755 "${pkgdir}/usr/lib/pkgconfig"

	sed -e "s#%LUAMAJOR%#${_luamajor}#g" \
	    -e "s#%LUAVER%#${pkgver}#g" \
	    "${srcdir}/lua.pc" > "${pkgdir}/usr/lib/pkgconfig/lua.pc"
}

md5sums=('53a9c68bcc0eda58bdc2095ad5cdfc63'
         'bbfaf6ac509640c99cf0752856e5b32d'
         'b7dd7093767d432bec7b96f0341a0170')

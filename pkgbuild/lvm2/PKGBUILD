pkgname=lvm2
pkgver=2.02.177
pkgrel=1
pkgdesc="Logical Volume Manager 2"
arch=('x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2.1')
#depends=()
source=("ftp://sourceware.org/pub/lvm2/releases/LVM2.${pkgver}.tgz")

prepare() {
	cd "${srcdir}/LVM2.${pkgver}"

	sed -i "s#systemd >=#lib&#g" configure
}

build() {
	cd "${srcdir}/LVM2.${pkgver}"

	./configure --prefix=/usr \
		--with-confdir=/etc \
		--enable-applib \
		--enable-cmdlib \
		--enable-dmeventd \
		--enable-lvmetad \
		--enable-lvmpolld \
		--enable-pkgconfig \
		--enable-udev_rules \
		--enable-udev_sync \
		--enable-udev-systemd-background-jobs \
		--with-tmpfilesdir=/usr/lib/tmpfiles.d \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		systemdutildir=/usr/lib/systemd

	make
}

package() {
	cd "${srcdir}/LVM2.${pkgver}"

	make install DESTDIR="${pkgdir}"
	make install_systemd_units DESTDIR="${pkgdir}"
	make install_systemd_generators DESTDIR="${pkgdir}"
}

sha512sums=('e287aeae3ce5ce0efcdebce90f24c2f659cd8691839e3f3cb902848535259ae510ec48f5355481b99f7285d9cff01883e6ef2b101072bdf9c99fba8e9cb54ded')

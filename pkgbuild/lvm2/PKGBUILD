pkgname=lvm2
pkgver=2.02.183
pkgrel=1
pkgdesc="Logical Volume Manager 2"
arch=('x86_64')
url="https://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2.1')
#depends=()
#source=("ftp://sourceware.org/pub/lvm2/releases/LVM2.${pkgver}.tgz")
source=("ftp://sources.redhat.com/pub/lvm2/LVM2.${pkgver}.tgz")

prepare() {
	cd "${srcdir}/LVM2.${pkgver}"

	sed -i "s#systemd >=#lib&#g" configure
}

build() {
	cd "${srcdir}/LVM2.${pkgver}"

	./configure --prefix=/usr \
		--with-confdir=/etc \
		--enable-applib \
		--enable-cmdlib \
		--enable-dmeventd \
		--enable-lvmetad \
		--enable-lvmpolld \
		--enable-pkgconfig \
		--enable-udev_rules \
		--enable-udev_sync \
		--enable-udev-systemd-background-jobs \
		--with-tmpfilesdir=/usr/lib/tmpfiles.d \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		systemdutildir=/usr/lib/systemd

	make
}

package() {
	cd "${srcdir}/LVM2.${pkgver}"

	make install DESTDIR="${pkgdir}"
	make install_systemd_units DESTDIR="${pkgdir}"
	make install_systemd_generators DESTDIR="${pkgdir}"
}

sha512sums=('3947523c6b0862ada40677ed171ed0cf95e070119e377296fc5ccb153e9c4212d34c0b16a887dbd68ccf265525345dabfed2aa59fb3604555429a7e2ecfff4d7')

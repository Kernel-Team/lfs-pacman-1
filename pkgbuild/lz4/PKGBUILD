pkgbase=lz4
pkgname=('lz4' 'lib32-lz4')
pkgver=1.9.3
pkgrel=1
arch=('x86_64')
url="https://lz4.github.io/lz4/"
license=('GPL2')
#depends=()
source=("https://github.com/lz4/lz4/archive/v${pkgver}/lz4-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/lz4-${pkgver}"

	sed -i "/install.*liblz4.a/d" lib/Makefile
	sed -i "/CFLAGS/s#-O3#${CFLAGS}#g" lib/Makefile programs/Makefile

	cp -a "${srcdir}"/lz4{,32}-${pkgver}
}

build() {
	cd "${srcdir}/lz4-${pkgver}"

	make -C lib PREFIX=/usr
	make -C programs PREFIX=/usr lz4 lz4c

	cd "${srcdir}/lz432-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	make -C lib PREFIX=/usr LIBDIR=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH
}

package_lz4() {
	pkgdesc="Extremely fast compression algorithm"

	cd "${srcdir}/lz4-${pkgver}"

	make install PREFIX=/usr DESTDIR="${pkgdir}"
}

package_lib32-lz4() {
	pkgdesc="Extremely fast compression algorithm (32 bit)"

	cd "${srcdir}/lz432-${pkgver}"

	make -C lib PREFIX=/usr LIBDIR=/usr/lib32 install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('c246b0bda881ee9399fa1be490fa39f43b291bb1d9db72dba8a85db1a50aad416a97e9b300eee3d2a4203c2bd88bda2762e81bc229c3aa409ad217eb306a454c')

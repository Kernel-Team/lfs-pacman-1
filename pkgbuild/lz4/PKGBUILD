pkgbase=lz4
pkgname=('lz4' 'lib32-lz4')
pkgver=1.7.5
pkgrel=1
arch=('x86_64')
url="http://www.lz4.org/"
license=('GPL2')
#depends=()
source=("lz4-${pkgver}.tar.gz::https://github.com/lz4/lz4/archive/v${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/lz4-${pkgver}"

	sed -i "/install.*liblz4.a/d" lib/Makefile
	sed -i "/CFLAGS/s#-O3#${CFLAGS}#g" lib/Makefile programs/Makefile

	cp -a "${srcdir}"/lz4{,32}-${pkgver}
}

build() {
	cd "${srcdir}/lz4-${pkgver}"

	make -C lib PREFIX=/usr
	make -C programs PREFIX=/usr lz4 lz4c

	cd "${srcdir}/lz432-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	make -C lib PREFIX=/usr LIBDIR=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH
}

package_lz4() {
	pkgdesc="Extremely fast compression algorithm"

	cd "${srcdir}/lz4-${pkgver}"

	make install PREFIX=/usr DESTDIR="${pkgdir}"
}

package_lib32-lz4() {
	pkgdesc="Extremely fast compression algorithm (32 bit)"

	cd "${srcdir}/lz432-${pkgver}"

	make -C lib PREFIX=/usr LIBDIR=/usr/lib32 install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('b4e5b17fe06805e676608e636a45f0b480b79a02c6b7ffce84dc4d607861cb2652b2852493ab9d9249e7caeae6f00b6834801fdc39f58d901cbc1c7347915295')

pkgbase=lz4
pkgname=('lz4' 'lib32-lz4')
pkgver=1.8.1.2
pkgrel=1
arch=('x86_64')
url="http://www.lz4.org/"
license=('GPL2')
#depends=()
source=("https://github.com/lz4/lz4/archive/v${pkgver}/lz4-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/lz4-${pkgver}"

	sed -i "/install.*liblz4.a/d" lib/Makefile
	sed -i "/CFLAGS/s#-O3#${CFLAGS}#g" lib/Makefile programs/Makefile

	cp -a "${srcdir}"/lz4{,32}-${pkgver}
}

build() {
	cd "${srcdir}/lz4-${pkgver}"

	make -C lib PREFIX=/usr MANDIR=/usr/share/man/man1
	make -C programs PREFIX=/usr lz4 lz4c

	cd "${srcdir}/lz432-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	make -C lib PREFIX=/usr LIBDIR=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH
}

package_lz4() {
	pkgdesc="Extremely fast compression algorithm"

	cd "${srcdir}/lz4-${pkgver}"

	make install PREFIX=/usr MANDIR=/usr/share/man/man1 DESTDIR="${pkgdir}"
}

package_lib32-lz4() {
	pkgdesc="Extremely fast compression algorithm (32 bit)"

	cd "${srcdir}/lz432-${pkgver}"

	make -C lib PREFIX=/usr LIBDIR=/usr/lib32 install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('f4ed450bc05477cc6c2b42e6fe1fbb1fb0907f1d05b68b1d69d975b555ddb385550f204258e6d91642e14ce373815141dc218cda03b711793935d5458bc45c7f')

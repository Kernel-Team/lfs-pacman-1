pkgname=make
pkgver=4.2.1
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('x86_64')
url="https://www.gnu.org/software/make/"
license=('GPL3')
#depends=()
source=("https://ftp.gnu.org/gnu/make/make-${pkgver}.tar.bz2"
	"make-fixes.patch")

prepare() {
	cd "${srcdir}/make-${pkgver}"

	patch -Np1 -i "${srcdir}/make-fixes.patch"
}

build() {
	cd "${srcdir}/make-${pkgver}"

	sed -i "/GLOB_INTERFACE_VERSION/s# 1# 2#" configure

	./configure --prefix=/usr --without-guile

	make
}

package() {
	cd "${srcdir}/make-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('9cf00869a2f938492554f71d8cb288b5b009b3bd0489ef164f2c8f6532fc37db5c7e20af1dea288536e7c9710ee0bc6e1ddcdfc4928a8540e6e43661741825b8'
            'a7a6d4754761781ec1429b30cfc1f681d71411c141d67b4ff3cc334d0364249745d75989d55f041cb7b6fdde22610844a76a188437259756f7f4719846582cee')

pkgname=man-db
pkgver=2.9.3
pkgrel=1
pkgdesc="A utility for reading man pages"
arch=('x86_64')
url="http://www.nongnu.org/man-db/"
license=('GPL' 'LGPL')
#depends=()
install=man-db.install
source=("https://savannah.nongnu.org/download/man-db/man-db-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/man-db-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-cache-owner=root \
		--disable-setuid \
		--with-browser=/usr/bin/lynx \
		--with-vgrind=/usr/bin/vgrind \
		--with-grap=/usr/bin/grap \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/man-db-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/lib/systemd/system/multi-user.target.wants"
	ln -sfv ../man-db.timer "${pkgdir}/usr/lib/systemd/system/multi-user.target.wants/man-db.timer"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('ca1c1214753483f6e22efe69a9df9852e0de01a9ad3b9950dcbbc9f38e6060100b98a84333256f8c734002e66e2fd6256bc017a31bd9acfc42002dca2c0f879b')

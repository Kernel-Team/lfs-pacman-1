pkgname=man-db
pkgver=2.7.6.1
pkgrel=1
pkgdesc="A utility for reading man pages"
arch=('x86_64')
url="http://www.nongnu.org/man-db/"
license=('GPL' 'LGPL')
#depends=()
install=man-db.install
source=("http://download.savannah.gnu.org/releases/man-db/man-db-${pkgver}.tar.xz"
	"man-db.service"
	"man-db.timer")

build() {
	cd "${srcdir}/man-db-${pkgver}"

	sed -i "s:man man:root root:g" init/systemd/man-db.conf

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-cache-owner=root \
		--disable-setuid \
		--with-browser=/usr/bin/lynx \
		--with-vgrind=/usr/bin/vgrind \
		--with-grap=/usr/bin/grap

	make
}

package() {
	cd "${srcdir}/man-db-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/man-db.timer" "${pkgdir}/lib/systemd/system/man-db.timer"
	install -v -Dm644 "${srcdir}/man-db.service" "${pkgdir}/lib/systemd/system/man-db.service"

	install -v -dm755 "${pkgdir}/lib/systemd/system/multi-user.target.wants"
	ln -sfv ../man-db.timer "${pkgdir}/lib/systemd/system/multi-user.target.wants/man-db.timer"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('623c5e7f8b7c289908b2c926f8777293b8d39aeceef0d2509d701a8b0bfa81408650f655c8608318221786c751a79ee91124b07993de5298cd7fa6d8bb737301'
            '36587635b2b4d496a353a53c0bb2f863d321b5b329a5549643f09076d3d176efc13ebe3715aa5c14e217ef581f0a335eae2b4fcf20dea7873858eab232355e87'
            '2ed529500fbe18ba00ac7a6fc4c9da59e396464afb256db33f462b1127e497916602370e65e485c8d788c839f5b1b1130028502f61e1cc9ec8571ad6dd993738')

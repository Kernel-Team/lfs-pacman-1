pkgbase=mesa
pkgname=('mesa' 'lib32-mesa')
pkgver=19.0.0
pkgrel=1
arch=('x86_64')
url="https://mesa.freedesktop.org/"
license=('custom')
#depends=()
source=("https://mesa.freedesktop.org/archive/mesa-${pkgver/.rc/-rc}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	local configure_shared=(
		--prefix /usr
		--sysconfdir /etc
		--localstatedir /var
		--buildtype=plain
		-Dplatforms="drm,surfaceless,wayland,x11"
		-Ddri3=true
		-Ddri-drivers="i965"
		-Dgallium-drivers="radeonsi,swrast"
		-Dgallium-vdpau=true
		-Dgallium-xvmc=false
		-Dgallium-omx=disabled
		-Dgallium-va=true
		-Dgallium-xa=false
		-Dgallium-nine=true
		-Dgallium-opencl=disabled
		-Dvulkan-drivers="amd,intel"
		-Dshared-glapi=true
		-Dgles1=true
		-Dgles2=true
		-Dgbm=true
		-Dglx=dri
		-Degl=true
		-Dshared-llvm=true
		-Dvalgrind=false
		-Dlibunwind=true
		-Dlmsensors=false
	)

	cd "${srcdir}/build64"

	meson "${configure_shared[@]}" "${srcdir}/mesa-${pkgver/.rc/-rc}"

	cd "${srcdir}/build32"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
	export LLVM_CONFIG=/usr/bin/llvm-config32

	i386 meson "${configure_shared[@]}" \
		--libdir /usr/lib32 \
		"${srcdir}/mesa-${pkgver/.rc/-rc}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	ninja -C build64 ${MAKEFLAGS}
	ninja -C build32 ${MAKEFLAGS}
}

package_mesa() {
	pkgdesc="Mesa 3D graphics library"

	cd "${srcdir}/build64"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}"/usr/lib/libwayland-egl.so*
	rm -rf "${pkgdir}"/usr/lib/pkgconfig/wayland-egl.pc
}

package_lib32-mesa() {
	pkgdesc="Mesa 3D graphics library (32 bit)"

	cd "${srcdir}/build32"

	DESTDIR="${PWD}/dest" ninja install

	install -v -dm755 "${pkgdir}/usr"

	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/share "${pkgdir}/usr"

	rm -rf "${pkgdir}"/usr/lib32/libwayland-egl.so*
	rm -rf "${pkgdir}"/usr/lib32/pkgconfig/wayland-egl.pc
	rm -rf "${pkgdir}/usr/share/drirc.d"
}

sha512sums=('5759b85275bcd145513cf14a9ef7505595766fb33b82c53738f74ede462e5850580d48ab4af326b41209e7f4b05aab75539f2bfebf67c3098a4680ea95c37591')

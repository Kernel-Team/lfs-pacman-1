pkgbase=mesa
pkgname=('mesa' 'lib32-mesa')
pkgver=18.1.0
pkgrel=1
arch=('x86_64')
url="https://mesa.freedesktop.org/"
license=('custom')
#depends=()
source=("https://mesa.freedesktop.org/archive/mesa-${pkgver/.rc/-rc}.tar.xz")

prepare() {
	#cd "${srcdir}/mesa-${pkgver/.r/-r}"
	#autoreconf -fi

	cp -a "${srcdir}"/mesa{,32}-${pkgver/.rc/-rc}
}

build() {
	configure_flags=(--disable-valgrind
		--disable-xvmc
		--enable-texture-float
		--enable-llvm-shared-libs
		--enable-gles1
		--enable-gles2
		--enable-gbm
		--enable-vdpau
		--enable-va
		--enable-glx-tls
		--with-dri-drivers=i965
		--with-platforms=drm,surfaceless,wayland,x11
		--with-gallium-drivers=radeonsi,swrast
		--with-vulkan-drivers=intel,radeon)

	export DLOPEN_LIBS="-ldl"

	cd "${srcdir}/mesa-${pkgver/.rc/-rc}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		"${configure_flags[@]}"

	cd "${srcdir}/mesa32-${pkgver/.rc/-rc}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
	export LLVM_CONFIG=/usr/bin/llvm-config32
	export LDFLAGS="-lcompiler-rt"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib32 \
		--build=i686-pc-linux-gnu \
		--host=i686-pc-linux-gnu \
		"${configure_flags[@]}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/mesa-${pkgver/.rc/-rc}"
	make -C "${srcdir}/mesa32-${pkgver/.rc/-rc}"
}

package_mesa() {
	pkgdesc="Mesa 3D graphics library"

	cd "${srcdir}/mesa-${pkgver/.rc/-rc}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/libwayland-egl.so*
	rm -rf "${pkgdir}"/usr/lib/pkgconfig/wayland-egl.pc
}

package_lib32-mesa() {
	pkgdesc="Mesa 3D graphics library (32 bit)"

	cd "${srcdir}/mesa32-${pkgver/.rc/-rc}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"

	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/share "${pkgdir}/usr"

	rm -rf "${pkgdir}"/usr/lib32/libwayland-egl.so*
	rm -rf "${pkgdir}"/usr/lib32/pkgconfig/wayland-egl.pc
}

sha512sums=('8b26af2df8b94373cbc339521974cd568c1d4ff4204986ee7b439e4cf3ebe14d822ea081a7769b68eca9263b7bc6dbca01836b8bb0d6495d2e2614c4e3d601ad')

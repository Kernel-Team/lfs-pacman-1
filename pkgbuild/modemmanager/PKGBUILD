pkgname=modemmanager
pkgver=1.10.0
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/ModemManager/"
license=('GPL2' 'LGPL2.1')
#depends=()
source=("https://www.freedesktop.org/software/ModemManager/ModemManager-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/ModemManager-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-more-warnings \
		--enable-gtk-doc \
		--with-polkit=permissive \
		--with-suspend-resume=systemd \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		--with-udev-base-dir=/usr/lib/udev

	make
}

package() {
	cd "${srcdir}/ModemManager-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('676b11892b284c2da587895e71903f80ec96978c66eafae5e2d90e5c0f1dae18e50efc68b704c7907f01f208d585f5a8140316cf83e093df3f782c80949c33bc')

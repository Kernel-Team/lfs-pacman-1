pkgname=mpv
pkgver=0.26.0
_wafrelease=1.9.8
pkgrel=1
pkgdesc="A free, open source, and cross-platform media player"
arch=('x86_64')
url="https://mpv.io/"
license=('GPL')
#depends=()
source=("mpv-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz"
	"waf::https://waf.io/waf-${_wafrelease}")

prepare() {
	cd "${srcdir}/mpv-${pkgver}"

	install -v -m755 "${srcdir}/waf" waf
}

build() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf configure --prefix=/usr \
		--confdir=/etc/mpv \
		--lua=luajit \
		--enable-cdda \
		--enable-openal \
		--enable-sdl2 \
		--enable-libmpv-shared

	./waf build
}

package() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf install --destdir="${pkgdir}"
}

sha512sums=('fd772b4765317eb1ea3b54f12b86e0ac6c4d5e9d16895eb5bebe2e074a01f6b790981b9b34a87d9b23e4291149d3a7154b09fba4a259cec1949a6aa88d6ddec7'
            'e7493767f65deeb38aa162adef22adf60ebbd038410e9cdf4fe6f0de8c43395dbbba14962aa2c3855e1277136085fc22ad12ebfb8ac4e76f16358296039b8ea2')

pkgname=mpv
pkgver=0.28.0
_wafrelease=1.9.8
pkgrel=1
pkgdesc="A free, open source, and cross-platform media player"
arch=('x86_64')
url="https://mpv.io/"
license=('GPL')
#depends=()
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}/mpv-${pkgver}.tar.gz"
	"waf::https://waf.io/waf-${_wafrelease}")

prepare() {
	cd "${srcdir}/mpv-${pkgver}"

	install -v -m755 "${srcdir}/waf" waf
}

build() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf configure --prefix=/usr \
		--confdir=/etc/mpv \
		--lua=luajit \
		--enable-cdda \
		--enable-dvbin \
		--enable-dvdnav \
		--enable-dvdread \
		--enable-libsmbclient \
		--enable-openal \
		--enable-sdl2 \
		--enable-tv \
		--enable-libmpv-shared

	./waf build
}

package() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf install --destdir="${pkgdir}"
}

sha512sums=('33e889f9ca2dd16ad055ccd9fc5549fcead3fd15dcbc52c6b5f3ef608cf87b3d9952e9236e3dec8e7f32240b68d0b0cff1499f511d39c8f3a0501334f5c57a70'
            'e7493767f65deeb38aa162adef22adf60ebbd038410e9cdf4fe6f0de8c43395dbbba14962aa2c3855e1277136085fc22ad12ebfb8ac4e76f16358296039b8ea2')

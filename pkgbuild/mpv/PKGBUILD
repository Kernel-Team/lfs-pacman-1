pkgname=mpv
pkgver=0.25.0
_wafrelease=1.9.8
pkgrel=1
pkgdesc="A free, open source, and cross-platform media player"
arch=('x86_64')
url="https://mpv.io/"
license=('GPL')
#depends=()
source=("mpv-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz"
	"waf::https://waf.io/waf-${_wafrelease}")

prepare() {
	cd "${srcdir}/mpv-${pkgver}"

	install -v -m755 "${srcdir}/waf" waf
}

build() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf configure --prefix=/usr \
		--confdir=/etc/mpv \
		--lua=luajit \
		--enable-cdda \
		--enable-openal \
		--enable-sdl2 \
		--enable-libmpv-shared

	./waf build
}

package() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf install --destdir="${pkgdir}"
}

md5sums=('73b3d233c3b4428d0cfd5491c5cb6c41'
         'fbb646eafa430f959743010c85e269be')

pkgname=mpv
pkgver=0.28.2
_wafrelease=1.9.8
pkgrel=1
pkgdesc="A free, open source, and cross-platform media player"
arch=('x86_64')
url="https://mpv.io/"
license=('GPL')
#depends=()
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}/mpv-${pkgver}.tar.gz"
	"waf::https://waf.io/waf-${_wafrelease}")

prepare() {
	cd "${srcdir}/mpv-${pkgver}"

	install -v -m755 "${srcdir}/waf" waf
}

build() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf configure --prefix=/usr \
		--confdir=/etc/mpv \
		--lua=luajit \
		--enable-cdda \
		--enable-dvbin \
		--enable-dvdnav \
		--enable-dvdread \
		--enable-libsmbclient \
		--enable-openal \
		--enable-sdl2 \
		--enable-tv \
		--enable-libmpv-shared

	./waf build
}

package() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf install --destdir="${pkgdir}"
}

sha512sums=('e6206e8c65dca084e2cfa73ab09278b7113cdfd90b89877d1f75a1068c6379a093ea3955a023857357683edce1aba8c12ee441fd195d887af4d939fc7b66cef4'
            'e7493767f65deeb38aa162adef22adf60ebbd038410e9cdf4fe6f0de8c43395dbbba14962aa2c3855e1277136085fc22ad12ebfb8ac4e76f16358296039b8ea2')

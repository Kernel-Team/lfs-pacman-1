pkgname=mpv
pkgver=0.29.1
_wafrelease=2.0.9
pkgrel=1
pkgdesc="A free, open source, and cross-platform media player"
arch=('x86_64')
url="https://mpv.io/"
license=('GPL')
#depends=()
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}/mpv-${pkgver}.tar.gz"
	"waf::https://waf.io/waf-${_wafrelease}")

prepare() {
	cd "${srcdir}/mpv-${pkgver}"

	install -v -m755 "${srcdir}/waf" waf
}

build() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf configure --prefix=/usr \
		--confdir=/etc/mpv \
		--lua=luajit \
		--enable-cdda \
		--enable-dvbin \
		--enable-dvdnav \
		--enable-dvdread \
		--enable-libsmbclient \
		--enable-openal \
		--enable-sdl2 \
		--enable-tv \
		--enable-libmpv-shared

	./waf build
}

package() {
	cd "${srcdir}/mpv-${pkgver}"

	./waf install --destdir="${pkgdir}"
}

sha512sums=('ec57c9ceaaf2915ee237dd5a1c5ea5d22725d8611e28a9b998e5bb0d8ab5bdf3631d0267fc7b54da31cb1eaa145ef35841e68846bd41c3b9e1024902e92fd086'
            '0755170ded115b03e78a2096f197cd9fff62b5036db764153d26ce426f182ea7d7b8231cfa1a10b4a564a8480428502f3691851df66e7b83c7bca8e5fe26de7d')

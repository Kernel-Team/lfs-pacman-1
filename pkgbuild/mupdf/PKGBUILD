pkgname=mupdf
pkgver=1.16.1
pkgrel=1
pkgdesc="A lightweight PDF, XPS, and E-book viewer"
arch=('x86_64')
url="https://www.mupdf.com/"
license=('AGPL3')
#depends=()
source=("https://mupdf.com/downloads/archive/mupdf-${pkgver}-source.tar.xz"
	"mupdf-shared-libs.patch")

prepare() {
	cd "${srcdir}/mupdf-${pkgver}-source"

	rm -rf thirdparty/curl thirdparty/freetype thirdparty/harfbuzz
	rm -rf thirdparty/libjpeg thirdparty/openjpeg thirdparty/zlib

	patch -Np1 -i "${srcdir}/mupdf-shared-libs.patch"
}

build() {
	cd "${srcdir}/mupdf-${pkgver}-source"

	USE_SYSTEM_LIBS=yes make XCFLAGS="${CFLAGS}" build=release verbose=yes
}

package() {
	cd "${srcdir}/mupdf-${pkgver}-source"

	USE_SYSTEM_LIBS=yes make XCFLAGS="${CFLAGS}" build=release install prefix="${pkgdir}/usr"
}

sha512sums=('fa657e6b9251aff91c25e91b335ea829b797dd91a759505bfd7259f8236613f590a044bc741e9b0587da55817a1ab8c1499c067d65d683d099259d06d91a8f50'
            'c7019069c1f5770e07da128b37f1d65ba67568a01f468a942a696e7e514dc4e85c2b9014d1df24d8d644b9936250cf840b86152684096e9ad46d07368e2d9adf')

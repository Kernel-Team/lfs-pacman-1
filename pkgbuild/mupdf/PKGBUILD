pkgname=mupdf
pkgver=1.14.0
pkgrel=1
pkgdesc="A lightweight PDF, XPS, and E-book viewer"
arch=('x86_64')
url="https://www.mupdf.com/"
license=('AGPL3')
#depends=()
source=("https://mupdf.com/downloads/archive/mupdf-${pkgver}-source.tar.gz"
	"mupdf-shared-libs.patch")

prepare() {
	cd "${srcdir}/mupdf-${pkgver}-source"

	rm -rf thirdparty/curl thirdparty/freetype thirdparty/harfbuzz
	rm -rf thirdparty/libjpeg thirdparty/openjpeg thirdparty/zlib

	patch -Np1 -i "${srcdir}/mupdf-shared-libs.patch"
}

build() {
	cd "${srcdir}/mupdf-${pkgver}-source"

	USE_SYSTEM_LIBS=yes make XCFLAGS="${CFLAGS}" build=release verbose=yes
}

package() {
	cd "${srcdir}/mupdf-${pkgver}-source"

	USE_SYSTEM_LIBS=yes make XCFLAGS="${CFLAGS}" build=release install prefix="${pkgdir}/usr"
}

sha512sums=('2dbe4e1d02e296dd01f59471d34771066eaab10b81d9a357f5ea9661298116b5dbfec61858dc0fb3e6714d66bde9fa22d2b665b90165b8642342ec838289dcf9'
            '5e40697827605b58d2480a17a105bd814af982a97a8b36dafdfb20aa31097bc632f2a3086f79bff3ed4a62444caf8ec717ef5dd48c23b5fa6693ddd5c69f750b')

pkgname=mutter
pkgver=3.38.2
pkgrel=1
pkgdesc="A window manager for GNOME"
arch=('x86_64')
url="https://git.gnome.org/browse/mutter/"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/mutter/${pkgver:0:4}/mutter-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/mutter-${pkgver}"
	
	sed -i "/Werror/d" meson.build
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dtests=false \
		-Dprofiler=false \
		-Dinstalled_tests=false \
		"${srcdir}/mutter-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('8bd826fdb643bc966f6b7c38ed12cec9806440bf9cf4792cb90aa62dd96d2fd1018be39148ebd4432ba799cfdd30ed5c3952620ca13b9fac933fb0a477c9937a')

pkgname=mutter
pkgver=3.32.0
pkgrel=1
pkgdesc="A window manager for GNOME"
arch=('x86_64')
url="https://git.gnome.org/browse/mutter/"
license=('GPL')
#depends=()
source=("https://download.gnome.org/sources/mutter/${pkgver:0:4}/mutter-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/mutter-${pkgver}"
	
	sed -i "/Werror/d" meson.build
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dcogl_tests=false \
		-Dclutter_tests=false \
		-Dinstalled_tests=false \
		-Dtests=false \
		"${srcdir}/mutter-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('7f26e7f752a5cedbe066b05b34eec8c4086aee1a7cbd787fad3e9b9c3a440fc8a74ada2440717a115bb69bb0781184f01e55bbbdfaa20f58d060ce6102895de1')

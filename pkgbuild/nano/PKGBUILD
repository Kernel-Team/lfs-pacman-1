pkgname=nano
pkgver=2.9.5
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
url="http://www.nano-editor.org"
license=('GPL')
#depends=()
source=("https://nano-editor.org/dist/v${pkgver:0:3}/nano-${pkgver}.tar.xz"
	"nanorc")

build() {
	cd "${srcdir}/nano-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc --enable-utf8

	make
}

package() {
	cd "${srcdir}/nano-${pkgver}"

	make install DESTDIR="${pkgdir}"

	ln -sfv nano "${pkgdir}/usr/bin/editor"

	install -v -Dm644 "${srcdir}/nanorc" "${pkgdir}/etc/nanorc"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('24e8b5124bd08c67e34603bf2c5ba972cb8e92e8f13750b460ae824ba26d65a6d7b13dabff083caa40cc9d46436b8757cdf0411f2fbb58be3a175826090704f5'
            '90473cf8487334cf695684fffcc528a291a895f4c2e37a1f6bc5dd9fd42e52b0ff02e1fc4d74a3c08099c19ebe75268968aaa8901e57166d1385ad61861a46ab')

pkgname=net-snmp
pkgver=5.9
pkgrel=1
pkgdesc="A suite of applications used to implement SNMP v1, SNMP v2c and SNMP v3 using both IPv4 and IPv6"
arch=('x86_64')
url="http://www.net-snmp.org/"
license=('BSD')
#depends=()
source=("https://downloads.sourceforge.net/net-snmp/net-snmp-${pkgver}.tar.gz"
	"snmpd.service"
	"snmptrapd.service")

prepare() {
	cd "${srcdir}/net-snmp-${pkgver}"
}

build() {
	cd "${srcdir}/net-snmp-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-default-snmp-version="3" \
		--with-sys-contact="root@localhost" \
		--with-sys-location="Unknown" \
		--with-logfile="/var/log/snmpd.log" \
		--with-persistent-directory="/var/lib/net-snmp" \
		--with-agentx-socket="/var/lib/agentx/master" \
		--enable-ucd-snmp-compatibility

	make
}

package() {
	cd "${srcdir}/net-snmp-${pkgver}"

	make install INSTALLDIRS=vendor DESTDIR="${pkgdir}" INSTALL_PREFIX="${pkgdir}"

	rm -rf "${pkgdir}/usr/lib/perl"

	install -v -Dm644 "${srcdir}/snmpd.service" "${pkgdir}/usr/lib/systemd/system/snmpd.service"
	install -v -Dm644 "${srcdir}/snmptrapd.service" "${pkgdir}/usr/lib/systemd/system/snmptrapd.service"
}

sha512sums=('df3273f03065ea5cb7d63398308ada06bcd186e0bc48929e81ba647f392b646ab81ddc241aebcd75408ec29231377375af62edf2835e9c3eb01d0a6856b79434'
            '4da98eaa0f18e51033a2b19c09227c3c74e4882b42f078b780fbd879b9b13b5638c8c1341c09c386884b435aad2263ae36c3989316c34327f501d9172c86c958'
            'a6fa907a4bc18a0f0b767ce824679bb5c227745ee3cf4edf7f2c11a96c4bebb4762193afad17223c4a979adf74d21cf708dd9c607f83789a559d73ae5d122eba')

pkgname=networkmanager
pkgver=1.8.2
pkgrel=1
pkgdesc="Network connection manager and user applications"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/NetworkManager"
license=('GPL2' 'LGPL2.1')
_pppver=2.4.7
#depends=()
options=(emptydirs)
source=("https://download.gnome.org/sources/NetworkManager/${pkgver:0:3}/NetworkManager-${pkgver}.tar.xz"
	"NetworkManager.conf"
	"NetworkManager.tmpfiles")

build() {
	cd "${srcdir}/NetworkManager-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-ld-gc \
		--disable-qt \
		--enable-gtk-doc \
		--with-config-dhcp-default=dhclient \
		--with-dhclient=/usr/sbin/dhclient \
		--with-pppd-plugin-dir=/usr/lib/pppd/${_pppver} \
		--with-session-tracking=systemd \
		--with-suspend-resume=systemd \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		--with-udev-dir=/usr/lib/udev

	sed -i "s#none required#-ldl#g" $(grep -rl "none required")

	make
}

package() {
	cd "${srcdir}/NetworkManager-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/lib/systemd/system/network-online.target.wants" "${pkgdir}/var/run"

	install -v -Dm644 "${srcdir}/NetworkManager.conf" "${pkgdir}/etc/NetworkManager/NetworkManager.conf"
	install -v -Dm644 "${srcdir}/NetworkManager.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/NetworkManager.conf"
}

sha512sums=('7bebea66d8f494c052313224e9d5b1b4cf665482f07ee5d93defc3543f32296545a2d1097ab1fff27dbebb58e07a4bc47fbc0101163db92953ae66ac9b95524d'
            '1f4e6c09da5a8921ee7759861cfa5e808c64cd27e733cd62cfb35776db05a226a73443c071730035480c9ba4abe85fa7daeb14aeffbbdae20ed193d78341f451'
            '61ce398a21f1cadb0e1d29dc4a1254d23153a637ae500e884e1fb8a043c06d5f9f765b8d941a8bbf12bfa3fbada8ace6d0420c4ded82833bff9eaa7978eddc01')

pkgname=networkmanager
pkgver=1.10.8
pkgrel=1
pkgdesc="Network connection manager and user applications"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/NetworkManager"
license=('GPL2' 'LGPL2.1')
_pppver=2.4.7
#depends=()
options=(emptydirs)
source=("https://download.gnome.org/sources/NetworkManager/${pkgver:0:4}/NetworkManager-${pkgver}.tar.xz"
	"NetworkManager.conf"
	"NetworkManager.tmpfiles")

build() {
	cd "${srcdir}/NetworkManager-${pkgver}"

	sed -i "s#OVS_INTERFACE)#OVS_INTERFACE, NMDevice)#g" src/devices/ovs/nm-device-ovs-interface.c

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-ld-gc \
		--disable-qt \
		--with-config-dhcp-default=dhclient \
		--with-dhclient=/usr/sbin/dhclient \
		--with-pppd-plugin-dir=/usr/lib/pppd/${_pppver} \
		--with-session-tracking=systemd
#		--with-systemdsystemunitdir=/usr/lib/systemd/system \
#		--with-udev-dir=/usr/lib/udev

	sed -i "s#none required#-ldl#g" $(grep -rl "none required")

	make
}

package() {
	cd "${srcdir}/NetworkManager-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/NetworkManager.conf" "${pkgdir}/etc/NetworkManager/NetworkManager.conf"
	install -v -Dm644 "${srcdir}/NetworkManager.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/NetworkManager.conf"
}

sha512sums=('5253baab926ccc3cd59a71ed5ea10e9e054047b94968a04ee9f167bb4e2ac1c7b33668ca6a218eba9364ebcb00751b6b934329b1657fdbb013bb1fede7840e4c'
            '6fb1f8ecc6a84e93be257bf0361416cfcfac080549de4c8f106e51017a511fdbc154234cae2a14cc4a37101db8bb05f4fcbb77ec1f3ce86d65719fb14e4ddfb6'
            '61ce398a21f1cadb0e1d29dc4a1254d23153a637ae500e884e1fb8a043c06d5f9f765b8d941a8bbf12bfa3fbada8ace6d0420c4ded82833bff9eaa7978eddc01')

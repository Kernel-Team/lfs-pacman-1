pkgname=nspr
pkgver=4.17
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
#depends=()
source=("https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/nspr-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	sed -ri 's#^(RELEASE_BINS =).*#\1#' pr/src/misc/Makefile.in
	sed -i 's#$(LIBRARY) ##' config/rules.mk
}

build() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	./configure --prefix=/usr \
		--with-mozilla \
		--with-pthreads \
		--enable-64bit

	make
}

package() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('3589f533b88d7dbbdee078c2a836bf7f7cb48466cb8861b6a42b05f12a5c6ce49426aa8fb3b153d548acd1c9ccf0376feea5649b48e3d72f3da1af5df3bed882')

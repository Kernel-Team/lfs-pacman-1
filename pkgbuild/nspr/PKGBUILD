pkgname=nspr
pkgver=4.21
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
#depends=()
source=("https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/nspr-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	sed -ri 's#^(RELEASE_BINS =).*#\1#' pr/src/misc/Makefile.in
	sed -i 's#$(LIBRARY) ##' config/rules.mk
}

build() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	./configure --prefix=/usr \
		--with-mozilla \
		--with-pthreads \
		--enable-64bit

	make
}

package() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('4f152c6ca32d9dd4cf3d92b4b54f249ac1fa39047811f7c0c7a67bdc73173cf7de0dce36dc278ad781189ea2fdc71fa70e044597676a8b1f5e6d664907e5ab7c')

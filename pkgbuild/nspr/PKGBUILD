pkgname=nspr
pkgver=4.29
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
#depends=()
source=("https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/nspr-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	sed -ri 's#^(RELEASE_BINS =).*#\1#' pr/src/misc/Makefile.in
	sed -i 's#$(LIBRARY) ##' config/rules.mk
}

build() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	./configure --prefix=/usr \
		--with-mozilla \
		--with-pthreads \
		--enable-64bit

	make
}

package() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('ba5ac275fe0beb69d7a7674c9ee9e4429bd5761daed285edd975ccc829af30d062bf4a0f5e44361e3bd191f21b1905f96ab146d53b55324020f13ecb3c05609b')

pkgname=nspr
pkgver=4.19
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
#depends=()
source=("https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/nspr-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	sed -ri 's#^(RELEASE_BINS =).*#\1#' pr/src/misc/Makefile.in
	sed -i 's#$(LIBRARY) ##' config/rules.mk
}

build() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	./configure --prefix=/usr \
		--with-mozilla \
		--with-pthreads \
		--enable-64bit

	make
}

package() {
	cd "${srcdir}/nspr-${pkgver}/nspr"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('45a48111d0f89db0649a5e1356719868ff81881f9cf48c22c1d13b0831978b103f19e9840d6a5359fcdc90d763c0a60f0a29f7840d756a5efbd53ce1afd36844')

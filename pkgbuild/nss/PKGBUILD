pkgname=nss
pkgver=3.60
pkgrel=1
pkgdesc="Network Security Services"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
license=('MPL' 'GPL')
#depends=()
options=(staticlibs)
source=("https://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/nss-${pkgver}.tar.gz"
	"nss-standalone.patch")

prepare() {
	cd "${srcdir}/nss-${pkgver}/nss"

	patch -Np2 -i "${srcdir}/nss-standalone.patch"
}

build() {
	cd "${srcdir}/nss-${pkgver}/nss"

	export XCFLAGS="${CFLAGS}"

	unset CFLAGS

	make -j1 \
		BUILD_OPT=1 \
		NSPR_INCLUDE_DIR=/usr/include/nspr \
		USE_SYSTEM_ZLIB=1 \
		ZLIB_LIBS=-lz \
		NSS_ENABLE_WERROR=0 \
		NSS_USE_SYSTEM_SQLITE=1 \
		USE_64=1 \
		CC="${CC:-gcc}" CCC="${CXX:-g++}"
}

package() {
	cd "${srcdir}/nss-${pkgver}/nss"

	install -v -dm755 "${pkgdir}/usr/bin"
	install -v -dm755 "${pkgdir}/usr/include/nss"
	install -v -dm755 "${pkgdir}/usr/lib/pkgconfig"
	install -v -dm755 "${pkgdir}/usr/share/man/man1"

	install -v -m755 ../dist/Linux*/bin/certutil "${pkgdir}/usr/bin"
	install -v -m755 ../dist/Linux*/bin/nss-config "${pkgdir}/usr/bin"
	install -v -m755 ../dist/Linux*/bin/pk12util "${pkgdir}/usr/bin"

	cp -RL ../dist/public/nss/* "${pkgdir}/usr/include/nss"

	install -v -m755 ../dist/Linux*/lib/*.so "${pkgdir}/usr/lib"
	install -v -m644 ../dist/Linux*/lib/libcrmf.a "${pkgdir}/usr/lib"
	install -v -m644 ../dist/Linux*/lib/*.chk "${pkgdir}/usr/lib"
	install -v -m644 ../dist/Linux*/lib/pkgconfig/nss.pc "${pkgdir}/usr/lib/pkgconfig/nss.pc"

	install -v -m644 doc/nroff/certutil.1 "${pkgdir}/usr/share/man/man1"
	install -v -m644 doc/nroff/pk12util.1 "${pkgdir}/usr/share/man/man1"

	# Replace built-in trust with p11-kit connection
	ln -sfv pkcs11/p11-kit-trust.so "${pkgdir}/usr/lib/p11-kit-trust.so"
	ln -sfv p11-kit-trust.so "${pkgdir}/usr/lib/libnssckbi.so"
}

sha512sums=('6463b2da28b5d9f1f20d45f77a3179e2b93c874af5742c7fc51eb7c44cef93270acacf79174dc63905f227256cbcee23a36f98f1cfed10dd5c56ffc0a76e2695'
            'c2b9abcdeb24f228aac15fb30e585d538e53209ab5158cbb5954cd83cc70fd45eff39b7a9dad6791616f9f7b71e9e016a84e9689bc0282ddb7242a8ecf966ae6')

pkgname=nss
pkgver=3.34.1
pkgrel=1
pkgdesc="Network Security Services"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
license=('MPL' 'GPL')
#depends=()
options=(staticlibs)
source=("https://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/nss-${pkgver}.tar.gz"
	"nss-standalone.patch")

prepare() {
	cd "${srcdir}/nss-${pkgver}/nss"

	sed -i "/WARNING_CFLAGS/s:Werror:Wall:g" coreconf/Werror.mk

	patch -Np2 -i "${srcdir}/nss-standalone.patch"
}

build() {
	cd "${srcdir}/nss-${pkgver}/nss"

	export XCFLAGS="${CFLAGS}"
	unset CFLAGS

	make nss_build_all BUILD_OPT=1 \
		NSPR_INCLUDE_DIR=/usr/include/nspr \
		USE_SYSTEM_ZLIB=1 \
		ZLIB_LIBS=-lz \
		NSS_USE_SYSTEM_SQLITE=1 \
		USE_64=1 \
		CC="${CC}" CCC="${CXX}" -j1
}

package() {
	cd "${srcdir}/nss-${pkgver}/nss"

	install -v -dm755 "${pkgdir}/usr/bin"
	install -v -dm755 "${pkgdir}/usr/include/nss"
	install -v -dm755 "${pkgdir}/usr/lib/pkgconfig"

	install -v -m755 ../dist/Linux*/bin/certutil "${pkgdir}/usr/bin"
	install -v -m755 ../dist/Linux*/bin/nss-config "${pkgdir}/usr/bin"
	install -v -m755 ../dist/Linux*/bin/pk12util "${pkgdir}/usr/bin"

	cp -RL ../dist/public/nss/* "${pkgdir}/usr/include/nss"
	cp -RL ../dist/private/nss/* "${pkgdir}/usr/include/nss"

	install -v -m755 ../dist/Linux*/lib/*.so "${pkgdir}/usr/lib"
	install -v -m644 ../dist/Linux*/lib/libcrmf.a "${pkgdir}/usr/lib"
	install -v -m644 ../dist/Linux*/lib/*.chk "${pkgdir}/usr/lib"
	install -v -m644 ../dist/Linux*/lib/pkgconfig/nss.pc "${pkgdir}/usr/lib/pkgconfig/nss.pc"
}

sha512sums=('6cc4826df4202e865e903a2ed05b49f708a047347b7b4d58f9b83ed097115a128239c4596a033ddeb9ee3fbfe6345a024e11eacb6149bce2d71fbe82c0a41c63'
            '3e78b07cb11a2298486d38fad0c21690c126205ffa722bc8647259802c3cd82bf3ba7f5ff17a53f8db89c24b8ab8df78f40e9c0031322eaf8c936b94dc8dc9a0')

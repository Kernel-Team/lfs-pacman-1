pkgname=ntfs-3g
pkgver=2017.3.23
pkgrel=1
pkgdesc="NTFS filesystem driver and utilities"
arch=('x86_64')
url="https://www.tuxera.com/company/open-source/"
license=('GPL2')
#depends=()
source=("https://tuxera.com/opensource/ntfs-3g_ntfsprogs-${pkgver}.tgz")

build() {
	cd "${srcdir}/ntfs-3g_ntfsprogs-${pkgver}"

	./configure --prefix=/usr \
		--exec-prefix=/usr \
		--with-fuse=external \
		--disable-ldconfig \
		--enable-extras \
		--enable-posix-acls

	make
}

package() {
	cd "${srcdir}/ntfs-3g_ntfsprogs-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/sbin"

	ln -sfv mkntfs "${pkgdir}/usr/sbin/mkfs.ntfs"

	ln -sfv ../bin/lowntfs-3g "${pkgdir}/usr/sbin/mount.lowntfs-3g"
	ln -sfv ../bin/ntfs-3g "${pkgdir}/usr/sbin/mount.ntfs-3g"
	ln -sfv ../bin/ntfs-3g "${pkgdir}/usr/sbin/mount.ntfs"

	ln -sfv ntfs-3g.8.gz "${pkgdir}/usr/share/man/man8/mount.ntfs.8.gz"
}

sha512sums=('3a607f0d7be35204c992d8931de0404fbc52032c13b4240d2c5e6f285c318a28eb2a385d7cf5ac4cd445876aee5baa5753bb636ada0d870d84a9d3fdbce794ef')

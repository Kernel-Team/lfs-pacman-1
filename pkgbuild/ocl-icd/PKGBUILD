pkgbase=ocl-icd
pkgname=('ocl-icd' 'lib32-ocl-icd')
pkgver=2.2.14
pkgrel=1
arch=('x86_64')
url="https://forge.imag.fr/projects/ocl-icd/"
license=('custom')
#depends=()
source=("https://github.com/OCL-dev/ocl-icd/archive/v${pkgver}/ocl-icd-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/ocl-icd-${pkgver}"

	autoreconf -fi

	cp -a "${srcdir}"/ocl-icd{,32}-${pkgver}
}

build() {
	cd "${srcdir}/ocl-icd-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/ocl-icd32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/ocl-icd-${pkgver}"
	make -C "${srcdir}/ocl-icd32-${pkgver}"
}

package_ocl-icd() {
	pkgdesc="OpenCL ICD Bindings"

	cd "${srcdir}/ocl-icd-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-ocl-icd() {
	pkgdesc="OpenCL ICD Bindings (32 bit)"

	cd "${srcdir}/ocl-icd32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('78510b6fa4e2c6a52141a51ccf0d0ef3110b0b4902a43bb97f7622ff0ce470b108dc05c9619c28ce8758ccea1e1cf6b2e7f1a296f8b07f52532f23b2b036a5cf')

pkgbase=ocl-icd
pkgname=('ocl-icd' 'lib32-ocl-icd')
pkgver=2.2.12
_dlid=836
pkgrel=1
arch=('x86_64')
url="https://forge.imag.fr/projects/ocl-icd/"
license=('custom')
#depends=()
source=("https://forge.imag.fr/frs/download.php/${_dlid}/ocl-icd-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/ocl-icd{,32}-${pkgver}
}

build() {
	cd "${srcdir}/ocl-icd-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/ocl-icd32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/ocl-icd-${pkgver}"
	make -C "${srcdir}/ocl-icd32-${pkgver}"
}

package_ocl-icd() {
	pkgdesc="OpenCL ICD Bindings"

	cd "${srcdir}/ocl-icd-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-ocl-icd() {
	pkgdesc="OpenCL ICD Bindings (32 bit)"

	cd "${srcdir}/ocl-icd32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('8a5afe80da8ece3dcb5a8893942b77f7e44466d9aefd1d58ad8f0bcd686d83e3566192bf59880363c39d0b96b0f33f777e0f221f6b346bbe62c3e2b27faf2d1b')

pkgname=openal-soft
pkgver=1.20.0
pkgrel=1
pkgdesc="Cross-platform 3D audio library, software implementation"
arch=('x86_64')
url="https://github.com/kcat/openal-soft"
license=('LGPL')
#depends=()
source=("https://kcat.strangesoft.net/openal-releases/openal-soft-${pkgver}.tar.bz2")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DALSOFT_EXAMPLES=OFF \
		-DALSOFT_TESTS=OFF \
		-DALSOFT_NO_CONFIG_UTIL=ON \
		"${srcdir}/openal-soft-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('7b103ffeeb8a6541d4bd31245ca5e34fec27d121fe04c21655480fb3907604f4171f912411f1e49622ec27db79d6cb85eb452ed78523abc906f18af57ffb755e')

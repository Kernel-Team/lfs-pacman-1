pkgname=openal-soft
pkgver=1.19.1
pkgrel=1
pkgdesc="Cross-platform 3D audio library, software implementation"
arch=('x86_64')
url="https://github.com/kcat/openal-soft"
license=('LGPL')
#depends=()
source=("http://kcat.strangesoft.net/openal-releases/openal-soft-${pkgver}.tar.bz2")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DALSOFT_EXAMPLES=OFF \
		-DALSOFT_TESTS=OFF \
		-DALSOFT_NO_CONFIG_UTIL=ON \
		"${srcdir}/openal-soft-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('356a3f12bbe284bfac71f4f2a9f83d084083a832012222e73743042ac27812cf051c12b0ddcf53c010ff77e44ae2e9033cea9b9324d316a22a59a154307a64a4')

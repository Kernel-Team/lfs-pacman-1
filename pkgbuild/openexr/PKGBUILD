pkgname=openexr
pkgver=2.4.0
pkgrel=1
pkgdesc="A high dynamic-range image file format library"
arch=('x86_64')
url="https://www.openexr.com"
license=('BSD')
#depends=()
source=("https://github.com/AcademySoftwareFoundation/openexr/archive/v${pkgver}/openexr-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		"${srcdir}/openexr-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	sed -e 's|=include|=${prefix}/include|g' -e 's|=lib|=${prefix}/lib|g' \
		-i "${pkgdir}/usr/lib/pkgconfig/OpenEXR.pc" \
		-i "${pkgdir}/usr/lib/pkgconfig/IlmBase.pc"
}

sha512sums=('b544b1fc9d785cad029f35388894d1e9dae278dfda3c80d5615280386206f2af5c94b72c17e1e1d1514f73a7151b5be0f166a4be5b74abefc2f27fdf0500dfe8')


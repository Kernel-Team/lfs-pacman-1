pkgname=openexr
pkgver=2.5.4
pkgrel=1
pkgdesc="A high dynamic-range image file format library"
arch=('x86_64')
url="https://www.openexr.com"
license=('BSD')
#depends=()
source=("https://github.com/AcademySoftwareFoundation/openexr/archive/v${pkgver}/openexr-${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		"${srcdir}/openexr-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	sed -e 's|=include|=${prefix}/include|g' -e 's|=lib|=${prefix}/lib|g' \
		-i "${pkgdir}/usr/lib/pkgconfig/OpenEXR.pc" \
		-i "${pkgdir}/usr/lib/pkgconfig/IlmBase.pc"
}

sha512sums=('f0fe305987981e0c7e5a854367702585e4935ad37b0e8c10dcbc7468ae3a6d34bf963ec9ec75cc3abe4cf00e359644476b643978d0289dca46c9785a25d3f7f1')

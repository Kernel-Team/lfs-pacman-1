pkgname=openjade
pkgver=1.3.2
pkgrel=1
pkgdesc="Implementation of the DSSSL language"
arch=('x86_64')
url="http://openjade.sourceforge.net/"
license=('custom')
#depends=()
install=openjade.install
source=("https://downloads.sourceforge.net/openjade/openjade-${pkgver}.tar.gz"
	"openjade.patch")

prepare() {
	cd "${srcdir}/openjade-${pkgver}"

	patch -Np1 -i "${srcdir}/openjade.patch"
}

build() {
	cd "${srcdir}/openjade-${pkgver}"

	export CXX="${CXX} -std=c++03"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--datadir=/usr/share/sgml/OpenJade \
		--mandir=/usr/share/man \
		--enable-http \
		--enable-default-catalog=/etc/sgml/catalog \
		--enable-default-search-path=/usr/share/sgml

	make
}

package() {
	cd "${srcdir}/openjade-${pkgver}"

	make install install-man DESTDIR="${pkgdir}"

	install -m644 dsssl/catalog "${pkgdir}/usr/share/sgml/OpenJade"
	install -m644 dsssl/*.{dtd,dsl,sgm} "${pkgdir}/usr/share/sgml/OpenJade"

	ln -sfv openjade "${pkgdir}/usr/bin/jade"

	for file in grove spgrove style
	do
		ln -sfv libo${file}.so "${pkgdir}/usr/lib/lib${file}.so"
	done
}

sha512sums=('c39f70ec8020bd7a2b3e125cbd146b49ddb57910a042bd212da02722617ed5681f32dab60acc26ab89ac658997c6f604911d7be3be391d6278267481f3bcf2f1'
            '2a978a1879839d15e922026c9025060be399204c333e8176fd4c3d649c85d8dbfe4685ff8ae285ea86ddbab420d6f6fbb092d0b17e613c8874e45d56bb142844')

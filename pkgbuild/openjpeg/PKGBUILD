pkgname=openjpeg
pkgver=2.2.0
pkgrel=1
pkgdesc="An open source JPEG 2000 codec, version 2"
arch=('x86_64')
url="http://www.openjpeg.org"
license=('custom: BSD')
#depends=()
source=("openjpeg-${pkgver}.tar.gz::https://github.com/uclouvain/openjpeg/archive/v${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/openjpeg-${pkgver}"

	sed -i "s#defined(OPJ_STATIC)#0 \&\& &#g" src/lib/openjp2/openjpeg.h

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		"${srcdir}/openjpeg-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('20651c380bee582ab1950994c424cc00061ad852e9c5438fb32a9809e3f275571a4cc7e92589add0d91debf2394262e58f441c2dd918809fc1c602ed68396a3a')

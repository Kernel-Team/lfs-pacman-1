pkgname=openldap
pkgver=2.4.48
pkgrel=1
url="http://www.openldap.org/"
arch=('x86_64')
url="http://www.openldap.org/"
license=('custom')
backup=(etc/openldap/ldap.conf
	etc/openldap/slapd.conf
	etc/openldap/slapd.ldif)
options=(emptydirs)
#depends=()
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
	"openldap-blfs-paths.patch"
	"openldap-symbol-versions.patch"
	"slapd.default"
	"slapd.service"
	"slapd.sysusers"
	"slapd.tmpfiles")

prepare() {
	cd "${srcdir}/openldap-${pkgver}"

	patch -Np1 -i "${srcdir}/openldap-blfs-paths.patch"
	patch -Np1 -i "${srcdir}/openldap-symbol-versions.patch"

	autoconf
}

build() {
	cd "${srcdir}/openldap-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib \
		--disable-debug \
		--enable-dynamic \
		--enable-crypt \
		--enable-modules \
		--enable-rlookups \
		--enable-backends=mod \
		--enable-overlays=mod \
		--disable-bdb \
		--disable-hdb \
		--disable-ndb \
		--disable-sql \
		--enable-spasswd

	make depend
	make
}

package() {
	cd "${srcdir}/openldap-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/var/run"
	install -dm755 "${pkgdir}/etc/openldap/slapd.d" "${pkgdir}/var/lib/openldap"

	install -v -Dm644 "${srcdir}/slapd.default" "${pkgdir}/etc/default/slapd"
	install -v -Dm644 "${srcdir}/slapd.service" "${pkgdir}/usr/lib/systemd/system/slapd.service"

	install -v -Dm644 "${srcdir}/slapd.sysusers" "${pkgdir}/usr/lib/sysusers.d/slapd.conf"
	install -v -Dm644 "${srcdir}/slapd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/slapd.conf"

	chmod 700 "${pkgdir}/etc/openldap/slapd.d" "${pkgdir}/var/lib/openldap"
	chown -R 83:83 "${pkgdir}/etc/openldap/slapd.d" "${pkgdir}/var/lib/openldap"

	chmod 640 "${pkgdir}/etc/openldap/slapd.conf" "${pkgdir}/etc/openldap/slapd.ldif"
	chown root:83 "${pkgdir}/etc/openldap/slapd.conf" "${pkgdir}/etc/openldap/slapd.ldif"
}

sha512sums=('cf694a415be0bd55cc7f606099da2ed461748efd276561944cd29d7f5a8252a9be799d8778fac2d4fa9f382731eb4ca48c6b85630cb58a3b8249843561ae8feb'
            'a0dd72955c614442d54b769d28106a437ba2f676b555f30bac43c8c88a40e740562d9e920c308b6be37607f9cddf3967f2af7a567ea11541879064efd43a35c5'
            '3782b643888716f7f202e223b018561fe7b6c0dd3444be5573c6dc305b04fbe923fccd0009b765aacd933dee10fb321fd2d61345f63490601a3d0b5a56530504'
            'e2ef7b08039db950d4327cdb50868a4c8f196d683d93ebb186599b8e5226a5c18dc590387b06fe238262668591db2c21aea2c5070c2f4d47488ba3ae447acb2a'
            'c862bbc85b1c764aebd6c062c3d5a505f4381bd4ad755d2c2c8ce6b93b1412a291009cc055cf40d93c9d35d8bc5dc1902c2e19257d12d9730bd2fd576933280d'
            '9d5f3f79a4af4fd6e0954a4e121570a23f4a1ad20cf40ca4d4817c8327f167c33c5161a0b674f14e82ef71c479c918420567f590627401303a55809a01c2ee16'
            'e403b43d3ea9201f2df3b05e63d81790b731c8da961a5f82d00402c61c321d94c3ff144f101bb9072fae08c2744c9f0c3e6880bbd0ba5a9613cb963e3c3bf9ba')

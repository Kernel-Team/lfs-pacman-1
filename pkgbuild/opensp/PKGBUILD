pkgname=opensp
pkgver=1.5.2
pkgrel=1
pkgdesc="A library and a set of tools for validating, parsing and manipulating SGML and XML documents"
arch=('x86_64')
url="http://openjade.sourceforge.net/"
license=('BSD')
#depends=()
source=("https://downloads.sourceforge.net/openjade/OpenSP-${pkgver}.tar.gz"
	"OpenSP.patch")

prepare() {
	cd "${srcdir}/OpenSP-${pkgver}"

	patch -Np1 -i "${srcdir}/OpenSP.patch"
}

build() {
	cd "${srcdir}/OpenSP-${pkgver}"

	export CXX="${CXX} -std=c++03"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--mandir=/usr/share/man \
		--disable-doc-build \
		--enable-http \
		--enable-default-catalog=/etc/sgml/catalog \
		--enable-default-search-path=/usr/share/sgml

	make pkgdatadir=/usr/share/sgml/OpenSP
}

package() {
	cd "${srcdir}/OpenSP-${pkgver}"

	make install DESTDIR="${pkgdir}" pkgdatadir=/usr/share/sgml/OpenSP

	for file in nsgmls sgmlnorm spam spcat spent sx
	do
	ln -sfv o${file} "${pkgdir}/usr/bin/${file}"
	done

	ln -sfv osx "${pkgdir}/usr/bin/sgml2xml"
	ln -sfv libosp.so "${pkgdir}/usr/lib/libsp.so"
}

sha512sums=('a7dcc246ba7f58969ecd6d107c7b82dede811e65f375b7aa3e683621f2c6ff3e7dccefdd79098fcadad6cca8bb94c2933c63f4701be2c002f9a56f1bbe6b047e'
            '8f7fb781f6c3308b0fe61e02fdc0f4f8ca6d82024dc1b570893d94cdf243cac6cbac38dc59e500fa3fe16908d0612dd5c856ea6f75efe1679d9b92d85ef6a59b')

pkgname=openssh
pkgver=7.5p1
pkgrel=1
pkgdesc="Free version of the SSH connectivity tools"
arch=('x86_64')
url="https://www.openssh.com/portable.html"
license=('custom:BSD')
#depends=()
source=("http://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/openssh-${pkgver}.tar.gz"
	"sshd.pam"
	"sshd.sysusers"
	"sshd.tmpfiles"
	"sshdgenkeys.service"
	"sshd.service"
	"sshd@.service"
	"sshd.socket")

build() {
	cd "${srcdir}/openssh-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc/ssh \
		--localstatedir=/var \
		--with-mantype=doc \
		--with-4in6 \
		--with-privsep-path=/run/sshd \
		--with-kerberos5=/usr \
		--with-pam \
		--with-ssl-engine \
		--with-xauth=/usr/bin/xauth \
		--with-default-path=/usr/local/bin:/usr/bin:/bin \
		--with-superuser-path=/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin

	make
}

package() {
	cd "${srcdir}/openssh-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/run"

	install -v -m755 contrib/ssh-copy-id "${pkgdir}/usr/bin"
	install -v -m644 contrib/ssh-copy-id.1 "${pkgdir}/usr/share/man/man1"

	sed -i "/OpenBSD/d" "${pkgdir}"/etc/ssh/*

	sed -i "s:^#X11Forwarding no:X11Forwarding yes:g" "${pkgdir}/etc/ssh/sshd_config"
	sed -i "s:^#UsePAM no:UsePAM yes:g" "${pkgdir}/etc/ssh/sshd_config"

	install -v -Dm644 "${srcdir}/sshd.pam" "${pkgdir}/etc/pam.d/sshd"
	install -v -Dm644 "${srcdir}/sshd.sysusers" "${pkgdir}/usr/lib/sysusers.d/sshd.conf"
	install -v -Dm644 "${srcdir}/sshd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/sshd.conf"

	install -v -Dm644 "${srcdir}/sshdgenkeys.service" "${pkgdir}/usr/lib/systemd/system/sshdgenkeys.service"
	install -v -Dm644 "${srcdir}/sshd.service" "${pkgdir}/usr/lib/systemd/system/sshd.service"
	install -v -Dm644 "${srcdir}/sshd@.service" "${pkgdir}/usr/lib/systemd/system/sshd@.service"
	install -v -Dm644 "${srcdir}/sshd.socket" "${pkgdir}/usr/lib/systemd/system/sshd.socket"
}

sha512sums=('58c542e8a110fb4316a68db94abb663fa1c810becd0638d45281df8aeca62c1f705090437a80e788e6c29121769b72a505feced537d3118c933fde01b5285c81'
            'f50250c6817915327001d18ea7c14c935775d5e81ec94f8410fcaa50551455b5c8a7f0424575cd50814b6947dae305922ceca4d783785849ff6ec37ebb6f3d37'
            '000d3d51130deebdbd30249948cd46d25c6fa69c2bcc811e98f47442bf25433c2d5acbdf6934839336fd201e21ea5e60c76d26002e4cf1958729214f67770f7a'
            '295762d9fa0008598411831b50188a1c0894c74cb8ad6a2ff3977da313b9cc0a3b662ed18f58791fa3da3bc9e4ab182b6d74af0b602d32297a55dd5dc8f59157'
            'e8cda24aebd0c865915a3d2067815fce3795c9954acce273aca541886a9c1c208c442650a032d909510644b38416648e9250b05939fc0c156014a3e393dd5c74'
            '7875dbc858967161790f7482a4cba3d8166b9861f7ba563420e48253823a052950acf3ad4c3ed44173d2e2698b63f75684546a7f891878e6dd18599d284618c1'
            '0ea0d159d7cf74ebf9bf31d5c7d4c674de0acef3b42506e771414367c935f5ea50f37db4f37b0ad98529a35d1d02d0e3fb65650251c1f69cfc96298a8a305818'
            'ea1d31d84ca30fffa60b6eb06d1f532c75ff5a8acec893479cbe0f3669c62e5da9ee81be8549bae75d63e4b6fe69a4ffe6dfd4e3008e731e320d6da4bc4beae9')

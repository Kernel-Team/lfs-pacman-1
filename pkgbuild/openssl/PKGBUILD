pkgbase=openssl
pkgname=('openssl' 'lib32-openssl')
_basever=1.1.1b
pkgver=${_basever/[a-z]/.${_basever//[0-9.]/}}
pkgrel=1
arch=('x86_64')
url="https://www.openssl.org"
license=('custom:BSD')
#depends=()
options=(emptydirs)
source=("https://www.openssl.org/source/openssl-${_basever}.tar.gz")

prepare() {
	cd "${srcdir}/openssl-${_basever}"

	sed -i "s#(libdir)/engines#(libdir)/openssl/engines#g" Configurations/unix-Makefile.tmpl
	sed -i "s#./demoCA#/etc/ssl#g" apps/openssl.cnf

	cp -a "${srcdir}"/openssl{,32}-${_basever}
}

build() {
	cd "${srcdir}/openssl-${_basever}"

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib \
		shared \
		no-ssl3-method \
		enable-ec_nistp_64_gcc_128 \
		linux-x86_64 \
		"-Wa,--noexecstack ${CFLAGS}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	cd "${srcdir}/openssl32-${_basever}"

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib32 \
		shared \
		no-ssl3-method \
		linux-elf \
		"-Wa,--noexecstack ${CFLAGS}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/openssl-${_basever}"
	make -C "${srcdir}/openssl32-${_basever}"

}

package_openssl() {
	pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"

	cd "${srcdir}/openssl-${_basever}"

	make install_sw install_ssldirs install_man_docs DESTDIR="${pkgdir}" MANDIR=/usr/share/man MANSUFFIX=ssl
}

package_lib32-openssl() {
	pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (32 bit)"

	cd "${srcdir}/openssl32-${_basever}"

	make install_sw DESTDIR="${PWD}/dest" MANDIR=/usr/share/man MANSUFFIX=ssl

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('b54025fbb4fe264466f3b0d762aad4be45bd23cd48bdb26d901d4c41a40bfd776177e02230995ab181a695435039dbad313f4b9a563239a70807a2e19ecf045d')

pkgbase=openssl
pkgname=('openssl' 'lib32-openssl')
_basever=1.0.2l
pkgver=${_basever/[a-z]/.${_basever//[0-9.]/}}
pkgrel=1
arch=('x86_64')
url="https://www.openssl.org"
license=('custom:BSD')
#depends=()
options=(emptydirs)
source=("https://www.openssl.org/source/openssl-${_basever}.tar.gz"
	"openssl.patch"
	"openssl-fixes.patch")

prepare() {
	cd "${srcdir}/openssl-${_basever}"

	patch -Np1 -i "${srcdir}/openssl.patch"
	patch -Np1 -i "${srcdir}/openssl-fixes.patch"

	cp -a "${srcdir}"/openssl{,32}-${_basever}
}

build() {
	cd "${srcdir}/openssl-${_basever}"

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib \
		shared \
		no-ssl3-method \
		enable-ec_nistp_64_gcc_128 \
		linux-x86_64 \
		"-Wa,--noexecstack ${CFLAGS}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	cd "${srcdir}/openssl32-${_basever}"

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib32 \
		shared \
		no-ssl3-method \
		linux-elf \
		"-Wa,--noexecstack ${CFLAGS}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/openssl-${_basever}"
	make -C "${srcdir}/openssl32-${_basever}"

}

package_openssl() {
	pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"

	cd "${srcdir}/openssl-${_basever}"

	make install INSTALL_PREFIX="${pkgdir}" MANDIR=/usr/share/man MANSUFFIX=ssl

	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libcrypto.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libcrypto.so") "${pkgdir}/usr/lib/libcrypto.so"

	mv -v "${pkgdir}"/usr/lib/libssl.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libssl.so") "${pkgdir}/usr/lib/libssl.so"
}

package_lib32-openssl() {
	pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (32 bit)"

	cd "${srcdir}/openssl32-${_basever}"

	make install INSTALL_PREFIX="${PWD}/dest" MANDIR=/usr/share/man MANSUFFIX=ssl

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('f85123cd390e864dfbe517e7616e6566'
         '4eb9c732853759e6413ede085da242e8'
         'db010eb5d83da0242807ad76782ec8a8')

pkgbase=openssl
pkgname=('openssl' 'lib32-openssl')
_basever=1.0.2m
pkgver=${_basever/[a-z]/.${_basever//[0-9.]/}}
pkgrel=1
arch=('x86_64')
url="https://www.openssl.org"
license=('custom:BSD')
#depends=()
options=(emptydirs)
source=("https://www.openssl.org/source/openssl-${_basever}.tar.gz"
	"openssl.patch"
	"openssl-fixes.patch"
	"no-sslv3-method.patch::https://github.com/openssl/openssl/commit/98fe34c30fe1fb7be3023e4c1458da73f8e55da9.patch")

prepare() {
	cd "${srcdir}/openssl-${_basever}"

	patch -Np1 -i "${srcdir}/openssl.patch"
	patch -Np1 -i "${srcdir}/openssl-fixes.patch"
	patch -Np1 -i "${srcdir}/no-sslv3-method.patch"

	cp -a "${srcdir}"/openssl{,32}-${_basever}
}

build() {
	cd "${srcdir}/openssl-${_basever}"

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib \
		shared \
		no-ssl3-method \
		enable-ec_nistp_64_gcc_128 \
		linux-x86_64 \
		"-Wa,--noexecstack ${CFLAGS}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	cd "${srcdir}/openssl32-${_basever}"

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib32 \
		shared \
		no-ssl3-method \
		linux-elf \
		"-Wa,--noexecstack ${CFLAGS}"

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/openssl-${_basever}"
	make -C "${srcdir}/openssl32-${_basever}"

}

package_openssl() {
	pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"

	cd "${srcdir}/openssl-${_basever}"

	make install INSTALL_PREFIX="${pkgdir}" MANDIR=/usr/share/man MANSUFFIX=ssl
}

package_lib32-openssl() {
	pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (32 bit)"

	cd "${srcdir}/openssl32-${_basever}"

	make install INSTALL_PREFIX="${PWD}/dest" MANDIR=/usr/share/man MANSUFFIX=ssl

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('7619aa223ee50d0f5e270ac9090e95b2b1ba5dfc656c98f625a9a277dda472fb960a4e89a7ba300044cb401b2072b2ca6a6fcce8206d927bf373d1c981806a93'
            'be7e6b1b449c2b88e83f7c69b176b7ef250bb1a982c856ba9b7c16fcf05f0503e16351f6413affab536dcdb82c80c94120750be2b073429910f5fbf5375341c1'
            '46353d4c617e5844cea202730d98200a1950d12d0537cd3e4b04b1d8ea4480e52af17b1ef8ade1fa5e263066b910db608a08022df137736c6a940c9734823f06'
            'f2a7e4edf63641c9dd6f7d463b47bba9f0260207a5d230aae1d77f93edc0446ce20c8fd168a3b9db2854a96e902324d6f19d0740c3b3aa626efbb37578b6a1ee')

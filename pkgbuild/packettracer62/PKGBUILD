pkgname=packettracer62
pkgver=6.2
pkgrel=1
pkgdesc="Network design and emulation software for Cisco's Networking Academy instructors and students (version 6.2)"
arch=('x86_64')
url="http://www.netacad.com/about-networking-academy/packet-tracer"
license=('custom')
#depends=()
options=(!strip)
source=("Cisco Packet Tracer 6.2 for Linux - Ubuntu installation - Student version.tar.gz"
	"linguist"
	"packettracer"
	"packettracer.sh")

package() {
	cd "${srcdir}/PacketTracer${pkgver/./}Student"

	local _dest="${pkgdir}/usr/share/packettracer"

	install -v -dm755 "${pkgdir}/usr/bin"
	install -v -dm755 "${pkgdir}/usr/lib32/packettracer"
	install -v -dm755 "${pkgdir}/usr/share/applications"
	install -v -dm755 "${pkgdir}/usr/share/mime/packages"

	install -v -dm755 "${_dest}"

	cp -r lib/* "${pkgdir}/usr/lib32/packettracer"

	for d in art backgrounds bin extensions help LANGUAGES saves sounds templates
	do
		install -v -dm755 "${_dest}/${d}"
		cp -r ${d}/* "${_dest}/${d}"
	done

	for d in pka pkt pkz
	do
		mv "${_dest}/bin/Cisco-${d}.xml" "${pkgdir}/usr/share/mime/packages/Cisco-${d}.xml"
		install -v -Dm644 art/${d}.png "${pkgdir}/usr/share/icons/hicolor/48x48/mimetypes/application-x-${d}.png"
	done

	mv "${_dest}/bin/Cisco-PacketTracer.desktop" "${pkgdir}/usr/share/applications/Cisco-PacketTracer.desktop"
	sed -i "s#/usr/local/PacketTracer6#/usr/share/packettracer#g" \
		"${pkgdir}/usr/share/applications/Cisco-PacketTracer.desktop"

	install -v -Dm755 "${srcdir}/linguist" "${_dest}/linguist"
	install -v -Dm755 "${srcdir}/packettracer" "${_dest}/packettracer"

	ln -sfv ../share/packettracer/packettracer "${pkgdir}/usr/bin/packettracer"

	install -v -Dm644 "${srcdir}/packettracer.sh" "${pkgdir}/etc/profile.d/packettracer.sh"
}

sha512sums=('231ef1cacdb5c77c78fa517ec4c3767cab64cca3d7f519b3c09432a327ed226ba0b9bdda9b5dc9836f87b4529ead6be976a48335860a9d15232fa0a33fa66be8'
            '58125fcb03cecd111f3789bd7e3cb4c05681199feb5ec6db91041c3826804eec137cf68fe54835f56e0f05c6aba878aa63e6e2b9a2dc889b41a14353d981ad80'
            '1d7f22df5ee78571b29e42f86fbae6f517501abe4fa911482b5c8c16f7befdba877de41099d112698d50e2b30fb513c51a8737f77e1dd4bbe216de0cb130bf8b'
            '3f4732213a9ca7c95f742edbdccf4d84c95e1c9e00d3dfa72e79b8039ef86bed29bc5b76586402a233ce3af409c0a56c759c2554e17962c292a6bd333654ce71')

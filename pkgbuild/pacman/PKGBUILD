pkgname=pacman
pkgver=5.2.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://sources.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"makepkg.conf")

prepare() {
	cd "${srcdir}/pacman-${pkgver}"

	sed -i "s#CPPFLAGS CFLAGS CXXFLAGS LDFLAGS#CC CXX &#" scripts/libmakepkg/buildenv.sh.in
}

build() {
	cd "${srcdir}/pacman-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var

	make
}

package() {
	cd "${srcdir}/pacman-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/bash_completion.d"

	install -v -Dm644 "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
}

sha512sums=('1f9c569fb9cfe90afeeb7e3715bfa821ec4c57fdbbd7e09cd1e2519fad1a555b2f5378dedb2c2e551d2e92db92f1db9684969b472507f676c5bb932cdf436eda'
            'dc216ce87578817a437e37966416bf2a2fd68bfe2fa8e58a9abb19ebaed4dc56f98e6874bcf9edb571725987b057b9059c6dbd26f7bda2cbcdba3971616df63e')

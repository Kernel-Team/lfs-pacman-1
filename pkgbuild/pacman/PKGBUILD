pkgname=pacman
pkgver=5.1.1
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://sources.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"makepkg.conf")

prepare() {
	cd "${srcdir}/pacman-${pkgver}"

	sed -i "s#CPPFLAGS CFLAGS CXXFLAGS LDFLAGS#CC CXX &#" scripts/makepkg.sh.in
	sed -i "/C*LAGS+=.*DEBUG/s#FLAGS+=#FLAGS=#g" scripts/makepkg.sh.in
}

build() {
	cd "${srcdir}/pacman-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var

	make
}

package() {
	cd "${srcdir}/pacman-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/bash_completion.d"

	install -v -Dm644 "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
}

sha512sums=('7112025dbd3e263c16f5b0ab34c9db3e8d631a0801bb086b47a2252d1764172261be6a14df1f24598c62935dcdfd74c89fa9d116deea41efb9e0f53dcadeb61c'
            '0bcdcad4887c0b0d02b71e5a9086cbf4dfeda9b391466ed303de9308934c1246468286214855e698366c89358310591f0cac6313786774f5cafe86e26fd5ade4')

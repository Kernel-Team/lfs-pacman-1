pkgname=pacman
pkgver=5.2.1
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://sources.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"makepkg.conf")

prepare() {
	cd "${srcdir}/pacman-${pkgver}"

	sed -i "s#CPPFLAGS CFLAGS CXXFLAGS LDFLAGS#CC CXX &#" scripts/libmakepkg/buildenv.sh.in
}

build() {
	cd "${srcdir}/pacman-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var

	make
}

package() {
	cd "${srcdir}/pacman-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/bash_completion.d"

	install -v -Dm644 "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
}

sha512sums=('7814420b8d71090313f1bc7d9a513138bda74c5cbcb43d9741acc27ea7b4d180a38848a7d7059969cc7afed1b108d26ba2622a16d8761566357c50a5da456981'
            '83f321e3edf42ab992aff133384317e442072b748001daee8f9de8053cd0fc00897a2ba315e40c65b2a6362f76b6a42e25a9fb077ae0da747cb7410c71581d6b')

pkgname=pacman
pkgver=5.1.3
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://sources.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"makepkg.conf")

prepare() {
	cd "${srcdir}/pacman-${pkgver}"

	sed -i "s#CPPFLAGS CFLAGS CXXFLAGS LDFLAGS#CC CXX &#" scripts/makepkg.sh.in
	sed -i "/C*LAGS+=.*DEBUG/s#FLAGS+=#FLAGS=#g" scripts/makepkg.sh.in
}

build() {
	cd "${srcdir}/pacman-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var

	make
}

package() {
	cd "${srcdir}/pacman-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/bash_completion.d"

	install -v -Dm644 "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
}

sha512sums=('b556b6d01d651a675ce1e153ede776e682ca0eb671cd2be00d7b7c602504dd119291f38ef5c318e675b4ce865db7e1c57e0ebc157510f0fc72e656feb8947e54'
            'e2a85b2059c705620139414f6c337bacdcab2592ce971d93f7363209bce9c99156803b9be76af28ab063cf6a009ee245b609201e1bfc99d88c6bdcab0b984a84')

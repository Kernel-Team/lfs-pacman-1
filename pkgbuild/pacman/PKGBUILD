pkgname=pacman
pkgver=5.0.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://sources.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"makepkg.conf")

prepare() {
	cd "${srcdir}/pacman-${pkgver}"

	sed -i "s#CPPFLAGS CFLAGS CXXFLAGS LDFLAGS#CC CXX &#" scripts/makepkg.sh.in
	sed -i "/C*LAGS+=.*DEBUG/s#FLAGS+=#FLAGS=#g" scripts/makepkg.sh.in
}

build() {
	cd "${srcdir}/pacman-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var

	make
	make -C contrib
}

package() {
	cd "${srcdir}/pacman-${pkgver}"

	make install DESTDIR="${pkgdir}"
	make -C contrib install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/bash_completion.d"

	install -v -Dm644 "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
}

sha512sums=('94a8cce1a52d2365a993c72f16537f4dbea6100feb8f22e8782cc7d2c1ef8a525a63f3c40bb183294c0faedcc743e3d806d2fc3c50a21ab9b03df2910039d628'
            '2cf9b2ec039d31dd137c2e861852a0590f73f26ce6ad32c458bdf39a2803f6281be58813c387c3b94649cc20adab94addf35d57a9eb0b32cbef51a07a2434524')

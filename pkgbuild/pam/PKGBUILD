pkgname=pam
pkgver=1.3.0
pkgrel=1
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
url="http://linux-pam.org"
license=('GPL2')
#depends=()
options=(emptydirs)
source=("http://linux-pam.org/library/Linux-PAM-${pkgver}.tar.bz2"
	"other"
	"pam.conf"
	"system-account"
	"system-auth"
	"system-password"
	"system-session")

build() {
	cd "${srcdir}/Linux-PAM-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--enable-securedir=/lib/security \
		--disable-regenerate-docu

	make
}

package() {
	cd "${srcdir}/Linux-PAM-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -m644 "${srcdir}/pam.conf" "${pkgdir}/etc"
	install -v -dm755 "${pkgdir}/etc/pam.d"

	install -v -m644 "${srcdir}/other" "${pkgdir}/etc/pam.d"
	install -v -m644 "${srcdir}/system-account" "${pkgdir}/etc/pam.d"
	install -v -m644 "${srcdir}/system-auth" "${pkgdir}/etc/pam.d"
	install -v -m644 "${srcdir}/system-password" "${pkgdir}/etc/pam.d"
	install -v -m644 "${srcdir}/system-session" "${pkgdir}/etc/pam.d"

	chmod 4755 "${pkgdir}/sbin/unix_chkpwd"

	for file in pam pam_misc pamc
	do
		mv -v "${pkgdir}"/usr/lib/lib${file}.so.* "${pkgdir}/lib"
		ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/lib${file}.so") "${pkgdir}/usr/lib/lib${file}.so"
	done

	rm -rf "${pkgdir}/var"
}

md5sums=('da4b2289b7cfb19583d54e9eaaef1c3a'
         '579d2c6d9dd24c9721bc1021ee76b767'
         '442bc0bb2bad861471477a859d3ee8a1'
         'd881e93bd77ba6c847e5641163cbb7d2'
         'eeefa45a1182a0fc9830f882e948db50'
         '909aabdf44a76cafce62e1f103d5a380'
         '9cf9e3d951ff959ef42258615553c00c')

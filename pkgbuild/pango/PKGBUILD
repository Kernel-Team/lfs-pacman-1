pkgname=pango
pkgver=1.48.0
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('x86_64')
url="https://www.pango.org/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/pango/${pkgver:0:4}/pango-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/pango-${pkgver}"

	sed -i 's#from "@filename@"##' pango/pango-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/pango-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/libexec" "${pkgdir}/usr/share/installed-tests"
}

sha512sums=('4819575a583134083819c1548d86bba71af97fd927f7cac05e3903b6d1c84de0ab1b593eea1e17b974f194e2d81123aa46e3af942eef258ad1bd14c72322342e')

pkgname=pango
pkgver=1.44.7
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('x86_64')
url="https://www.pango.org/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/pango/${pkgver:0:4}/pango-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/pango-${pkgver}"

	sed -i 's#from "@filename@"##' pango/pango-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/pango-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/libexec" "${pkgdir}/usr/share/installed-tests"
}

sha512sums=('6e0a3d5cd32c209fd71a2ce5f705a60db318c2133bcca07360a047a73892f642a1661c1f0fb90353fc43e75336dfc22909bf4969f1bd03324c9689344af08350')

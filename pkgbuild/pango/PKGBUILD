pkgname=pango
pkgver=1.40.13
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('x86_64')
url="https://www.pango.org/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/pango/${pkgver:0:4}/pango-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/pango-${pkgver}"

	sed -i 's#from "@filename@"##' pango/pango-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/pango-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/libexec" "${pkgdir}/usr/share/installed-tests"
}

sha512sums=('0dfc782c0e602eceb2152981e282933a4af3cc0736d3086c103987949f55f5bdda551a37cb77223d082896981312f84ae3ac17c23276596ee28591b1509db48a')

pkgname=pango
pkgver=1.40.14
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('x86_64')
url="https://www.pango.org/"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/pango/${pkgver:0:4}/pango-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/pango-${pkgver}"

	sed -i 's#from "@filename@"##' pango/pango-enum-types.h.template
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Denable_docs=true \
		"${srcdir}/pango-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/libexec" "${pkgdir}/usr/share/installed-tests"
}

sha512sums=('35ba7bc8be3992f206ccc2cc5aca0b94e2a3832f887fc9c45b0e29fddcb9051ce05a74377de0ca4ff95a87983b15688fa5d379d592faf87aa8eaca25ac18b7ea')

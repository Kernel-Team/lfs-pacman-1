pkgbase=pcre
pkgname=('pcre' 'lib32-pcre')
pkgver=8.44
pkgrel=1
arch=('x86_64')
url="http://www.pcre.org/"
license=('BSD')
#depends=()
source=("https://ftp.pcre.org/pub/pcre/pcre-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/pcre{,32}-${pkgver}
}

build() {
	cd "${srcdir}/pcre-${pkgver}"

	./configure --prefix=/usr \
		--enable-unicode-properties \
		--enable-pcre16 \
		--enable-pcre32 \
		--enable-jit \
		--enable-pcregrep-libz \
		--enable-pcregrep-libbz2 \
		--enable-pcretest-libreadline

	cd "${srcdir}/pcre32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--enable-unicode-properties \
		--enable-pcre16 \
		--enable-pcre32 \
		--enable-jit

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/pcre-${pkgver}"
	make -C "${srcdir}/pcre32-${pkgver}"
}

package_pcre() {
	pkgdesc="A library that implements Perl 5-style regular expressions"

	cd "${srcdir}/pcre-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-pcre() {
	pkgdesc="A library that implements Perl 5-style regular expressions (32 bit)"

	cd "${srcdir}/pcre32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('f26d850aab5228799e58ac8c2306fb313889332c39e29b118ef1de57677c5c90f970d68d3f475cabc64f8b982a77f04eca990ff1057f3ccf5e19bd137997c4ac')

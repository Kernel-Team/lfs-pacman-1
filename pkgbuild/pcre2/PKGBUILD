pkgbase=pcre2
pkgname=('pcre2' 'lib32-pcre2')
pkgver=10.36
pkgrel=1
arch=('x86_64')
url="http://www.pcre.org/"
license=('BSD')
#depends=()
source=("https://ftp.pcre.org/pub/pcre/pcre2-${pkgver}.tar.bz2")

prepare() {
	cp -a "${srcdir}"/pcre2{,32}-${pkgver}
}

build() {
	cd "${srcdir}/pcre2-${pkgver}"

	./configure --prefix=/usr \
		--enable-pcre2-16 \
		--enable-pcre2-32 \
		--enable-jit \
		--enable-pcre2grep-libz \
		--enable-pcre2grep-libbz2 \
		--enable-pcre2test-libreadline

	cd "${srcdir}/pcre232-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--enable-pcre2-16 \
		--enable-pcre2-32 \
		--enable-jit

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/pcre2-${pkgver}"
	make -C "${srcdir}/pcre232-${pkgver}"

}

package_pcre2() {
	pkgdesc="A library that implements Perl 5-style regular expressions, version 2"

	cd "${srcdir}/pcre2-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-pcre2() {
	pkgdesc="A library that implements Perl 5-style regular expressions, version 2 (32 bit)"

	cd "${srcdir}/pcre232-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('fc2a920562c80c3d31cedd94028fab55314ae0fb168cac7178f286c344a11fc514939edc3b83b8e0b57c872db4e595fd5530fd1d4b8c779be629553e9ec965a3')

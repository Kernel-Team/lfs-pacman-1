pkgname=perl
_perlmajor=5.28
_perlminor=1
pkgver=${_perlmajor}.${_perlminor}
pkgrel=1
pkgdesc="A highly capable, feature-rich programming language"
arch=('x86_64')
url="http://www.perl.org"
license=('GPL' 'PerlArtistic')
#depends=()
options=(emptydirs)
source=("http://www.cpan.org/src/5.0/perl-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/perl-${pkgver}"

	export BUILD_ZLIB=False
	export ZLIB_INCLUDE=/usr/include
	export ZLIB_LIB=/usr/lib

	export BUILD_BZIP2=0
	export BZIP2_INCLUDE=/usr/include
	export BZIP2_LIB=/usr/lib

	export _perlmajor

	sh Configure -Dprefix=/usr   \
		-Dusethreads    \
		-Duselargefiles \
		-Dprivlib=/usr/share/perl/${_perlmajor} \
		-Darchlib=/usr/lib/perl/${_perlmajor}   \
		-Dvendorprefix=/usr          \
		-Dvendorlib=/usr/share/perl5 \
		-Dvendorarch=/usr/lib/perl5  \
		-Dsiteprefix=/usr/local      \
		-Dsitelib=/usr/local/share/perl/${pkgver} \
		-Dsitearch=/usr/local/lib/perl/${pkgver}  \
		-Dman1dir=/usr/share/man/man1  \
		-Dman3dir=/usr/share/man/man3  \
		-Dpager=/usr/bin/pager         \
		-Duseshrplib                   \
		-Dlibperl=libperl.so.${pkgver} \
		-des -Uafs -Ud_csh -Ud_ualarm  \
		-Uusesfio -Uusenm -Ui_libutil  \
		-Dcccdlflags="-fPIC" \
		-Dcc="${CC}" \
		-Doptimize="${CFLAGS}" \
		-A ccflags="-O2"

	make libperl.so.${pkgver} SHRPLDFLAGS='$(LDDLFLAGS) -Wl,-soname,libperl.so.${_perlmajor}'

	ln -sfv libperl.so.${pkgver} libperl.so.${_perlmajor}
	ln -sfv libperl.so.${_perlmajor} libperl.so

	make all
}

package() {
	cd "${srcdir}/perl-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/bin/perl${pkgver}"
	rm -rf "${pkgdir}/usr/lib/perl/${_perlmajor}/.packlist"
	rm -rf "${pkgdir}/usr/lib/perl/${_perlmajor}/CORE/libperl.so"
	rm -rf "${pkgdir}/usr/lib/perl/${_perlmajor}/CORE/libperl.so.${_perlmajor}"

	mv -v "${pkgdir}/usr/lib/perl/${_perlmajor}/CORE/libperl.so.${pkgver}" "${pkgdir}/usr/lib"
	ln -sfv libperl.so.${pkgver} "${pkgdir}/usr/lib/libperl.so.${_perlmajor}"
	ln -sfv libperl.so.${_perlmajor} "${pkgdir}/usr/lib/libperl.so"

	chmod 755 "${pkgdir}/usr/lib/libperl.so.${pkgver}"

	ln -sfv perl "${pkgdir}/usr/bin/perl${pkgver}"

	sed -e '/(makepl_arg =>/   s/""/"INSTALLDIRS=vendor"/' \
		-e '/(mbuildpl_arg =>/ s/""/"installdirs=vendor"/' \
		-i "${pkgdir}/usr/share/perl/${_perlmajor}/CPAN/FirstTime.pm"

	mv -v "${pkgdir}/usr/lib/perl/${_perlmajor}" "${pkgdir}/usr/lib/perl/${pkgver}"
	mv -v "${pkgdir}/usr/share/perl/${_perlmajor}" "${pkgdir}/usr/share/perl/${pkgver}"

	ln -sfv ${pkgver} "${pkgdir}/usr/lib/perl/${_perlmajor}"
	ln -sfv ${pkgver} "${pkgdir}/usr/share/perl/${_perlmajor}"
}

sha512sums=('6d18e9684c3a15bea2ccd28f116d1829c3acd5547551ee3539f0060c0d1a75246dfe570dfb9d5f00625a994a0afb0cbd6a5a5f9a407fef75a421e7dbc6491b43')

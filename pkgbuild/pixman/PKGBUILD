pkgbase=pixman
pkgname=('pixman' 'lib32-pixman')
pkgver=0.34.0
pkgrel=1
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
#depends=()
source=("https://www.cairographics.org/releases/pixman-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}"/pixman{,32}-${pkgver}
}

build() {
	cd "${srcdir}/pixman-${pkgver}"

	./configure --prefix=/usr --disable-gtk --disable-libpng

	cd "${srcdir}/pixman32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--disable-gtk \
		--disable-libpng

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/pixman-${pkgver}"
	make -C "${srcdir}/pixman32-${pkgver}"
}

package_pixman() {
	pkgdesc="The pixel-manipulation library for X and cairo"

	cd "${srcdir}/pixman-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-pixman() {
	pkgdesc="The pixel-manipulation library for X and cairo (32 bit)"

	cd "${srcdir}/pixman32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('e80ebae4da01e77f68744319f01d52a3')

pkgname=polkit
_commit=766a2ea
pkgver=0.113+${_commit}
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/polkit/"
license=('LGPL')
#depends=()
options=(emptydirs)
source=("git+https://anongit.freedesktop.org/git/polkit.git#commit=${_commit}"
	"polkit-mozjs38.patch"
	"polkit-1.pam"
	"polkit.sysusers")

prepare() {
	cd "${srcdir}/polkit"

	patch -Np1 -i "${srcdir}/polkit-mozjs38.patch"

	NOCONFIGURE=1 ./autogen.sh

	sed -i "s:/sys/fs/cgroup/systemd/:/bin:g" configure
}

build() {
	cd "${srcdir}/polkit"

	export CXX="${CXX} -std=c++11"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-gtk-doc \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/polkit"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/polkit-1.pam" "${pkgdir}/etc/pam.d/polkit-1"
	install -v -Dm644 "${srcdir}/polkit.sysusers" "${pkgdir}/usr/lib/sysusers.d/polkit.conf"

	chmod 700 "${pkgdir}/etc/polkit-1/rules.d" "${pkgdir}/usr/share/polkit-1/rules.d"
	chown 27:27 "${pkgdir}/etc/polkit-1/rules.d" "${pkgdir}/usr/share/polkit-1/rules.d"
}

sha512sums=('SKIP'
            '319c57d4deaec45913d1e543350eed95433f062657e3b6607ae10eb356b770567db48a653a9dbecbadbb7365b6cf8988e3c2b3a4393e86838f6e8791ff57c132'
            '3f6dbf7119083ce09cbfc543e93c1007e91d5706d0cfefdcecd89d788cb3827d0764c11205fa562ebc936bacda60bd645c7d40f85c42d334faab9f21f325beac'
            '327822bfd25ca317e75626b206cec5520482ddbbba86a4e933b71a1fed2052ff1ba0d9e50a498a50f64d626dccd8a3573b9572237bb919c3f7fa4c63b52ef467')

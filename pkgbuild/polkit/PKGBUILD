pkgname=polkit
_commit=766a2ea
pkgver=0.113+${_commit}
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/polkit/"
license=(LGPL)
#depends=()
options=(emptydirs)
source=("git+https://anongit.freedesktop.org/git/polkit.git#commit=${_commit}"
	"polkit-mozjs38.patch"
	"polkit-1.pam"
	"polkit.sysusers")

prepare() {
	cd "${srcdir}/polkit"

	patch -Np1 -i "${srcdir}/polkit-mozjs38.patch"

	NOCONFIGURE=1 ./autogen.sh

	sed -i "s:/sys/fs/cgroup/systemd/:/bin:g" configure
}

build() {
	cd "${srcdir}/polkit"

	export CXX="${CXX} -std=c++11"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-gtk-doc \
		--with-systemdsystemunitdir=/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/polkit"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/polkit-1.pam" "${pkgdir}/etc/pam.d/polkit-1"
	install -v -Dm644 "${srcdir}/polkit.sysusers" "${pkgdir}/usr/lib/sysusers.d/polkit.conf"

	chmod 700 "${pkgdir}/etc/polkit-1/rules.d" "${pkgdir}/usr/share/polkit-1/rules.d"
	chown 27:27 "${pkgdir}/etc/polkit-1/rules.d" "${pkgdir}/usr/share/polkit-1/rules.d"
}

md5sums=('SKIP'
         '5034798259834cbe838f919bf93ea9aa'
         '4ce755378335620cc004fd5802a46907'
         '6006bfdf134f1f8c97762155584de238')

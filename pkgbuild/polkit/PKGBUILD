pkgname=polkit
pkgver=0.113
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/polkit/"
license=(LGPL)
#depends=()
options=(emptydirs)
source=("https://www.freedesktop.org/software/polkit/releases/polkit-${pkgver}.tar.gz"
	"polkit-mozjs24.patch"
	"polkit-1.pam"
	"polkit.sysusers")

prepare() {
	cd "${srcdir}/polkit-${pkgver}"

	patch -Np1 -i "${srcdir}/polkit-mozjs24.patch"

	sed -i "s:/sys/fs/cgroup/systemd/:/bin:g" configure
}

build() {
	cd "${srcdir}/polkit-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-systemdsystemunitdir=/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/polkit-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/polkit-1.pam" "${pkgdir}/etc/pam.d/polkit-1"
	install -v -Dm644 "${srcdir}/polkit.sysusers" "${pkgdir}/usr/lib/sysusers.d/polkit.conf"

	chmod 700 "${pkgdir}/etc/polkit-1/rules.d" "${pkgdir}/usr/share/polkit-1/rules.d"
	chown 27:27 "${pkgdir}/etc/polkit-1/rules.d" "${pkgdir}/usr/share/polkit-1/rules.d"
}

md5sums=('4b77776c9e4f897dcfe03b2c34198edf'
         'fb0b81782309ece1ad705dc88c13a20f'
         '4ce755378335620cc004fd5802a46907'
         '6006bfdf134f1f8c97762155584de238')

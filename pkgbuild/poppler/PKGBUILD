pkgname=poppler
pkgver=0.60.1
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0"
arch=('x86_64')
url="https://poppler.freedesktop.org/"
license=('GPL')
#depends=()
source=("https://poppler.freedesktop.org/poppler-${pkgver}.tar.xz")

prepare() {
        install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	export CXX="${CXX} -std=c++11"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DENABLE_GTK_DOC=ON \
		-DENABLE_XPDF_HEADERS=ON \
		"${srcdir}/poppler-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/bin/poppler-glib-demo"
}

sha512sums=('db73524b7413dddee38d5a049f3926338ee123b8fc4893d80ee9cf5aaae47c8c7b75b112293b156cd375894e548ec68e5fbf4af305e77b43bc3868ad1c966cdc')

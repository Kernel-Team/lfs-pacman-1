pkgname=poppler
pkgver=0.75.0
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0"
arch=('x86_64')
url="https://poppler.freedesktop.org/"
license=('GPL')
#depends=()
source=("https://poppler.freedesktop.org/poppler-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DENABLE_GTK_DOC=ON \
		-DENABLE_UNSTABLE_API_ABI_HEADERS=ON \
		-DBUILD_CPP_TESTS=OFF \
		-DBUILD_GTK_TESTS=OFF \
		-DBUILD_QT5_TESTS=OFF \
		"${srcdir}/poppler-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}/usr/bin/poppler-glib-demo"
}

sha512sums=('6a1b76cc5095b7c5809eaa5072aeb4ea615744ff8ffce537f40a3f73a81fc12cc18d596876ceda1c78b5312ad20535f72366e69c861b620d108616f24a98e6b8')

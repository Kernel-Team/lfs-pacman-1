pkgname=ppp
pkgver=2.4.7
pkgrel=1
pkgdesc="A daemon which implements the Point-to-Point Protocol for dial-up networking"
arch=('x86_64')
url="https://ppp.samba.org/"
license=('custom:GPL/BSD')
#depends=()
options=(emptydirs)
source=("https://www.samba.org/ftp/ppp/ppp-${pkgver}.tar.gz"
	"ppp-distro.patch")

prepare() {
	cd "${srcdir}/ppp-${pkgver}"

	patch -Np1 -i "${srcdir}/ppp-distro.patch"

	sed -i "s:^#FILTER=y:FILTER=y:" pppd/Makefile.linux
	sed -i "s:^#HAVE_INET6=y:HAVE_INET6=y:" pppd/Makefile.linux
	sed -i "s:^#CBCP=y:CBCP=y:" pppd/Makefile.linux

	sed -i "/ppp_defs.h/a #define _LINUX_IN6_H" pppd/plugins/rp-pppoe/plugin.c
	sed -i "/ppp_defs.h/a #define _LINUX_IN_H" pppd/plugins/rp-pppoe/plugin.c
}

build() {
	cd "${srcdir}/ppp-${pkgver}"

	CFLAGS="${CFLAGS} -D_GNU_SOURCE" ./configure --prefix=/usr

	make
}

package() {
	cd "${srcdir}/ppp-${pkgver}"

	make install INSTROOT="${pkgdir}"

	install -v -dm755 "${pkgdir}/etc/ppp/ipv6-down.d" "${pkgdir}/etc/ppp/peers"

	install -v -Dm755 scripts/pon   "${pkgdir}/usr/bin/pon"
	install -v -Dm755 scripts/poff  "${pkgdir}/usr/bin/poff"
	install -v -Dm755 scripts/plog  "${pkgdir}/usr/sbin/plog"
	install -v -Dm644 scripts/pon.1 "${pkgdir}/usr/share/man/man1/pon.1"

	install -v -m600 etc.ppp/pap-secrets  "${pkgdir}/etc/ppp/pap-secrets"
	install -v -m600 etc.ppp/chap-secrets "${pkgdir}/etc/ppp/chap-secrets"

	chmod 644 "${pkgdir}"/usr/share/man/man8/*.8

	install -v -Dm755 distro/ip-up   "${pkgdir}/etc/ppp/ip-up"
	install -v -Dm755 distro/ip-down "${pkgdir}/etc/ppp/ip-down"
	install -v -Dm755 distro/ip-up.d.dns.sh   "${pkgdir}/etc/ppp/ip-up.d/00-dns.sh"
	install -v -Dm755 distro/ip-down.d.dns.sh "${pkgdir}/etc/ppp/ip-down.d/00-dns.sh"
	install -v -Dm755 distro/ipv6-up   "${pkgdir}/etc/ppp/ipv6-up"
	install -v -Dm755 distro/ipv6-down "${pkgdir}/etc/ppp/ipv6-down"

	install -v -Dm755 distro/ipv6-up.d.iface-config.sh "${pkgdir}/etc/ppp/ipv6-up.d/00-iface-config.sh"

	install -v -Dm644 distro/options     "${pkgdir}/etc/ppp/options"
	install -v -Dm644 distro/ppp.systemd "${pkgdir}/lib/systemd/system/ppp@.service"
}

sha512sums=('e34ce24020af6a73e7a26c83c4f73a9c83fa455b7b363794dba27bf01f70368be06bff779777843949bd77f4bc9385d6ad455ea48bf8fff4e0d73cc8fef16ae2'
            'e690fcdc54a7883efe1aa5bb8189bc26fdced75e5b02bb9eae73873105e888b36ea023411ca12ba2d9445c364e9fa3c8c172ce517c815ee73de7f6d3d73e954e')

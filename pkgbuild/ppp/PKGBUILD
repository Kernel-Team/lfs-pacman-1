pkgname=ppp
pkgver=2.4.8
pkgrel=1
pkgdesc="A daemon which implements the Point-to-Point Protocol for dial-up networking"
arch=('x86_64')
url="https://ppp.samba.org/"
license=('custom:GPL/BSD')
#depends=()
options=(emptydirs)
source=("https://www.samba.org/ftp/ppp/ppp-${pkgver}.tar.gz"
	"ppp-upstream-fixes.patch"
	"ppp-distro.patch")

prepare() {
	cd "${srcdir}/ppp-${pkgver}"

	patch -Np1 -i "${srcdir}/ppp-upstream-fixes.patch"
	patch -Np1 -i "${srcdir}/ppp-distro.patch"

	sed -i "s:^#SYSTEMD=y:SYSTEMD=y:" pppd/Makefile.linux
	sed -i "s:^#USE_PAM=y:USE_PAM=y:" pppd/Makefile.linux
	sed -i "s:^#CBCP=y:CBCP=y:" pppd/Makefile.linux
}

build() {
	cd "${srcdir}/ppp-${pkgver}"

	CFLAGS="${CFLAGS} -D_GNU_SOURCE" ./configure --prefix=/usr

	make
}

package() {
	cd "${srcdir}/ppp-${pkgver}"

	make install DESTDIR="${pkgdir}/usr"

	install -v -dm755 "${pkgdir}/etc/ppp/ipv6-down.d" "${pkgdir}/etc/ppp/peers"

	install -v -Dm755 scripts/pon   "${pkgdir}/usr/bin/pon"
	install -v -Dm755 scripts/poff  "${pkgdir}/usr/bin/poff"
	install -v -Dm755 scripts/plog  "${pkgdir}/usr/sbin/plog"
	install -v -Dm644 scripts/pon.1 "${pkgdir}/usr/share/man/man1/pon.1"

	install -v -m600 etc.ppp/pap-secrets  "${pkgdir}/etc/ppp/pap-secrets"
	install -v -m600 etc.ppp/chap-secrets "${pkgdir}/etc/ppp/chap-secrets"

	chmod 644 "${pkgdir}"/usr/share/man/man8/*.8

	install -v -Dm755 distro/ip-up   "${pkgdir}/etc/ppp/ip-up"
	install -v -Dm755 distro/ip-down "${pkgdir}/etc/ppp/ip-down"
	install -v -Dm755 distro/ip-up.d.dns.sh   "${pkgdir}/etc/ppp/ip-up.d/00-dns.sh"
	install -v -Dm755 distro/ip-down.d.dns.sh "${pkgdir}/etc/ppp/ip-down.d/00-dns.sh"
	install -v -Dm755 distro/ipv6-up   "${pkgdir}/etc/ppp/ipv6-up"
	install -v -Dm755 distro/ipv6-down "${pkgdir}/etc/ppp/ipv6-down"

	install -v -Dm755 distro/ipv6-up.d.iface-config.sh "${pkgdir}/etc/ppp/ipv6-up.d/00-iface-config.sh"

	install -v -Dm644 distro/options     "${pkgdir}/etc/ppp/options"
	install -v -Dm644 distro/ppp.systemd "${pkgdir}/usr/lib/systemd/system/ppp@.service"
}

sha512sums=('580a5856333e1149a66cfb218f7fb5e698f0f0c11d492d5c87b760827e76b5ef1e2e461c95d1abb368b9c92499c366e7ddb31dd3029d5a9026c3ab10b50bdbb6'
            'fa1610063ca8d87f064cfbac74f4b311008b0c1644386323376fe82ec06473e669ef6b165c31d349a7aa02d7fb67a3f4a7103852b584f4ca7b91667a4eb17549'
            '21ac3fa8f8904a558f7747e7a5c57be29fffff43018e1ae0dfc48c4df10ad84ebf40b8d5487dbc0650a05b1df8ba3a8777a66c84127ffbfe1d8a463fc5990788')

pkgname=procps-ng
pkgver=3.3.12
pkgrel=1
pkgdesc="Utilities for monitoring your system and its processes"
arch=('x86_64')
url="https://gitlab.com/procps-ng/procps"
license=('GPL' 'LGPL')
#depends=()
source=("https://downloads.sourceforge.net/procps-ng/procps-ng-${pkgver}.tar.xz"
	"99-procps.conf")

build() {
	cd "${srcdir}/procps-ng-${pkgver}"

	sed -i "s:ncursesw/::g" watch.c

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-watch8bit

	make
}

package() {
	cd "${srcdir}/procps-ng-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/usr/lib/sysctl.d"

	install -v -m644 "${srcdir}/99-procps.conf" "${pkgdir}/usr/lib/sysctl.d"
}

sha512sums=('ea816740ff72cc53d99b5d2e78a511b627ea5b295e0efff67605c56d1ab075abc6b0e2da3df4facbfe903fc9d679e892d351b6884a966d37c6f4bfd9141a924b'
            'c9ff7b04d1bf5a4c8c474d62f17a99e80f000dd9ab88c5ee31e5cc944eb5626158858720c3e4c414c5cd5d687142ed9f4e741d3ef3450e8e50751371253bead0')

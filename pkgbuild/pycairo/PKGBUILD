pkgbase=python-pycairo
pkgname=('python2-cairo' 'python3-cairo')
pkgver=1.14.0
pkgrel=1
pkgdesc="Python bindings for the Cairo graphics library"
arch=('x86_64')
url="https://pycairo.readthedocs.io"
license=('LGPL2.1' 'MPL')
#depends=()
source=("https://github.com/pygobject/pycairo/releases/download/v${pkgver}/pycairo-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/pycairo-${pkgver}" "${srcdir}/pycairo-${pkgver}-py2"
}

build() {
	cd "${srcdir}/pycairo-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/pycairo-${pkgver}-py2"

	python2 setup.py build
}

package_python3-cairo() {
	cd "${srcdir}/pycairo-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-cairo() {
	cd "${srcdir}/pycairo-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('5727979acbbc62222c34802098db3e91e7d6f610c95b95c100370fc06703a06a6731034ede99dd5001a45efd0db8472e2e12b5739e3954d5501ff34a678ab341')

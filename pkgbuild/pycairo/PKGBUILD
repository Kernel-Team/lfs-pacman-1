pkgbase=python-pycairo
pkgname=('python2-cairo' 'python3-cairo')
pkgver=1.15.6
pkgrel=1
pkgdesc="Python bindings for the Cairo graphics library"
arch=('x86_64')
url="https://pycairo.readthedocs.io"
license=('LGPL2.1' 'MPL')
#depends=()
source=("https://github.com/pygobject/pycairo/releases/download/v${pkgver}/pycairo-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/pycairo-${pkgver}" "${srcdir}/pycairo-${pkgver}-py2"
}

build() {
	cd "${srcdir}/pycairo-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/pycairo-${pkgver}-py2"

	python2 setup.py build
}

package_python3-cairo() {
	cd "${srcdir}/pycairo-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-cairo() {
	cd "${srcdir}/pycairo-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('f1d9268e75f7ac0d72a134462866055c05e14e897a3ff779dca7c9bd99cc4cc448fa2e530da875ce6a1a11ba5c527a42214b9bd7c30cbb035e450fb39412a73c')

pkgbase=pygobject
pkgname=('pygobject-common' 'python2-gobject' 'python3-gobject')
pkgver=3.32.0
pkgrel=1
arch=('x86_64')
url="https://wiki.gnome.org/Projects/PyGObject"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/pygobject/${pkgver:0:4}/pygobject-${pkgver}.tar.xz")

build() {
	install -v -dm755 "${srcdir}/build-python2"

	cd "${srcdir}/build-python2"
	
	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dpython=python2 \
		"${srcdir}/pygobject-${pkgver}"

	install -v -dm755 "${srcdir}/build-python3"

	cd "${srcdir}/build-python3"
	
		meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dpython=python3 \
		"${srcdir}/pygobject-${pkgver}"

	ninja ${MAKEFLAGS} -C "${srcdir}/build-python2"
	ninja ${MAKEFLAGS} -C "${srcdir}/build-python3"
}

package_pygobject-common() {
	pkgdesc="Python Bindings for GLib/GObject/GIO/GTK+ (common files)"

	DESTDIR="${pkgdir}" ninja -C "${srcdir}/build-python3" install 

	rm -rf "${pkgdir}"/usr/lib/python*
}

package_python2-gobject() {
	pkgdesc="Python 2 Bindings for GLib/GObject/GIO/GTK+"

	DESTDIR="${pkgdir}" ninja -C "${srcdir}/build-python2" install

	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"
}

package_python3-gobject() {
	pkgdesc="Python 3 Bindings for GLib/GObject/GIO/GTK+"

	DESTDIR="${pkgdir}" ninja -C "${srcdir}/build-python3" install

	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"
}

sha512sums=('beb0625e3257cdc2be251624dbf852d2f51bcb95d13bff676b93d46a80d9df94f6fb6ef9850e5c38c56325b72854165fba9ce20ab78f13d99552b3b10ff0fa81')

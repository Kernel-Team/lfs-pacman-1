pkgbase=pygobject
pkgname=('pygobject-common' 'python2-gobject' 'python3-gobject')
pkgver=3.28.1
pkgrel=1
arch=('x86_64')
url="https://wiki.gnome.org/Projects/PyGObject"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/pygobject/${pkgver:0:4}/pygobject-${pkgver}.tar.xz")

build() {
	install -v -dm755 "${srcdir}/build-python2"

	cd "${srcdir}/build-python2"

	"${srcdir}"/pygobject-${pkgver}/configure --prefix=/usr --with-python=/usr/bin/python2

	install -v -dm755 "${srcdir}/build-python3"

	cd "${srcdir}/build-python3"

	"${srcdir}"/pygobject-${pkgver}/configure --prefix=/usr --with-python=/usr/bin/python3

	make -C "${srcdir}/build-python2"
	make -C "${srcdir}/build-python3"
}

package_pygobject-common() {
	pkgdesc="Python Bindings for GLib/GObject/GIO/GTK+ (common files)"

	make -C "${srcdir}/build-python2" install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/python*
}

package_python2-gobject() {
	pkgdesc="Python 2 Bindings for GLib/GObject/GIO/GTK+"

	make -C "${srcdir}/build-python2" install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"
}

package_python3-gobject() {
	pkgdesc="Python 3 Bindings for GLib/GObject/GIO/GTK+"

	make -C "${srcdir}/build-python3" install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"
}

sha512sums=('48a21ecf92a8a2302413df048771dd659dd858507dcc0157d4664fa3afcbea13dc6a645369fa76fe0df69ec0897e5a80bf29c3fa1d4425303653879b7ea021ff')

pkgbase=python-babel
pkgname=('python2-babel' 'python3-babel')
pkgver=2.4.0
pkgrel=1
pkgdesc="Internationalization utilities"
arch=('x86_64')
url="https://pypi.python.org/pypi/Babel/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/B/Babel/Babel-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Babel-${pkgver}" "${srcdir}/Babel-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py build
}

package_python3-babel() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/pybabel{,3}
}

package_python2-babel() {
	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('4e1341144dfb319c172a446facc47204c9b9f49972c8646b62e082975d8ec41c35e503e610d7114ef5346a5c073e87ed8d40a65a60d9540fb7ca947681cc0beb')

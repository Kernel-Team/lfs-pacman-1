pkgbase=python-babel
pkgname=('python2-babel' 'python3-babel')
pkgver=2.5.3
pkgrel=1
pkgdesc="Internationalization utilities"
arch=('x86_64')
url="https://pypi.python.org/pypi/Babel/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/B/Babel/Babel-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Babel-${pkgver}" "${srcdir}/Babel-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py build
}

package_python3-babel() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/pybabel{,3}
}

package_python2-babel() {
	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('1a41f69141da9f66e27110e7a1a5955f76d8ae9353ff7e54182c5bb6ca16e8b4799031e4df1049af54de513da9f101e171e4312b2e7775f2cab9116d5b50c0fb')

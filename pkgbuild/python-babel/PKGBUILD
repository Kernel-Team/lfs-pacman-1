pkgbase=python-babel
pkgname=('python2-babel' 'python3-babel')
pkgver=2.6.0
pkgrel=1
pkgdesc="Internationalization utilities"
arch=('x86_64')
url="https://pypi.python.org/pypi/Babel/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/B/Babel/Babel-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Babel-${pkgver}" "${srcdir}/Babel-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py build
}

package_python3-babel() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/pybabel{,3}
}

package_python2-babel() {
	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('a87f26c25f1e48a64d1e7ee112bc0c325a6268e0e716a81cb6814a53f0add8f3d579babd2ba064f160298ebe3ac9711aa5d28f112582ee287767b6ed8427b6e0')

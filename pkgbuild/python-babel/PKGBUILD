pkgbase=python-babel
pkgname=('python2-babel' 'python3-babel')
pkgver=2.5.1
pkgrel=1
pkgdesc="Internationalization utilities"
arch=('x86_64')
url="https://pypi.python.org/pypi/Babel/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/B/Babel/Babel-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Babel-${pkgver}" "${srcdir}/Babel-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py build
}

package_python3-babel() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/pybabel{,3}
}

package_python2-babel() {
	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('a2999bd75213bf2447079a210f774d49ae0d658476563ba5cdc4ddce8b58d4eb84bc70812d7e972923d1fa6283bc282b226a045db39ec65e2e95327d82271eb9')

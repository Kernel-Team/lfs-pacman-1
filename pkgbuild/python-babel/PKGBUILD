pkgbase=python-babel
pkgname=('python2-babel' 'python3-babel')
pkgver=2.7.0
pkgrel=1
pkgdesc="Internationalization utilities"
arch=('x86_64')
url="https://pypi.python.org/pypi/Babel/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/B/Babel/Babel-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Babel-${pkgver}" "${srcdir}/Babel-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py build
}

package_python3-babel() {
	cd "${srcdir}/Babel-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/pybabel{,3}
}

package_python2-babel() {
	cd "${srcdir}/Babel-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('96314c7b26f6915a825e0344fa06e4f1d9a71b97a52b16f2f696c2e81c69318974547620135912b11edb95668a717ec49f1be68eef5a4753092d8e4cdc39a1e5')

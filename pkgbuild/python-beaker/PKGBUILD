pkgbase=python-beaker
pkgname=('python2-beaker' 'python3-beaker')
pkgver=1.9.1
pkgrel=1
pkgdesc="A Session and Caching library with WSGI Middleware"
arch=('x86_64')
url="https://pypi.python.org/pypi/Beaker/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/B/Beaker/Beaker-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Beaker-${pkgver}" "${srcdir}/Beaker-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Beaker-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Beaker-${pkgver}-py2"

	python2 setup.py build
}

package_python3-beaker() {
	cd "${srcdir}/Beaker-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-beaker() {
	cd "${srcdir}/Beaker-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('b75985b76553d87d1570020a7ad86159fe504b8ffa4812bd4862e87a8bd2995fc5657b11c1978f3394d19cb6da8146b9893b6e081a10418baf6e7f7d9fbdc3e7')

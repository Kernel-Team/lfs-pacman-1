pkgbase=python-chardet
pkgname=('python2-chardet' 'python3-chardet')
pkgver=3.0.4
pkgrel=1
pkgdesc="Universal encoding detector for Python 2 and 3"
arch=('x86_64')
url="https://pypi.python.org/pypi/chardet/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/c/chardet/chardet-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/chardet-${pkgver}" "${srcdir}/chardet-${pkgver}-py2"
}

build() {
	cd "${srcdir}/chardet-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/chardet-${pkgver}-py2"

	python2 setup.py build
}

package_python3-chardet() {
	cd "${srcdir}/chardet-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/chardetect{,3}
}

package_python2-chardet() {
	cd "${srcdir}/chardet-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('61a03b23447a2bfe52ceed4dd1b9afdb5784da1933a623776883ee9f297e341f633e27f0ce0230bd5fdc5fdb5382105ab42736a74a417ddeb9f83af57455dba5')

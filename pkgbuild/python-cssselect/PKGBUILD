pkgbase=python-cssselect
pkgname=('python2-cssselect' 'python3-cssselect')
pkgver=1.0.3
pkgrel=1
pkgdesc="cssselect parses CSS3 Selectors and translates them to XPath 1.0"
arch=('x86_64')
url="https://pypi.python.org/pypi/cssselect/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/c/cssselect/cssselect-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/cssselect-${pkgver}" "${srcdir}/cssselect-${pkgver}-py2"
}

build() {
	cd "${srcdir}/cssselect-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/cssselect-${pkgver}-py2"

	python2 setup.py build
}

package_python3-cssselect() {
	cd "${srcdir}/cssselect-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-cssselect() {
	cd "${srcdir}/cssselect-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('dec659e17d378b2afc23aecd59ccb80538c92978dd9622b9d909d398817a6dadcd28957c5983e7930135ca73ed0aad402617e41bc65088aa4a5b15c4324cadb0')

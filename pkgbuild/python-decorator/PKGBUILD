pkgbase=python-decorator
pkgname=('python2-decorator' 'python3-decorator')
pkgver=4.0.11
pkgrel=1
pkgdesc="Python Decorator module"
arch=('x86_64')
url="https://pypi.python.org/pypi/decorator/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/d/decorator/decorator-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/decorator-${pkgver}" "${srcdir}/decorator-${pkgver}-py2"
}

build() {
	cd "${srcdir}/decorator-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/decorator-${pkgver}-py2"

	python2 setup.py build
}

package_python3-decorator() {
	cd "${srcdir}/decorator-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-decorator() {
	cd "${srcdir}/decorator-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('acce545d26c05ad03e4a3889214c3e94d1d47a9ccba42318ff8acb4d9384e676839fdcdb8d501a808f9332f532e4087a60398c273a58e07dfbc02f04f04af69c')

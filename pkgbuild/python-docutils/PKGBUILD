pkgbase=python-docutils
pkgname=('python2-docutils' 'python3-docutils')
pkgver=0.13.1
pkgrel=1
pkgdesc="Python Documentation Utilities"
arch=('x86_64')
url="https://pypi.python.org/pypi/docutils/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/d/docutils/docutils-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/docutils-${pkgver}" "${srcdir}/docutils-${pkgver}-py2"
}

build() {
	cd "${srcdir}/docutils-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/docutils-${pkgver}-py2"

	python2 setup.py build
}

package_python3-docutils() {
	cd "${srcdir}/docutils-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*.py
	do
		mv ${file} ${file%.py}3.py
	done
}

package_python2-docutils() {
	cd "${srcdir}/docutils-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*.py
	do
		ln -sf $(basename ${file}) ${file%.py}
	done
}

sha512sums=('6a68b27dac3705ff532cb79d6b6808071206544a1c653e6a24d46971a5e10edffc7d275834eec4e80d948eb066bb099cae0195c0ab674e68747820e54f0ea64e')

pkgbase=python-future
pkgname=('python2-future' 'python3-future')
pkgver=0.18.2
pkgrel=1
pkgdesc="Clean single-source support for Python 3 and 2"
arch=('x86_64')
url="https://pypi.python.org/pypi/future/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/f/future/future-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/future-${pkgver}" "${srcdir}/future-${pkgver}-py2"
}

build() {
	cd "${srcdir}/future-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/future-${pkgver}-py2"

	python2 setup.py build
}

package_python3-future() {
	cd "${srcdir}/future-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*
	do
		mv ${file} ${file%.py}3
	done
}

package_python2-future() {
	cd "${srcdir}/future-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('91c025f7d94bcdf93df838fab67053165a414fc84e8496f92ecbb910dd55f6b6af5e360bbd051444066880c5a6877e75157bd95e150ead46e5c605930dfc50f2')

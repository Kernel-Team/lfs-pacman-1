pkgbase=python-idna
pkgname=('python2-idna' 'python3-idna')
pkgver=2.10
pkgrel=1
pkgdesc="Support for the Internationalised Domain Names in Applications (IDNA) protocol as specified in RFC 5891"
arch=('x86_64')
url="https://pypi.python.org/pypi/idna/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/i/idna/idna-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/idna-${pkgver}" "${srcdir}/idna-${pkgver}-py2"
}

build() {
	cd "${srcdir}/idna-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/idna-${pkgver}-py2"

	python2 setup.py build
}

package_python3-idna() {
	cd "${srcdir}/idna-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	find "${pkgdir}" -type f -exec chmod 644 {} \;
}

package_python2-idna() {
	cd "${srcdir}/idna-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1

	find "${pkgdir}" -type f -exec chmod 644 {} \;
}

sha512sums=('83b412de2f79a4bc86fb4bdac7252521b9d84f0be54f4fb1bde1ee13a210bbfa4b1a98247affbc7921046fb117a591316c12694c1be72865767646554c5207ac')

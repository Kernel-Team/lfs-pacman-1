pkgbase=python-imagesize
pkgname=('python2-imagesize' 'python3-imagesize')
pkgver=1.0.0
pkgrel=1
pkgdesc="Getting image size from png/jpeg/jpeg2000/gif file"
arch=('x86_64')
url="https://pypi.python.org/pypi/imagesize/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/i/imagesize/imagesize-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/imagesize-${pkgver}" "${srcdir}/imagesize-${pkgver}-py2"
}

build() {
	cd "${srcdir}/imagesize-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/imagesize-${pkgver}-py2"

	python2 setup.py build
}

package_python3-imagesize() {
	cd "${srcdir}/imagesize-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-imagesize() {
	cd "${srcdir}/imagesize-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('1111d284cfdb8b14bc09dd0dd569434c62225f0b8da5ca5c00a740fce095c382efafcba73a4000e38b91418775518707693675c2d3c2dafb1036fc4aadb91e64')

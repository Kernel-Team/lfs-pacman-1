pkgbase=python-isodate
pkgname=('python2-isodate' 'python3-isodate')
pkgver=0.5.4
pkgrel=1
pkgdesc="An ISO 8601 date/time/duration parser and formatter"
arch=('x86_64')
url="https://pypi.python.org/pypi/isodate/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/i/isodate/isodate-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/isodate-${pkgver}" "${srcdir}/isodate-${pkgver}-py2"
}

build() {
	cd "${srcdir}/isodate-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/isodate-${pkgver}-py2"

	python2 setup.py build
}

package_python3-isodate() {
	cd "${srcdir}/isodate-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-isodate() {
	cd "${srcdir}/isodate-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('ef5fd27ffa61ad10abf499723c452fd8fd6c2f08a0d5aca6ead88857e5d159f50a3d01769b9fe0aed59a57b0f89a70b359135b0d7c5e6a75d023daa824e5d80a')

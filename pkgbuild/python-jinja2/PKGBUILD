pkgbase=python-jinja2
pkgname=('python2-jinja2' 'python3-jinja2')
pkgver=2.11.2
pkgrel=1
pkgdesc="A small but fast and easy to use stand-alone template engine"
arch=('x86_64')
url="https://pypi.python.org/pypi/Jinja2/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/J/Jinja2/Jinja2-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Jinja2-${pkgver}" "${srcdir}/Jinja2-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Jinja2-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Jinja2-${pkgver}-py2"

	python2 setup.py build
}

package_python3-jinja2() {
	cd "${srcdir}/Jinja2-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-jinja2() {
	cd "${srcdir}/Jinja2-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('baf62118702a7e113e6a22d9af58a5fa66af8017ded08f18705107a4cb93c9727c7fa118436aadfe0729d7f496241ca7bcce27863c5b19deaee3852eda68c082')

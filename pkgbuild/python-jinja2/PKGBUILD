pkgbase=python-jinja2
pkgname=('python2-jinja2' 'python3-jinja2')
pkgver=2.10.1
pkgrel=1
pkgdesc="A small but fast and easy to use stand-alone template engine"
arch=('x86_64')
url="https://pypi.python.org/pypi/Jinja2/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/J/Jinja2/Jinja2-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Jinja2-${pkgver}" "${srcdir}/Jinja2-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Jinja2-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Jinja2-${pkgver}-py2"

	python2 setup.py build
}

package_python3-jinja2() {
	cd "${srcdir}/Jinja2-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-jinja2() {
	cd "${srcdir}/Jinja2-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('a00153a0e07bb7d67f301b4eaf7af657726a1985e9ffc7ae2d76bdbb4c062d672efc8065e398767e1039b18a483a0092e206deac91e4047aad64920b56869623')

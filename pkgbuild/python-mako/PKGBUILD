pkgbase=python-mako
pkgname=('python2-mako' 'python3-mako')
pkgver=1.0.7
pkgrel=1
pkgdesc="A super-fast templating language"
arch=('x86_64')
url="https://pypi.python.org/pypi/Mako/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/M/Mako/Mako-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Mako-${pkgver}" "${srcdir}/Mako-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Mako-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Mako-${pkgver}-py2"

	python2 setup.py build
}

package_python3-mako() {
	cd "${srcdir}/Mako-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/mako-render{,3}
}

package_python2-mako() {
	cd "${srcdir}/Mako-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('d67af6788bf8603010361ce8b6d0355b0191657c07f3b5845f9d5e91653c3e349a7b35d5b36aa9c7f291973c83911ce94075e1cad78e6935d0ed4ee9c6e2a571')

pkgbase=python-markupsafe
pkgname=('python2-markupsafe' 'python3-markupsafe')
pkgver=1.1.1
pkgrel=1
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python"
arch=('x86_64')
url="https://pypi.python.org/pypi/MarkupSafe/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/M/MarkupSafe/MarkupSafe-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/MarkupSafe-${pkgver}" "${srcdir}/MarkupSafe-${pkgver}-py2"
}

build() {
	cd "${srcdir}/MarkupSafe-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/MarkupSafe-${pkgver}-py2"

	python2 setup.py build
}

package_python3-markupsafe() {
	cd "${srcdir}/MarkupSafe-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-markupsafe() {
	cd "${srcdir}/MarkupSafe-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('f3014e6131a3ab866914c5635b5397ef71906bffb1b6f8c5f2ed2acf167429ff7914236d38943e872683a57a9be9669f4c5aace6274f3307ab21ef25373db0b6')

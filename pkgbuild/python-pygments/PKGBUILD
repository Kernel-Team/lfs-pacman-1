pkgbase=python-pygments
pkgname=('python2-pygments' 'python3-pygments')
pkgver=2.3.1
pkgrel=1
pkgdesc="Syntax highlighting package written in Python"
arch=('x86_64')
url="https://pypi.python.org/pypi/Pygments/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/P/Pygments/Pygments-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Pygments-${pkgver}" "${srcdir}/Pygments-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Pygments-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Pygments-${pkgver}-py2"

	python2 setup.py build
}

package_python3-pygments() {
	cd "${srcdir}/Pygments-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	mv "${pkgdir}"/usr/bin/pygmentize{,3}
}

package_python2-pygments() {
	cd "${srcdir}/Pygments-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('2c36cb42f8dd62e04b7664b5e87f951a8428ccbb6dbe5b5b43d8c7e6923ada0ab55a231bb8e9ed79eb5a85344ed64d3acc8e7bc991ab1f49c58eb612b8002c1e')

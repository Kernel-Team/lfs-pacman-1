pkgbase=python-pyparsing
pkgname=('python2-pyparsing' 'python3-pyparsing')
pkgver=2.3.1
pkgrel=1
pkgdesc="Python parsing module"
arch=('x86_64')
url="https://pypi.python.org/pypi/pyparsing/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/p/pyparsing/pyparsing-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/pyparsing-${pkgver}" "${srcdir}/pyparsing-${pkgver}-py2"
}

build() {
	cd "${srcdir}/pyparsing-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/pyparsing-${pkgver}-py2"

	python2 setup.py build
}

package_python3-pyparsing() {
	cd "${srcdir}/pyparsing-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pyparsing() {
	cd "${srcdir}/pyparsing-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('fc8342780c38fd08c07d0b885c4d6c77d59f38098aa13c3abd05a790ee6caf2065a0bdab33bc290a6e2da123f4ac7e38c049e389b740546499dc48eba7ae431f')

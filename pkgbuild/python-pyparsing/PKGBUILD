pkgbase=python-pyparsing
pkgname=('python2-pyparsing' 'python3-pyparsing')
pkgver=2.2.0
pkgrel=1
pkgdesc="Python parsing module"
arch=('x86_64')
url="https://pypi.python.org/pypi/pyparsing/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/p/pyparsing/pyparsing-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/pyparsing-${pkgver}" "${srcdir}/pyparsing-${pkgver}-py2"
}

build() {
	cd "${srcdir}/pyparsing-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/pyparsing-${pkgver}-py2"

	python2 setup.py build
}

package_python3-pyparsing() {
	cd "${srcdir}/pyparsing-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pyparsing() {
	cd "${srcdir}/pyparsing-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('6a409f429d40a5ac202d144e4150de2c80d7c7120147f603f72a4a99797d48c50822a6437f590ec5ae4418d51aa1f6b1d4e5d95a57e19bec0a127b04e4937008')

pkgbase=python-pytz
pkgname=('python2-pytz' 'python3-pytz')
pkgver=2019.3
pkgrel=1
pkgdesc="World timezone definitions, modern and historical"
arch=('x86_64')
url="https://pypi.python.org/pypi/pytz/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/p/pytz/pytz-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/pytz-${pkgver}" "${srcdir}/pytz-${pkgver}-py2"
}

build() {
	cd "${srcdir}/pytz-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/pytz-${pkgver}-py2"

	python2 setup.py build
}

package_python3-pytz() {
	cd "${srcdir}/pytz-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pytz() {
	cd "${srcdir}/pytz-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('b925b59d197e7007dae8098d35518a470e4b7588d9114f51e4d8816813243a9f816f662055f690bedf56b86cc9ece06428ed8c55e5b3ed676ba0f504f7cb43af')

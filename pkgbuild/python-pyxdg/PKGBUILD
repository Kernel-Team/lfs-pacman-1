pkgbase=python-pyxdg
pkgname=('python2-pyxdg' 'python3-pyxdg')
pkgver=0.27
pkgrel=1
pkgdesc="Implementations of freedesktop.org standards in Python"
arch=('x86_64')
url="https://pypi.python.org/pypi/pyxdg/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/p/pyxdg/pyxdg-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/pyxdg-${pkgver}" "${srcdir}/pyxdg-${pkgver}-py2"
}

build() {
	cd "${srcdir}/pyxdg-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/pyxdg-${pkgver}-py2"

	python2 setup.py build
}

package_python3-pyxdg() {
	cd "${srcdir}/pyxdg-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pyxdg() {
	cd "${srcdir}/pyxdg-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('acb24153a4e3e0d8333f1c965c024b15d44f623497b561fc0e9be60d4411441af1dc61f11dcfb22587a510451237a341440bf7ff77a6ab6446dceb83b3c380e2')

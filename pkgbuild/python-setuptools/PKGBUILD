pkgbase=python-setuptools
pkgname=('python2-setuptools' 'python3-setuptools')
pkgver=30.4.0
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('x86_64')
url="https://pypi.python.org/pypi/setuptools/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/s/setuptools/setuptools-${pkgver}.zip")

prepare() {
	cp -a "${srcdir}/setuptools-${pkgver}" "${srcdir}/setuptools-${pkgver}-py2"
}

build() {
	cd "${srcdir}/setuptools-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/setuptools-${pkgver}-py2"

	python2 setup.py build
}

package_python3-setuptools() {
	cd "${srcdir}/setuptools-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	rm -rf "${pkgdir}/usr/bin/easy_install"
}

package_python2-setuptools() {
	cd "${srcdir}/setuptools-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

md5sums=('f0c2eb38728a29659c60a9d0184cc402')

pkgbase=python-six
pkgname=('python2-six' 'python3-six')
pkgver=1.13.0
pkgrel=1
pkgdesc="Python 2 and 3 compatibility utilities"
arch=('x86_64')
url="https://pypi.python.org/pypi/six/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/s/six/six-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/six-${pkgver}" "${srcdir}/six-${pkgver}-py2"
}

build() {
	cd "${srcdir}/six-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/six-${pkgver}-py2"

	python2 setup.py build
}

package_python3-six() {
	cd "${srcdir}/six-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-six() {
	cd "${srcdir}/six-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('55eb8e23e529cb0d2ab9e7c85868e695f36c8235e914b1548eb4b9fd561e4237266bf1839457f15a9513d57a343cbf8ab809d6187499300791fd9356854fcb00')

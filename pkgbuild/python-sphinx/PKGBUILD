pkgbase=python-sphinx
pkgname=('python2-sphinx' 'python3-sphinx')
pkgver=1.6.5
pkgrel=1
pkgdesc="Python documentation generator"
arch=('x86_64')
url="https://pypi.python.org/pypi/Sphinx/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/S/Sphinx/Sphinx-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Sphinx-${pkgver}" "${srcdir}/Sphinx-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Sphinx-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Sphinx-${pkgver}-py2"

	python2 setup.py build
}

package_python3-sphinx() {
	cd "${srcdir}/Sphinx-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*
	do
		mv ${file} ${file}3
	done
}

package_python2-sphinx() {
	cd "${srcdir}/Sphinx-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('c6ba7ba639f9814867d9abaaac48d58daa9d0ea9ceae4495bdbd311856833314eade2d9df7213c0357d5eea53d525c5a1235bc977a07cc7ef82e1ff0ecb51f3d')

pkgbase=python-sphinx
pkgname=('python2-sphinx' 'python3-sphinx')
pkgver=1.5.5
pkgrel=1
pkgdesc="Python documentation generator"
arch=('x86_64')
url="https://pypi.python.org/pypi/Sphinx/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/S/Sphinx/Sphinx-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Sphinx-${pkgver}" "${srcdir}/Sphinx-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Sphinx-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Sphinx-${pkgver}-py2"

	python2 setup.py build
}

package_python3-sphinx() {
	cd "${srcdir}/Sphinx-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*
	do
		mv ${file} ${file}3
	done
}

package_python2-sphinx() {
	cd "${srcdir}/Sphinx-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('67bd2800da6886391a852175db52d9f9115eb71fa0c925d49809f3ca15f58deafb55cc4fc42d83b4c9494b38962a61f33de52c21b4b140ec377f696f4fa7bf7e')

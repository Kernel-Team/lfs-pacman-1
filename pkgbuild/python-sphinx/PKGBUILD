pkgbase=python-sphinx
pkgname=('python2-sphinx' 'python3-sphinx')
pkgver=1.6.3
pkgrel=1
pkgdesc="Python documentation generator"
arch=('x86_64')
url="https://pypi.python.org/pypi/Sphinx/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/S/Sphinx/Sphinx-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/Sphinx-${pkgver}" "${srcdir}/Sphinx-${pkgver}-py2"
}

build() {
	cd "${srcdir}/Sphinx-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/Sphinx-${pkgver}-py2"

	python2 setup.py build
}

package_python3-sphinx() {
	cd "${srcdir}/Sphinx-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1

	for file in "${pkgdir}"/usr/bin/*
	do
		mv ${file} ${file}3
	done
}

package_python2-sphinx() {
	cd "${srcdir}/Sphinx-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('d5aec58e19239db4e61fcd6b103fcc838d97aad5a7aaf9279ff1097791d9cd1ba245989307a2d1049c123058b84ca4e91db192a1672c040d80fafed3907736ae')

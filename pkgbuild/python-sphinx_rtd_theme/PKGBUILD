pkgbase=python-sphinx_rtd_theme
pkgname=('python2-sphinx_rtd_theme' 'python3-sphinx_rtd_theme')
pkgver=0.4.0
pkgrel=1
pkgdesc="ReadTheDocs.org theme for Sphinx, 2013 version"
arch=('x86_64')
url="https://pypi.python.org/pypi/sphinx_rtd_theme/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/s/sphinx_rtd_theme/sphinx_rtd_theme-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/sphinx_rtd_theme-${pkgver}" "${srcdir}/sphinx_rtd_theme-${pkgver}-py2"
}

build() {
	cd "${srcdir}/sphinx_rtd_theme-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/sphinx_rtd_theme-${pkgver}-py2"

	python2 setup.py build
}

package_python3-sphinx_rtd_theme() {
	cd "${srcdir}/sphinx_rtd_theme-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-sphinx_rtd_theme() {
	cd "${srcdir}/sphinx_rtd_theme-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('21da583b4b6819f8681948f71808a1cb6cba6ef7f8d601b945a3ef164d10f7e11748634a115d35b4442d061ee8b7ec512d568487d2e744dc5dfce045401bf1bd')

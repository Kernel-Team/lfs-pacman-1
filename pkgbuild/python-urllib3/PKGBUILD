pkgbase=python-urllib3
pkgname=('python2-urllib3' 'python3-urllib3')
pkgver=1.22
pkgrel=1
pkgdesc="HTTP library with thread-safe connection pooling, file post, and more."
arch=('x86_64')
url="https://pypi.python.org/pypi/urllib3/"
license=('PSF')
#depends=()
source=("https://files.pythonhosted.org/packages/source/u/urllib3/urllib3-${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/urllib3-${pkgver}" "${srcdir}/urllib3-${pkgver}-py2"
}

build() {
	cd "${srcdir}/urllib3-${pkgver}"

	python3 setup.py build

	cd "${srcdir}/urllib3-${pkgver}-py2"

	python2 setup.py build
}

package_python3-urllib3() {
	cd "${srcdir}/urllib3-${pkgver}"

	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-urllib3() {
	cd "${srcdir}/urllib3-${pkgver}-py2"

	python2 setup.py install --root="${pkgdir}" --optimize=1
}

sha512sums=('132b69d7f390916bdcde66d46fbf0189d0c1f976f91778dcb0a9ef8174487b7bb4b37b9139e84f7c70234803be95284448aba0b820f3b54530c0c497ca7a1dc9')

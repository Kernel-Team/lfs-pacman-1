pkgname=python2
_py2majorver=2.7
_py2minorver=15
pkgver=${_py2majorver}.${_py2minorver}
pkgrel=1
pkgdesc="A high-level scripting language"
arch=('x86_64')
url="http://www.python.org/"
license=('PSF')
#depends=()
source=("https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz"
	"python2-bdb.patch")

prepare() {
	cd "${srcdir}/Python-${pkgver}"

	patch -Np1 -i "${srcdir}/python2-bdb.patch"

	rm -rf Modules/expat Modules/zlib Modules/_ctypes/{darwin,libffi}*

	echo "nis nismodule.c -lnsl -I/usr/include/tirpc" >> Modules/Setup.dist
}

build() {
	cd "${srcdir}/Python-${pkgver}"

	./configure --prefix=/usr \
		--enable-shared \
		--with-system-expat \
		--with-system-ffi \
		--enable-unicode=ucs4 \
		--with-dbmliborder=bdb:gdbm:ndbm

	make
}

package() {
	cd "${srcdir}/Python-${pkgver}"

	make install DESTDIR="${pkgdir}"

	sed -i "s#$PWD:##" "${pkgdir}/usr/lib/python${_py2majorver}/config/Makefile" \
		"${pkgdir}/usr/lib/python${_py2majorver}/_sysconfigdata.py"

	sed -i "s#$PWD##" "${pkgdir}/usr/lib/python${_py2majorver}/config/Makefile" \
		"${pkgdir}/usr/lib/python${_py2majorver}/_sysconfigdata.py"

	mv "${pkgdir}"/usr/bin/2to3{,-${_py2majorver}}

	rm -rf "${pkgdir}"/usr/lib/python${_py2majorver}/{distutils/tests,lib-tk/test,sqlite3/test,json/tests,test}
	rm -rf "${pkgdir}"/usr/lib/python${_py2majorver}/{ctypes/test,lib2to3/tests,email/test,unittest/test,bsddb/test}
}

sha512sums=('27ea43eb45fc68f3d2469d5f07636e10801dee11635a430ec8ec922ed790bb426b072da94df885e4dfa1ea8b7a24f2f56dd92f9b0f51e162330f161216bd6de6'
            '831f402f5fa8f4fbf7b93d5f49568bd734e4f284a649badc2770b7eacac61e94d433bd1bef7a71c9692c364a6836dee63eb245b1417ffaac90fb7a7f8b2962b5')

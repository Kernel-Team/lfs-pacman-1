pkgname=python3
_py3majorver=3.7
_py3minorver=2
pkgver=${_py3majorver}.${_py3minorver}
pkgrel=1
pkgdesc="A high-level scripting language"
arch=('x86_64')
url="http://www.python.org/"
license=('PSF')
#depends=()
source=("https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/Python-${pkgver}"

	rm -rf Modules/expat Modules/_ctypes/{darwin,libffi}*

	sed -i "/INSTALL_SHARED/s:555:755:g" Makefile.pre.in
	sed -i "s|^#.* /usr/local/bin/python|#!/usr/bin/python3|" Lib/cgi.py
}

build() {
	cd "${srcdir}/Python-${pkgver}"

	./configure --prefix=/usr \
		--enable-shared \
		--with-system-expat \
		--with-system-ffi \
		--without-ensurepip \
		--with-dbmliborder=bdb:gdbm:ndbm

	make
}

package() {
	cd "${srcdir}/Python-${pkgver}"

	make install DESTDIR="${pkgdir}"

	sed -i "s#$PWD:##" "${pkgdir}/usr/lib/python${_py3majorver}/config-${_py3majorver}m-x86_64-linux-gnu/Makefile" \
		"${pkgdir}/usr/lib/python${_py3majorver}/_sysconfigdata_m_linux_x86_64-linux-gnu.py"

	sed -i "s#$PWD##" "${pkgdir}/usr/lib/python${_py3majorver}/config-${_py3majorver}m-x86_64-linux-gnu/Makefile" \
		"${pkgdir}/usr/lib/python${_py3majorver}/_sysconfigdata_m_linux_x86_64-linux-gnu.py"

	rm -rf "${pkgdir}"/usr/lib/python${_py3majorver}/{distutils/tests,unittest/test,lib2to3/tests,idlelib/idle_test}
	rm -rf "${pkgdir}"/usr/lib/python${_py3majorver}/{ctypes/test,tkinter/test,test,sqlite3/test}
}

sha512sums=('6cd2d6d8455558783b99d55985cd7b22d67b98f41a09b4fdd96f680a630a4e035220d2b903f8c59ed513aa5ffe6730fa947ddb55bb72ce36f0e945ef8af5d971')

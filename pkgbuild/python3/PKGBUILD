pkgname=python3
_py3majorver=3.6
_py3minorver=5
pkgver=${_py3majorver}.${_py3minorver}
pkgrel=1
pkgdesc="A high-level scripting language"
arch=('x86_64')
url="http://www.python.org/"
license=('PSF')
#depends=()
source=("https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz"
	"python3-bdb.patch")

prepare() {
	cd "${srcdir}/Python-${pkgver}"

	patch -Np1 -i "${srcdir}/python3-bdb.patch"

	rm -rf Modules/expat Modules/zlib Modules/_ctypes/{darwin,libffi}*

	sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python3|" Lib/cgi.py

	echo "nis nismodule.c -lnsl -I/usr/include/tirpc" >> Modules/Setup.dist
}

build() {
	cd "${srcdir}/Python-${pkgver}"

	./configure --prefix=/usr \
		--enable-shared \
		--with-system-expat \
		--with-system-ffi \
		--without-ensurepip \
		--with-dbmliborder=bdb:gdbm:ndbm

	make
}

package() {
	cd "${srcdir}/Python-${pkgver}"

	make install DESTDIR="${pkgdir}"

	sed -i "s#$PWD:##" "${pkgdir}/usr/lib/python${_py3majorver}/config-${_py3majorver}m-x86_64-linux-gnu/Makefile" \
		"${pkgdir}/usr/lib/python${_py3majorver}/_sysconfigdata_m_linux_x86_64-linux-gnu.py"

	sed -i "s#$PWD##" "${pkgdir}/usr/lib/python${_py3majorver}/config-${_py3majorver}m-x86_64-linux-gnu/Makefile" \
		"${pkgdir}/usr/lib/python${_py3majorver}/_sysconfigdata_m_linux_x86_64-linux-gnu.py"

	rm -rf "${pkgdir}"/usr/lib/python${_py3majorver}/{distutils/tests,unittest/test,lib2to3/tests,idlelib/idle_test}
	rm -rf "${pkgdir}"/usr/lib/python${_py3majorver}/{ctypes/test,tkinter/test,test,sqlite3/test}
}

sha512sums=('6b26fcd296b9bd8e67861eff10d14db7507711ddba947288d16d6def53135c39326b7f969c04bb2b2993f924d9e7ad3f5c5282a3915760bc0885cf0a8ea5eb51'
            '18338db251551c4f6606862a4abf6f7ef05ab90d98dd3a011eefcf6a3c140d56beb7a08c05484bf62b5c51de208cc73567f566011563cfc420e2b87977bf1504')

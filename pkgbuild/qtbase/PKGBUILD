pkgname=qtbase
pkgver=5.10.0
pkgrel=1
pkgdesc="A cross-platform application and UI framework"
arch=('x86_64')
url="https://www.qt.io/"
license=('GPL3' 'LGPL3' 'FDL' 'custom')
#depends=()
source=("https://download.qt.io/official_releases/qt/${pkgver:0:4}/${pkgver}/submodules/qtbase-everywhere-src-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/qtbase-everywhere-src-${pkgver}"

        CFLAGS+=" -isystem /usr/include/c++/v1"

	sed -i "s#^\(QMAKE_CFLAGS_RELEASE.*\)#\1 ${CFLAGS}#g" mkspecs/common/gcc-base.conf
	sed -i "s#config.atspi#libs.atspi#g" src/gui/configure.json

	./configure -prefix /usr \
		-sysconfdir /etc/xdg \
		-headerdir /usr/include/qt5 \
		-archdatadir /usr/lib/qt5 \
		-datadir /usr/share/qt5 \
		-docdir /usr/share/doc/qt5 \
		-translationdir /usr/share/qt5/translations \
		-examplesdir /usr/share/doc/qt5/examples \
		-confirm-license \
		-opensource \
		-c++std c++14 \
		-dbus-linked \
		-openssl-linked \
		-journald \
		-libinput \
		-system-harfbuzz \
		-system-sqlite \
		-nomake examples \
		-reduce-relocations \
		-no-rpath \
		-no-strip \
		-no-separate-debug-info \
		-optimized-qmake \
		-no-use-gold-linker \
		-platform linux-clang-libc++

	make

	find . -name "*.pc" -exec perl -pi -e "s, -L${PWD}/?\S+,,g" {} \;
}

package() {
	cd "${srcdir}/qtbase-everywhere-src-${pkgver}"

	make install INSTALL_ROOT="${pkgdir}"

	sed -i "s:${PWD}:/usr/lib/qt5:g" \
		"${pkgdir}/usr/lib/qt5/mkspecs/modules/qt_lib_bootstrap_private.pri"

	sed -i "/^QMAKE_PRL_BUILD_DIR/d" "${pkgdir}"/usr/lib/*.prl

	rm -rf "${pkgdir}/usr/share"
}

sha512sums=('8f832fb7bc5087df4b897cd23145e5350f03ca06e2fc5ccb11a7b998e4885c1b936c3e4b989c15fc076134a6a081a35efa37f402b5e8b85e2c780006641b2cdc')

pkgname=qttools
pkgver=5.12.2
pkgrel=1
pkgdesc="A cross-platform application and UI framework (Development Tools, QtHelp)"
arch=('x86_64')
url="https://www.qt.io/"
license=('GPL3' 'LGPL3' 'FDL' 'custom')
#depends=()
source=("https://download.qt.io/official_releases/qt/${pkgver:0:4}/${pkgver}/submodules/qttools-everywhere-src-${pkgver}.tar.xz"
	"assistant.desktop"
	"designer.desktop"
	"linguist.desktop"
	"qdbusviewer.desktop")

build() {
	cd "${srcdir}/qttools-everywhere-src-${pkgver}"

	qmake
	make
}

package() {
	cd "${srcdir}/qttools-everywhere-src-${pkgver}"

	make install INSTALL_ROOT="${pkgdir}"

	sed -i "/^QMAKE_PRL_BUILD_DIR/d" "${pkgdir}"/usr/lib/*.prl

	install -v -Dm644 src/assistant/assistant/images/assistant-128.png \
		"${pkgdir}/usr/share/pixmaps/assistant.png"

	install -v -Dm644 "${srcdir}/assistant.desktop" "${pkgdir}/usr/share/applications/assistant.desktop"

	install -v -Dm644 src/designer/src/designer/images/designer.png \
		"${pkgdir}/usr/share/pixmaps/designer.png"

	install -v -Dm644 "${srcdir}/designer.desktop" "${pkgdir}/usr/share/applications/designer.desktop"

	install -v -Dm644 src/linguist/linguist/images/icons/linguist-128-32.png \
		"${pkgdir}/usr/share/pixmaps/linguist.png"

	install -v -Dm644 "${srcdir}/linguist.desktop" "${pkgdir}/usr/share/applications/linguist.desktop"

	install -v -Dm644 src/qdbus/qdbusviewer/images/qdbusviewer-128.png \
		"${pkgdir}/usr/share/pixmaps/qdbusviewer.png"

	install -v -Dm644 "${srcdir}/qdbusviewer.desktop" "${pkgdir}/usr/share/applications/qdbusviewer.desktop"

}

sha512sums=('bfc415d2764ecee50b5eb1b5641ac709ebeaf3cfbbc73bc431acab307b9fd3f056f093e6b4010e2f7802f5714b56564c39ebe9fc280ce514c175fb44e0a35b8c'
            '110020047861c1d39453330e7893f0f6f2c1237232cfcb2e1e6cdbffe234fb97111bfd63116c952e5aa26c0ed88e231fe460a3e8e8b272e0a25cf2f6ace6b13b'
            'a040a196d6875aea3157814651c3793298cfbb8f5668faad0a7487c487226b0e6f2456ff32518b77423e1e16330428a4dd0febe6bc566610bdd86256d70c74ce'
            '61b95adb7d04a9571cca6b794bd13c2a42c5b144de2e6a419e8c9f4601da5084df608ece6968187d60b454f076ac0c5f1869445705a29139250659fa7d0c2aa6'
            'cf8c8326fcd1e78fc1a4306fa56558a7fd3a0ee169b26b92b5c86daeedb4c9ae6f5a096fea115fa58fe3b2c3ee162abccc06070e1f577a12d0c250d001133195')

pkgname=qtwebengine
pkgver=5.9.0
pkgrel=1
pkgdesc="Provides support for web applications using the Chromium browser project"
arch=('x86_64')
url="https://www.qt.io/"
license=('GPL3' 'LGPL2.1' 'BSD')
#depends=()
source=("https://download.qt.io/official_releases/qt/${pkgver:0:3}/${pkgver}/submodules/qtwebengine-opensource-src-${pkgver}.tar.xz"
	"qtwebengine-clang.patch")

prepare() {
	cd "${srcdir}/qtwebengine-opensource-src-${pkgver}"

	patch -Np1 -i "${srcdir}/qtwebengine-clang.patch"
}

build() {
	cd "${srcdir}/qtwebengine-opensource-src-${pkgver}"

	qmake CONFIG+="proprietary-codecs" WEBENGINE_CONFIG+="use_proprietary_codecs"
	make
}

package() {
	cd "${srcdir}/qtwebengine-opensource-src-${pkgver}"

	make install INSTALL_ROOT="${pkgdir}"

	sed -i "/^QMAKE_PRL_BUILD_DIR/d;s#${PWD}#/usr#g" "${pkgdir}"/usr/lib/*.prl
}

sha512sums=('1759804457baa0bf1abf710d07cbffc8d64e3412b8b35c492fa11c192fcd8a5f7927b3a1b16499ade33ed8b76ad02bddf63de910d7989f7b3b08958928e70461'
            '55b83dcf12f757b1546cec8760b434eafdfdcb9268f540b897ffb41a76c714ee86bf28d61d2ef96719f0047329e6b62c5d90a1e4c8e8f9b2f56d18a9d93f8482')

pkgname=qtwebkit
pkgver=5.212.0.alpha4
pkgrel=1
pkgdesc="Classes for a WebKit2 based implementation and a new QML API"
arch=('x86_64')
url="https://www.qt.io/"
license=('GPL3' 'LGPL3' 'FDL' 'custom')
#depends=()
source=("https://github.com/annulen/webkit/releases/download/qtwebkit-${pkgver/.alpha/-alpha}/qtwebkit-${pkgver/.alpha/-alpha}.tar.xz"
	"icu68.patch"
	"qt5-webkit-python-3.9.patch::https://github.com/qtwebkit/qtwebkit/commit/78360c01.patch"
	"qt5-webkit-bison-3.7.patch::https://github.com/qtwebkit/qtwebkit/commit/d92b11fe.patch")

prepare() {
	cd "${srcdir}/qtwebkit-${pkgver/.alpha/-alpha}"

	patch -Np0 -i "${srcdir}/icu68.patch"
	patch -Np1 -i "${srcdir}/qt5-webkit-python-3.9.patch"
	patch -Np1 -i "${srcdir}/qt5-webkit-bison-3.7.patch"

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

        cmake -DCMAKE_INSTALL_PREFIX=/usr \
                -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG -w" \
                -DPORT=Qt \
		-DENABLE_TOOLS=OFF \
		"${srcdir}/qtwebkit-${pkgver/.alpha/-alpha}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('33f11270bd030599beff9c1983a6c5ff2d61f407cc8a6825f7f405d46f9184c720fc7f60c7359f08f828db96a2170092875066a0d5c0a21ff09bc48a2603fbf6'
            'c4714b40d9b516698490cc3f587f7fefe8097545a2310e144be1dfa39549ad056bf7bd67f5f89334e54c3a9bb95136876a62d0a392991a872030c4dff0d7c820'
            '9bcb07cfa61684d154f9b80b231c32423a8b80820445372634ef620c11a9267e98ae0318e143e6358f0668b10f90a44b163f0eeeee5476a0b5fbd31af5c4bf9a'
            'cf21eff51e1026f4e506724a512b9925d4619909f5924bf01055244817ec9594e9d3ce105b510cd28ecce4772d122d1393fb7273501e20e16749f43282a3200b')

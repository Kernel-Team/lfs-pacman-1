pkgname=qtwebkit
pkgver=5.212.0.alpha2
pkgrel=1
pkgdesc="Classes for a WebKit2 based implementation and a new QML API"
arch=('x86_64')
url="https://www.qt.io/"
license=('GPL3' 'LGPL3' 'FDL' 'custom')
#depends=()
source=("https://github.com/annulen/webkit/releases/download/qtwebkit-${pkgver/.alpha/-alpha}/qtwebkit-${pkgver/.alpha/-alpha}.tar.xz"
	"qtwebkit-cmake3.10.patch"
	"qtwebkit-gcc7.patch"
	"qtwebkit-null-pointer-dereference.patch")

prepare() {
	cd "${srcdir}/qtwebkit-${pkgver/.alpha/-alpha}"

	patch -Np1 -i "${srcdir}/qtwebkit-cmake3.10.patch"
	patch -Np1 -i "${srcdir}/qtwebkit-gcc7.patch"
	patch -Np1 -i "${srcdir}/qtwebkit-null-pointer-dereference.patch"

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

        cmake -DCMAKE_INSTALL_PREFIX=/usr \
                -DCMAKE_BUILD_TYPE=Release \
                -DPORT=Qt \
		-DENABLE_TOOLS=OFF \
		"${srcdir}/qtwebkit-${pkgver/.alpha/-alpha}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	# Fix pkgconfig files
	sed -i 's|qt/Qt5WebKit|qt/QtWebKit|' "${pkgdir}/usr/lib/pkgconfig/Qt5WebKit.pc"
	sed -i 's|qt/Qt5WebKitWidgets|qt/QtWebKitWidgets|' "${pkgdir}/usr/lib/pkgconfig/Qt5WebKitWidgets.pc"
	sed -i '/Name/a Description: Qt WebKit module' "${pkgdir}/usr/lib/pkgconfig/Qt5WebKit.pc"
	sed -i '/Name/a Description: Qt WebKitWidgets module' "${pkgdir}/usr/lib/pkgconfig/Qt5WebKitWidgets.pc"
}

sha512sums=('b15985aab20c5618dc1f71a0d91f02dbed993516272090a4a12990714bf4c9554ccbdcf9d6a143bf46fcc2c170f691e571114d61686fe49791f8d5c540785758'
            '998b6f6c7a7f2aaaf8ae79db990e99f5fd8ca535cbbf3ed896c7dbbc9b40a1b156943485f750c09bb0b09b3a745ecd002fed265663138f5a374ba4b6ea6f1b05'
            'ccc00c228d174e114c9b1371549ed0b7f758f867e54ee3cbe2419a0691d241250e2de68a0e9925bd72786464b49d65c45b3c918c0509556881b52149a19113ff'
            '1e414cb90e54332a5b0790ad9327281fcd325982a717a7c620dd25bbbea744fd375f0bb19a6018b5a8b6a44470306000f29190c67f828fbd0c14c4e21e33851b')

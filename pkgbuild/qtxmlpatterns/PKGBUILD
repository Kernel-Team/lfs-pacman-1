pkgname=qtxmlpatterns
pkgver=5.10.1
pkgrel=1
pkgdesc="Support for XPath, XQuery, XSLT and XML schema validation"
arch=('x86_64')
url="https://www.qt.io/"
license=('GPL3' 'LGPL3' 'FDL' 'custom')
#depends=()
source=("https://download.qt.io/official_releases/qt/${pkgver:0:4}/${pkgver}/submodules/qtxmlpatterns-everywhere-src-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/qtxmlpatterns-everywhere-src-${pkgver}"

	qmake
	make
}

package() {
	cd "${srcdir}/qtxmlpatterns-everywhere-src-${pkgver}"

	make install INSTALL_ROOT="${pkgdir}"

	sed -i "/^QMAKE_PRL_BUILD_DIR/d;s#${PWD}#/usr#g" "${pkgdir}"/usr/lib/*.prl
}

sha512sums=('942188fbfde248d956af009e69639beb90f860febc11b6b5d779376516e007d6d6c883ca68b8b70a2f7dbc6b353e65a2cccbde01766224d821052f07d6d56134')

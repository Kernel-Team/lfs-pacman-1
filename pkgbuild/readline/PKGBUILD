pkgbase=readline
pkgname=('readline' 'lib32-readline')
_basever=7.0
_patchlevel=003
pkgver=${_basever}.${_patchlevel}
pkgrel=1
arch=('x86_64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
#depends=()
source=("https://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz"
	inputrc)

if [ ${_patchlevel} -gt 0 ]
then
	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-${_basever}-patches/readline${_basever//.}-$(printf "%03d" ${_p}))
	done
fi

prepare() {
	cd "${srcdir}/readline-${_basever}"

	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		patch -p0 -i ../readline${_basever//.}-$(printf "%03d" ${_p})
	done

	cp -a "${srcdir}"/readline{,32}-${_basever}
}

build() {
	cd "${srcdir}/readline-${_basever}"

	./configure --prefix=/usr

	cd "${srcdir}/readline32-${_basever}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/readline-${_basever}" SHLIB_LIBS=-lncurses
	make -C "${srcdir}/readline32-${_basever}" SHLIB_LIBS=-lncurses
}

package_readline() {
	pkgdesc="GNU readline library"

	cd "${srcdir}/readline-${_basever}"

	make SHLIB_LIBS=-lncurses install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/inputrc" "${pkgdir}/etc/inputrc"
}

package_lib32-readline() {
	pkgdesc="GNU readline library (32 bit)"

	cd "${srcdir}/readline32-${_basever}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('18243189d39bf0d4c8a76cddcce75243c1bae8824c686e9b6ba352667607e5b10c5feb79372a1093c1c388d821841670702e940df12eae94bcebdeed90047870'
            'feeb1c15a2882bc79cc934e0c750d6c1d75feab4a58cce5e8f962a7f3c16a2648bb568499fb665728ec8075c2faba26cbb310b8edd2f1e760077a8cc8e8f6fb5'
            '4402186905af8cd42c609d640c2e13b9ad61c7778e5a3fd2c2d9da301f0deab05b04d7836f31527262f44f406517823dbb18cb07f2c73931186c806b494699ec'
            '13d1489578508d4d2c3a1618024198a709dbce74a6bbf0f6d7ec67d2419c55bfec9f0ca9de0ed93f129d21d5c3a94307ccdc49408455bbb301c5e3a772b03185'
            'eaf962a1480eb3870519017b81ecc5cef171e4c41fcf8c17da61ccbfd0379ed6bca85c17b03e2207ae4d51509f33fd010294c75f4bd0433a52118015d4160385')

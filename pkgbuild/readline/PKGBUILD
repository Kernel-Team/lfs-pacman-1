pkgbase=readline
pkgname=('readline' 'lib32-readline')
_basever=8.1
_patchlevel=0
pkgver=${_basever}.${_patchlevel}
pkgrel=1
arch=('x86_64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
#depends=()
source=("https://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz"
	inputrc)

if [ ${_patchlevel} -gt 0 ]
then
	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-${_basever}-patches/readline${_basever//.}-$(printf "%03d" ${_p}))
	done
fi

prepare() {
	cd "${srcdir}/readline-${_basever}"

	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ ))
	do
		patch -p0 -i ../readline${_basever//.}-$(printf "%03d" ${_p})
	done

	cp -a "${srcdir}"/readline{,32}-${_basever}
}

build() {
	cd "${srcdir}/readline-${_basever}"

	./configure --prefix=/usr

	cd "${srcdir}/readline32-${_basever}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/readline-${_basever}" SHLIB_LIBS=-lncurses
	make -C "${srcdir}/readline32-${_basever}" SHLIB_LIBS=-lncurses
}

package_readline() {
	pkgdesc="GNU readline library"

	cd "${srcdir}/readline-${_basever}"

	make SHLIB_LIBS=-lncurses install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/inputrc" "${pkgdir}/etc/inputrc"
}

package_lib32-readline() {
	pkgdesc="GNU readline library (32 bit)"

	cd "${srcdir}/readline32-${_basever}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('27790d0461da3093a7fee6e89a51dcab5dc61928ec42e9228ab36493b17220641d5e481ea3d8fee5ee0044c70bf960f55c7d3f1a704cf6b9c42e5c269b797e00'
            'feeb1c15a2882bc79cc934e0c750d6c1d75feab4a58cce5e8f962a7f3c16a2648bb568499fb665728ec8075c2faba26cbb310b8edd2f1e760077a8cc8e8f6fb5')

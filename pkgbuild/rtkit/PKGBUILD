pkgname=rtkit
pkgver=0.12
pkgrel=1
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('x86_64')
url="http://0pointer.de/blog/projects/rtkit.html"
license=('GPL3' 'custom:BSD')
#depends=()
source=("https://github.com/heftig/rtkit/releases/download/v${pkgver}/rtkit-${pkgver}.tar.xz"
	"rtkit.sysusers")

build() {
	cd "${srcdir}/rtkit-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/rtkit-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/rtkit.sysusers" "${pkgdir}/usr/lib/sysusers.d/rtkit.conf"
}

sha512sums=('cee48058367b5b8a705203865538d0ab4bda9d2f1d06d155360922cb095b5d6a5b0311fdda6c45d9f33227324394ca5fd3b5a399fc71e11ea4c609eab18a7b04'
            '07a56a757db178b3df56fcba2a7378112e608f4d1619cb9ae49e1910d9d3549b5c72eb48b3a64b4a77a68b17dd459753f0e9a366a8b048e8e2bbcf35bcdfca45')

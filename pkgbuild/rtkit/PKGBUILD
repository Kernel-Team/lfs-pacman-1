pkgname=rtkit
pkgver=0.11
pkgrel=1
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('x86_64')
url="http://0pointer.de/blog/projects/rtkit.html"
license=('GPL3' 'custom:BSD')
#depends=()
source=("http://0pointer.de/public/rtkit-${pkgver}.tar.xz"
	"rtkit-fixes.patch"
	"rtkit.sysusers")

prepare() {
	cd "${srcdir}/rtkit-${pkgver}"

	patch -Np1 -i "${srcdir}/rtkit-fixes.patch"

	autoreconf -fi

	sed -i "s:libsystemd-daemon:libsystemd:g" configure
}

build() {
	cd "${srcdir}/rtkit-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-systemdsystemunitdir=/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/rtkit-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -dm755 "${pkgdir}/usr/share/dbus-1/interfaces"

	./rtkit-daemon --introspect > "${pkgdir}/usr/share/dbus-1/interfaces/org.freedesktop.RealtimeKit1.xml"

	install -v -Dm644 "${srcdir}/rtkit.sysusers" "${pkgdir}/usr/lib/sysusers.d/rtkit.conf"
}

sha512sums=('4641af965dc64c0f498ae469fbee1ceb337204fa7d778fbbb41f2b5c5e88947e633c7e9ad037274fdd778c3c097a4a1dbb0b2add287d74eccbcd2d357cb2da22'
            'c658facb0fcfb2125904f48490eff68817e20adb257bab3b10532bfc64c8cb4c6dae1bf0404b2d3d1b74884f77cf9822b3cb9c2cb2253bc56b5a5250c49ee8e5'
            '07a56a757db178b3df56fcba2a7378112e608f4d1619cb9ae49e1910d9d3549b5c72eb48b3a64b4a77a68b17dd459753f0e9a366a8b048e8e2bbcf35bcdfca45')

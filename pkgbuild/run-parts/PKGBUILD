pkgname=run-parts
pkgver=4.8.6
pkgrel=1
pkgdesc="run scripts or programs in a directory"
arch=('x86_64')
url="https://packages.qa.debian.org/d/debianutils.html"
license=('GPL')
#depends=()
source=("http://http.debian.net/debian/pool/main/d/debianutils/debianutils_${pkgver}.tar.xz")

build() {
	cd "${srcdir}/debianutils-${pkgver}"

	./configure --prefix=/usr

	make
}

package() {
	cd "${srcdir}/debianutils-${pkgver}"

	install -Dm755 run-parts "${pkgdir}/usr/bin/run-parts"
	install -Dm644 run-parts.8 "${pkgdir}/usr/share/man/man8/run-parts.8"
}

sha512sums=('5913729cc3c56962e8939b71803258952df0d7204df8090a7504fbefc7f5d1da8e749d606fe23148ea3294909ef172bc1f4f821690169e861729caab70921a2f')

pkgname=rust
pkgver=1.34.0
pkgrel=1
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('MIT' 'APACHE')
#depends=()
options=(!strip)
source=("https://static.rust-lang.org/dist/rust-${pkgver}-x86_64-unknown-linux-gnu.tar.gz")

package() {
	cd "${srcdir}/rust-${pkgver}-x86_64-unknown-linux-gnu"

	./install.sh --prefix=/usr --destdir="${pkgdir}" --disable-ldconfig

	chmod 755 "${pkgdir}"/usr/lib/*.so
	chrpath -d "${pkgdir}"/usr/lib/*.so

	rm -rf "${pkgdir}/usr/etc" "${pkgdir}/usr/share/zsh"
	rm -rf "${pkgdir}/usr/lib/rustlib/install.log"

	sed -i "s#${pkgdir}##;s#${srcdir}##g" "${pkgdir}"/usr/lib/rustlib/manifest-*
}

sha512sums=('a4f8753b705812c89caf03ef07a01ad5a6838f64e1c67cc3214384ec0601c2547a9805da098aa6e14572674a2d91a86bd9d2190533871c568470703dddfde44c')

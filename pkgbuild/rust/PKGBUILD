pkgname=rust
pkgver=1.19.0
pkgrel=1
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('MIT' 'APACHE')
#depends=()
options=(!strip)
source=("https://static.rust-lang.org/dist/rust-${pkgver}-x86_64-unknown-linux-gnu.tar.gz")

package() {
	cd "${srcdir}/rust-${pkgver}-x86_64-unknown-linux-gnu"

	./install.sh --prefix=/usr --destdir="${pkgdir}" --disable-ldconfig

	chmod 755 "${pkgdir}"/usr/lib/*.so
	chrpath -d "${pkgdir}"/usr/lib/*.so

	rm -rf "${pkgdir}/usr/etc" "${pkgdir}/usr/share/zsh"
	rm -rf "${pkgdir}/usr/lib/rustlib/install.log"

	sed -i "s#${pkgdir}##;s#${srcdir}##g" \
	       "${pkgdir}/usr/lib/rustlib/manifest-cargo" \
	       "${pkgdir}/usr/lib/rustlib/manifest-rustc" \
	       "${pkgdir}/usr/lib/rustlib/manifest-rust-docs" \
	       "${pkgdir}/usr/lib/rustlib/manifest-rust-analysis-x86_64-unknown-linux-gnu" \
	       "${pkgdir}/usr/lib/rustlib/manifest-rust-std-x86_64-unknown-linux-gnu"
}

sha512sums=('fc98ad7728a72c9bc93505458f7558405cc6a608eb19aa6c609e0155e7c081123f116cf023d19af2f0cd9cb81b8e1bbe69efd88f7bb61cca2d258d5fa2ed6bba')

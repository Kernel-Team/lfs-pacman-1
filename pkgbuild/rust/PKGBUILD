pkgname=rust
pkgver=1.26.0
pkgrel=1
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('MIT' 'APACHE')
#depends=()
options=(!strip)
source=("https://static.rust-lang.org/dist/rust-${pkgver}-x86_64-unknown-linux-gnu.tar.gz")

package() {
	cd "${srcdir}/rust-${pkgver}-x86_64-unknown-linux-gnu"

	./install.sh --prefix=/usr --destdir="${pkgdir}" --disable-ldconfig

	chmod 755 "${pkgdir}"/usr/lib/*.so
	chrpath -d "${pkgdir}"/usr/lib/*.so

	rm -rf "${pkgdir}/usr/etc" "${pkgdir}/usr/share/zsh"
	rm -rf "${pkgdir}/usr/lib/rustlib/install.log"

	sed -i "s#${pkgdir}##;s#${srcdir}##g" "${pkgdir}"/usr/lib/rustlib/manifest-*
}

sha512sums=('2a8740623c79a51560c04730338409233ab5920680df6c997182547e80bd0d5ba7cee43d7609eea71b869b0c4ac828df05278dbe98ec97e2bba28bb27b68f348')

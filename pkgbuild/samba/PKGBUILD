pkgname=samba
pkgver=4.10.0
pkgrel=1
pkgdesc="SMB Fileserver and AD Domain server"
arch=('x86_64')
url="https://www.samba.org/"
license=('GPL3')
#depends=()
backup=(etc/samba/smb.conf)
options=(emptydirs)
source=("https://www.samba.org/ftp/samba/samba-${pkgver}.tar.gz"
	"samba.pam"
	"samba.sysusers"
	"samba.tmpfiles")

_samba4_idmap_modules=idmap_ad,idmap_rid,idmap_adex,idmap_hash,idmap_tdb2
_samba4_pdb_modules=pdb_tdbsam,pdb_ldap,pdb_ads,pdb_smbpasswd,pdb_wbc_sam,pdb_samba4
_samba4_auth_modules=auth_unix,auth_wbc,auth_server,auth_netlogond,auth_script,auth_samba4

prepare() {
	cd "${srcdir}/samba-${pkgver}"

	sed -i 's|MYGROUP|WORKGROUP|g' examples/smb.conf.default
	sed -i 's|log file = .*$|log file = /var/log/samba/log.%m|g' examples/smb.conf.default
	sed -i 's|/usr/spool/samba|/var/spool/samba|g' examples/smb.conf.default
	sed -i 's|log.%m|%m.log|g' examples/smb.conf.default

	sed -i "s#sysconfig#default#g" packaging/systemd/*.service.in
}

build() {
	cd "${srcdir}/samba-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--enable-fhs \
		--with-configdir=/etc/samba \
		--with-lockdir=/run/samba \
		--with-sockets-dir=/run/samba \
		--with-piddir=/run \
		--with-pammodulesdir=/usr/lib/security \
		--enable-gnutls \
		--with-systemd \
		--systemd-install-services \
		--bundled-libraries=!tdb,!talloc,!pytalloc-util,!tevent,!popt,!ldb,!pyldb-util \
		--with-shared-modules=${_samba4_idmap_modules},${_samba4_pdb_modules},${_samba4_auth_modules} \
		--disable-rpath-install

	make
}

package() {
	cd "${srcdir}/samba-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/run" "${pkgdir}/var/run"

	install -v -dm755 "${pkgdir}/usr/lib/cups/backend"
	install -v -dm1770 -g 97 "${pkgdir}/var/lib/samba/usershares"
	install -v -dm1777 "${pkgdir}/var/spool/samba"

	ln -sfv /usr/bin/smbspool "${pkgdir}/usr/lib/cups/backend/smb"

	install -v -Dm644 examples/smb.conf.default "${pkgdir}/etc/samba/smb.conf"
	install -v -Dm644 "${srcdir}/samba.pam" "${pkgdir}/etc/pam.d/samba"

	install -v -Dm644 "${srcdir}/samba.sysusers" "${pkgdir}/usr/lib/sysusers.d/samba.conf"
	install -v -Dm644 "${srcdir}/samba.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/samba.conf"

	mv "${pkgdir}"/etc/{sysconfig,default}
}

sha512sums=('e82a8ec78cea666a653bddab066eaa22382f5b016b38f7618492e39f470d6f4c3ddd6dc21e9f0e9fde73ab98c4dd3da1a3823fd457f085ac14c36070b447ac90'
            'd05772aedd6a55faade4a683f13782de3d463db3cfd781acfc9855bd4d30860bf1965e836feb69222cb07abc2e0b5ea75557b3a87bbc8f90ca88dc268dd74b7b'
            '3798cc28c8a25132bd72e30efcda1489da5cfb1f9838b05473669b4856be9a4e1663a71ae1f1c87a9c9fcee065e05355053aea826a8c2fa19ee1e9246c23e341'
            'a57c7813ef9ffb524c30582884188f4c26b33da120d4021d1bec5fc028d9f1ecb986ecc00ea56f261a794292777fc0a8773598e8df2902c3a4608d6ece7ccbe9')

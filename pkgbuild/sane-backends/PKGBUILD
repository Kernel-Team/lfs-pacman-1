pkgname=sane-backends
pkgver=1.0.31
pkgrel=1
pkgdesc="Scanner Access Now Easy"
arch=('x86_64')
url="http://www.sane-project.org/"
license=('GPL')
#depends=()
backup=(etc/sane.d/dll.conf)
source=("https://gitlab.com/sane-project/backends/uploads/8bf1cae2e1803aefab9e5331550e5d5d/sane-backends-${pkgver}.tar.gz"
	"sane-backends.sysusers")

build() {
	cd "${srcdir}/sane-backends-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-avahi \
		--enable-pthread \
		--disable-locking \
		--without-api-spec

	make
}

package() {
	cd "${srcdir}/sane-backends-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 tools/udev/libsane.rules "${pkgdir}/usr/lib/udev/rules.d/49-sane.rules"
	install -v -Dm644 "${srcdir}/sane-backends.sysusers" "${pkgdir}/usr/lib/sysusers.d/sane-backends.conf"
}

sha512sums=('5d49ca698af38a30a3d23349f2fe82a4a21262b014ca38407d5ea0b3fe7cec72860a39c8651c8a5e2f561a9a740008658a36ed6bd96324be84646dbcf1461bfb'
            '0ea7d0937be75e086a93e64dceb8cd0d60f0150eda488a716c38cb0c78918ab0ab0b64385c2ad9155694a15f596d746242b048494218765d7bbf4a73603c63f9')

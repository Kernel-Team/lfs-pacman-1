pkgname=serf
pkgver=1.3.9
pkgrel=1
pkgdesc="High-performance asynchronous HTTP client library"
arch=('x86_64')
url="http://code.google.com/p/serf/"
license=('APACHE')
#depends=()
source=("https://archive.apache.org/dist/serf/serf-${pkgver}.tar.bz2")

prepare() {
	cd "${srcdir}/serf-${pkgver}"

	sed -i "/Append/s:RPATH=libdir,::"          SConstruct
	sed -i "/Default/s:lib_static,::"           SConstruct
	sed -i "/Alias/s:install_static,::"         SConstruct
	sed -i "/  print/{s/print/print(/; s/$/)/}" SConstruct
}

build() {
	cd "${srcdir}/serf-${pkgver}"

	scons CC="${CC}" CFLAGS="${CFLAGS}" PREFIX=/usr
}

package() {
	cd "${srcdir}/serf-${pkgver}"

	scons CC="${CC}" CFLAGS="${CFLAGS}" PREFIX="${pkgdir}/usr" install
}

sha512sums=('9f5418d991840a08d293d1ecba70cd9534a207696d002f22dbe62354e7b005955112a0d144a76c89c7f7ad3b4c882e54974441fafa0c09c4aa25c49c021ca75d')

pkgname=shared-mime-info
pkgver=1.12
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/shared-mime-info"
license=('GPL2')
#depends=()
source=("https://gitlab.freedesktop.org/xdg/shared-mime-info/uploads/80c7f1afbcad2769f38aeb9ba6317a51/shared-mime-info-${pkgver}.tar.xz"
	"update-mime-database.hook")

build() {
	cd "${srcdir}/shared-mime-info-${pkgver}"

	export ac_cv_func_fdatasync=no
	./configure --prefix=/usr --disable-update-mimedb

	make -j1
}

package() {
	cd "${srcdir}/shared-mime-info-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/update-mime-database.hook" "${pkgdir}/usr/share/libalpm/hooks/update-mime-database.hook"
}

sha512sums=('c9db138586fe4c789b5a89d3cdcf865d5b3258241accd254deecd7665e8b51fc538adeaeeda4fe332474f7d7ffa84f101cba26539a58e134201673d6d1495a2f'
            '365e2bca04e108a6a65c1e8a2b82efcee3916630ca522808e51074db7fb499d7f7c67bb84b304f5f398c17379ff26ec7fd1e44d56991a47581111c1c360ffdb8')

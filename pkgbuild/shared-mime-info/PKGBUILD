pkgname=shared-mime-info
pkgver=1.10
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/shared-mime-info"
license=('GPL2')
#depends=()
source=("https://freedesktop.org/~hadess/shared-mime-info-${pkgver}.tar.xz"
	"update-mime-database.hook")

build() {
	cd "${srcdir}/shared-mime-info-${pkgver}"

	export ac_cv_func_fdatasync=no
	./configure --prefix=/usr --disable-update-mimedb

	make -j1
}

package() {
	cd "${srcdir}/shared-mime-info-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/update-mime-database.hook" "${pkgdir}/usr/share/libalpm/hooks/update-mime-database.hook"
}

sha512sums=('efc2b63cae11e6b0332f8607ba57a885dec68a23ac299bbda3eb6e2c7ed071c7c40dba73e9cf23ab675fcaa86ea3777fa931a9f25000a08ec6a748734c88beda'
            '365e2bca04e108a6a65c1e8a2b82efcee3916630ca522808e51074db7fb499d7f7c67bb84b304f5f398c17379ff26ec7fd1e44d56991a47581111c1c360ffdb8')

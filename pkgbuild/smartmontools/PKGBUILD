pkgname=smartmontools
pkgver=6.5
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives"
arch=('x86_64')
url="https://www.smartmontools.org/"
license=('GPL')
#depends=()
options=(emptydirs)
source=("https://downloads.sourceforge.net/smartmontools/smartmontools-${pkgver}.tar.gz"
	"smartd.default")

build() {
	cd "${srcdir}/smartmontools-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-systemdsystemunitdir=/lib/systemd/system \
		--with-smartdscriptdir=/usr/share/smartmontools \
		--with-smartdplugindir=/usr/share/smartmontools/smartd_warning.d

	make
}

package() {
	cd "${srcdir}/smartmontools-${pkgver}"

	sed -e "s:sysconfig/smartmontools:default/smartd:" \
	    -e "s:smartd_opts:SMARTD_OPTS:"                \
	    -e "/syslog.target/d" -i smartd.service

	make install DESTDIR="${pkgdir}"

	install -v -Dm644 "${srcdir}/smartd.default" "${pkgdir}/etc/default/smartd"
}

sha512sums=('dca7a6363ab7280ee4197155025f624c978886dcc94fc47b524f6f849138b62c471e966c0d4bf59c7bba50519dc122264618e5ded80a406863ddd10b43d928d3'
            '1f5dc297b724b43bcb4c70db7879e4c04a50357c75a6200329896184ca201278bd499a9620d31a9ea0ddc09974ef32f15ad73bfda9a708f0a47bb289df5e1aab')

pkgname=soxr
pkgver=0.1.2
pkgrel=1
pkgdesc="The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio"
arch=('x86_64')
url="https://sourceforge.net/p/soxr/wiki/Home/"
license=('GPL')
#depends=()
source=("https://downloads.sourceforge.net/soxr/soxr-${pkgver}-Source.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_TESTS=OFF \
		-DWITH_AVFFT=OFF \
		"${srcdir}/soxr-${pkgver}-Source"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('ee902c1bc713e05b748f917d78222b68b5fe01d43f405e0954c21ce4d1ee494d01000caa79f696cb8d202c662a4c4d958b332dae034d6f929e0480abf27cd07c')

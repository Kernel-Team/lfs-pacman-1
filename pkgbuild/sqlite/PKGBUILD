pkgname=sqlite
_srcver=3240000
pkgver=3.24.0
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
url="http://www.sqlite.org/"
license=('custom:Public Domain')
#depends=()
source=("https://sqlite.org/2018/sqlite-autoconf-${_srcver}.tar.gz")

build() {
	export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 \
		-DSQLITE_ENABLE_FTS5=1            \
		-DSQLITE_ENABLE_COLUMN_METADATA=1 \
		-DSQLITE_ENABLE_UNLOCK_NOTIFY=1   \
		-DSQLITE_ENABLE_DBSTAT_VTAB=1     \
		-DSQLITE_ENABLE_FTS3_TOKENIZER=1  \
		-DSQLITE_SECURE_DELETE=1"

	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	./configure --prefix=/usr --enable-fts5

	make

	cd "${srcdir}/sqlite-autoconf-${_srcver}/tea"

	./configure --prefix=/usr \
		--with-system-sqlite \
		--with-tcl=/usr/lib

	make
}

package() {
	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	make install DESTDIR="${pkgdir}"
	make -C tea install DESTDIR="${pkgdir}"
}

sha512sums=('eaec866de26003ec36559aab15dd18dc0e6029453002a4eec5e176bb35a712b8b06c235436e6c1a226b67c7eb90d7a26c2b3b3d9a5e6e92a5af485236b77c878')

pkgname=sqlite
_srcver=3340000
pkgver=3.34.0
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
url="https://sqlite.org/"
license=('custom:Public Domain')
#depends=()
source=("https://sqlite.org/2020/sqlite-autoconf-${_srcver}.tar.gz")

build() {
	export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 \
		-DSQLITE_ENABLE_FTS5=1            \
		-DSQLITE_ENABLE_COLUMN_METADATA=1 \
		-DSQLITE_ENABLE_UNLOCK_NOTIFY=1   \
		-DSQLITE_ENABLE_DBSTAT_VTAB=1     \
		-DSQLITE_ENABLE_FTS3_TOKENIZER=1  \
		-DSQLITE_SECURE_DELETE=1"

	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	./configure --prefix=/usr --enable-fts3

	make

	cd "${srcdir}/sqlite-autoconf-${_srcver}/tea"

	./configure --prefix=/usr \
		--with-system-sqlite \
		--with-tcl=/usr/lib

	make
}

package() {
	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	make install DESTDIR="${pkgdir}"
	make -C tea install DESTDIR="${pkgdir}"
}

sha512sums=('75a1a2d86ab41354941b8574e780b1eae09c3c01f8da4b08f606b96962b80550f739ec7e9b1ceb07bba1cedced6d18a1408e4c10ff645eb1829d368ad308cf2f')

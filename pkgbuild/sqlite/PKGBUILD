pkgname=sqlite
_srcver=3210000
pkgver=3.20.0
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
url="http://www.sqlite.org/"
license=('custom:Public Domain')
#depends=()
source=("https://sqlite.org/2017/sqlite-autoconf-${_srcver}.tar.gz")

build() {
	export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 \
		-DSQLITE_ENABLE_FTS5=1            \
		-DSQLITE_ENABLE_COLUMN_METADATA=1 \
		-DSQLITE_ENABLE_UNLOCK_NOTIFY=1   \
		-DSQLITE_ENABLE_DBSTAT_VTAB=1     \
		-DSQLITE_ENABLE_FTS3_TOKENIZER=1  \
		-DSQLITE_SECURE_DELETE=1"

	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	./configure --prefix=/usr --enable-fts5

	make

	cd "${srcdir}/sqlite-autoconf-${_srcver}/tea"

	./configure --prefix=/usr \
		--with-system-sqlite \
		--with-tcl=/usr/lib

	make
}

package() {
	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	make install DESTDIR="${pkgdir}"
	make -C tea install DESTDIR="${pkgdir}"
}

sha512sums=('8f00708965eacef171620c49538f75768d350ec3f2bfef412a5c70af865ab03b6a64ce1e65d9fdb3d66f4d00262a30fc162b01fd69f63c05c14d5243374b6298')

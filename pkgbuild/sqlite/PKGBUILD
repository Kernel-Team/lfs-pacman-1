pkgname=sqlite
_srcver=3270200
pkgver=3.27.2
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
url="http://www.sqlite.org/"
license=('custom:Public Domain')
#depends=()
source=("https://sqlite.org/2019/sqlite-autoconf-${_srcver}.tar.gz")

build() {
	export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 \
		-DSQLITE_ENABLE_FTS5=1            \
		-DSQLITE_ENABLE_COLUMN_METADATA=1 \
		-DSQLITE_ENABLE_UNLOCK_NOTIFY=1   \
		-DSQLITE_ENABLE_DBSTAT_VTAB=1     \
		-DSQLITE_ENABLE_FTS3_TOKENIZER=1  \
		-DSQLITE_SECURE_DELETE=1"

	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	./configure --prefix=/usr --enable-fts5

	make

	cd "${srcdir}/sqlite-autoconf-${_srcver}/tea"

	./configure --prefix=/usr \
		--with-system-sqlite \
		--with-tcl=/usr/lib

	make
}

package() {
	cd "${srcdir}/sqlite-autoconf-${_srcver}"

	make install DESTDIR="${pkgdir}"
	make -C tea install DESTDIR="${pkgdir}"
}

sha512sums=('0ac2515c7816932a4f725e657122c9f202bd7aba637bad9af5b4592b85efdd10a55ad34ac621b60a7aea91b1021c2ef0924c6ddfe05b2edb4f70e3d34b005972')

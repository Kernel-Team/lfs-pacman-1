pkgname=stlink
pkgver=1.3.1
pkgrel=1
pkgdesc="Firmware programmer for STM32 STLINK v1/v2 protocol"
arch=('x86_64')
url="https://github.com/texane/stlink"
license=('BSD')
#depends=()
source=("stlink-${pkgver}.tar.gz::https://github.com/texane/stlink/archive/${pkgver}.tar.gz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DSTLINK_UDEV_RULES_DIR=/usr/lib/udev/rules.d \
		-DSTLINK_MODPROBED_DIR=/usr/lib/modprobe.d \
		"${srcdir}/stlink-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/usr/lib/libstlink.a"
}

sha512sums=('ce77d0cf76e4c4b973389599af5d736a0f22ffc2ef7231b2b157dae6dfe5018c2d2ed1b28c14bbbaaa7819847382fe93e437ae682b7eb30d4e44552f4e948043')

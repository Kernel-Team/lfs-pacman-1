pkgname=subversion
pkgver=1.9.6
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('x86_64')
url="http://subversion.apache.org/"
license=('APACHE')
#depends=()
source=("https://archive.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2"
	"svnserve.default"
	"svnserve.service"
	"svnserve.tmpfiles")

build() {
	cd "${srcdir}/subversion-${pkgver}"

	export PYTHON="/usr/bin/python2"
	export RUBYDIR=$(ruby -e "require 'rbconfig'" -e "CONFIG = RbConfig::MAKEFILE_CONFIG" -e "puts RbConfig::expand(CONFIG[\"vendorarchdir\"])")

	sed -i "s#-Werror=unknown-warning-option ##g" configure

	./configure --prefix=/usr \
		--with-apr=/usr \
		--with-apr-util=/usr \
		--with-zlib=/usr \
		--with-serf=/usr \
		--with-sqlite=/usr \
		--with-gnome-keyring \
		--without-berkeley-db \
		--without-kwallet \
		--with-ruby-sitedir=${RUBYDIR}

	make
	make swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
		swig_pydir_extra=/usr/lib/python2.7/site-packages/svn swig-py swig-pl swig-rb
}

package() {
	cd "${srcdir}/subversion-${pkgver}"

	LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH} \
	make -j1 DESTDIR="${pkgdir}" INSTALLDIRS=vendor LT_LDFLAGS="-L${pkgdir}/usr/lib" \
		swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
		swig_pydir_extra=/usr/lib/python2.7/site-packages/svn \
		install install-swig-py install-swig-pl install-swig-rb

	rm -rf "${pkgdir}/usr/lib/perl"

	install -v -Dm644 "${srcdir}/svnserve.default" "${pkgdir}/etc/default/svnserve"
	install -v -Dm644 "${srcdir}/svnserve.service" "${pkgdir}/usr/lib/systemd/system/svnserve.service"
	install -v -Dm644 "${srcdir}/svnserve.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/svnserve.conf"
}

sha512sums=('bdca362ff45a7f028e2123efbc9bfd41a07e6ebdfcf62627ce24f413e0304b45011bbac1f142ceb1c031a07622c06786982f86cd9109065cae5e26fd689fc11e'
            '43a6d736e64e2a0c84014829ec1dffa65cf8f0d591c2e1a4afd393bc63f4a15a8d6cb551635356c937d7354db23cf3620de788c9623835d08b3af3afb816957c'
            '005e37efeb14bc32ce8ed58312bc97acd14267584b87b055a2340225c1509f427f22085b06c414fc0a425436b4779b262378e26f628441679a36a41fdef7f970'
            'e816cb8aeae607dfa3cb4c3e0f67410d83493fccb381951ec8426b8e1fd871ebead2f295df477071d0858f00ba601304868c90afc7ea720bf4adfb6fdf468343')

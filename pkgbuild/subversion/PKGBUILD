pkgname=subversion
pkgver=1.9.5
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('x86_64')
url="http://subversion.apache.org/"
license=('APACHE')
#depends=()
source=("https://archive.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2"
	"svnserve.default"
	"svnserve.service"
	"svnserve.tmpfiles")

build() {
	cd "${srcdir}/subversion-${pkgver}"

	export PYTHON="/usr/bin/python2"
	export RUBYDIR=$(ruby -e "require 'rbconfig'" -e "CONFIG = RbConfig::MAKEFILE_CONFIG" -e "puts RbConfig::expand(CONFIG[\"vendorarchdir\"])")

	sed -i "s#-Werror=unknown-warning-option ##g" configure

	./configure --prefix=/usr \
		--with-apr=/usr \
		--with-apr-util=/usr \
		--with-zlib=/usr \
		--with-serf=/usr \
		--with-sqlite=/usr \
		--with-gnome-keyring \
		--without-berkeley-db \
		--without-kwallet \
		--with-ruby-sitedir=${RUBYDIR}

	make
	make swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
		swig_pydir_extra=/usr/lib/python2.7/site-packages/svn swig-py swig-pl swig-rb
}

package() {
	cd "${srcdir}/subversion-${pkgver}"

	LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH} \
	make -j1 DESTDIR="${pkgdir}" INSTALLDIRS=vendor LT_LDFLAGS="-L${pkgdir}/usr/lib" \
		swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
		swig_pydir_extra=/usr/lib/python2.7/site-packages/svn \
		install install-swig-py install-swig-pl install-swig-rb

	rm -rf "${pkgdir}/usr/lib/perl"

	install -v -Dm644 "${srcdir}/svnserve.default" "${pkgdir}/etc/default/svnserve"
	install -v -Dm644 "${srcdir}/svnserve.service" "${pkgdir}/lib/systemd/system/svnserve.service"
	install -v -Dm644 "${srcdir}/svnserve.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/svnserve.conf"
}

md5sums=('9fcbae352a5efe73d46a88c97c6bba14'
         'f70afe5bba43d5c13602a46aec47e608'
         '99959855cafe04b52508edb846f0048f'
         '22dc8094bb162f8dc7ff19ab04d04e27')

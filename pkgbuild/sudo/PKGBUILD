pkgname=sudo
_pkgver=1.8.21p2
pkgver=${_pkgver/p/.p}
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('x86_64')
url="https://www.sudo.ws/sudo/"
license=('custom')
#depends=()
options=(emptydirs)
source=("https://www.sudo.ws/sudo/dist/sudo-${_pkgver}.tar.gz"
	"sudo.pam")

build() {
	cd "${srcdir}/sudo-${_pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--enable-tmpfiles.d \
		--with-all-insults \
		--with-env-editor \
		--with-linux-audit \
		--with-pam \
		--with-selinux \
		--with-rundir=/run/sudo \
		--with-passprompt="[sudo] password for %p: " \
		--with-secure-path=/sbin:/bin:/usr/sbin:/usr/bin:/usr/local/sbin:/usr/local/bin

	make
}

package() {
	cd "${srcdir}/sudo-${_pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/run"

	install -v -Dm644 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"
}

sha512sums=('f04bbff54ad74ba73c078e15c75d2f41332d4912078ed66157ba7346b7fff914bd0747460cb4cd0c472af2d3b344fa72f5c62c95169df68a9cac74d7245c720c'
            'a8c8ae1d8c500bcbefd48109af1b28932d17cec83ffcca9b04e2cd827c011c68106fddd7a1066b6f89927f9195286d60ab771d4006dd08e446d99c709741b8a9')

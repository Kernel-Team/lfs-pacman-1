pkgname=sudo
_pkgver=1.9.4p2
pkgver=${_pkgver/p/.p}
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('x86_64')
url="https://www.sudo.ws/"
license=('custom')
#depends=()
options=(emptydirs)
source=("https://www.sudo.ws/sudo/dist/sudo-${_pkgver}.tar.gz"
	"sudo.pam")

build() {
	cd "${srcdir}/sudo-${_pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--enable-tmpfiles.d \
		--with-all-insults \
		--with-env-editor \
		--with-pam \
		--with-rundir=/run/sudo \
		--with-passprompt="[sudo] password for %p: " \
		--with-secure-path=/sbin:/bin:/usr/sbin:/usr/bin:/usr/local/sbin:/usr/local/bin

	make
}

package() {
	cd "${srcdir}/sudo-${_pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/run"

	install -v -Dm644 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"
}

sha512sums=('5258531d3dca0e812df5f4f8f26665f643c036d42534e3b98f5dd463624e9df0bba2a92c15da0c9958bda7e68f6f10b7b34875792ac9c205d1e70a12bc4f98a4'
            'a8c8ae1d8c500bcbefd48109af1b28932d17cec83ffcca9b04e2cd827c011c68106fddd7a1066b6f89927f9195286d60ab771d4006dd08e446d99c709741b8a9')

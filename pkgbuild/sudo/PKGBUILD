pkgname=sudo
_pkgver=1.8.27
pkgver=${_pkgver/p/.p}
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('x86_64')
url="https://www.sudo.ws/sudo/"
license=('custom')
#depends=()
options=(emptydirs)
source=("https://www.sudo.ws/sudo/dist/sudo-${_pkgver}.tar.gz"
	"sudo.pam")

build() {
	cd "${srcdir}/sudo-${_pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--enable-tmpfiles.d \
		--with-all-insults \
		--with-env-editor \
		--with-pam \
		--with-rundir=/run/sudo \
		--with-passprompt="[sudo] password for %p: " \
		--with-secure-path=/sbin:/bin:/usr/sbin:/usr/bin:/usr/local/sbin:/usr/local/bin

	make
}

package() {
	cd "${srcdir}/sudo-${_pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/run"

	install -v -Dm644 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"
}

sha512sums=('0480def650ab880ab9e6c51c606a06897fd638f0381e99c038f5aa47d064aaa2fb35b73eee7f86e73185e18d5dbb8b6ba49c616b1785a1edb2dd6d7b2fa4fcac'
            'a8c8ae1d8c500bcbefd48109af1b28932d17cec83ffcca9b04e2cd827c011c68106fddd7a1066b6f89927f9195286d60ab771d4006dd08e446d99c709741b8a9')

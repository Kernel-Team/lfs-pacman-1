pkgname=syntax-highlighting
pkgver=5.37.0
pkgrel=1
pkgdesc="Syntax highlighting engine for structured text and code"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
#depends=()
options=(docs)
source=("https://download.kde.org/stable/frameworks/${pkgver:0:4}/syntax-highlighting-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/syntax-highlighting-${pkgver}"

	sed -i.orig 's|w-|w\\-|' data/syntax/rest.xml
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DKDE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DBUILD_TESTING=OFF \
		"${srcdir}/syntax-highlighting-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('d49f62934283c07c1b6e8fad11b9fa469b21bb55f35ddbc6c28ab29bd79b7cba67a93d0b46abb8b3d86ee7ba7a90ea50a98553aac7bdfe396bc2b010611c24c2')

pkgname=systemd
pkgver=239
pkgrel=1
pkgdesc="system and service manager"
arch=('x86_64')
url="https://www.github.com/systemd/systemd"
license=('GPL2' 'LGPL2.1')
#depends=()
backup=(etc/rc.local
	etc/systemd/coredump.conf
	etc/systemd/journald.conf
	etc/systemd/journal-remote.conf
	etc/systemd/journal-upload.conf
	etc/systemd/logind.conf
	etc/systemd/resolved.conf
	etc/systemd/system.conf
	etc/systemd/timesyncd.conf
	etc/systemd/user.conf)
options=(emptydirs)
install=systemd.install
source=("https://github.com/systemd/systemd/archive/v${pkgver}/systemd-${pkgver}.tar.gz"
	"locale.sh"
	"rc.local"
	"systemd-hook"
	"systemd-binfmt.hook"
	"systemd-catalog.hook"
	"systemd-daemon-reload.hook"
	"systemd-hwdb.hook"
	"systemd-sysctl.hook"
	"systemd-sysusers.hook"
	"systemd-tmpfiles.hook"
	"systemd-udev-reload.hook"
	"systemd-user")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/systemd-${pkgver}"

	sed -i 's/GROUP="render", //' rules/50-udev-default.rules.in
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dfirstboot=false \
		-Dseccomp=false \
		-Dselinux=false \
		-Dima=false \
		-Dsmack=false \
		-Dgnu-efi=true \
		-Dlibidn2=true \
		-Dsysvinit-path=/etc/init.d \
		-Dsysvrcnd-path=/etc \
		-Dhalt-local="" \
		-Drc-local=/etc/rc.local \
		-Ddbuspolicydir=/etc/dbus-1/system.d \
		-Ddbussessionservicedir=/usr/share/dbus-1/services \
		-Ddbussystemservicedir=/usr/share/dbus-1/system-services \
		-Dbashcompletiondir=no \
		-Drpmmacrosdir=no \
		-Dzshcompletiondir=no \
		-Ddefault-hierarchy=unified \
		-Dwheel-group=false \
		-Dnobody-group=nogroup \
		"${srcdir}/systemd-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}"/usr/lib/lib{systemd,udev}.so*

	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"
	rm -rf "${pkgdir}/usr/lib/sysusers.d/basic.conf"

	echo 'disable *' > "${pkgdir}/usr/lib/systemd/system-preset/99-default.preset"

	chown 27:root "${pkgdir}/usr/share/polkit-1/rules.d"
	chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d"

	chown root:systemd-journal "${pkgdir}/var/log/journal"
	chmod 2755 "${pkgdir}/var/log/journal"

	rm -rf "${pkgdir}/var/log/journal/remote"

	rm -rf "${pkgdir}/etc/init.d" "${pkgdir}/etc/systemd/system/ctrl-alt-del.target"
	rm -rf "${pkgdir}"/etc/systemd/system/multi-user.target.wants/systemd-{networkd,resolved,timesyncd}.service
	rm -rf "${pkgdir}"/etc/systemd/system/multi-user.target.wants/machines.target
	rm -rf "${pkgdir}"/etc/systemd/system/{network-online,sockets,sysinit}.target.wants
	rm -rf "${pkgdir}"/usr/lib/systemd/system/sysinit.target.wants/systemd-update-done.service

	sed -i "s@root lock@root root@g" "${pkgdir}/usr/lib/tmpfiles.d/legacy.conf"
	sed -i "s@lock 0755@lock 1777@g" "${pkgdir}/usr/lib/tmpfiles.d/legacy.conf"

	ln -sfv /dev/null "${pkgdir}/etc/udev/rules.d/80-net-setup-link.rules"

	install -v -Dm644 "${srcdir}/locale.sh" "${pkgdir}/etc/profile.d/locale.sh"
	install -v -Dm644 "${srcdir}/systemd-user" "${pkgdir}/etc/pam.d/systemd-user"

	install -v -Dm755 "${srcdir}/rc.local" "${pkgdir}/etc/rc.local"

	install -v -Dm755 "${srcdir}/systemd-hook" "${pkgdir}/usr/share/libalpm/scripts/systemd-hook"

	install -v -Dm644 "${srcdir}/systemd-binfmt.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-binfmt.hook"
	install -v -Dm644 "${srcdir}/systemd-catalog.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-catalog.hook"
	install -v -Dm644 "${srcdir}/systemd-daemon-reload.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-daemon-reload.hook"
	install -v -Dm644 "${srcdir}/systemd-hwdb.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-hwdb.hook"
	install -v -Dm644 "${srcdir}/systemd-sysctl.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-sysctl.hook"
	install -v -Dm644 "${srcdir}/systemd-sysusers.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-sysusers.hook"
	install -v -Dm644 "${srcdir}/systemd-tmpfiles.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-tmpfiles.hook"
	install -v -Dm644 "${srcdir}/systemd-udev-reload.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-udev-reload.hook"

	install -v -dm755 "${pkgdir}/usr/sbin"

	for file in halt poweroff reboot runlevel shutdown telinit
	do
		ln -sfv ../bin/systemctl "${pkgdir}/usr/sbin/${file}"
	done

	ln -sfv ../lib/systemd/systemd "${pkgdir}/usr/sbin/init"
}

sha512sums=('fd44590dfd148504c5ed1e67521efce50d84b627b7fc77015fa95dfa76d7a42297c56cc89eff40181809732024b16d48f2a87038cf435e0c63bc2b95ecd86b0f'
            'c0ea27d3ddc0ff93e18c2dcb2c30cd302c7e860809ec5543045683e6d14b07457a0a8234155d019a75314714585b57080feb6c9db956780954bf832f7c11599f'
            '212f12991ff0b236c50f34207db9de396fece7bee886143c26300d256655b25baa526ef08b90cd54e02a14fad6c7a06c6c003fb2c69602640e65f0f833225e9a'
            '462ed39bd5c90168079956a402abafe8f0910882e6876b165a2c27af73833d0cad1be9cdbcb3549b34652ea86e5d0dba044946a38797bd533fdd1f5a0083f63b'
            '46f93725bc94381300535737fd0186a3c096fa83661179eab0c450c7b164a87d9a5dd9abcf6ae98bdeb4bf50a4ba4f1944769948c236e4814f166ff03b0ee177'
            '4cff2ebd962e26e2f516d8b4ac45c839dbfa54dd0588b423c224a328b9f7c62306ca7b2f6cb55240c564caf9972d5bcd2e0efaf2de49d64729aeb3bc1560c9eb'
            '872de70325e9798f0b5a77e991c85bd2ab6de24d9b9ba4e35002d2dd5df15f8b30739a0042a624776177ffc14a838cde7ee98622016ed41df3efda9a659730b2'
            '471342b8d0e05533908cda5d6a906050a51e3181beda1239e91d717029ee40a9eaed714996a445417d87c4e31b7f8522a665de176077fe0536d538369594996d'
            '3b11e8956169e6d80eca6e6de1b3e42641454d9d7be48961d400754f2242077d69fb7bfbeb0904f35ce569511036a7c9614a4a1cc3096fba993f46ae65e02895'
            'bf3225011760695040e9f7be2560348e68e86eac0295f5a17a6f7e3dda7ad7c008812a15904e2071b53d5f8048891602c8a9a18608ac64930f2d8cc4fac2a319'
            'ff1429a7c88e21d578c25d07e8cd9568577feb5a940fe39a7a815cf8431c57ca951ac6b394c53d2cdeb4efc645572c0b1b670a48cafcc405db41a6602b548e35'
            'e4a9d7607fe93daf1d45270971c8d8455c4bfc2c0bea8bcad05aeb89847edee23cd1a41073a72042622acf417018fe254f5bfc137604fe2c71292680bf67a1c2'
            '5f48d342537c284ba85477bf9f78e93a46daf868381cd10a130d4f421687526dd4ee84b62cdf9564579506248d41d8c39265848bfc4047cdcf42e36766220a2e')

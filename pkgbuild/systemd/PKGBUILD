pkgname=systemd
pkgver=234
pkgrel=1
pkgdesc="system and service manager"
arch=('x86_64')
url="https://www.github.com/systemd/systemd"
license=('GPL2' 'LGPL2.1')
#depends=()
backup=(etc/rc.local
	etc/systemd/coredump.conf
	etc/systemd/journald.conf
	etc/systemd/journal-remote.conf
	etc/systemd/journal-upload.conf
	etc/systemd/logind.conf
	etc/systemd/resolved.conf
	etc/systemd/system.conf
	etc/systemd/timesyncd.conf
	etc/systemd/user.conf)
options=(emptydirs)
install=systemd.install
source=("https://github.com/systemd/systemd/archive/v${pkgver}/systemd-${pkgver}.tar.gz"
	"locale.sh"
	"rc.local"
	"systemd-hwdb.hook"
	"systemd-sysusers.hook"
	"systemd-tmpfiles.hook"
	"systemd-user")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/systemd-${pkgver}"

	sed -i "/MemoryDenyWriteExecute=yes/d" units/systemd-timesyncd.service.in
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dfirstboot=false \
		-Dima=false \
		-Dsmack=false \
		-Dwheel-group=false \
		-Dpamlibdir=/usr/lib/security \
		-Dgnu-efi=true \
		-Dsysvinit-path=/etc/init.d \
		-Dsysvrcnd-path=/etc \
		-Dhalt-local="" \
		-Drc-local=/etc/rc.local \
		-Ddbuspolicydir=/etc/dbus-1/system.d \
		-Ddbussessionservicedir=/usr/share/dbus-1/services \
		-Ddbussystemservicedir=/usr/share/dbus-1/system-services \
		-Dbashcompletiondir=no \
		-Drpmmacrosdir=no \
		-Dzshcompletiondir=no \
		-Dnobody-group=nogroup \
		"${srcdir}/systemd-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install

	rm -rf "${pkgdir}"/usr/lib/lib{systemd,udev}.so*

	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"
	rm -rf "${pkgdir}/usr/lib/sysusers.d/basic.conf"

	echo 'disable *' > "${pkgdir}/usr/lib/systemd/system-preset/99-default.preset"

	chown 27:root "${pkgdir}/usr/share/polkit-1/rules.d"
	chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d"

	chown root:systemd-journal "${pkgdir}/var/log/journal"
	chmod 2755 "${pkgdir}/var/log/journal"

	rm -rf "${pkgdir}/var/log/journal/remote"

	rm -rf "${pkgdir}/etc/init.d" "${pkgdir}/etc/systemd/system/ctrl-alt-del.target"
	rm -rf "${pkgdir}"/etc/systemd/system/multi-user.target.wants/systemd-{networkd,resolved,timesyncd}.service
	rm -rf "${pkgdir}"/etc/systemd/system/multi-user.target.wants/machines.target
	rm -rf "${pkgdir}"/etc/systemd/system/{network-online,sockets,sysinit}.target.wants
	rm -rf "${pkgdir}"/usr/lib/systemd/system/sysinit.target.wants/systemd-update-done.service

	sed -i "s@root lock@root root@g" "${pkgdir}/usr/lib/tmpfiles.d/legacy.conf"
	sed -i "s@lock 0755@lock 1777@g" "${pkgdir}/usr/lib/tmpfiles.d/legacy.conf"

	ln -sfv /dev/null "${pkgdir}/etc/udev/rules.d/80-net-setup-link.rules"

	install -v -Dm644 "${srcdir}/locale.sh" "${pkgdir}/etc/profile.d/locale.sh"
	install -v -Dm644 "${srcdir}/systemd-user" "${pkgdir}/etc/pam.d/systemd-user"

	install -v -Dm755 "${srcdir}/rc.local" "${pkgdir}/etc/rc.local"

	install -v -Dm644 "${srcdir}/systemd-hwdb.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-hwdb.hook"
	install -v -Dm644 "${srcdir}/systemd-sysusers.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-sysusers.hook"
	install -v -Dm644 "${srcdir}/systemd-tmpfiles.hook" "${pkgdir}/usr/share/libalpm/hooks/systemd-tmpfiles.hook"

	install -v -dm755 "${pkgdir}/usr/sbin"

	for file in halt poweroff reboot runlevel shutdown telinit
	do
		ln -sfv ../bin/systemctl "${pkgdir}/usr/sbin/${file}"
	done

	ln -sfv ../lib/systemd/systemd "${pkgdir}/usr/sbin/init"
}

sha512sums=('762336a7d96c6583cf71cad62efce95a0ed93cd0a0d7251f128d10dba8200c0c8df0e5a7d168179ababa5b221295a231e73b7e7ea2697cb3fb5c1b33538efa68'
            'c0ea27d3ddc0ff93e18c2dcb2c30cd302c7e860809ec5543045683e6d14b07457a0a8234155d019a75314714585b57080feb6c9db956780954bf832f7c11599f'
            '212f12991ff0b236c50f34207db9de396fece7bee886143c26300d256655b25baa526ef08b90cd54e02a14fad6c7a06c6c003fb2c69602640e65f0f833225e9a'
            '3db62a75c89a8fc1a9ecfa950af24dcc5ab12861b7b8c742b2f68b9d875dc754b6aecbc5d09a411bad1eb35bbd791f8b624ebb618b9e0c0b90a3323c7e795c49'
            '9d27d97f172a503f5b7044480a0b9ccc0c4ed5dbb2eb3b2b1aa929332c3bcfe38ef0c0310b6566f23b34f9c05b77035221164a7ab7677784c4a54664f12fca22'
            '0f4efddd25256e09c42b953caeee4b93eb49ecc6eaebf02e616b4dcbfdac9860c3d8a3d1a106325b2ebc4dbc6e08ac46702abcb67a06737227ccb052aaa2a067'
            '5f48d342537c284ba85477bf9f78e93a46daf868381cd10a130d4f421687526dd4ee84b62cdf9564579506248d41d8c39265848bfc4047cdcf42e36766220a2e')

pkgname=tdb
pkgver=1.3.16
pkgrel=1
pkgdesc="A Trivial Database similar to GDBM but allows simultaneous commits"
arch=('x86_64')
url="https://tdb.samba.org/"
license=('GPL3')
#depends=()
source=("https://www.samba.org/ftp/tdb/tdb-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/tdb-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc/samba \
		--localstatedir=/var \
		--disable-rpath \
		--disable-rpath-install \
		--bundled-libraries=NONE \
		--builtin-libraries=replace

	make
}

package() {
	cd "${srcdir}/tdb-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('7b17852986e48a32f3f8f303dd2a26503a69fcf7849f22f51483334c9abda9f189b521679e51b4ee5a80197a8f304a084dde0f56d92cfe953d3a4ede557526d2')

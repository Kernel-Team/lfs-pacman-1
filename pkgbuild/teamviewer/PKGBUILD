pkgname=teamviewer
pkgver=14.2.2558
pkgrel=1
pkgdesc="All-In-One Software for Remote Support and Online Meetings"
arch=('x86_64')
url="https://www.teamviewer.com/"
license=('custom')
#depends=()
options=(emptydirs !strip)
source=("https://dl.tvcdn.de/download/linux/version_${pkgver:0:2}x/teamviewer_${pkgver}_amd64.tar.xz")

package() {
	install -v -dm755 "${pkgdir}/etc/teamviewer"
	install -v -dm755 "${pkgdir}/opt"
	install -v -dm755 "${pkgdir}/var/log/teamviewer${pkgver:0:2}"

	cd "${srcdir}"

	sed -i "/TV_PKGTYPE/s|TAR_NI|TAR_IN|g" teamviewer/tv_bin/script/tvw_config

	cp -a teamviewer "${pkgdir}/opt"

	cd "${srcdir}/teamviewer/tv_bin/script"

	install -v -Dm644 teamviewerd.service "${pkgdir}/usr/lib/systemd/system/teamviewerd.service"

	install -v -Dm644 com.teamviewer.TeamViewer.Desktop.service "${pkgdir}/usr/share/dbus-1/services/com.teamviewer.TeamViewer.Desktop.service"
	install -v -Dm644 com.teamviewer.TeamViewer.service "${pkgdir}/usr/share/dbus-1/services/com.teamviewer.TeamViewer.service"
	install -v -Dm644 com.teamviewer.TeamViewer.policy "${pkgdir}/usr/share/polkit-1/actions/com.teamviewer.TeamViewer.policy"

	cd "${srcdir}/teamviewer/tv_bin/desktop"

	install -v -Dm644 com.teamviewer.TeamViewer.desktop "${pkgdir}/usr/share/applications/com.teamviewer.TeamViewer.desktop"

	for sz in 16 20 24 32 48 256
	do
		install -v -Dm644 teamviewer_${sz}.png "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/apps/TeamViewer.png"
	done
}

sha512sums=('c64ca29d0a90c597c61993e90d6f66d85ffe3c9d8c59115abbde8814e2141c1e69092b8ce16aac413ed1bfa5eb2bd47cb9f0f737f14b22055a8a92c120e8a66e')

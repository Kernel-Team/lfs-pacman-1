pkgname=texlive-bin
pkgver=20190410
pkgrel=1
pkgdesc="TeX Live distribution"
arch=('x86_64')
url="http://tug.org/texlive/"
license=('GPL')
#depends=()
install=texlive.install
source=("ftp://tug.org/texlive/historic/${pkgver:0:4}/texlive-${pkgver}-source.tar.xz"
	"ftp://tug.org/texlive/historic/${pkgver:0:4}/texlive-${pkgver}-extra.tar.xz")
noextract=("texlive-${pkgver}-extra.tar.xz")

prepare() {
	cd "${srcdir}/texlive-${pkgver}-source"

	sed -i "s#have_libgs#HAVE_LIBGS#g" texk/dvisvgm/configure

	#patch -Np1 -i "${srcdir}/texlive-poppler.patch"

	#cp -v texk/web2c/pdftexdir/pdftoepdf{-newpoppler,}.cc
	#cp -v texk/web2c/pdftexdir/pdftosrc{-newpoppler,}.cc

	install -v -dm755 build
}

build() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	#CXXFLAGS+=" -Wno-reserved-user-defined-literal" # Required for building in C++11 mode
	#export CXX="${CXX} -std=c++11"

	# An ugly hack, to avoid future rebuilds when bumping poppler
	# export LDFLAGS="-Wl,-rpath,/usr/lib/texlive-poppler"

	../configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--datarootdir=/usr/share \
		--datadir=/usr/share \
		--disable-native-texlive-build \
		--disable-dialog \
		--disable-multiplatform \
		--disable-psutils \
		--disable-t1utils \
		--enable-shared \
		--with-system-cairo \
		--with-system-freetype2 \
		--with-system-gd \
		--with-system-gmp \
		--with-system-graphite2 \
		--with-system-harfbuzz \
		--with-system-icu \
		--with-system-libpaper \
		--with-system-libpng \
		--with-system-mpfr \
		--with-system-pixman \
		--with-system-zlib \
		--with-system-zziplib \
		--with-banner-add=" - Krejzi"

		#--with-system-poppler \
		#--with-system-xpdf \

	make
}

package() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	make install DESTDIR="${pkgdir}"
	make texlinks DESTDIR="${pkgdir}"

	tar --wildcards --strip-components=1 -xf "${srcdir}/texlive-${pkgver}-extra.tar.xz" -C "${pkgdir}/usr/share" "*/tlpkg"

	rm -rf "${pkgdir}/usr/share/texmf-dist"

	# Add current libpoppler.so
	#install -v -dm755 "${pkgdir}/usr/lib/texlive-poppler"
	#cp -rv /usr/lib/libpoppler.so.* "${pkgdir}/usr/lib/texlive-poppler"
}

sha512sums=('7759d8e668e53c0c73e48907096da9d8fb6384dd9c3342ddfb05e17d077054c5351d35256cf4e63a6da502de2f09f521979bede7209cdd5bd8ae80eba0efed2e'
            '528030c5d5ae48daf2bb7a0f0ac602372cec8c6489786a210122e304f140b231e0b91569e2e562d1331aa5eedc93c7c1d94eaa2233b7476d390641a748a723d2')

pkgname=texlive-bin
pkgver=20170524
pkgrel=1
pkgdesc="TeX Live distribution"
arch=('x86_64')
url="http://tug.org/texlive/"
license=('GPL')
#depends=()
install=texlive.install
source=("ftp://tug.org/texlive/historic/2017/texlive-${pkgver}-source.tar.xz"
	"ftp://tug.org/texlive/historic/2017/texlive-${pkgver}-extra.tar.xz"
	"texlive-poppler.patch")
noextract=("texlive-${pkgver}-extra.tar.xz")

prepare() {
	cd "${srcdir}/texlive-${pkgver}-source"

	patch -Np1 -i "${srcdir}/texlive-poppler.patch"

	install -v -dm755 build
}

build() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	CXXFLAGS+=" -Wno-reserved-user-defined-literal" # Required for building in C++11 mode

	export CXX="${CXX} -std=c++11"

	# An ugly hack, to avoid future rebuilds when bumping poppler
	export LDFLAGS="-Wl,-rpath,/usr/lib/texlive-poppler"

	../configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--datarootdir=/usr/share \
		--datadir=/usr/share \
		--disable-native-texlive-build \
		--disable-dialog \
		--disable-multiplatform \
		--disable-psutils \
		--disable-t1utils \
		--enable-shared \
		--with-system-cairo \
		--with-system-freetype2 \
		--with-system-gd \
		--with-system-gmp \
		--with-system-graphite2 \
		--with-system-harfbuzz \
		--with-system-icu \
		--with-system-kpathsea \
		--with-system-libgs \
		--with-system-libpaper \
		--with-system-libpng \
		--with-system-mpfr \
		--with-system-pixman \
		--with-system-poppler \
		--with-system-xpdf \
		--with-system-zlib \
		--with-system-zziplib \
		--with-banner-add=" - Krejzi"

	make
}

package() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	make install DESTDIR="${pkgdir}"
	make texlinks DESTDIR="${pkgdir}"

	tar --wildcards --strip-components=1 -xf "${srcdir}/texlive-${pkgver}-extra.tar.xz" -C "${pkgdir}/usr/share" "*/tlpkg"

	rm -rf "${pkgdir}/usr/share/texmf-dist"

	# Add current libpoppler.so
	install -v -dm755 "${pkgdir}/usr/lib/texlive-poppler"
	cp -rv /usr/lib/libpoppler.so.* "${pkgdir}/usr/lib/texlive-poppler"
}

sha512sums=('18c18940992ab94ce22e22ccd4ad798253ea14ac2ab296a10ea5e9a8da9f33989f2e2641b42b564d1d5fb53bd371da3e68726b676d706b469016ad146cd19daa'
            '5f32f0dfeadb70412918c9d876d744f4c595310cbad8d343619d2170b22105f97eab63bbae288d0ac08ae52e49250dd2d0d26fb4cde0968dcfba4aea49878acb'
            '65a78a7931f8b03acc62f8cf160993f2d0932ebf18f42e380db6034c17ad5cb737f941fbc4b35d61d5019975efdfb5962dfb6bcc3198ab3cffdddb4eb2a8489b')

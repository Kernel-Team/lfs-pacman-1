pkgname=texlive-bin
pkgver=20180414
pkgrel=1
pkgdesc="TeX Live distribution"
arch=('x86_64')
url="http://tug.org/texlive/"
license=('GPL')
#depends=()
install=texlive.install
source=("ftp://tug.org/texlive/historic/${pkgver:0:4}/texlive-${pkgver}-source.tar.xz"
	"ftp://tug.org/texlive/historic/${pkgver:0:4}/texlive-${pkgver}-extra.tar.xz"
	"texlive-poppler.patch")
noextract=("texlive-${pkgver}-extra.tar.xz")

prepare() {
	cd "${srcdir}/texlive-${pkgver}-source"

	patch -Np1 -i "${srcdir}/texlive-poppler.patch"

	cp -v texk/web2c/pdftexdir/pdftoepdf{-newpoppler,}.cc
	cp -v texk/web2c/pdftexdir/pdftosrc{-newpoppler,}.cc

	install -v -dm755 build
}

build() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	#CXXFLAGS+=" -Wno-reserved-user-defined-literal" # Required for building in C++11 mode
	#export CXX="${CXX} -std=c++11"

	# An ugly hack, to avoid future rebuilds when bumping poppler
	export LDFLAGS="-Wl,-rpath,/usr/lib/texlive-poppler"

	../configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--datarootdir=/usr/share \
		--datadir=/usr/share \
		--disable-native-texlive-build \
		--disable-dialog \
		--disable-multiplatform \
		--disable-psutils \
		--disable-t1utils \
		--enable-shared \
		--with-system-cairo \
		--with-system-freetype2 \
		--with-system-gd \
		--with-system-gmp \
		--with-system-graphite2 \
		--with-system-harfbuzz \
		--with-system-icu \
		--with-system-kpathsea \
		--with-system-libpaper \
		--with-system-libpng \
		--with-system-mpfr \
		--with-system-pixman \
		--with-system-poppler \
		--with-system-xpdf \
		--with-system-zlib \
		--with-system-zziplib \
		--with-banner-add=" - Krejzi"

	make
}

package() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	make install DESTDIR="${pkgdir}"
	make texlinks DESTDIR="${pkgdir}"

	tar --wildcards --strip-components=1 -xf "${srcdir}/texlive-${pkgver}-extra.tar.xz" -C "${pkgdir}/usr/share" "*/tlpkg"

	rm -rf "${pkgdir}/usr/share/texmf-dist"

	# Add current libpoppler.so
	install -v -dm755 "${pkgdir}/usr/lib/texlive-poppler"
	cp -rv /usr/lib/libpoppler.so.* "${pkgdir}/usr/lib/texlive-poppler"
}

sha512sums=('ecac9b8203d2747be7ba7f82d68096d859fb35713276106d7049035dd3a8cfca07f8a8648982f0317b3812f8480db7326714de32f95998e3e6f73571de2aa140'
            'd8d33208ce4e74dcae45d9c91805399d2c226e2ba0f9387bb18e791021bbe5d8e9e676254e3d10b5919f338b5e88ad8c5c64733b43934411a85453f41924e1cc'
            '3a2f466a0e82d14aa977faa71ca03594b4544c4c103e76e345f8f86efe3329d4d180b4d1c98e5eba6675057e217b9fc5e7eab6b942f920c9da3b481041039361')

pkgname=texlive-texmf
pkgver=20200406
pkgrel=1
pkgdesc="TeX Live distribution - TeXmf Data"
arch=('x86_64')
url="http://tug.org/texlive/"
license=('GPL')
#depends=()
options=(!strip)
source=("ftp://tug.org/texlive/historic/${pkgver:0:4}/texlive-${pkgver}-texmf.tar.xz")
noextract=("texlive-${pkgver}-texmf.tar.xz")

package() {
	cd "${srcdir}"

	install -dm755 "${pkgdir}/usr/share"

	bsdtar  --exclude=*/texmf-dist/doc --exclude=*/texmf-dist/source \
		--no-same-permissions --strip-components=1 \
		-xf texlive-${pkgver}-texmf.tar.xz -C "${pkgdir}/usr/share"

	sed -i "s@TEXMFROOT = \$SELFAUTOPARENT@TEXMFROOT = /usr/share@g" "${pkgdir}/usr/share/texmf-dist/web2c/texmf.cnf"
	sed -i "s@TEXMFSYSVAR = \$TEXMFROOT/texmf-var@TEXMFSYSVAR = /var/lib/texmf@g" "${pkgdir}/usr/share/texmf-dist/web2c/texmf.cnf"
	sed -i "s@TEXMFSYSCONFIG = \$TEXMFROOT/texmf-config@TEXMFSYSCONFIG = /etc/texmf@g" "${pkgdir}/usr/share/texmf-dist/web2c/texmf.cnf"
	sed -i "s@TEXMFHOME = ~/texmf@TEXMFHOME = ~/.texmf@g" "${pkgdir}/usr/share/texmf-dist/web2c/texmf.cnf"
}

sha512sums=('8e23beb9ad08adde5081dc939678630f7b8cfa795f656395c8da71afffe04768fde537976db676311918aac40d1bfd2e460cf14b3771ed5e474cf4658efccda9')

pkgname=texlive-texmf
pkgver=20180414
pkgrel=1
pkgdesc="TeX Live distribution - TeXmf Data"
arch=('x86_64')
url="http://tug.org/texlive/"
license=('GPL')
#depends=()
options=(!strip)
source=("ftp://tug.org/texlive/historic/${pkgver:0:4}/texlive-${pkgver}-texmf.tar.xz")
noextract=("texlive-${pkgver}-texmf.tar.xz")

package() {
	cd "${srcdir}"

	install -dm755 "${pkgdir}/usr/share"

	bsdtar  --exclude=*/texmf-dist/doc --exclude=*/texmf-dist/source \
		--no-same-permissions --strip-components=1 \
		-xf texlive-${pkgver}-texmf.tar.xz -C "${pkgdir}/usr/share"

	# libkpathsea
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktex.opt
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktexdir{,.opt}
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktexnam{,.opt}
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktexupd
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/texmf.cnf
}

sha512sums=('024617b75ccbff064cf32dfafa535209e6c29aafe0b33e1ed12722c49a6b00f3ddfd04e985afee47c0a32a8de5d0944e946191b592e930dabd2e1c17f113dbf4')

pkgname=texlive
pkgver=20170524
pkgrel=1
pkgdesc="TeX Live distribution"
arch=('x86_64')
url="http://tug.org/texlive/"
license=('GPL')
#depends=()
install=texlive.install
options=(!strip)
source=("ftp://tug.org/texlive/historic/2017/texlive-${pkgver}-source.tar.xz"
	"ftp://tug.org/texlive/historic/2017/texlive-${pkgver}-texmf.tar.xz"
	"ftp://tug.org/texlive/historic/2017/texlive-${pkgver}-extra.tar.xz"
	"texlive-poppler.patch")
noextract=("texlive-${pkgver}-texmf.tar.xz"
	   "texlive-${pkgver}-extra.tar.xz")

prepare() {
	cd "${srcdir}/texlive-${pkgver}-source"

	patch -Np1 -i "${srcdir}/texlive-poppler.patch"

	install -v -dm755 build
}

build() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	CXXFLAGS+=" -Wno-reserved-user-defined-literal" # Required for building in C++11 mode

	export CXX="${CXX} -std=c++11"

	# An ugly hack, to avoid future rebuilds when bumping poppler
	export LDFLAGS="-Wl,-rpath,/usr/lib/texlive-poppler"

	../configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--datarootdir=/usr/share \
		--datadir=/usr/share \
		--disable-native-texlive-build \
		--disable-dialog \
		--disable-multiplatform \
		--disable-psutils \
		--disable-t1utils \
		--enable-shared \
		--with-system-cairo \
		--with-system-freetype2 \
		--with-system-gd \
		--with-system-gmp \
		--with-system-graphite2 \
		--with-system-harfbuzz \
		--with-system-icu \
		--with-system-kpathsea \
		--with-system-libgs \
		--with-system-libpaper \
		--with-system-libpng \
		--with-system-mpfr \
		--with-system-pixman \
		--with-system-poppler \
		--with-system-xpdf \
		--with-system-zlib \
		--with-system-zziplib \
		--with-banner-add=" - Krejzi"

	make
}

package() {
	cd "${srcdir}/texlive-${pkgver}-source/build"

	make install DESTDIR="${pkgdir}"
	make texlinks DESTDIR="${pkgdir}"

	tar --exclude=*/texmf-dist/doc --exclude=*/texmf-dist/source --no-same-permissions --strip-components=1 -xf "${srcdir}/texlive-${pkgver}-texmf.tar.xz" -C "${pkgdir}/usr/share"
	tar --wildcards --strip-components=1 -xf "${srcdir}/texlive-${pkgver}-extra.tar.xz" -C "${pkgdir}/usr/share" "*/tlpkg"

	# libkpathsea
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktex.opt
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktexdir{,.opt}
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktexnam{,.opt}
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/mktexupd
	rm -rf "${pkgdir}"/usr/share/texmf-dist/web2c/texmf.cnf

	# Add current libpoppler.so
	install -v -dm755 "${pkgdir}/usr/lib/texlive-poppler"
	cp -rv /usr/lib/libpoppler.so.* "${pkgdir}/usr/lib/texlive-poppler"

	# Manual strip, pacman strip is taking ages
	find "${pkgdir}/usr/bin" "${pkgdir}/usr/lib" -type f 2>/dev/null | while read BUILD_BINARY
	do
		case "$(file -bi "${BUILD_BINARY}")" in *application/x-sharedlib* | *application/x-executable*)
			strip --strip-unneeded "${BUILD_BINARY}"
		esac
	done
}

sha512sums=('18c18940992ab94ce22e22ccd4ad798253ea14ac2ab296a10ea5e9a8da9f33989f2e2641b42b564d1d5fb53bd371da3e68726b676d706b469016ad146cd19daa'
            'b421267ca03034aa0c9104707096a0c4ace71c2efa700e0f9d709ecb02907f7d0480d59cca56511d3089ecd57394ee796a893a24021cf4b2d1884314390a9acb'
            '5f32f0dfeadb70412918c9d876d744f4c595310cbad8d343619d2170b22105f97eab63bbae288d0ac08ae52e49250dd2d0d26fb4cde0968dcfba4aea49878acb'
            'af141cb481da9a4c806bc9a28a9ba13d7bebf73847ebe666a0da7920516107779ad2792e40f0350be124882c1f9992fe035b431917d1f633ddff04dd2161a989')

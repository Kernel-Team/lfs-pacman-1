pkgname=tk
_tkmajor=8.6
_tkminor=6
pkgver=${_tkmajor}.${_tkminor}
pkgrel=1
pkgdesc="A windowing toolkit for use with Tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/tcl/tk${pkgver}-src.tar.gz")

build() {
	cd "${srcdir}/tk${pkgver}/unix"

	./configure --prefix=/usr \
		--mandir=/usr/share/man \
		--enable-64bit \
		--enable-threads

	make
}

package() {
	cd "${srcdir}/tk${pkgver}/unix"

	sed -e "s@^\(TK_SRC_DIR='\).*@\1/usr/include'@" \
		-e "/TK_B/s@='\(-L\)\?.*unix@='\1/usr/lib@" \
		-i tkConfig.sh

	make install install-private-headers DESTDIR="${pkgdir}"

	ln -sfv wish${_tkmajor} "${pkgdir}/usr/bin/wish"

	chmod 644 "${pkgdir}/usr/lib/libtkstub${_tkmajor}.a"
}

sha512sums=('223c72ab5a6f6040928c56843598b878efafab44ed4152a38e68aa2800f2c181adc50a8bf23279b7d446493eec43854920e8b649b6a34ecdf31ecc07ccc74044')

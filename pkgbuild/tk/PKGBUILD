pkgname=tk
_tkmajor=8.6
_tkminor=7
pkgver=${_tkmajor}.${_tkminor}
pkgrel=1
pkgdesc="A windowing toolkit for use with Tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/tcl/tk${pkgver}-src.tar.gz")

build() {
	cd "${srcdir}/tk${pkgver}/unix"

	./configure --prefix=/usr \
		--mandir=/usr/share/man \
		--enable-64bit \
		--enable-threads

	make
}

package() {
	cd "${srcdir}/tk${pkgver}/unix"

	sed -e "s@^\(TK_SRC_DIR='\).*@\1/usr/include'@" \
		-e "/TK_B/s@='\(-L\)\?.*unix@='\1/usr/lib@" \
		-i tkConfig.sh

	make install install-private-headers DESTDIR="${pkgdir}"

	ln -sfv wish${_tkmajor} "${pkgdir}/usr/bin/wish"

	chmod 644 "${pkgdir}/usr/lib/libtkstub${_tkmajor}.a"
}

sha512sums=('adf35991b20878d18a6940cb3d24d821586828537edf6d20f9223f2989d317adbcbb5360f3226014d96fe44e7ec06cfdf434309438c90a09878345b7106c7dd9')

pkgname=tk
_tkmajor=8.6
_tkminor=8
pkgver=${_tkmajor}.${_tkminor}
pkgrel=1
pkgdesc="A windowing toolkit for use with Tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
#depends=()
source=("https://downloads.sourceforge.net/tcl/tk${pkgver}-src.tar.gz")

build() {
	cd "${srcdir}/tk${pkgver}/unix"

	./configure --prefix=/usr \
		--mandir=/usr/share/man \
		--enable-64bit \
		--enable-threads

	make
}

package() {
	cd "${srcdir}/tk${pkgver}/unix"

	sed -e "s@^\(TK_SRC_DIR='\).*@\1/usr/include'@" \
		-e "/TK_B/s@='\(-L\)\?.*unix@='\1/usr/lib@" \
		-i tkConfig.sh

	make install install-private-headers DESTDIR="${pkgdir}"

	ln -sfv wish${_tkmajor} "${pkgdir}/usr/bin/wish"

	chmod 644 "${pkgdir}/usr/lib/libtkstub${_tkmajor}.a"
}

sha512sums=('d0d30637b733fb0b0d38461b6d872934c04af422fcdb7ec41a72331b2529cc94414e6f441f95a98642d69505e4f41469847fedd1cbe58bd08cdd818fcabd1db9')

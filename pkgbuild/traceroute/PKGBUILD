pkgname=traceroute
pkgver=2.1.0
pkgrel=1
pkgdesc="Tracks the route taken by packets over an IP network"
arch=('x86_64')
url="http://traceroute.sourceforge.net/"
license=('GPL2')
#depends=()
source=("https://downloads.sourceforge.net/traceroute/traceroute-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/traceroute-${pkgver}"

	sed -i "s:-O2:${CFLAGS}:g;/CC/s:gcc:${CC:-gcc}:g" Make.rules
}

build() {
	cd "${srcdir}/traceroute-${pkgver}"

	unset CFLAGS

	make
}

package() {
	cd "${srcdir}/traceroute-${pkgver}"

	make install prefix=/usr DESTDIR="${pkgdir}"

	ln -sfv traceroute "${pkgdir}/usr/bin/traceroute6"
	ln -sfv traceroute.8 "${pkgdir}/usr/share/man/man8/traceroute6.8"
}

sha512sums=('3578007c734091ea0c906637c03fd133a8b0154fbf2e6b5c0c881184947918196bc03aeaf872d3bd53777b9b771cba5cf97f73fb5916bb53b75037f429b40ed3')

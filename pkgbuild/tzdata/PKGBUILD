pkgname=tzdata
pkgver=2017c
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('any')
url="http://www.iana.org/time-zones"
license=('GPL')
options=(!strip)
source=("https://www.iana.org/time-zones/repository/releases/tzdata${pkgver}.tar.gz")

timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'pacificnew' 'etcetera' 'backward'
           'systemv' 'factory')

package() {
	cd "${srcdir}"

	zic -y ./yearistype -d "${pkgdir}/usr/share/zoneinfo" ${timezones[@]}
	zic -y ./yearistype -d "${pkgdir}/usr/share/zoneinfo/posix" ${timezones[@]}
	zic -y ./yearistype -d "${pkgdir}/usr/share/zoneinfo/right" -L leapseconds ${timezones[@]}
  
	zic -y ./yearistype -d "${pkgdir}/usr/share/zoneinfo" -p America/New_York
	install -m444 -t "${pkgdir}/usr/share/zoneinfo" iso3166.tab zone1970.tab zone.tab
}

sha512sums=('feb5c544e617b30c73a397e5a1c51f9847d0fddf1dfa0f9ace2e6f686786f09b666937b2048e07075c84593fc584bd5a25d639fce70eca9735267df28621ab4c')

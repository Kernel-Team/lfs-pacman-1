pkgname=uchardet
pkgver=0.0.6
pkgrel=1
pkgdesc="Encoding detector library ported from Mozilla"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/uchardet/"
license=('MPL')
#depends=()
source=("https://www.freedesktop.org/software/uchardet/releases/uchardet-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		"${srcdir}/uchardet-${pkgver}"

	make
}

package() {
	cd "${srcdir}/build"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('eceeadae060bf277e298d709856609dde32921271140dc1fb0a33c7b6e1381033fc2960d616ebbd82c92815936864d2c0743b1b5ea1b7d4a200df87df80d6de5')

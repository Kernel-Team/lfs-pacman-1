pkgname=unrar
pkgver=5.5.6
pkgrel=1
pkgdesc="The RAR uncompression program"
arch=('x86_64')
url="http://www.rarlab.com/rar_add.htm"
license=('custom')
#depends=()
source=("http://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/unrar"

	sed -i "s:-O2:${CFLAGS} -w:g;/CXX/s:c++:${CXX}:g" makefile
}

build() {
	cd "${srcdir}/unrar"

	unset CFLAGS CXXFLAGS

	make
}

package() {
	cd "${srcdir}/unrar"

	install -v -Dm755 unrar "${pkgdir}/usr/bin/unrar"
}


sha512sums=('591575ba573e7f1b658041595b8439c7f1f60d354bc2b0a6aa9d3be6340d4892b2752e8ce94a928cf0270a0e1c491ce3ee59b3c36167174c53cfd657b9c85837')

pkgname=unzip
pkgver=6.0
_pkgver=${pkgver/./}
pkgrel=1
pkgdesc="For extracting and viewing files in .zip archives"
arch=('x86_64')
url="http://www.info-zip.org/UnZip.html"
license=('custom')
#depends=()
source=("http://downloads.sourceforge.net/infozip/${pkgname}${_pkgver}.tar.gz"
	"unzip-fixes.patch")

export CC=clang

prepare() {
	cd "${srcdir}/unzip${_pkgver}"

	patch -Np1 -i "${srcdir}/unzip-fixes.patch"

	sed -i "s/CFLAGS=\"-O -Wall/& -DNO_LCHMOD -fPIC ${CFLAGS}/" unix/Makefile
	sed -i "s/=gcc/=${CC}/" unix/Makefile
}

build() {
	cd "${srcdir}/unzip${_pkgver}"

	unset CFLAGS

	make -f unix/Makefile linux_noasm
}

package() {
	cd "${srcdir}/unzip${_pkgver}"

	make -f unix/Makefile install prefix="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man/man1"
}

sha512sums=('0694e403ebc57b37218e00ec1a406cae5cc9c5b52b6798e0d4590840b6cdbf9ddc0d9471f67af783e960f8fa2e620394d51384257dca23d06bcd90224a80ce5d'
            'f2817c5ad02dfce2e0ac10dd2d7bea3ba3143ac19376be0244e4ad20b5c99c2385ca217d45e8cbcb68e69f3673c759ec238bd1f1d5305af0fd1e26ec331b9bc2')

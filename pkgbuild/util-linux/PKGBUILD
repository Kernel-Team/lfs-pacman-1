pkgname=util-linux
pkgver=2.30.2
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
arch=('x86_64')
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/util-linux/v${pkgver:0:4}/util-linux-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/util-linux-${pkgver}"

	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--libdir=/usr/lib \
		--sbindir=/usr/sbin \
		--enable-mesg \
		--disable-nologin \
		--disable-chfn-chsh \
		--disable-su \
		--disable-login \
		--disable-kill \
		--with-systemd \
		--enable-fs-paths-default=/usr/sbin \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		PYTHON=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/util-linux-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/*.so* "${pkgdir}"/usr/lib/*.a
	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"

	rm -rf "${pkgdir}"/usr/bin/{mount,umount}
	rm -rf "${pkgdir}"/usr/share/man/man8/{mount,umount}.8
	rm -rf "${pkgdir}/usr/share/man/man5/fstab.5"
}

sha512sums=('b92ec16d5e8a405ad9ebbac8673bb49c5bf0a7f630118477f12c7a3c6b3b3ee5d56868632dd71fffd5c0434821207c2769f6859b9d398c2df770ec6487d7c32d')

pkgname=util-linux
pkgver=2.32.rc2
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
arch=('x86_64')
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=('GPL2')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/util-linux/v${pkgver:0:4}/util-linux-${pkgver/.rc/-rc}.tar.xz")

build() {
	cd "${srcdir}/util-linux-${pkgver/.rc/-rc}"

	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--libdir=/usr/lib \
		--sbindir=/usr/sbin \
		--enable-mesg \
		--disable-nologin \
		--disable-chfn-chsh \
		--disable-su \
		--disable-login \
		--disable-kill \
		--with-systemd \
		--enable-fs-paths-default=/usr/sbin \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		PYTHON=/usr/bin/python3

	make
}

package() {
	cd "${srcdir}/util-linux-${pkgver/.rc/-rc}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/lib/*.so* "${pkgdir}"/usr/lib/*.a
	rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/lib/pkgconfig"

	rm -rf "${pkgdir}"/usr/bin/{mount,umount}
	rm -rf "${pkgdir}"/usr/share/man/man8/{mount,umount}.8
	rm -rf "${pkgdir}/usr/share/man/man5/fstab.5"
}

sha512sums=('a7c3b5b84fa78aded1fc7b4e3111c140512f5e2e5851162659423a3905eadd746d65beedd3faf813b77e823d63f3c9b33db1f44f7b85e9006ff64f82fb3cadb7')

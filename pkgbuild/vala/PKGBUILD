pkgname=vala
pkgver=0.38.4
pkgrel=1
pkgdesc="Compiler for the GObject type system"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/Vala"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/vala/${pkgver:0:4}/vala-${pkgver}.tar.xz"
	"vala-remove-valadoc.patch")

prepare() {
	cd "${srcdir}/vala-${pkgver}"

	patch -Np1 -i "${srcdir}/vala-remove-valadoc.patch"

	autoreconf -fi
}

build() {
	cd "${srcdir}/vala-${pkgver}"

	./configure --prefix=/usr

	make
}

package() {
	cd "${srcdir}/vala-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('3015ae0abd8439e65948df1b360f8efae19ee0a1e1d63e2942f31e2bc06defa8bf91af86cefb26786d5ed9c1916e129640e41197d8897a3233e06e49935a8847'
            '04299aaccc56348d461366e94f7e39404ca688bb61ebfd34cac526eae32595d667c6c3d147f347b6a6d5e0016e02633b8d0107a88d6b17665c4773786c5e8bee')

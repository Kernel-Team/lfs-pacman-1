pkgname=vala
pkgver=0.38.6
pkgrel=1
pkgdesc="Compiler for the GObject type system"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/Vala"
license=('LGPL')
#depends=()
source=("https://download.gnome.org/sources/vala/${pkgver:0:4}/vala-${pkgver}.tar.xz"
	"vala-remove-valadoc.patch")

prepare() {
	cd "${srcdir}/vala-${pkgver}"

	patch -Np1 -i "${srcdir}/vala-remove-valadoc.patch"

	autoreconf -fi
}

build() {
	cd "${srcdir}/vala-${pkgver}"

	./configure --prefix=/usr

	make
}

package() {
	cd "${srcdir}/vala-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

sha512sums=('e6a36868e197c9a86360e180be845e07449de129cac4d7d48725ca3b2d0e9522eb40e422873ed04686dd850d335e63e412a8c78a44b371c61078e37bfc17c566'
            '04299aaccc56348d461366e94f7e39404ca688bb61ebfd34cac526eae32595d667c6c3d147f347b6a6d5e0016e02633b8d0107a88d6b17665c4773786c5e8bee')

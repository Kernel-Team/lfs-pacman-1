pkgbase=vulkan
pkgname=('vulkan' 'lib32-vulkan')
pkgver=1.1.70.0
pkgrel=1
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('custom')
#depends=()
options=(emptydirs)
source=("https://github.com/KhronosGroup/Vulkan-LoaderAndValidationLayers/archive/sdk-${pkgver}/Vulkan-LoaderAndValidationLayers-sdk-${pkgver}.tar.gz")

prepare() {

	cp -a "${srcdir}"/Vulkan-LoaderAndValidationLayers{,32}-sdk-${pkgver}

	install -v -dm755 "${srcdir}"/build{32,64}
}

build() {
	local _flags=(-DCMAKE_INSTALL_PREFIX=/usr
		-DCMAKE_BUILD_TYPE=Release
		-DCMAKE_INSTALL_SYSCONFDIR=/etc
		-DCMAKE_INSTALL_DATADIR=share
		-DCMAKE_SKIP_RPATH=ON
		-DBUILD_TESTS=OFF
		-DBUILD_WSI_XCB_SUPPORT=ON
		-DBUILD_WSI_XLIB_SUPPORT=ON
		-DBUILD_WSI_WAYLAND_SUPPORT=ON
		-DBUILD_WSI_MIR_SUPPORT=OFF)

	cd "${srcdir}/build64"

	bash -e "${srcdir}/Vulkan-LoaderAndValidationLayers-sdk-${pkgver}/update_external_sources.sh"

	cmake "${srcdir}/Vulkan-LoaderAndValidationLayers-sdk-${pkgver}" \
		-DCMAKE_INSTALL_LIBDIR=lib \
		${_flags[@]}

	cd "${srcdir}/build32"

	export ASFLAGS="--32"
	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	bash -e "${srcdir}/Vulkan-LoaderAndValidationLayers32-sdk-${pkgver}/update_external_sources.sh"

	cmake "${srcdir}/Vulkan-LoaderAndValidationLayers32-sdk-${pkgver}" \
		-DCMAKE_INSTALL_LIBDIR=lib32 \
		${_flags[@]}

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset ASFLAGS CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	make -C "${srcdir}/build64"
	make -C "${srcdir}/build32"
}

package_vulkan() {
	pkgdesc="Vulkan Installable Client Driver (ICD) Loader and Validation Layers"

	cd "${srcdir}/build64"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/etc/vulkan/icd.d"
	mv "${pkgdir}/etc" "${pkgdir}/usr/share"
}

package_lib32-vulkan() {
	pkgdesc="Vulkan Installable Client Driver (ICD) Loader and Validation Layers (32 bit)"

	cd "${srcdir}/build32"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr/bin"
	mv dest/usr/lib32 "${pkgdir}/usr"
	mv dest/usr/bin/vulkaninfo "${pkgdir}/usr/bin/vulkaninfo32"
}

sha512sums=('ae57e6890225b6e4607fde04a296a14bc54fd6b61b360b771bd24f00607dcb6fff06b3ec38a761173c92c90628a139f1424445e174c21adad4af6f445b91ed26')

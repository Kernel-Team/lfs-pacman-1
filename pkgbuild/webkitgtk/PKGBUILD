pkgname=webkitgtk
pkgver=2.18.0
pkgrel=1
pkgdesc="GTK+ Web content engine library"
arch=('x86_64')
url="https://webkitgtk.org/"
license=('custom')
#depends=()
source=("https://webkitgtk.org/releases/webkitgtk-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/webkitgtk-${pkgver}"

	sed -i "s#enchant_dict_free_suggestions#enchant_dict_free_string_list#g" Source/WebCore/platform/text/enchant/TextCheckerEnchant.cpp

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	CXXFLAGS+=" -w"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DPORT=GTK \
		-DLIB_INSTALL_DIR=/usr/lib \
		"${srcdir}/webkitgtk-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('4a248e0f8128a61c123107baebb37efa9944fe490922b474f42400d68f3007e7d28a20886f9e60bfd86e357ea1aa9ede0c8bba3c34e2e3899720805b9145fc48')

pkgname=webkitgtk
pkgver=2.16.5
pkgrel=1
pkgdesc="GTK+ Web content engine library"
arch=('x86_64')
url="https://webkitgtk.org/"
license=('custom')
#depends=()
source=("https://webkitgtk.org/releases/webkitgtk-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/webkitgtk-${pkgver}"

	sed -i "/unistd.h/i#include <stdio.h>" Source/WTF/wtf/NumberOfCores.cpp
	sed -i "/stdlib.h/i#include <stdio.h>" Source/WebKit2/UIProcess/linux/MemoryPressureMonitor.cpp

	install -v -dm755 "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	CXXFLAGS+=" -w"

	cmake -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DPORT=GTK \
		-DLIB_INSTALL_DIR=/usr/lib \
		"${srcdir}/webkitgtk-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('9d7573da44267edcd83b4918e5f1e0516eb8d84c58ac6b239a2328448f96b39067b62bcd18e7d730ec0ef44b9f4b0a03712d17f9f465f00346a1f45a0a4ebb10')

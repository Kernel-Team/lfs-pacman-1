pkgname=weston
pkgver=9.0.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('x86_64')
url="https://wayland.freedesktop.org/"
license=('MIT')
#depends=()
source=("https://wayland.freedesktop.org/releases/weston-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/weston-${pkgver}"

	sed -i "s#pangocairo#gobject-2.0 &#" clients/meson.build
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dbackend-rdp=false \
		-Dsimple-dmabuf-drm=intel \
		"${srcdir}/weston-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('ccc263f8279b7b23e5c593b4a8a023de2c3dc178b1b8d6593599171770bcfe97608de9fcb77aa1cab39255451d289d323e51c317dae190c7641282e085b84f90')

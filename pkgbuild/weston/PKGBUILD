pkgname=weston
pkgver=7.0.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('x86_64')
url="https://wayland.freedesktop.org/"
license=('MIT')
#depends=()
source=("https://wayland.freedesktop.org/releases/weston-${pkgver}.tar.xz")

prepare() {
	install -v -dm755 "${srcdir}/build"

	cd "${srcdir}/weston-${pkgver}"

	sed -i "s#pangocairo#gobject-2.0 &#" clients/meson.build
}

build() {
	cd "${srcdir}/build"

	meson --prefix /usr \
		--sysconfdir /etc \
		--localstatedir /var \
		--buildtype=plain \
		-Dbackend-rdp=false \
		-Dsimple-dmabuf-drm=intel \
		"${srcdir}/weston-${pkgver}"

	ninja ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"

	DESTDIR="${pkgdir}" ninja install
}

sha512sums=('fa14467ab89fb12175830d9cf29d89dc04899fd21d801e60e85836e8e52e17e144be78098b3eedfab6c51b7fb055ecfabb456a8a74f4e40485134da59ac58ea3')

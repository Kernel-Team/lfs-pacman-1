pkgname=wireless-regdb
pkgver=2017.03.07
pkgrel=1
pkgdesc="Central Regulatory Domain Database"
arch=('x86_64')
url="https://wireless.wiki.kernel.org/en/developers/Regulatory"
license=('custom')
#depends=()
source=("https://www.kernel.org/pub/software/network/wireless-regdb/wireless-regdb-${pkgver}.tar.xz"
	"wireless-regdom.default")

package() {
	cd "${srcdir}/wireless-regdb-${pkgver}"

	install -v -Dm644 regulatory.bin "${pkgdir}/usr/lib/crda/regulatory.bin"
	install -v -Dm644 sforshee.key.pub.pem "${pkgdir}/usr/lib/crda/pubkeys/sforshee.key.pub.pem"
	install -v -Dm644 regulatory.bin.5 "${pkgdir}/usr/share/man/man5/regulatory.bin.5"

	install -v -Dm644 "${srcdir}/wireless-regdom.default" "${pkgdir}/etc/default/wireless-regdom"

	for dom in $(grep ^country db.txt | cut -d' ' -f2 | sed "s#:##g")
	do
		echo "#WIRELESS_REGDOM=\"${dom}\"" >> "${srcdir}/wireless-regdom.tmp"
	done

	sort -u "${srcdir}/wireless-regdom.tmp" >> "${pkgdir}/etc/default/wireless-regdom"
}

sha512sums=('91e2458feb5bf4d4b7d9008afd9c2d40b700d53cb898855f6278b93a7779aefeed283bfff2dd77b89bcbcc98c5e131f5187730356e5bba858d96774d610ab450'
            '389e4edf429de5336ec902203ccdfcf786b65ff6da3b1a4b4e4708037fc88b407430a8e211c7a8b3954562e7e1a7d05f0618c957ad22e0992ec5ffbd55dd04f2')

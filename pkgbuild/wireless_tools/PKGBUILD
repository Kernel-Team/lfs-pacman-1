pkgname=wireless_tools
pkgver=30.pre9
_pkgver=${pkgver%.pre*}
pkgrel=1
pkgdesc="Tools allowing to manipulate the Wireless Extensions"
arch=('x86_64')
url="https://hewlettpackard.github.io/wireless-tools/Tools.html"
license=('GPL')
#depends=()
source=("https://hewlettpackard.github.io/wireless-tools/wireless_tools.${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/wireless_tools.${_pkgver}"

	sed -i "s:BUILD_STATIC:# &:" Makefile
	sed -i "s:-Os:& ${CFLAGS}:g" Makefile
	sed -i "s:CC = gcc:CC = ${CC}:g" Makefile
}

build() {
	cd "${srcdir}/wireless_tools.${_pkgver}"

	unset CFLAGS

	make
}

package() {
	cd "${srcdir}/wireless_tools.${_pkgver}"

	make PREFIX="${pkgdir}/usr" INSTALL_MAN="${pkgdir}/usr/share/man" install

	mv "${pkgdir}/usr/sbin" "${pkgdir}"

	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libiw.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libiw.so") "${pkgdir}/usr/lib/libiw.so"

	rm -rf "${pkgdir}"/usr/share/man/[^man{1..8,n}]*
}

sha512sums=('0561923bca075af4754d21e1be303cbd1ce3558cb6481c9c12bc7d330a353acf8ad81a822488570e859480287999c3bb2a698c993556a92f0699743e0a527ff1')

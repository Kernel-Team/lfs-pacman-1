pkgname=wpa_supplicant
pkgver=2.6
pkgrel=1
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=('x86_64')
url="https://w1.fi/wpa_supplicant/"
license=('GPL')
#depends=()
options=(docs)
source=("https://w1.fi/releases/wpa_supplicant-${pkgver}.tar.gz"
	"config")

prepare() {
	cd "${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant"

	install -v -m644 "${srcdir}/config" .config

	sed -i "s#wpa_supplicant -u#& -s -O /run/wpa_supplicant#g" dbus/*.service.in
	sed -i "s#wpa_supplicant -u#& -s -O /run/wpa_supplicant#g" systemd/wpa_supplicant.service.in
}

build() {
	cd "${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant"

	make BINDIR=/usr/sbin LIBDIR=/usr/lib
}

package() {
	cd "${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant"

	install -v -dm755 "${pkgdir}/usr/lib/systemd/system" "${pkgdir}/usr/sbin" "${pkgdir}/usr/bin"
	install -v -dm755 "${pkgdir}/usr/share/doc/wpa_supplicant" "${pkgdir}/usr/share/man/man5"
	install -v -dm755 "${pkgdir}/usr/share/man/man8"

	install -v -m755 wpa_cli wpa_supplicant "${pkgdir}/usr/sbin"
	install -v -m755 wpa_passphrase "${pkgdir}/usr/bin"

	install -v -m644 doc/docbook/wpa_supplicant.conf.5 "${pkgdir}/usr/share/man/man5"
	install -v -m644 doc/docbook/wpa_background.8 doc/docbook/wpa_cli.8 "${pkgdir}/usr/share/man/man8"
	install -v -m644 doc/docbook/wpa_passphrase.8 doc/docbook/wpa_supplicant.8 "${pkgdir}/usr/share/man/man8"

	install -v -Dm644 dbus/fi.epitest.hostap.WPASupplicant.service \
		"${pkgdir}/usr/share/dbus-1/system-services/fi.epitest.hostap.WPASupplicant.service"

	install -v -Dm644 dbus/fi.w1.wpa_supplicant1.service \
		"${pkgdir}/usr/share/dbus-1/system-services/fi.w1.wpa_supplicant1.service"

	install -v -Dm644 dbus/dbus-wpa_supplicant.conf "${pkgdir}/etc/dbus-1/system.d/wpa_supplicant.conf"

	install -v -m644 examples/*.conf "${pkgdir}/usr/share/doc/wpa_supplicant"
	install -v -m644 systemd/*.service "${pkgdir}"/usr/lib/systemd/system
}

sha512sums=('46442cddb6ca043b8b08d143908f149954c238e0f3a57a0df73ca4fab9c1acd91b078f3f26375a1d99cd1d65625986328018c735d8705882c8f91e389cad28a6'
            'd023da33b5e134ae96533b795369d03b390e004a57127dab32e41c9053ca3e8555e6032b776e8724aa4edb456a6426deb5a7a6b1e120ce40fb459191536ae166')

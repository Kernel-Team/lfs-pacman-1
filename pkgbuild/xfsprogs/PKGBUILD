pkgname=xfsprogs
pkgver=4.20.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
url="http://oss.sgi.com/projects/xfs/"
license=('LGPL')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/fs/xfs/xfsprogs/xfsprogs-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/xfsprogs-${pkgver}"

	export OPTIMIZER="${CFLAGS}"
	unset CFLAGS

	make DEBUG=-DNDEBUG     \
		INSTALL_USER=root  \
		INSTALL_GROUP=root \
		LOCAL_CONFIGURE_OPTIONS="--enable-readline --disable-lib64 --disable-lto"
}

package() {
	cd "${srcdir}/xfsprogs-${pkgver}"

	make -j1 install install-dev DESTDIR="${pkgdir}" PKG_ROOT_LIB_DIR=/usr/lib PKG_ROOT_SBIN_DIR=/usr/sbin
}

sha512sums=('6adf93fbe20d6b3250c4690237bda041a6dd0cfdc2387f5c88704fc06e6f095bf047964d701ccd65efef7607c15fb8e97b6046f3d631ef327aa8f4c118a36e22')

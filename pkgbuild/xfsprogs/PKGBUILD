pkgname=xfsprogs
pkgver=4.15.1
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
url="http://oss.sgi.com/projects/xfs/"
license=('LGPL')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/fs/xfs/xfsprogs/xfsprogs-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/xfsprogs-${pkgver}"

	export OPTIMIZER="${CFLAGS}"
	unset CFLAGS

	sed -i "s#copy_file_range(#xfs_&#g" io/copy_file_range.c

	make DEBUG=-DNDEBUG     \
		INSTALL_USER=root  \
		INSTALL_GROUP=root \
		LOCAL_CONFIGURE_OPTIONS="--enable-readline --disable-lib64"
}

package() {
	cd "${srcdir}/xfsprogs-${pkgver}"

	make -j1 install install-dev DESTDIR="${pkgdir}" PKG_ROOT_LIB_DIR=/usr/lib PKG_ROOT_SBIN_DIR=/usr/sbin
}

sha512sums=('4ec3f0438f33322f0ff6e5acb27cbb554ab48df3ae0ef11071992b085016f84b49ca7bdbaf3a6859427877ea9c419bcd37ac3058914d67409ba2d65e586508ee')

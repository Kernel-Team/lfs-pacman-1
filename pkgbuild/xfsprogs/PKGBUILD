pkgname=xfsprogs
pkgver=4.11.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
url="http://oss.sgi.com/projects/xfs/"
license=('LGPL')
#depends=()
source=("https://www.kernel.org/pub/linux/utils/fs/xfs/xfsprogs/xfsprogs-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/xfsprogs-${pkgver}"

	export OPTIMIZER="${CFLAGS}"
	unset CFLAGS

	make DEBUG=-DNDEBUG     \
		INSTALL_USER=root  \
		INSTALL_GROUP=root \
		LOCAL_CONFIGURE_OPTIONS="--enable-readline --disable-lib64"
}

package() {
	cd "${srcdir}/xfsprogs-${pkgver}"

	make -j1 install install-dev DESTDIR="${pkgdir}"

	ln -sfv ../../lib/$(readlink "${pkgdir}/lib/libhandle.so") "${pkgdir}/usr/lib/libhandle.so"
	rm -rf "${pkgdir}/lib/libhandle.a" "${pkgdir}/lib/libhandle.so"
}

sha512sums=('2db59553e4442c999a718ccf712b01de80b1e3acd1c3e5f9f7f5dc59e6c9180aa1a42085363303fe454d441bc2f886e118fc2958320afa49c7a8886fcfb5d72c')

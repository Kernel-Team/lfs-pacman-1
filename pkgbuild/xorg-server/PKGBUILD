pkgname=xorg-server
pkgver=1.20.10
pkgrel=1
pkgdesc="Xorg X server"
arch=('x86_64')
url="https://www.x.org/wiki/"
license=('custom')
#depends=()
options=(emptydirs)
source=("https://www.x.org/releases/individual/xserver/xorg-server-${pkgver}.tar.bz2")

build() {
	cd "${srcdir}/xorg-server-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-install-setuid \
		--disable-selective-werror \
		--enable-dri2 \
		--enable-dri3 \
		--enable-glamor \
		--enable-present \
		--enable-suid-wrapper \
		--enable-systemd-logind \
		--enable-xephyr \
		--enable-xnest \
		--enable-xvfb \
		--enable-xwayland \
		--with-systemd-daemon \
		--with-xkb-output=/var/lib/xkb

	make
}

package() {
	cd "${srcdir}/xorg-server-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}/var/log"
	install -v -dm755 "${pkgdir}/etc/X11/xorg.conf.d"
}

sha512sums=('a07bee380bb72f2117fe6f831a6e4aded19bea1f2b36e42a019a30348e98d6fe65c0617cf819be9c6b405502f88cafb829df30aab32393774b71f1418a4cefae')

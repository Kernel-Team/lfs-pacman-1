pkgbase=xz
pkgname=('xz' 'lib32-xz')
pkgver=5.2.5
pkgrel=1
arch=('x86_64')
url="http://tukaani.org/xz/"
license=('GPL' 'LGPL' 'custom')
#depends=()
source=("https://tukaani.org/xz/xz-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/xz{,32}-${pkgver}
}

build() {
	cd "${srcdir}/xz-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/xz32-${pkgver}"

	export CC_HOLD="${CC:-gcc}" CXX_HOLD="${CXX:-g++}"
	export CC="${CC:-gcc} -m32" CXX="${CXX:-g++} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/xz-${pkgver}"
	make -C "${srcdir}/xz32-${pkgver}"
}

package_xz() {
	pkgdesc="Library and command line tools for XZ and LZMA compressed files"

	cd "${srcdir}/xz-${pkgver}"

	make install DESTDIR="${pkgdir}"
}

package_lib32-xz() {
	pkgdesc="Library and command line tools for XZ and LZMA compressed files (32 bit)"

	cd "${srcdir}/xz32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

sha512sums=('59266068a51cb616eb31b67cd8f07ffeb2288d1391c61665ae2ec6814465afac80fec69248f6a2f2db45b44475af001296a99af6a32287226a9c41419173ccbb')

pkgname=zip
pkgver=3.0
_pkgver=${pkgver/./}
pkgrel=1
pkgdesc="Compressor/archiver for creating and modifying zipfiles"
arch=('x86_64')
url="http://www.info-zip.org/Zip.html"
license=('custom')
#depends=()
source=("http://downloads.sourceforge.net/infozip/${pkgname}${_pkgver}.tar.gz")

build() {
	cd "${srcdir}/zip${_pkgver}"

	sed -i "s:-O3:${CFLAGS}:g" unix/configure

	unset CFLAGS

	make -f unix/Makefile generic CC="${CC}"
}

package() {
	cd "${srcdir}/zip${_pkgver}"

	make -f unix/Makefile install prefix="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man/man1"
}

sha512sums=('c1c3d62bf1426476c0f9919b568013d6d7b03514912035f09ee283226d94c978791ad2af5310021e96c4c2bf320bfc9d0b8f4045c48e4667e034d98197e1a9b3')

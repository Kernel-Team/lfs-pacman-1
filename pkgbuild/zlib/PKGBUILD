pkgbase=zlib
pkgname=('zlib' 'lib32-zlib')
pkgver=1.2.11
pkgrel=1
arch=('x86_64')
url="http://www.zlib.net/"
license=('custom')
#depends=()
source=("http://zlib.net/zlib-${pkgver}.tar.xz")

prepare() {
	cp -a "${srcdir}"/zlib{,32}-${pkgver}
}

build() {
	cd "${srcdir}/zlib-${pkgver}"

	./configure --prefix=/usr

	cd "${srcdir}/zlib32-${pkgver}"

	export CC_HOLD="${CC}" CXX_HOLD="${CXX}"
	export CC="${CC} -m32" CXX="${CXX} -m32"
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	./configure --prefix=/usr --libdir=/usr/lib32

	export CC="${CC_HOLD}" CXX="${CXX_HOLD}"
	unset CC_HOLD CXX_HOLD PKG_CONFIG_PATH

	cd "${srcdir}"

	make -C "${srcdir}/zlib-${pkgver}"
	make -C "${srcdir}/zlib32-${pkgver}"
}

package_zlib() {
	pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP"

	cd "${srcdir}/zlib-${pkgver}"

	make install DESTDIR="${pkgdir}"

	install -v -dm755 "${pkgdir}/lib"

	mv -v "${pkgdir}"/usr/lib/libz.so.* "${pkgdir}/lib"
	ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libz.so") "${pkgdir}/usr/lib/libz.so"
}

package_lib32-zlib() {
	pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP (32 bit)"

	cd "${srcdir}/zlib32-${pkgver}"

	make install DESTDIR="${PWD}/dest"

	install -v -dm755 "${pkgdir}/usr"
	mv dest/usr/lib32 "${pkgdir}/usr"
}

md5sums=('85adef240c5f370b308da8c938951a68')
